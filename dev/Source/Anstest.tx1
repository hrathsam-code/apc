. PL/B ANSWER PROGRAM
.
. 
PORTN    DIM       30
TODAY    INIT      "mm/dd/yy"          DATE PASSED IN COMMON
SECURITY FORM      1                   SEE DESCRIPTION BELOW
DATE6    DIM       6
ENVS     DIM       4
INVNUMB  INIT      "     "
ACCTNUMB INIT      "    "
VENDNUMB INIT      "   "
FROMPGM  INIT      "        "
FIRSTIME INIT      " "
TESTONE  INIT      " "
TESTTWO  INIT      " "
TESTTHRE INIT      " "
ERRORSW1 INIT      " "
ERRORSW2 INIT      " "
ERRORSW3 INIT      " "
ERRORSW4 INIT      " "
IOTEST   INIT      " "
IOTYPE   INIT      "      "
IOERFILE INIT      "                   "
USERCODE INIT      "         "
USERNAME INIT      "                    "
APPCODE  INIT      "  "
APPSECUR FORM      2
.
.
DPORT    DIM       12
WSNUM    DIM       2
. SCRATCH VARIABLES
.
CWK2     DIM       2
CWK3     DIM       3
NWK2     FORM      2
NWK9     FORM      9
DIMWK9   DIM       9
MM       DIM       2
DD       DIM       2
YY       DIM       2
*................................................................
USERS    IFILE     KEYLEN=9
USERID   DIM       9         1 -  9          USER ID
NAME     DIM       20       10 - 29          USER'S NAME
.
USERKEY  DIM       9
*................................................................
. ROLCHAIN FILE                          ROLLOUT CHAIN FILE
SEQ      FORM      "-1"
*................................................................
. OUTPUT PARAMETERS OF SUBROUTINE "GETDATE"
.                                      TIME IN 24-HR. FORMAT
TIME     INIT      "hh:mm:ss"          hours:minutes:seconds
DAY      FORM      2
MONTH    FORM      2                   mm/dd/yy
YEAR     FORM      2
*
JDAY     FORM      3                   JULIAN DATE
CDAY     DIM       3                   DAY (CHARACTER STRING)
CYEAR    DIM       2                   YEAR (CHARACTER STRING)
*
NFEB     FORM      "28"                # OF DAYS IN FEBRUARY
N30      FORM      "30"                USED FOR 30-DAY MONTHS
N31      FORM      "31"                USED FOR 31-DAY MONTHS
*................................................................
REPLY    DIM       1
         INCLUDE   LOGDATA.TXT
. 
LINE1    INIT      034,"C@@C@@@B",034,"@NENE"
LINE2    INIT      034,"B@LECAELAEK@GGAEJ@DELEI@GEAEG@",034,"@MENF"
.
EOR      INIT      0372
REC0     FORM      "00"
.
ALPHWORK DIM       30
LEN      FORM      3
DATETXT  DIM       17
DATETXT1 INIT      "Today is "
FLD01TXT INIT      "What is your identification number?"
STATTXT  DIM       30
STATTXT1 INIT      "You are logged on at "
STATTXT2 INIT      "Hello, "
NULLFLD  INIT      "                              "
TWO      FORM      "2"
RESULT   FORM      4
.
TITLTXT  INIT      "SUPERIOR WASHER INFORMATION NETWORK"
TITLE    STATTEXT
PRMT01   STATTEXT
DATEDSP  STATTEXT
STATMSG  STATTEXT
.
ENTRY01  EDITTEXT
.
LogButton  BUTTON
.
GRAY     COLOR
TEST1    COLOR
.
+................................................................
. MAINLINE
.
         SETMODE   *LTGRAY=ON,*3D=ON,*FULLSCR=ON,*CARET=BLOCK
         DISPLAY   *BGCOLOR=*YELLOW,*ES,*BLUE
         CLOCK     DATE TO TODAY
         PACKKEY   DATETXT WITH DATETXT1,TODAY
.         CREATE    GRAY=*GRAY
         CREATE    TEST1=35:115:65
 CREATE  TITLE=4:5:14:66,TITLTXT,"TIMES(12,BOLD)",FGCOLOR=TEST1,BORDER,STYLE=3DOUT
         DISPLAY   *BLUE
         CREATE    PRMT01=10:11:20:49,FLD01TXT,"HELVETICA",BORDER,STYLE=3DOUT
         DISPLAY   *BLACK
     CREATE  DATEDSP=7:8:33:48,DATETXT,"HELVETICA",CENTER,BORDER,STYLE=3DFLAT
     CREATE  ENTRY01=10:11:51:61,FONT="HELVETICA",PASSWORD,BORDER,MAXCHARS=9
     CREATE  STATMSG=13:14:26:55,STATTXT,"HELVETICA",CENTER,BORDER,STYLE=3DFLAT
         CREATE    LogButton=22:23:31:50,"&Logon"
         DISPLAY   *BLUE
         GOTO      LOGOPEN
.
.         INCLUDE   LOGIO.TXT
LOGOPEN
.
.
*................................................................
. SET THE TRAPS SO THAT IF ANYTHING GOES WRONG THE USER STILL
. CANNOT LOG ON WITHOUT AUTHORIZATION
. 
         TRAP      BADANS IF CFAIL
         TRAP      BADANS IF FORMAT
         TRAP      BADANS IF RANGE
         TRAP      BADANS IF PARITY
. 
.
         CLOCK     SYSPORT TO PORTN
         CLOCK     TIME TO TIME
         CLOCK     YEAR TO CYEAR
         CLOCK     DAY TO CDAY
         CALL      GETDATE
.
*................................................................
. OPEN THE FILE OF AUTHORIZED USERS
. 
         TRAP      NOUSER IF IO
         OPEN      USERS,"SC\SRESUF.ISI"
.
         TRAPCLR   IO
         GOTO      INIT
*................................................................
. FILE CONTAINING AUTHORIZATIONS IS MISSING
.
NOUSER   ALERT     STOP,"UNABLE TO OPEN FILE OF AUTHORIZED USERS",RESULT
         GOTO      HANG
.
HANG     GOTO      HANG         
*................................................................
. LOG THE USER ONTO THE SYSTEM
.
INIT
         ACTIVATE  TITLE
         ACTIVATE  PRMT01
         ACTIVATE  DATEDSP
         ACTIVATE  STATMSG
         ACTIVATE  ENTRY01
         ACTIVATE  LogButton
         DISABLEITEM  LogButton
.
LOOP
         ACTIVATE  ENTRY01,LOGON,RESULT
         SETFOCUS  ENTRY01
         ACTIVATE  LogButton,READID,RESULT
         LOOP
           WAITEVENT
         REPEAT
.         STOP
. 
.
*................................................................
. CHECK TO SEE IF THE USER IS AUTHORIZED
. 
LOGON
         COMPARE   "1" TO RESULT
         GOTO      RESETB IF EQUAL
         CLEAR     DIMWK9
         GETITEM   ENTRY01,0,DIMWK9
         PACK      USERKEY,DIMWK9,NULLFLD
         ENABLEITEM  LogButton
         RETURN
RESETB   DISABLEITEM  LogButton
         RETURN         
*................................................................
. SEE IF THE USER IS AUTHORIZED
.
READID   TRAP      BADID IF IO
         READ      USERS,USERKEY;USERID,NAME,SECURITY
         GOTO      BADID IF OVER
         MOVE      NULLFLD TO STATTXT
         PACK      STATTXT WITH STATTXT2,NAME
         MOVELPTR  STATTXT TO LEN
         SFORMAT   ALPHWORK TO LEN
         MOVE      STATTXT TO ALPHWORK
         SETITEM   STATMSG,0,ALPHWORK
         PAUSE     TWO
         SETITEM   STATMSG,0,NULLFLD
         MOVE      NULLFLD TO STATTXT
         PACK      STATTXT WITH STATTXT1,TIME
         MOVELPTR  STATTXT TO LEN
         SFORMAT   ALPHWORK TO LEN
         MOVE      STATTXT TO ALPHWORK
         SETITEM   STATMSG,0,ALPHWORK
         PAUSE     TWO
         SETITEM   STATMSG,0,NULLFLD
         MOVE      USERID TO USERCODE
         MOVE      NAME TO USERNAME
         MOVE      "00" TO APPSECUR
         TRAP      BADANS IF IO
         NORETURN
         GOTO      LOCATE
.
*................................................................
. UN-AUTHORIZED USER
.
BADID    BEEP
         ALERT     STOP,"You are not an authorized user!",RESULT
         RETURN
.
.................................................................
.
LOCATE   MOVE      "SC  " TO ENVS
         RESET     ENVS
.
.
MSTRCHN  TRAPCLR   CFAIL
         TRAP      BADANS IF CFAIL
.
         MOVE      USERID TO DPORT
         CHAIN     "MASTEST.DBC"
.
.
*................................................................
. SUBROUTINE TO GET THE TIME, DAY AND YEAR
.
. ON EXIT VARIABLE:  TIME = "hr:mn:sc"
.                    DAY = "dd"
.                    MONTH = "mm"
.                    YEAR = "yy"
.                    TODAY = "mm/dd/yy"
.
GETDATE  CLOCK     YEAR TO CYEAR       GET THE YEAR
         CLOCK     DAY TO CDAY         GET THE DAY
         CLOCK     TIME TO TIME        GET THE TIME
*................................................................
. PERFORM BOUNDARY CONDITION CHECKS IF DESIRED
. 
.        CLOCK     DAY TO CWK3            IF TIME TAKEN BEFORE
.        MATCH     CDAY TO CWK3           MIDNIGHT AND DAY TAKEN
.        GOTO      GETDATE IF NOT EQUAL   AFTER MIDNIGHT, REPEAT
*
.        CLOCK     YEAR TO CWK2           IF DAY TAKEN BEFORE
.        MATCH     CYEAR TO CWK2          NEW YEARS & YEAR TAKEN
.        GOTO      GETDATE IF NOT EQUAL   AFTER NEW YEARS, REPEAT
*................................................................
         MOVE      CDAY TO JDAY
         MOVE      "0" TO MONTH        INITIALIZE
         MOVE      CYEAR TO YEAR
*
         COMPARE   "1" TO JDAY         SYSTEM INITIALIZED
         GOTO      NODATE IF LESS      WITHOUT DATE!
*................................................................
. PERFORM YEAR-CHECK IF DESIRED
.
.        COMPARE   "70" TO YEAR
.        GOTO      NODATE IF LESS
.        COMPARE   "80" TO YEAR
.        GOTO      NODATE IF NOT LESS
*................................................................
. MAKE SURE FEBRUARY IS HANDLED PROPERLY ON LEAP YEARS
.
         MOVE      YEAR TO NWK2
         DIVIDE    "4" INTO NWK2
         MULTIPLY  "4" INTO NWK2
         COMPARE   YEAR TO NWK2           IS IT A LEAP YEAR?
         GOTO      MDLOOP IF NOT EQUAL    NO, LEAVE NFEB = 28.
         MOVE      "29" TO NFEB           YES, SET NFEB = 29.
*................................................................
. COMPUTE THE MONTH
.
MDLOOP   ADD       "1" TO MONTH
         LOAD      NWK2 FROM MONTH OF N31,NFEB,N31:   JAN/FEB/MAR
                                      N30,N31,N30:    APR/MAY/JUN
                                      N31,N31,N30:    JUL/AUG/SEP
                                      N31,N30,N31     OCT/NOV/DEC
         SUBTRACT  NWK2 FROM JDAY
         GOTO      MDL1 IF EQUAL       SUBTRACT DAYS OF THE MONTH
         GOTO      MDLOOP IF NOT LESS  UNTIL MONTH FOUND
*
MDL1     ADD       NWK2 TO JDAY        UNBIAS FROM LAST SUBTRACT
         MOVE      JDAY TO DAY         TO GET DAY OF THE MONTH
*................................................................
. PUT THE DATE INTO mm/dd/yy FORMAT
.
MDL2
.         CLEAR     TODAY
.         APPEND    MONTH TO TODAY
.         APPEND    "/" TO TODAY
.         MOVE      DAY TO CWK2
.         CMATCH    " " TO CWK2         IS THERE A LEADING BLANK?
.         GOTO      MDL3 IF NOT EQUAL   NO, CONTINUE
.         CMOVE     "0" TO CWK2         YES, REPLACE IT WITH 0
.MDL3     APPEND    CWK2 TO TODAY
.         APPEND    "/" TO TODAY
.         MOVE      YEAR TO CWK2
.         CMATCH    " " TO CWK2         IS THERE A LEADING BLANK?
.         GOTO      MDL4 IF NOT EQUAL   NO, CONTINUE
.         CMOVE     "0" TO CWK2         YES, REPLACE IT WITH 0
.MDL4     APPEND    CWK2 TO TODAY
.         RESET     TODAY
         CLEAR     DATE6
         MOVE      MONTH TO MM
         REP       " 0"  IN MM
         MOVE      DAY   TO DD
         REP       " 0"  IN DD
         MOVE      YEAR  TO YY
         APPEND    MM    TO DATE6
         APPEND    DD    TO DATE6
         APPEND    YY    TO DATE6
         RESET     DATE6 TO 1
         RETURN
. 
. 
*................................................................
. DATE IMPROPER OR NOT INITIALIZED
.
NODATE   BEEP
         KEYIN     *P1:8,*EF,"What is the current month? ",MONTH:
                   *N,"What is the current day? ",DAY:
                   *N,"What is the current year? ",YEAR
*................................................................
. CHECK FOR INVALID DAY ENTERED
. 
         COMPARE   "1" TO DAY
         GOTO      NODATE IF LESS
         COMPARE   "32" TO DAY
         GOTO      NODATE IF NOT LESS
*................................................................
. CHECK FOR INVALID MONTH ENTERED
. 
         COMPARE   "1" TO MONTH
         GOTO      NODATE IF LESS
         COMPARE   "13" TO MONTH
         GOTO      NODATE IF NOT LESS
*................................................................
. CHECK FOR INVALID YEAR IF DESIRED
. 
.        COMPARE   "70" TO YEAR
.        GOTO      NODATE IF LESS
.        COMPARE   "80" TO YEAR
.        GOTO      NODATE IF NOT LESS
         DISPLAY   *P1:12,"Thank you",*W,*W,*P1:8,*EF
         GOTO      MDL2
*................................................................
. A TRAP HAS OCCURED WHILE IN THE ANSWER PROGRAM.  DO NOT ALLOW
. A CHAIN TO THE MASTER PROGRAM
. 
BADANS   ALERT     STOP,"Unrecoverable system error!",RESULT
         GOTO      HANG
