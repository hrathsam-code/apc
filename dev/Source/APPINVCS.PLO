.   ORDER PROCESSING SYSTEM......INVOICE DATA ENTRY
. 
.  WRITTEN BY R. BACHARACH,  MICRO SYSTEMS CONSULTANTS
.			     41	TEELE DRIVE
.			     CORAM, NEW	YORK 11727
.			     (516) 698-5577
. 
	 INCLUDE   COMMON.TXT
. 
PASSCUST DIM	   30
INVCNUMB DIM	    5
PASSDT6	 DIM	   6
PASSACCT DIM	   4
PASSCOMP DIM	   1
. 
NAME	 INIT	   "ARFILE.ISI"
OUTPUT	 IFILE	   KEYL=5,UNCOMPRESSED
.
NEWFILE	 IFILE	   KEYL=5,UNCOMPRESSED
NEWFNAME INIT	   "NEWINVCS.ISI"
.
NINVCNO	 DIM	   5
NEWFKEY	 DIM	   5
.
REPLY	 DIM	   1
.FIELD	 TYPE	   LENGTH    FROM-TO   DESCRIPTION
.-----	 ----	   ------    -------   -----------
WORK01	 DIM	    1
WORK02	 DIM	    2
WORK03	 DIM	    3
WORK04	 DIM	    4
WORK08	 DIM	    8
WORK10	 DIM	   10
WORK09	 DIM	   9
FIELD09	 DIM	   7
NFIELD09 FORM	   7
.
DATE8	 DIM	   8		8 DIGIT	DATE WORK FIELD
MM	 DIM	   2
DD	 DIM	   2
YY	 DIM	   2
XMM	 DIM	   2
XDD	 DIM	   2
XYY	 DIM	   2
FORM5	 FORM	   5
. 
DTYPE	 DIM	   8
DSTAT	 DIM	   10
INDX	 FORM	   2
STARTKEY DIM	   5
PP	 DIM	   1
PC	 DIM	   1
LINES	 FORM	   2
PTERMS	 DIM	   18
HDRSW	 DIM	   1
PAGES	 FORM	   2
. 
	 INCLUDE   PART.TXT
. 
. CONTROL FILE AREA
. 
	 INCLUDE   CONTFILE.TXT
. 
NFIELD01 FORM	    4		       (NUMERIC	WORK FIELD)
FIELD01	 DIM	    4
. 
NFLD01	 FORM	   9
FLD01	 DIM	   9
. 
MULTIFLG DIM	   1
MULTIROW FORM	   2
MULTICNT FORM	   2
MULTIDSC DIM	   25
MD1	 DIM	   25
MD2	 DIM	   25
MD3	 DIM	   25
MD4	 DIM	   25
MD5	 DIM	   25
MD6	 DIM	   25
MD7	 DIM	   25
MD8	 DIM	   25
MD9	 DIM	   25
MD10	 DIM	   25
EXTRA	 INIT	   "XXXXXXXXXXXXXXXXXXX"
COMPLETE DIM	   1
UOM	 DIM	   2
PRODCODE DIM	   2
SHIPNAME DIM	   30
SHIPADDR DIM	   25
SHIPYADR DIM	   25
SHIPADR2 DIM	   25
SHIPCITY DIM	   18
SHIPSTAT DIM	   2
SHIPZIP	 DIM	   5
COMPLET	 DIM	   1
. 
NORMTERM INIT	   "2% 15 NET 30"
NEWTERM	 DIM	   15
. ACCOUNT FILE READ/WORK AREA
. 
CUSTFILE IFILE	   KEYL=4,UNCOMPRESSED
CSTNAME	 INIT	   "ACCOUNTS.ISI"
. 
	 INCLUDE   CUSTMAST.TXT
.
CLOSESW	 INIT	   " "
. 
PORTXX	 DIM	   12
. 
ADDKEY	 FORM	   5	       INCREMENT WORK FIELD FOR	NUMBERS
. 
.  WORK	 AREAS FOR INVOICE HEADER CREATION/WRITE
. 
	 INCLUDE   RCVBLES.TXT
. 
SAVE	 DIM	     10
TOTDOLS	 FORM	     6.2
FRGHT	 FORM	     2.2
. 
OLDDET	 DIM	   1
IOSW	 DIM	   1
LIBNET	 FORM	   7.2
REPNET	 FORM	   7.2
. 
+...........................................................
. 
DATE2X	 DIM	   2
DATE2N	 FORM	   2
. 
. 
INVCKEGS FORM	   7	 (ORDER	QTY)
PCSSOLD	 FORM	   7	 (SHIPPED QTY)
INVPCSKG FORM	   7	 (BACKORDER QTY)
. 
BUILDPT	 DIM	   7
. 
. RECORD ASSEMBLY AREA
.	    INVOICE DETAIL DETAIL RECORD = 45 BYTES
. 
INVCNO	 DIM	   5		       INVC NUMBER
INVCLINE DIM	   2		       INVC LINE NUMBER	SUFFIX FOR INDEXING
. 
.  NOTE	THAT INVCLINE WILL BE A	2 DIGIT	NUMERIC	SUFFIX
.  COUNTER-CREATED IN THIS PROGRAM
. 
INVLINES FORM	   "00"
IRECCNTR FORM	   6
DTLSTAT	 DIM	   1
ILIVCNTR FORM	   3
. 
ROW	 FORM	   2		    USED TO ROLL DOWN THE KEYIN	STMT
. 
NFIELD04 FORM	    5.2		       (NUMERIC	WORK FIELD)
FIELD04	 DIM	    8	      11- 18   PRICE LN	1		  5.2
NFIELD03 FORM	    3
FIELD03	 DIM	    3
NFIELD02 FORM	    4
FIELD02	 DIM	    4
. 
. EXTENSION CALCULATE WORK/DISPLAY AREA
. 
LNDOLS	 FORM	   5.2
KEGQTY	 FORM	   9
LINECOST FORM	   6.2
KGSAVAIL FORM	   6
CRAPMSG	 DIM	   18
CALCFLD	 FORM	   8.3
OLDFRGT	 FORM	   5.2
. 
FILLER	 INIT	   "........./........./........./........"
FILLER2	 INIT	   "..............."
.
FORM3	 FORM	   3
IPNSTDSX DIM	   6
IPNSTDSN FORM	   6
NONSTDSW DIM	   1
SCANSW	 DIM	   1
PARTUPSW DIM	   1
. 
KREPLY	 DIM	   1
. 
CHKPASS	 DIM	   5
.  
WEIGHT	 FORM	   6
HDRCHGSW DIM	   1
. 
INVDTKEY DIM	   7
. 
OLDXTEND FORM	   6.2
. 
. 
CALCNET	 FORM	   6.2
FRTALLOW FORM	   5.2
. 
. NEW DIRECT-ACCESS FILE ADDED HERE TO ALLOW WRITING THE ENTIRE	INVOICE
. TO ONE FILE FOR BALANCING AND	REVIEW/CORRECTION MODE
. 
TIMEDIM	 DIM	   8
DAFILE	 FILE	   FIX=128,UNCOMPRESSED
DANAME	 INIT	   "DAFILE.TXT"
. 
DARECNO	 FORM	   "0000"
REC0	 FORM	   "0000"
REC1	 FORM  	   "0001"
REC2	 FORM	   "0002"
RECZERO	 FORM	   "0000"
RECCNTR	 FORM	   4
RECLIMIT FORM	   "0099"
CUSTN	 FORM	   4
SPECMSG	 DIM	   80
MSGFLAG	 DIM	   1
PORTX	 DIM	   2
SPECSW	 DIM	   1
TIME	 INIT	   "HH/MM/SS"
. 
SEQ	 FORM	   "-1"
. 
INSERTFL IFILE	    KEYL=10,UNCOMPRESSED
INSERTNM INIT	   "ARFILEXX.ISI"   (KEY	IS ACCT,INVTYPE,INVC#)
INSERTKY DIM	   10
. 
ARFILEDT IFILE	   KEYL=11,UNCOMPRESSED
ARFLDTNM INIT	   "ARFILEDT.ISI"
ARFLDTKY DIM	   11
. 
. 
+........................................................................

	 INCLUDE   CALLERR1.INC
 
         MOVE      "ENTER AND PRINT INVOICES" TO TITLTEXT
 
         INCLUDE   CALLERR2.INC

. 
START	 DISPLAY   *ES,*P01:12,*HON,"INVOICE/CREDIT DATA ENTRY PROGRAM":
		   *HOFF:
		       *P01:14,"IF YOU HAVE COME HERE ERRONEOUSLY FROM THE ":
			       "SYSTEM MENU PROGRAMS":
		       *P01:15,"AND DO	N O T  HAVE ANY	 INVOICES TO ":
			       "ENTER........":
		       *P01:17,"YOU MAY	TERMINATE THIS PROGRAM BY PRESSING ":
			       "THE   F5   KEY"
	 KEYIN	   *P01:20,*HON,"KEY	 F5	TO  TERMINATE":
		   *P01:21,"KEY	    ENTER  TO  ADD INVOICES/CREDITS ",REPLY:
		   *HOFF
. 
.
	 CLOCK	   DATE	 TO  TODAY
	 KEYIN	   *ES,*P1:4,*HON,"I N V O I C E   P R O C E S S I N G":
		       *P1:6,"ALIGN THE	  T O P	 OF THE	PRINTER	RIBBON ":
			     "CARTRIDGE":
		       *P1:8,"E	V E N  WITH THE	INVOICE	PERFORATION ":
		       *P1:10,"PRESS   (F3)   WHEN READY ",REPLY,*HOFF
	 GOTO	   MAINLINE IF NOT F3
. 
MAINLIN1 DISPLAY   *HOFF,*ES,*P01:01,"PREPARING	DATA FILES FOR ENTRY"
	 MOVE	   " ",REPLY
	 MOVE	   " ",MSGFLAG
	 MOVE	   " ",SPECMSG
	 GOTO	   OPENFLS
. 
. 
MESSAGES KEYIN	   *ES,*P01:02,*HON,"WOULD YOU LIKE A SPECIAL MESSAGE TO AP":
			       "PEAR":
		       *P01:03," IN THE	BODY OF	ALL INVOICES TODAY ?   ":
		       " (Y/N)	",REPLY,*HOFF
	 CMATCH	   "N",REPLY
	 GOTO	   OPENFLS IF EQUAL
	 CMATCH	   "Y",REPLY
	 GOTO	   GIMMEMSG IF EQUAL
	 BEEP
	 MOVE	   " ",REPLY
	 GOTO	   MESSAGES
. 
GIMMEMSG DISPLAY   *P01:05,"PLEASE ENTER  T O D	A Y `S	SPECIAL	MESSAGE	":
			   "BELOW  (FREE FORM TO 80 CHARACTERS)"
	 KEYIN	   *P01:07,SPECMSG
	 MOVE	   "Y",MSGFLAG
	 DISPLAY   *P01:09,"YOUR MESSAGE IS:",*P01:10,SPECMSG
	 MOVE	   " ",REPLY
	 KEYIN	   *P01:11,"IS THAT WHAT YOU WANT ?  (Y/N) ",REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   OPENFLS IF EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   MESSAGES IF EQUAL
	 BEEP
	 GOTO	   MESSAGES
. 
. FILE OPENING SEQUENCE
OPENFLS
...	   DISPLAY   *P1:23,*EL,"OPENING DAFILE.TXT",*W3
 
...	   TRAP	     NOFILE  IF	IO
...	   PREPARE   DAFILE,DANAME
 
	 DISPLAY   *P1:23,*EL,"OPENING ARFILE.ISI",*W3
	 OPEN	   OUTPUT,NAME
 
	 DISPLAY   *P1:23,*EL,"OPENING ARFILE.ISI",*W3
	 CALL	   OPENRCVB
.
	 DISPLAY   *P1:23,*EL,"OPENING ARFILEXX.ISI",*W3
	 OPEN	   INSERTFL,INSERTNM
.
	 DISPLAY   *P1:23,*EL,"OPENING ARFILEDT.ISI",*W3
 
	 OPEN	   ARFILEDT,ARFLDTNM
	 DISPLAY   *P1:23,*EL,"ALL A/R FILES OPEN ",*W2,*P1:23,*EL
.
	 DISPLAY    *P1:23,*EL,"OPENING	NEWINVCS.ISI",*W2,*P1:23,*EL
	 OPEN	   NEWFILE,NEWFNAME
. 
	 MOVE	   TODAY TO MM
	 MOVE	   TODAY TO XMM
	 RESET	   TODAY TO 3
	 MOVE	   TODAY TO DD
	 MOVE	   TODAY TO XDD
	 RESET	   TODAY TO 5
	 MOVE	   TODAY TO YY
	 MOVE	   TODAY TO XYY
	 RESET	   TODAY TO 1
. 
. SET UP SIX DIGIT DATE
.
	 DISPLAY   *P1:23,*EL,"TODAY=",TODAY,*W
 
	 MOVE	   TODAY TO DATE8
	 MOVE	   TODAY TO INVDATE
	 MOVE	   INVDATE  TO	PASSDT6	     PASS 6 DIGIT DATE TO DTL PGM
	 GOTO	   ACCTOPEN
.
NOFILE	 DISPLAY   *ES,"INVOICE	FILES NOT ON LINE":
		   *N,"JOB IS ABORTING"
	 BEEP
	 CHAIN	   "APPINVC1"	     GO	BACK TO	DISPLAY	INVOICE	MENU
*...........................................................
. OPEN ISAM-LOOKUP FILE
.
ACCTOPEN TRAPCLR   IO
	 TRAP	   NOCUSTFL IF IO
	 CALL	   OPENCUST
	 CMATCH	   "H"	TO  IOSW
	 GOTO	   NOCUSTFL IF NOT EQUAL
	 TRAPCLR   IO
	 TRAP	   NOCTRLS IF IO
	 CALL	   OPENCTLS
	 CMATCH	   "H"	  TO IOSW
	 GOTO	   NOCTRLS IF NOT EQUAL
	 TRAPCLR   IO
. 
. NOTE OPEN STATEMENTS BELOW WILL BE REQUIRED IF INVENTORY SYSTEM 
. IS EVER INVOKED BY CLIENT
. 
........ TRAP	   NODETS  IF  IO
........ OPEN	   OUTPUT2,INVDTLF    (OPEN THE	DETAIL FILE ISAM)
........ INCLUDE   OPENPART
........ CALL	   OPENCOMM
........ CALL	   USAGOPEN
. 
. ........................................................................
. NOW SET UP TO	EITHER OPEN OR PREPARE A NEW DATA ENTRY	BATCH FILE
. WHOSE	NAME IS	IN THE FORM OF DAINVC##/TXT:NCLACCTS, WHERE ## IS A
. REPLACED BY THE PORT NUMBER (HIGH ORDER ZERO FILLED) IN ORDER	TO PERMIT
. BOTH TERMINALS TO DO DATA ENTRY
. 
. ........................................................................
. 
.
PREPDA	 CLOCK	   TIME	 TO  TIMEDIM
	 PREPARE   DAFILE,DANAME
.
ZERONETS TRAPCLR   IO		     (OKAY NOW LETS GO!)
	 MOVE	   "A" TO INVCOMP
	 GOTO	   WHATKIND
. 
NOCTRLS	 DISPLAY   *ES," CONTROLS  FILE	IS NOT ON-LINE",*W,*W
	 BEEP
. 
MNUCHAIN CHAIN	   "APPINVC1"
. 
NOCUSTFL DISPLAY   *ES,"INVENTORY FILE IS NOT ON-LINE, ABORTING"
	 BEEP
	 GOTO	   MNUCHAIN
. 
. INSURE THAT ONLY A SECURITY "9" PERSON   
.	 (AND THAT`S ONLY THE AUTHOR)
. CAN ENTER MANUAL INVOICE NUMBERS!
. 
WHATKIND COMPARE   "9" TO  SECURITY
	 GOTO	   NEWONES  ( IF NOT EQUAL )
. 
	 KEYIN	   *ES,*P01:12,"ARE YOU	ENTERING OLD INVOICES? ":
			       " Y or N	",REPLY
	 CMATCH	   "Y" TO REPLY
	 GOTO	   OLDONES IF EQUAL
	 CMATCH	   "N" TO REPLY
	 GOTO	   NEWONES IF EQUAL
	 BEEP
	 GOTO	   WHATKIND
OLDONES	 KEYIN	   *P01:12,*EL,"SINCE THESE ARE	OLD INVOICES YOU MUST ":
		   "ENTER THEIR	NUMBER AND DATE	- (hit enter)",REPLY
	 MOVE	   "O" TO TESTONE
	 GOTO	   DISPLAY
NEWONES	 KEYIN	   *P01:12,*HON,*EL,"COMPUTER WILL ASSIGN INVOICE NUMBER  ":
			   "(tap enter)",REPLY,*HOFF
	 MOVE	   "N" TO TESTONE
*...........................................................
. INITIAL SCREEN DISPLAY SEQUENCE
.
DISPLAY	 CLOCK	   TIME	TO TIME
. 
	 DISPLAY   *ES,*HON,*P11:1,"I N	V O I C	E / C R	E D I T	  D A T	A":
		   "   E N T R Y     ":
		   *HOFF:
		   *P11:2,"(I)nvoice or	(C)redit?  I	   ":
		   *P43:2,"Document Number A_____":
		   *P1:3,"Sold to  ",*P43:3,"Ship to ":
		   *P1:4,"Acct No",*P3:5,"____":
		   *HOFF:
		   *P11:7,"P.O.	______________":
		   *P51:7,"P.O.		      ":
		   *HON:
		   *P1:8,"PO NUMBER	 SHIPPED  WGHT CTNS  SHIP VIA":
		    "		 ":
		   *P54:8,"TERMS	    ",*P71:8,"COMP/PART",*HOFF
LOOP	 TRAPCLR   PARITY	(NOP)
. 
ORDERFOR MOVE	   "A" TO INVCOMP
	 GOTO	   K01
. 
MOVREP	 MOVE	   "APP" TO CTLKEY
	 GOTO	   K01K
. 
*...........................................................
. 
.  SET NORMAL INVOICE CONSTANTS	PRIOR TO DATA ENTRY
. 
............................................................
K01	 MOVE	   "O"	TO  INVSTAT
	 MOVE	   INVCOMP  TO	PASSCOMP
	 MOVE	   "I"	TO  INVTYPE
	 MOVE	   "C"	TO  INVFOB
	 MOVE	   " "	TO  INVCHECK
	 MOVE	   " "	TO  INVMESG
	 CLEAR	   INVORDNO
	 MOVE	   " "	TO  MOREORDR
	 MOVE	   "UPS		 "  TO	INVSHIP
	 DISPLAY   *P36:9,INVSHIP
	 MOVE	   "0"	    TO	 INVWGHT
	 MOVE	   "0"	    TO	 CARTONS
	 MOVE	   "000000.00"	 TO   INVTOTL
	 MOVE	   "000000.00"	 TO   IAMTRCVD
	 MOVE	   "0.00" TO LINEPRC
	 MOVE	   "0.00" TO LINECOST
	 MOVE	   "0.00" TO LNDOLS
	 MOVE	   "C"	    TO	 COMPLET
	 DISPLAY   *P73:9,"(",COMPLET,")"
. KEYIN	VARIABLES SEQUENCE
. 
INVTYPE	 DISPLAY   *P35:2,INVTYPE
	 KEYIN	   *P35:2,*RV,INVTYPE
	 CMATCH	   "I" TO INVTYPE
	 GOTO	   K01KA  IF EQUAL
	 CMATCH	   "C" TO INVTYPE
	 GOTO	   K01KA  IF EQUAL
	 BEEP
	 MOVE	   " " TO INVTYPE
	 GOTO	   INVTYPE
.
*...........................................................
. ACCOUNT ID NUMBER
. THIS FIELD IS	"BOTH" REQUIRED	& FILL-CONTROLLED
. FIELD	TYPE IS	NUMERIC
. FIELD	VALIDATION TYPE	IS ISAM-VERIFICATION
.
. 
K01KA	 MOVE	   INVCOMP TO PASSCOMP
	 MOVE	   "APP" TO CTLKEY
	 CMATCH	   "A" TO INVCOMP
	 GOTO	   MOVREP IF EQUAL
K01K	 KEYIN	   *P3:5,"____":
		   *P3:5,*DE,WORK04
	 CMATCH	   " " TO WORK04
	 GOTO	   K01K	IF EOS
K01A	 CLEAR	   INVCUST
	 MOVE	   WORK04 TO INVCUST
K01B	 RESET	   INVCUST TO  4      (FILL-CONTROLLED)
	 GOTO	   K01K	IF EOS
	 RESET	   INVCUST
	 MOVE	   INVCUST TO CUSTKEY
K01C	 DISPLAY   *P3:5,INVCUST
	 CALL	   GETCUST
	 CMATCH	   "H"	TO  IOSW
	 GOTO	   VCUST    IF	EQUAL
	 BEEP
	 DISPLAY   *P10:23,"INVALID ACCOUNT NUMBER - PLEASE RETRY ",*W,*W:
		   *P10:23,*EL
	 GOTO	   K01K
. 
.  NOW DISPLAY THE VENDOR DATA FOR THE OPERATOR
. 
VCUST	 MOVE	   CUSTKEY TO CUSTN
. 
OKAYCOMP DISPLAY   *P11:3,CUSTNAME:
		   *P11:4,CUSTADDR:
		   *P11:5,CUSTCITY,*P32:5,CUSTSTAT,*P35:5,CUSTZIP
. 
	 MOVE	   CUSTNAME TO SHIPNAME
	 MOVE	   CUSTADDR TO SHIPADDR
	 MOVE	   CUSTCITY TO SHIPCITY
	 MOVE	   CUSTSTAT TO SHIPSTAT
	 MOVE	   CUSTZIP  TO SHIPZIP
	 MOVE	   "			     " TO SHIPADR2
	 REP	   " 0"	IN SHIPZIP
. 
. NOW DISPLAY THE SHIP-TO DATA
. 
	 DISPLAY   *P51:3,SHIPNAME:
		   *P51:4,SHIPADDR:
		   *P51:5,SHIPADR2:
		   *P51:6,SHIPCITY:
		   *P72:6,SHIPSTAT:
		   *P75:6,SHIPZIP
. 
	 CMATCH	   "D",CUSTSTTS
	 GOTO	   DISPDELT IF EQUAL
. 
ASKCUST	 KEYIN	   *P01:23,*EL,"CORRECT	ACCOUNT	?  (Y/N) ",REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   PASSNAME  IF	 EQUAL
	 BEEP
	 DISPLAY   *P01:23,*EL
	 GOTO	   K01K
. 
INCOMPAT BEEP
	 DISPLAY   *ES,*P01:12,"ACCOUNT	",CUSTKEY," DOES NOT BELONG TO ":
			       "COMPANY	",CTLKEY:
		       *P01:14,"..CORRECT YOUR ERROR PLEASE! ",*W5
	 BEEP
	 GOTO	   DISPLAY	  (BACK	TO ORDERFOR ENTRY!)
. 
DISPDELT BEEP
	 DISPLAY   *ES,*P01:12,"THAT ACCOUNT IS	DELETED/INACTIVE!":
		   *N,"YOU MAY NOT INVOICE HIM UNTIL YOU UPDATE	HIS ":
		     "CUSTOMER MASTER RECORD",*W,*W
	 CHAIN	   "APPINVC1"
. 
PASSNAME MOVE	   CUSTNAME TO PASSCUST
	 MOVE	   CUSTKEY  TO PASSACCT	  PASS THE ACCT	NO. FOR	$ UPDATE
* ***********************************
. 
. NOW ALLOW CHANGES TO SHIP-TO DATA
. 
	 KEYIN	   *P51:3,*RV,SHIPNAME
	 DISPLAY   *P51:3,SHIPNAME
. 
	 CLEAR	   SHIPYADR
	 KEYIN	   *P51:4,SHIPYADR
	 RESET	   SHIPYADR
	 GOTO	   K03 IF EOS
	 MOVE	   SHIPYADR TO SHIPADDR
	 DISPLAY   *P51:4,SHIPADDR
	 KEYIN	   *P51:5,*RV,SHIPADR2
	 DISPLAY   *P51:5,SHIPADR2
	 KEYIN	   *P51:6,*RV,SHIPCITY
	 DISPLAY   *P51:6,SHIPCITY
	 KEYIN	   *P72:6,*RV,SHIPSTAT
	 DISPLAY   *P72:6,SHIPSTAT
	 KEYIN	   *P75:6,*RV,SHIPZIP
	 DISPLAY   *P75:6,SHIPZIP
* ************************************
K03	 DISPLAY   *P01:23,*EL
*...........................................................
. CUSTOMER ORDER NUMBER
. NO EDITING WILL BE PERFORMED ON THIS FIELD
.
	 TRAPCLR   PARITY
K03K	 KEYIN	   *P16:7,"______________":
		   *P16:7,INVORDNO
. 
	 RESET	   INVORDNO TO 1
	 GOTO	   K03K	IF EOS
. 
.
. DISPLAY IN THREE PLACES.
	 DISPLAY   *P16:7,INVORDNO,*P56:7,INVORDNO,*P1:9,INVORDNO
	 DISPLAY   *P15:9,TODAY,*P36:9,INVSHIP
. 
KLOC	 CMATCH	   "*" TO INVORDNO
	 GOTO	   K03KSPC IF NOT EQUAL
KLOCA	 REPLACE   "* "	  IN INVORDNO
	 KEYIN	   *P01:23,"Enter Additional Customer P.O. Numbers: ",MOREORDR
	 CMATCH	   " " TO  MOREORDR
	 GOTO	   KLOCA IF EOS
	 GOTO	   K040K
K03KSPC	 MATCH	   "SPECIAL"  TO  INVORDNO
	 GOTO	   K040K      IF  NOT  EQUAL
SPCKEY	 KEYIN	   *P01:22,"Describe the SPECIAL charges (up to	50 chars)":
		   *P01:23,INVMESG
	 CMATCH	   " "	TO  INVMESG
	 GOTO	   SPCKEY   IF	EOS
	 MOVE	   "S"	    TO	INVTYPE	       MAKE IT A SPECIAL
*...........................................................
K040K	 GOTO	   K08
	 KEYIN	   *P20:10,INVBLNO
	 CMATCH	   "<"	   TO INVBLNO
	 GOTO	   K03	IF EQUAL
K040A	 DISPLAY   *P20:10,INVBLNO
. 
.
*.................................
K04	 TRAPCLR   PARITY
. FOB OR COLLECT
. FIELD	VALIDATION TYPE	IS TABLE-LOOKUP
.
K07K	 KEYIN	   *P35:10,INVFOB
	 CMATCH	   "<" TO INVFOB
	 GOTO	   K07K	 IF EOS
	 GOTO	   K040K IF EQUAL
	 DISPLAY   *P35:10,INVFOB
*.................................
. FIELD	MUST BE	IN TABLE
..................................
	 CMATCH	   "P" TO INVFOB
	 GOTO	   K08 IF EQUAL
	 CMATCH	   "C" TO INVFOB
	 GOTO	   K08 IF EQUAL
	 BEEP
	 GOTO	   K07K
. 
K09	 DISPLAY   *P36:9,INVSHIP
	 KEYIN	   *P36:9,*RV,INVSHIP
	 CMATCH	   "<"	TO  INVSHIP 
	 GOTO	   K040K IF EQUAL
	 DISPLAY   *P36:9,INVSHIP
	 GOTO	   TERMSIN
. 
K08	 MOVE	   "1" TO INVWGHT
    	 DISPLAY   *P25:9,INVWGHT
	 KEYIN	   *P25:9,*RV,INVWGHT
	 DISPLAY   *P25:9,INVWGHT
. 
K081	 MOVE	   "1" TO CARTONS
	 DISPLAY   *P31:9,CARTONS
	 KEYIN	   *P31:9,*RV,CARTONS
	 DISPLAY   *P31:9,CARTONS
	 GOTO	   K09
. 
TERMSIN	 DISPLAY   *P51:9,NORMTERM
	 MOVE	   NORMTERM TO NEWTERM
	 KEYIN	   *P51:9,*RV,NEWTERM
. 
COMPLETE DISPLAY   *P73:9,"(",COMPLET,")"
	 KEYIN	   *P74:9,*RV,COMPLET
	 CMATCH	   "C" TO COMPLET
	 GOTO	   COK IF EQUAL
	 CMATCH	   "P" TO COMPLET
	 GOTO	   COK IF EQUAL
	 BEEP
	 GOTO	   COMPLETE
. 
WRITE	 CMATCH	   "O" TO TESTONE
	 GOTO	   PDT IF NOT EQUAL
	 KEYIN	   *P29:17,"OPERATOR",*P46:17,"____",*P46:17,*DE,INVKEY
	 CMATCH	   " " TO INVKEY
	 GOTO	   WRITE IF EOS
	 MOVE	   INVKEY   TO	FORM5
	 MOVE	   FORM5    TO	INVKEY
. 
PDT	 DISPLAY   *P01:24,*EL
	 KEYIN	   *P19:19,"(mmddyy) IS: ______",*P32:19,*DE,INVDATE
	 CMATCH	   " " TO INVDATE
	 GOTO	   PDT IF EOS
. 
DTKY	 MOVE	   " " TO REPLY
	 BEEP
	 KEYIN	   *P01:24,"IS THE DATE	CORRECT	? (Y/N)	",REPLY
	 CMATCH	   "Y" TO REPLY
	 GOTO	   PDT IF NOT EQUAL
	 DISPLAY   *P01:24,*EL
	 MOVE	   " " TO REPLY
. 
. ALWAYS INPUT THE INVOICE DATE
. 
	 RESET	   INVDATE  TO	6
	 GOTO	   PDT	IF  EOS
	 RESET	   INVDATE  TO	6
	 LENSET	   INVDATE
	 RESET	   INVDATE  TO	1    (LENGTH=6,	POINT AT 1)
. 
. NOW CHECK IF YOU WANT	TO INPUT THE DOLLARS
. 
COK	 MOVE	   PASSDT6 TO INVDATE
	 COMPARE   "9" TO SECURITY
	 GOTO	   COKI	IF NOT EQUAL
. 
COKI	 CMATCH	   "S"	TO  INVTYPE
	 GOTO	   ORDDOLS  IF	EQUAL
	 CMATCH	   "O"	TO  TESTONE
	 GOTO	   GETNUMB  IF	NOT  EQUAL
. 
. FALLTHRU INDICATES OLD INVOICE NO.
. SO CHECK IF WE WANT TO INPUT THE DOLLARS
. 
ORDDOLS	 KEYIN	   *P05:21,"INVOICE NET	DOLLARS	 _________",*P26:21,TOTDOLS
	 MOVE	   TOTDOLS  TO	INVTOTL
	 DISPLAY   *P26:21,INVTOTL
	 CMATCH	   "O"	    TO	TESTONE
	 GOTO	   W1  IF   EQUAL
. 
. FALLTHRU INDICATES A NEW INVOICE, SPECIAL OR MERCHANDISE
. 
GETNUMB	 TRAPCLR   PARITY	     (WE ALREADY READ THE CONTROL FILE!)
. 
	 FILEPI	   19;CONTROLS
. 
	 MOVE	   "APP" TO CTLKEY
	 CALL	   GETCTL
	 MOVE	   CTLINVC TO ADDKEY
	 ADD	   "1"	   TO ADDKEY
	 MOVE	   TODAY   TO CTLDATE
	 MOVE	   ADDKEY   TO	CTLINVC
	 CALL	   PUTCTL		   (UPDATE FILE, RESERVING NUMBER)
. 
. STOP THE PI HERE
. 
	 FILEPI	  0
	 MOVE	   ADDKEY   TO INVKEY
	 REP	   " 0"	    IN INVKEY
	 DISPLAY   *P60:2,*HON,INVKEY,*HOFF
	 MOVE	   " " TO SPECSW
. 
. NEW 4/23/83	FOR APP
. 
	 GOTO	   W1
..........................................
. 
. CHECK	TO SEE IF THIS IS A SPECIAL INVOICE!
.	IF SO, DONT EVEN ASK FOR THE SPECIAL MESSAGE ENTRY
.	AS IT OVERRIDES	THE DEFINITION OF THE SPECIAL CHARGE
.	WHICH HAS ALREADY BEEN STORED IN FIELD	- INVMESG -
. 
	 CMATCH	   "S",INVTYPE
	 GOTO	   OVRSPEC  IF NOT EQUAL
	 MOVE	   "X" TO SPECSW
	 GOTO	   W1
. 
. NOW ASK IF THEY WANT TO OVERRIDE THE SPECIAL MESSAGE
. JUST FOR THIS	CUSTOMER
. 
OVRSPEC	 KEYIN	  *P01:23,"OVERRIDE SPECIAL MESSAGE ? (Y/N) ",REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   ASKSPEC IF EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   W1 IF EQUAL
	 BEEP
	 MOVE	   " ",REPLY
	 GOTO	   OVRSPEC 
ASKSPEC	 KEYIN	   *P01:23,*EL,*P01:23,"ENTER THE MESSAGE HERE !",*W2:
		   *P01:23,*EL,*P01:24,INVMESG
	 DISPLAY   *P01:21,INVMESG,*P01:23,*EL
	 MOVE	   "X"	TO  SPECSW
. UPDATE THE CONTROL FILE LATER
. 
*...........................................................
. DISK-WRITE SEQUENCE
.
W1	 TRAPCLR   IO
	 MOVE	   "0000"  TO RECCNTR
	 TRAP	   BADDSK IF IO
	 MOVE	   INVKEY    TO	  INVCNUMB
	 MOVE	   CUSTNAME  TO	  PASSCUST
	 MOVE	   INVTOTL   TO	  IDOLSLFT
. 
. CHECK	FOR OVERRIDE ON	SPECIAL	MESSAGE
. 
	 CMATCH	   "X",SPECSW
	 GOTO	   MOVESPEC IF NOT EQUAL
	 MOVE	   " " TO SPECSW
	 GOTO	   W1A
. 
MOVESPEC MOVE	   SPECMSG   TO	  INVMESG
. 
. NOW WRITE TO THE NEW DA FILE FOR BATCH REVIEW/CORRECTION CAPABILITY
. 
W1A	 TRAPCLR   IO
	 TRAPCLR   FORMAT
	 TRAPCLR   RANGE
	 TRAPCLR   PARITY
	 TRAP	   BADDSK IF IO
	 TRAP	   BUMFMT IF FORMAT
	 WRITE	   DAFILE,REC1;INVKEY:
		   INVTYPE:
		   INVCOMP:
		   INVSTAT:
		   INVCUST:
       		   INVDATE:
		   INVTOTL:
		   INVORDNO:
		   MOREORDR:
		   INVSHIP:
		   INVBLNO:
		   INVFOB:
		   FRGTRATE:
		   INVWGHT:
		   INVDTPAY:
		   INVCHECK:
		   IAMTRCVD:
		   IDOLSLFT:
		   INVMESG:
		   RECCNTR:	     (TOTAL RECORD COUNTER)
		   RECCNTR	     (LIVE RECORD COUNTER)
. 
..............	8/10/85
.
	 MOVE	   REC1	TO DARECNO
.
..............	8/10/85
.
. NOW WRITE AN END-OF-FILE AT SECTOR 1 (IE REC #2)
.
...............	      WEOF	DAFILE,REC2
...............	      MOVE	REC2 TO	DARECNO
...............	      SUBTRACT	"1" FROM DARECNO
.
	 GOTO	   EOJ
.
BUMFMT	 BEEP
	 DISPLAY   *P1:23,"FORMAT ERROR	ON WRITE TO DAFILE"
	 GOTO	   EOJ
.
. 
BADDSK	 BEEP
	 DISPLAY   *ES,*P01:01,"BAD WRITE TO DISK, RECORD ZERO ON ":
			       "INVOICE	DA FILE":
		       *P01:03,"UNRECOVERABLE ERROR...CALL MICRO ":
			       "SYSTEMS	CONSULTANTS",*W2
	 GOTO	   BADDSK
. 
. ISAM DUPLICATE KEY TRAP
.
DUPKEY	 BEEP
	 DISPLAY   *P01:23,"KEY	",INVKEY," ALREADY EXISTS! ":
			   "ASSIGN A NEW ONE!",*W,*W,*W
	 GOTO	   WRITE
*...........................................................
. END OF JOB SEQUENCE
.
EOJ	 CMATCH	   "O"	TO  TESTONE
	 GOTO	   EOJ1	IF  EQUAL     (DONT ADD	AN OLD SPECIAL TO $TOTS)
. 
. FALLTHRU MEANS ITS NEW!
. 
	 CMATCH	   "S"	TO  INVTYPE
	 GOTO	   EOJ2	IF  NOT	EQUAL	(ITS NORMAL MERCHANDISE	INVOICE)
	 GOTO	   UPDTSEQ
. 
. EOJ2 IS ONLY FOR NEW REGULAR MERCHANDISE INVOICES
. 
EOJ2	 CALL	   PUTCTL
	 GOTO	   CHAINDET
. 
. EOJ1 IS FOR EITHER OLD MERCHANDISE OR	OLD SPECIAL INVOICES
. 
EOJ1	 CALL	   PUTCTL		    (ALWAYS WRITE IT BACK REGARDLESS)
	 CMATCH	   "S"	TO  INVTYPE	  (WAS IT AN OLD SPECIAL?)
	 GOTO	   KEYMOR   IF	EQUAL	  (YES..BYPASS DETAIL ENTRY OPTION)
. 
. FALLTHRU IS AN OLD INVOICE
.  DETAIL OPTION WAS PROVIDED JUST FOR NOVEMBER	1979 ENTRY
. 
KEYOLD	 DISPLAY   *P01:23,*EL
	 KEYIN	   *P01:23,"DO YOU WISH	TO ENTER DETAILS FOR THIS ":
		   "INVOICE ? (DEPLETE INVENTORY) Y or N ",OLDDET
	 CMATCH	   "Y"	TO  OLDDET
	 GOTO	   CHAINDET  IF	 EQUAL
	 CMATCH	   "N"	TO  OLDDET
	 GOTO	   KEYMOR    IF	 EQUAL
	 BEEP
	 GOTO	   KEYOLD
KEYMOR	 DISPLAY   *P01:23,*EL
	 KEYIN	   *P01:23,"ANY	MORE SPECIAL INVOICES TO ADD TO	THE ":
			   "INVOICE HEADER FILE? ",REPLY
	 CMATCH	   "Y" TO REPLY
	 GOTO	   WHATKIND IF EQUAL
	 CMATCH	   "N" TO REPLY
	 GOTO	   INDXIT IF EQUAL
	 BEEP
	 GOTO	   KEYMOR
. 
EOJOUT	 RELEASE
	 CHAIN	   "APPINVC1"
.
	 STOP
.
. FILE OPENING SEQUENCE
CHAINDET MOVE	   " "	TO  KREPLY
	 MOVE	   "0001" TO RECCNTR
	 MOVE	   INVCNUMB TO CHKPASS
	 RESET	   CHKPASS TO 5
	 CMATCH	   " " TO CHKPASS
	 GOTO	   CONTIN IF NOT EOS
. 
BADINVC	 BEEP
	 DISPLAY   *ES,*P01:12,"SORRY..YOU HAVE	NOT GIVEN ME A INVOICE NUMBER":
		       *P01:14,"  I CANNOT ALLOW YOU TO	CONTINUE",*W,*W
	 BEEP
	 GOTO	   MAINLINE
. 
CONTIN	 RESET	   CHKPASS
	 TYPE	   CHKPASS
	 GOTO	   BADINVC IF NOT EQUAL
	 GOTO	   HSKPG1
. 
HSKPG1	 TRAPCLR   IO
	 MOVE	   "00"	TO INVLINES
	 MOVE	   INVCNUMB TO INVKEY
	 MOVE	   INVCNUMB TO INVCNO
	 MOVE	   PASSDT6  TO	DATE2X
TYPIT	 TYPE	   DATE2X
	 GOTO	   DATEOK  IF  EQUAL
	 BEEP
KEYIT	 KEYIN	   *ES,*P01:12,"ENTER THE MONTH	AS TWO DIGITS,	SUCH AS	 09 ":
		   "FOR	SEPT. ",*DE,DATE2X
	 GOTO	   TYPIT
. 
DATEOK	 MOVE	   DATE2X  TO  DATE2N
	 COMPARE   "0"	   TO  DATE2N
	 GOTO	   KEYIT   IF  EQUAL
	 GOTO	   KEYIT   IF  LESS
	 COMPARE   "13"	   TO  DATE2N
	 GOTO	   KEYIT   IF  NOT  LESS
. 
.  GOT A GOOD MONTH THERE
. 
	 MOVE	   DATE2N  TO  USAGINDX
. 
. USAGE	IS NOW SET UP FOR UPDATE
. 
*...........................................................
. INITIAL SCREEN DISPLAY SEQUENCE
.
RECYCLE	 MOVE	   "10"	TO ROW
	 DISPLAY   *P01:10,*EF:
		   *HON:
		   "QTY	ORD  QTY SHP  QTY B/O  PART NUMBER		":
		   "PROD  PRICE	 UOM EXTENSION	",*HOFF
LOOP01	KEYIN	  *P01:23,*EL,*HON,"Press ENTER	to add data, F5	if ":
			  "done	",REPLY,*HOFF
	 GOTO	   BIGEOJ  IF  F5
	 CMATCH	   " " TO REPLY
	 GOTO	   LOOP01 IF NOT EOS
	 DISPLAY   *P01:23,*EL
*...........................................................
. KEYIN	VARIABLES SEQUENCE
	 ADD	   "01"	TO ROW
	 COMPARE   "21"	TO ROW
	 GOTO	   RECYCLE IF NOT LESS
. 
	 DISPLAY   *P01:ROW,"_______  _______  _______	":
			    "_________________________	  ":
			    "________ __  _________  "
. 
*...........................................................
. PART NO. 1
. THIS FIELD IS	"BOTH" REQUIRED	& FILL-CONTROLLED
. FIELD	TYPE IS	NUMERIC
.
	 MOVE	   "0" TO LNDOLS
	 MOVE	   "0" TO KEGQTY
	 MOVE	   "0" TO PCSSOLD
	 MOVE	   "0" TO INVCKEGS
	 MOVE	   "0" TO INVPCSKG
	 MOVE	   "0" TO LINECOST
*...........................................................
. QUANTITY ORDERED	     4/23/83
. FIELD	TYPE IS	NUMERIC
.
B03	 MOVE	   FIELD03 TO SAVE
B03K	 KEYIN	   *P01:ROW,"_______  ":
		   *P01:ROW,WORK09
. 
B03A	 CLEAR	   FIELD09
	 MOVE	   WORK09 TO FIELD09
B03B	 MOVE	   "0" TO NFIELD09
	 TYPE	   FIELD09     (NUMERIC	FIELD)
	 GOTO	   B03K	IF NOT EQUAL
. 
.  BEEP	IT HERE	TO WAKE	UP THE OPERATOR
. 
	 MOVE	   FIELD09 TO NFIELD09
B03C	 MOVE	   NFIELD09 TO FIELD09
	 DISPLAY   *P01:ROW,FIELD09
. 
	 MOVE	   NFIELD09 TO INVCKEGS
. 
. CHECK	IF ORDER IS COMPLETE AND DISPLAY ACCORDINGLY
. 
	 CMATCH	   "C",COMPLET
	 GOTO	   PCSKEG IF NOT EQUAL
. 
. FALLTHRU MEANS COMPLETE SO DISPLAY ORDER QUANTITY AS SHIPPED QTY
. AND DISPLAY ZERO IN BACKORDER	FIELD
. 
	 DISPLAY   *P10:ROW,INVCKEGS,*P19:ROW,"	     0"
	 MOVE	   INVCKEGS TO PCSSOLD
	 MOVE	   "0"	    TO INVPCSKG	    (ZERO OUT B/O FIELD)
	 GOTO	   GETPARTZ
. ........................................
. 
. NOTE USE INVCKEGS FOR	QUANTITY ORDERED
.      USE PCSSOLD  FOR	QUANTITY SHIPPED
.      USE INVPCSKG FOR	QUANTITY BACKORDERED
. 
. 
PCSKEG	 KEYIN	   *P10:ROW,"_______  ",*P10:ROW,PCSSOLD
. 
	 DISPLAY   *P10:ROW,PCSSOLD
. 
	 MOVE	   INVCKEGS TO KEGQTY
	 SUBTRACT  PCSSOLD  FROM  KEGQTY
	 MOVE	   KEGQTY   TO	  INVPCSKG
	 DISPLAY   *P19:ROW,INVPCSKG
	 GOTO	   GETPARTZ
. 
. 
AHA	 BEEP
	 GOTO	   AHA
. 
*.........................................................
GETPARTZ MOVE	   "0" TO MULTICNT
	 MOVE	   " " TO MULTIFLG
	 DISPLAY   *P1:23,*HON,"PRESS F1 FOR MULTI-LINE	PART NUMBER":
		   *HOFF
	 KEYIN	   *P28:ROW,"_________________________":
		   *P28:ROW,MULTIDSC
	 DISPLAY   *P28:ROW,MULTIDSC
	 MOVE	   MULTIDSC TO MD1
	 MOVE	   "1" TO MULTICNT
	 GOTO	   MULTISET IF F1    (IF F1..THERE IS MULTI-LINE DESCRIPTION)
.
	 GOTO	   B04
. 
*..........................................................
. 
MULTISET MOVE	   "X" TO MULTIFLG
	 BEEP
	 DISPLAY   *P1:23,*EL
	 DISPLAY   *P1:23,*HON,"MULTI-LINE (MAX	10) PART NUMBER",*W2,*HOFF
	 GOTO	   B04
...........................................................
. 
MULTIDSC MOVE	   ROW TO MULTIROW
MULTILP	 ADD	   "1" TO MULTIROW
	 COMPARE   "22"	  TO  MULTIROW
	 CALL	   RESETROW   IF  NOT LESS
	 KEYIN	   *P28:MULTIROW,"_________________________":
		   *P28:MULTIROW,MULTIDSC
	 DISPLAY   *P28:MULTIROW,MULTIDSC
	 RESET	   MULTIDSC
	 GOTO	   B04 IF EOS	    (IF	NOTHING	ENTERED...FORGET IT!)
	 ADD	   "1" TO MULTICNT
	 STORE	   MULTIDSC INTO MULTICNT OF MD1,MD2,MD3,MD4,MD5:
					     MD6,MD7,MD8,MD9,MD10
. 
	 COMPARE   "10"	TO MULTICNT
	 GOTO	   MULTIRTN IF EQUAL
. 
	 GOTO	   MULTILP IF F1
. 
MULTIRTN MOVE	   MULTIROW  TO	 ROW	(LET PROGRAM KNOW!)
	 RETURN			    (NO	MORE!)
. 
..............................................................
. 
RESETROW MOVE	   "10"	TO MULTIROW
RESETLP	 ADD	   "1"	TO MULTIROW
	 COMPARE   "21"	TO MULTIROW
	 GOTO	   RESETOUT IF EQUAL
	 DISPLAY   *P01:MULTIROW,*EL
	 GOTO	   RESETLP
RESETOUT MOVE	   "12"	TO MULTIROW
	 MOVE	   "12"	TO ROW
	 RETURN
. 
. 
EXTEND	 RETURN
WRTHDRIS RETURN
. 
	 INCLUDE   RCVBLSIO.TXT
. 
. 
. FALLTHRU IS NO MORE DESCRIPTIONS, SO GOTO B04
*...........................................................
. PRICE	LN 1
. THIS FIELD IS	REQUIRED
. FIELD	TYPE IS	NUMERIC
. 
B04	 MOVE	   FIELD04 TO SAVE
	 GOTO	   B04K
. 
	 KEYIN	   *P54:ROW,"__",*P54:ROW,PRODCODE
	 DISPLAY   *P54:ROW,PRODCODE
.
B04K	 KEYIN	   *P57:ROW,"________":
		   *P57:ROW,WORK08
	 CMATCH	   "<" TO WORK08
	 GOTO	   B04K	IF EOS
	 CMATCH	   ">" TO WORK08
	 GOTO	   B04A	IF NOT EQUAL
	 MOVE	   SAVE	TO FIELD04
	 DISPLAY   *P57:ROW,FIELD04
	 GOTO	   B04K
B04A	 CLEAR	   FIELD04
	 MOVE	   WORK08 TO FIELD04
B04B	 MOVE	   "0" TO NFIELD04
	 TYPE	   FIELD04     (NUMERIC	FIELD)
	 GOTO	   B04K	IF NOT EQUAL
	 MOVE	   FIELD04 TO NFIELD04
B04C	 MOVE	   NFIELD04 TO FIELD04
	 DISPLAY   *P57:ROW,FIELD04
	 MOVE	   NFIELD04 TO LNDOLS	  (SELLING PRICE/UOM)
. 
. GET THE UNIT OF MEASURE
. 
UOM	 MOVE	   "  "	TO  UOM
	 KEYIN	   *P66:ROW,UOM
. 
	 MATCH	   "GR"	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 MATCH	   "EA"	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 MATCH	   "M "	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 MATCH	   "LT"	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 MATCH	   "LB"	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 MATCH	   "C "	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 MATCH	   "FT"	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 MATCH	   "PK"	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 MATCH	   "BX"	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 MATCH	   "RD"	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 MATCH	   "NC"	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 BEEP
	 DISPLAY   *P66:ROW,"  "
	 GOTO	   UOM
. 
DUOM	 DISPLAY   *P66:ROW,UOM
. 
. 
CHK100X	 MATCH	   "C "	TO UOM
	 GOTO	   CHK1000 IF NOT EQUAL
	 MOVE	   PCSSOLD TO CALCFLD
	 DIVIDE	   "100" INTO CALCFLD
	 MULT	   LNDOLS BY CALCFLD
	 MOVE	   CALCFLD TO LINECOST
	 GOTO	   DSPCOSTX
. 
. 
CHK1000	 MATCH	   "M "	TO UOM
	 GOTO	   EACHX   IF NOT EQUAL
	 MOVE	   PCSSOLD TO CALCFLD
	 DIVIDE	   "1000"  INTO	CALCFLD
	 MULT	   LNDOLS  BY	CALCFLD
	 MOVE	   CALCFLD TO	LINECOST
	 GOTO	   DSPCOSTX
. 
EACHX	 MOVE	   PCSSOLD TO LINECOST
. 
EACHXX	 MULT	   LNDOLS BY LINECOST
. 
DSPCOSTX DISPLAY   *P70:ROW,LINECOST
. 
. FIRST	CHECK IF THE LINE IS OKAY!
. 
CHKLINE	 BEEP
	 MOVE	   " " TO REPLY
	 KEYIN	   *P1:22,*EL,*HON,"IS DATA CORRECT ? (Y/N) ",REPLY,*HOFF
	 DISPLAY   *P1:22,*EF
	 CMATCH	   "Y",REPLY
	 GOTO	   CONTXX IF EQUAL
. 
	 CMATCH	   "N",REPLY
	 GOTO	   LNRESET IF EQUAL
	 BEEP
	 GOTO	   CHKLINE
. 
LNRESET	 DISPLAY   *P1:ROW,*EL
	 SUBTRACT  "1" FROM ROW
	 GOTO	   LOOP01
. 
CONTXX	 ADD	   LINECOST TO TOTDOLS
	 ADD	   LINECOST TO INVTOTL
. 
CHKMULTI CMATCH	   "X",MULTIFLG
	 CALL	   MULTIDSC IF EQUAL	(GET THE EXTRA LINES)
. 
. NOTE IF EXTRA	DESCRIPTIVE LINES EXIST, THEN WRITE CONTINUATION RECORDS
.      FOR DESCRIPTIVE DATA
. 
. 
. .
. 8/8/81  BYPASS ALL UPDATE CODE AT THIS POINT IN TIME UNTIL
.	  IT IS	DETERMINED THAT	THE ENTIRE INVOICE IS CORRECT
.	  AT WHICH TIME	WE WILL	WRITE THE INVOICE HEADER AND
.	  ALL DETAILS FROM THE "DAINVCS/TXT  DA	FILE" TO THE
.	  INVCHDRS/ISI AND INVCDTLS/ISI	FILES, UPDATING	THE
.	  INVENTF/ISI, CUSTMAST/ISI, USAGE/ISI,	CONTROLS/ISI,
.	  COMMODS/ISI, AND MAYBE THE NONSTDS/ISI FILES
. 
	 GOTO	   LINEINCR	    (BYPASS THE	UPDATES	UNTIL LATER!)
*...........................................................
. 
. DISK-WRITE SEQUENCE
.
LINEINCR ADD	   "01"	  TO INVLINES
	 MOVE	   INVLINES  TO	 RECCNTR
	 MOVE	   INVCNO    TO	 INVDTKEY
	 RESET	   INVDTKEY  TO	 5
	 APPEND	   INVLINES  TO	 INVDTKEY
	 RESET	   INVDTKEY
	 REP	   " 0"	IN INVDTKEY
	 MOVE	   "X"	TO  DTLSTAT
	 MOVE	   MD1	TO  MULTIDSC
	 CALL	   PTDTLBK
. 
. NOW CHECK FOR	MULTI-DESCRIPTIVE LINES	AND WRITE MULTIPLE RECORDS NOW!
. 
	 CMATCH	   "X",MULTIFLG
	 GOTO	   COMP99 IF NOT EQUAL
. 
. FALLTHRU MEANS WRITE CONTINUATION RECORDS
. 
. FIRST	ZERO OUT THE QTY AND PRICE FIELDS
. 
	 MOVE	   "0" TO INVCKEGS
	 MOVE	   "0" TO INVPCSKG
	 MOVE	   "0" TO PCSSOLD
	 MOVE	   ".00"  TO LNDOLS
	 MOVE	   ".00"  TO LINECOST
. 
. STORE	LOOP
. 
	 MOVE	   "1"	TO  INDX
. 
STORLOOP ADD	   "1"	TO  INDX    (STARTS AT MD2, INDX VALUE=2)
	 COMPARE   INDX	TO  MULTICNT
	 GOTO	   COMP99 IF  LESS
. 
	 LOAD	   MULTIDSC FROM INDX OF MD1,MD2,MD3,MD4,MD5,MD6:
					 MD7,MD8,MD9,MD10
. 
	 ADD	   "01"	TO INVLINES
	 MOVE	   INVLINES TO RECCNTR
	 RESET	   INVDTKEY TO 5
	 APPEND	   INVLINES TO INVDTKEY
	 RESET	   INVDTKEY TO 1
	 REP	   " 0"	IN INVDTKEY
	 MOVE	   "*"	TO DTLSTAT
	 CALL	   PTDTLBK
	 GOTO	   STORLOOP
. 
............................................
PTDTLBK	 TRAPCLR   IO
	 TRAPCLR   PARITY
	 TRAPCLR   FORMAT
	 TRAPCLR   RANGE
	 MOVE	   "WRITE" TO IOTYPE
	 MOVE	   DANAME  TO IOERFILE
	 TRAP	   STDIOERR IF IO
	 TRAP	   STDFMTER IF FORMAT
	 TRAP	   STDRANGE IF RANGE
	 TRAP	   STDPARER IF PARITY
...............	      MOVE	RECCNTR	TO DARECNO
	 ADD	   "1"	   TO DARECNO
. 
	 WRITE	   DAFILE,DARECNO;INVDTKEY:
		   DTLSTAT:	     TEMPORARY STATUS CODE
		   MULTIDSC:	    PART DESCRIPTION
		   INVCKEGS:	    QTY	ORDERED
		   INVPCSKG:	  QTY BACKORDERED
		   PCSSOLD:	  TOTAL	PIECES SHIPPED
		   LNDOLS:	    SELLING PRICE
		   LINECOST:	    EXTENSION PRICE		    5.2
		   UOM:		    UNIT OF MEASURE
		   PRODCODE:	     PRODUCT CODE
		   FILLER:
		   FILLER2
. 
...............	      ADD	"1" TO DARECNO
...............	      WEOF	DAFILE,DARECNO
. NOW CLEAR ALL	NECESSARY FIELDS
. 
	 MOVE	   "0" TO LINECOST
PTDTLRTN RETURN
....................................................................
. 
COMP99	 COMPARE   "99"	 TO  INVLINES
	 GOTO	   NINENINE  IF	 EQUAL	     (END THIS INVOICE...NOW!)
	 GOTO	   LOOP01
. 
DETIOERR BEEP
	 DISPLAY   *ES,*P01:12,"WRITE ERROR ON APP INVOICE DETAIL FILE":
		       *P01:14,"PROGRAM	ABORTING",*W5
	 BEEP
	 GOTO	   INDXIT
. 
DADTLIO	 BEEP
	 DISPLAY   *ES,*P01:12,"WRITE ERROR ON RECORD ",DARECNO:
			       " OF INVOICE DA FILE":
		       *P01:14,"UNRECOVERABLE ERROR - CALL MICRO SYSTEMS":
			       " CONSULTANTS!"
	 GOTO	   DADTLIO
. 
PUTSTDS	 GOTO	   COMP99
. 
NINENINE BEEP
	 DISPLAY   *P01:23,*EL,"99 ITEMS KEYED FOR  INVC# ",INVCNO:
		     " I AM CLOSING THIS INVOICE",*W,*W,*W
	 BEEP
	 GOTO	   BIGEOJ
. 
NOTFND	 BEEP
	 DISPLAY   *P01:ROW,*EL
	 DISPLAY   *P01:ROW,"INVALID PART NUMBER - PLEASE RESUBMIT",*W2
	 SUBTRACT  "1" FROM ROW
	 GOTO	   LOOP01
. 
*...........................................................
. END OF JOB SEQUENCE
.
BIGEOJ	 TRAPCLR   IO
	 TRAP	   NOINVOIC  IF	IO
	 TRAPCLR   FORMAT
	 TRAP	   STDFMTER  IF	FORMAT
. 
. FIRST	WRITE AN END-OF-FILE MARK ON THE BATCH
. 
	 ADD	    "01" TO DARECNO
	 WEOF	   DAFILE,DARECNO
. 
. 
. NOW SHOW THE SUBTOTAL	AND ENTER THE SHIP/HANDLING AMOUNT
. 
DSPTOTS	 DISPLAY   *P1:21,*EL,*P50:21,"SUBTOTAL",*P70:21,INVTOTL
	 MOVE	   ".00" TO CALCNET
	 DISPLAY   *P1:22,*EL,*P50:22,"SHIP/HAND CHGS",*P70:22,CALCNET
	 KEYIN	   *P1:22,"ENTER SHIPPING AND HANDLING CHARGE":
		   *P70:22,*RV,CALCNET
	 DISPLAY   *P70:22,CALCNET
	 MOVE	   INVTOTL TO LIBNET
	 ADD	   CALCNET TO	LIBNET
	 DISPLAY   *P1:23,*EL,*P50:23,"INVOICE NET",*P69:23,LIBNET
	 KEYIN	   *P1:23,*HON,"TAP ENTER TO PROCEED ",REPLY,*HOFF
	 MOVE	   LIBNET TO INVTOTL
	 MOVE	   LIBNET TO IDOLSLFT
	 MOVE	   INVTOTL TO TOTDOLS
......	 ADD	   LIBNET TO REPNET
	 DISPLAY   *P1:23,"			     "
. 
. TEST FOR NEGATIVE INVOICE.
. 
	 CMATCH	   "I",INVTYPE
	 GOTO	   XCRED IF NOT	EQUAL
. 
	 COMPARE   ".00" TO INVTOTL
	 GOTO	   XCRED IF EQUAL
. 
	 COMPARE   ".01" TO INVTOTL
	 GOTO	   XCRED IF NOT	LESS
	 BEEP
	 DISPLAY   *P1:24,*EL,*HON,"NEGATIVE INVOICE DISALLOWED!",*W4:
		   *P1:24,*EL
	 GOTO	   DSPTOTS
. 
XCRED
................................   CALL	     GTRCZRO
	 MOVE	   LIBNET  TO  INVTOTL
	 MOVE	   INVTOTL TO  IDOLSLFT
. ....	 CALL	   CALCDATA
	 SUBTRACT  "1"	      FROM  RECCNTR
	 MOVE	   RECCNTR    TO    IRECCNTR
	 MOVE	   RECCNTR    TO    ILIVCNTR
	 CALL	   PTRCZRO
.
	 GOTO	   SHOWTOTS	 (DISPLAY FINAL	INVOICE	TOTALS)
. 
. THEN UPDATE RECORD ZERO WITH THE COUNTS PRIOR	TO REVIEW CYCLE
. 
GTRCZRO	 MOVE	   "DAREAD"  TO	IOTYPE
	 READ	   DAFILE,REC1;INVKEY:
				  INVTYPE:
				  INVCOMP:
				  INVSTAT:
				  INVCUST:
				  INVDATE:
				  INVTOTL:
				  INVORDNO:
				  MOREORDR:
				  INVSHIP:
				  INVBLNO:
				  INVFOB:
				  FRGTRATE:
				  INVWGHT:
				  INVDTPAY:
				  INVCHECK:
				  IAMTRCVD:
				  IDOLSLFT:
				  INVMESG:
				  IRECCNTR:
				  ILIVCNTR
. 
	 GOTO	   NOINVOIC  IF	OVER
	 MOVE	   "H"	TO   IOSW
	 RETURN
. 
. ............................................
. 
PTRCZRO	 WRITE	   DAFILE,REC1;INVKEY:
			   INVTYPE:
			   INVCOMP:
			   INVSTAT:
			   INVCUST:
			   INVDATE:
			   INVTOTL:
			   INVORDNO:
			   MOREORDR:
			   INVSHIP:
			   INVBLNO:
			   INVFOB:
			   FRGTRATE:
			   INVWGHT:
			   INVDTPAY:
			   INVCHECK:
			   IAMTRCVD:
			   IDOLSLFT:
			   INVMESG:
			   IRECCNTR:
			   ILIVCNTR
	 RETURN
. .....................................................
. 
SHOWTOTS DISPLAY   *ES,*P01:12,"INVOICE	NUMBER ",INVKEY," TOTAL	NET ":
			       "INVOICE	IS: $",TOTDOLS
CHECKIT	 MOVE	   " ",REPLY
	 BEEP
	 KEYIN	   *P01:16,"IS THIS INVOICE CORRECT ?  (Y/N) ",REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   UPDTSEQ IF EQUAL	 (NOW UPDATE ALL FILES!	7/81)
. 
	 CMATCH	   "N",REPLY
	 GOTO	   REVIEWIT IF EQUAL
. 
	 BEEP
	 GOTO	   CHECKIT
. 
. THE FOLLOWING	ROUTINE	IS USED	TO RETRIEVE ALL	LINE ITEMS
. ON THIS INVOICE AND TO MODIFY	THE STATUS CODE	FROM "X" TO "O"
. 
UPDTSEQ	 TRAPCLR   IO		     (NOW ADD THE UPDATE LOGIC)
. 
. WHAT MUST NOW	BE DONE	IS THE FOLLOWING:
. 
. 1.  WRITE THE	HEADER RECORD TO THE INVOICE MASTER FILE
. 2.  LOOP THROUGH ALL THE DETAIL RECORDS, SELECTING ONLY THOSE
.     WHICH HAVE A STATUS CODE OF "X", AND UPDATE THE INVENTORY	MASTER
.     BY DECREMENTING THE ON-HAND QTY ACCORDING	TO THE PASSCOMP,
.     THEN WRITE A RECORD ISAM TO THE INVOICE DETAIL FILE
. 3.  UPDATE THE CUSTOMER MASTER RECORD	WITH THE INVOICE DATA
. 4.  UPDATE THE COMMODITY RECORD
. 5.  UPDATE THE INVENTORY USAGE HISTORY RECORD
. 
. NOW INCLUDE THE   U P	D T S Q	N C  MODULE  IN-LINE HERE
. 
	 INCLUDE   APPUPDSQ.TXT
. 
. 
. THE FOLLOWING	ROUTINE	IS USED	TO PERMIT REVIEW/CORRECTION/DELETION
. OF DETAIL ITEMS ON THIS INVOICE, IN THE EVENT	THAT THE DATA ENTRY
. CLERK	INDICATES THAT THE INVOICE IS IN ERROR OR IN QUESTION!
. 
. NOTE THAT AN OPTION IS OFFERED WHICH PERMITS THE OPERATOR TO 
. COMPLETELY  C	A N C E	L  THIS	INVOICE	ENTRY,	WHICH WILL CAUSE
. THE INVOICE HEADER TO	BE UPDATED WITH	A "CANCEL CODE".
. THE CANCEL CODE MAY BE USED TO TRIGGER AN EXCEPTION LISTING WITH
. ALL CANCELED INVOICE NUMBERS.
. 
REVIEWIT TRAPCLR   PARITY		 (NOW DISPLAY/MODIFY/CORRECT DATA)
	 DISPLAY   *ES,*P01:02,"THE REVIEW FUNCTION WILL DISPLAY TO YOU	":
			       "ALL THE	DATA YOU HAVE KEYED":
		       *P01:04,"YOU WILL BE ABLE TO MAKE THE FOLLOWING ":
			       "CHANGES............":
		       *P01:06,"     - CANCEL THE INVOICE":
		       *P01:07,"     - CHANGE THE GROSS	WEIGHT":
		       *P01:08,"     - CHANGE THE FREIGHT RATE":
		       *P01:09,"     - ADD    DETAIL LINES":
		       *P01:10,"     - DELETE DETAIL LINES":
		       *P01:11,"     - MODIFY DETAIL LINES"
REVQUES	 KEYIN	   *P01:14,*EF,*P01:14,"DO YOU WISH TO REVIEW (H)eader or ":
			   "(D)etail DATA ?  ",REPLY
	 CMATCH	   "H",REPLY
	 GOTO	   REVHDRS IF EQUAL
	 CMATCH	   "D",REPLY
.
....	    GOTO      REVWDTLS IF EQUAL
.
	 GOTO	   REVHDRS IF EQUAL
	 BEEP
	 MOVE	   " ",REPLY
	 GOTO	   REVQUES
. 
REVWDTLS TRAPCLR   PARITY
. 
	 DISPLAY   *ES,*P22:01,"INVOICE	DETAIL CORRECTION OPTION":
		       "     Today is ",TODAY
	 CALL	   GTRCZRO
	 MOVE	   IRECCNTR  TO	 RECCNTR
	 ADD	   "1"	     TO	 RECCNTR
. 
REVDTLS	 SUBTRACT  "1"	   FROM	 RECCNTR
	 COMPARE   "0"	     TO	 RECCNTR
	 GOTO	   ADDMORE  IF	EQUAL
. 
	 READ	   DAFILE,RECCNTR;INVDTKEY,DTLSTAT,MULTIDSC,INVCKEGS:
				  INVPCSKG,PCSSOLD,LNDOLS,LINECOST:
				  UOM,PRODCODE,FILLER,FILLER2
	 CMATCH	   "D",DTLSTAT
	 GOTO	   REVDTLS IF EQUAL
. 
. BYPASS PREVIOUSLY DELETED ITEM.....DO	NOT DISPLAY
. 
. CALCULATED THE EXTENDED PRICE	FOR DISPLAY
. 
	 MOVE	   INVCKEGS  TO	 KEGSORD
	 MOVE	   PCSSOLD   TO	 PCSORD
	 MOVE	   LNDOLS    TO	 LINEPRC
. 
. ........THE ABOVE SETS UP THE	EXTEND MODULE
. 
. 
SHOWMODS DISPLAY   *P01:06,*EF:
		   *P01:08,"PART NUMBER",*P20:08,MULTIDSC:
		   *P01:09,"QTY	ORDERED	",*P20:09,INVCKEGS:
		   *P01:10,"QTY	SHIPPED	 ",*P20:10,PCSSOLD:
		   *P01:11,"QTY	BACKORD	 ",*P20:11,INVPCSKG:
		   *P01:12,"PRICE PER UOM",*P20:12,LNDOLS:
		   *P01:13,"EXTENDED PRICE",*P20:13,LINECOST:
		   *P01:14,"PRODUCT CODE",*P20:14,PRODCODE:
		   *P01:15,"UNIT OF MEASURE",*P20:15,UOM
..............................................................
	 MOVE	   LINECOST  TO	 OLDXTEND     (SAVE THE	EXTENDED SELLING PRICE)
. ................................................................
. 
BUMANSR1 MOVE	   " "	TO  REPLY
	 KEYIN	   *P01:24,*EL,"DO YOU WANT TO CHANGE or DELETE	THIS ITEM ? ":
			       "(Y/N) ",REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   KEYDTLMD  IF	 EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   BUMANSR1 IF NOT EQUAL
. 
. FALLTHRU MEANS NO! DONT CHANGE IT OR DELETE IT!
. 
EXPLAIN	 MOVE	   " " TO REPLY
	 KEYIN	   *P01:24,*EL,"YOU MAY	NOT ADD	DETAILS	UNTIL YOU`VE SEEN ALL":
			   " THAT EXIST	- (TAP ENTER) ",REPLY
. 
SEEMRDT	 MOVE	   " " TO REPLY
	 KEYIN	   *P01:24,*EL,"DO YOU WANT TO SEE MORE	DETAILS	? (Y/N)	":
		   REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   REVDTLS IF EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   EXPLAIN IF NOT EQUAL
.
. FALLTHRU MEANS I HAVE	MADE MY	CORRECTIONS AND	WANT TO	REPROVE
. 
	 DISPLAY   *P01:24,*EL,"OKAY....I WILL NOW PROVE THIS INVOICE ",*W2
	 GOTO	   SHOWTOTS
.......................................................................
. 
ADDMORE	 MOVE	   " " TO REPLY
	 BEEP
	 KEYIN	   *P01:24,*EL,"DO YOU WANT TO	 A D D	MORE DETAILS ? (Y/N) ":
		   REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   SETUPADD IF EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   ADDMORE IF NOT EQUAL
	 GOTO	   FINIDTLS
. 
. 
SETUPADD MOVE	   INVLINES TO IRECCNTR
	 MOVE	   INVLINES TO RECCNTR
	 MOVE	   INVLINES TO ILIVCNTR
	 GOTO	   RECYCLE		 (THIS WILL ALLOW ADDS)
. 
. .......................................................................
. 
. R E P	R O V E	.........NOTE THIS MUST	FIRST ACCESS THE EXISTING DETAILS
.			 DO MULTIPLE EXTENSIONS	AND VERIFY THAT	ALL IS OKAY
. 
REPROVE	 RETURN
. 
RECALC	 GOTO	   UPDTSEQ
. 
. ............................................................................
. 
KEYDTLMD MOVE	   " "	TO  REPLY
	 KEYIN	   *P01:24,*EL,"D E L E	T E  THIS ITEM ?  (Y/N)	 ",REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   DELIT IF EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   KD1 IF EQUAL
	 BEEP
	 GOTO	   KEYDTLMD
. 
DELIT	 MOVE	   "D"	TO  DTLSTAT
	 DISPLAY   *P01:24,*EL,"LINE ITEM HAS BEEN DELETED",*W2
	 SUBTRACT  OLDXTEND FROM TOTDOLS      (ADJUST THE TOTALS)
	 MOVE	   TOTDOLS  TO	 INVTOTL
	 MOVE	   TOTDOLS  TO	 IDOLSLFT
	 GOTO	   PUTDTLBK
. 
KD1	 KEYIN	   *P01:24,"PLEASE MAKE	YOUR CHANGES !":
		   *P20:09,*RV,INVCKEGS:
		   *P20:10,*RV,PCSSOLD:
		   *P20:11,*RV,INVPCSKG:
		   *P20:12,*RV,LNDOLS:
		   *P20:14,*RV,PRODCODE:
		   *P20:15,*RV,UOM
. 
	 MOVE	   PCSSOLD   TO	 PCSORD
	 MOVE	   INVCKEGS  TO	 KEGSORD
	 MOVE	   LNDOLS    TO	 LINEPRC
	 MOVE	   "X"	     TO	 DTLSTAT
. 
	 MOVE	   PCSSOLD  TO	CALCNET
	 MULTIPLY  LNDOLS   BY	CALCNET
. 
	 CMATCH	   "C",UOM
	 GOTO	   CHKZM    IF	NOT  EQUAL
	 DIVIDE	   "100"  INTO	CALCNET
	 GOTO	   KD1Z
. 
CHKZM	 CMATCH	   "M",UOM
	 GOTO	   KD1Z	IF  NOT	 EQUAL					      
	 DIVIDE	   "1000" INTO	CALCNET
. 
KD1Z	 MOVE	   CALCNET  TO	LINECOST
. 
. 
. NOW DO A CHECK OF THE	EXTEND CALCULATION   (3/13/82)
. TO SEE IF YOU	HAVE THROWN OFF	THE TOTAL!
. 
	 COMPARE   OLDXTEND TO	LINECOST
	 GOTO	   XTENDOK  IF	EQUAL
. 
. FALLTHRU MEANS THAT A	PRICE WAS CHANGED!  (ie. UNEQUAL EXTENDED LINE PRICES)
. 
	 SUBTRACT  OLDXTEND FROM TOTDOLS
	 ADD	   LINECOST TO	 TOTDOLS
	 MOVE	   TOTDOLS  TO	 INVTOTL
	 MOVE	   TOTDOLS  TO	 IDOLSLFT
. 
. THEREFORE EXECUTE THE	TWO INSTRUCTIONS ABOVE AND FIX THE TOTALS!
. 
XTENDOK	 DISPLAY   *P20:09,INVCKEGS:
		   *P20:10,INVPCSKG:
		   *P20:11,PCSSOLD:
		   *P20:12,LNDOLS:
		   *P20:13,LINECOST
. 
KD2	 MOVE	   " ",REPLY
	 KEYIN	   *P01:24,*EL,"IS THE DATA CORRECT NOW	? (Y/N)	",REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   KD3	IF  EQUAL
	 BEEP
	 DISPLAY   *P01:24,*EL
	 GOTO	   KD1
. 
KD3	 MOVE	   "X"	TO  DTLSTAT	    (MUST CHECK	THE KEGS AVAILABLE)
. 
	 DISPLAY   *P01:24,*EL
PUTDTLBK WRITE	   DAFILE,RECCNTR;INVDTKEY,DTLSTAT,MULTIDSC,INVCKEGS:
				  INVPCSKG,PCSSOLD,LNDOLS,LINECOST:
				  UOM,PRODCODE,FILLER,FILLER2
    	 GOTO	   REVDTLS
. 
FINIDTLS BEEP
	 DISPLAY   *ES,*P01:10,"YOU HAVE SEEN ALL THE CURRENTLY	EXISTING":
			       " DETAILS FOR THIS INVOICE ":
		       *P01:14,"THE INVOICE TOTAL WILL NOW BE COMPLETELY ":
			       "RECALCULATED",*W1
	 GOTO	   SHOWTOTS	  (3/13/82 VICE	RECALC)
. 
. 
. 
REVHDRS	 CALL	   GTRCZRO
. 
HDRMOD	 DISPLAY   *ES,*P20:01,"INVOICE	HEADER CORRECTION":
		       *P10:03,"PLEASE TAP THE ENTER KEY TO TAB	PAST":
		       " ANY DATA WHICH	IS CORRECT":
		       *P17:04,"RE-KEY ANY INCORRECT DATA AND TAP ENTER"
HDRDISP	 MOVE	   "INVOICE"  TO  DTYPE
	 CMATCH	   "C"	TO  INVTYPE
	 GOTO	   TYPEOK   IF	NOT  EQUAL
	 MOVE	   "CREDIT "  TO  DTYPE
TYPEOK	 MOVE	   INVCUST    TO  CUSTKEY
	 CALL	   GETCUST
	 CMATCH	   "H",IOSW
	 GOTO	   GOTCUST  IF	EQUAL
	 MOVE	   "INVALID ACCOUNT NUMBER"  TO	 CUSTNAME
GOTCUST	 MOVE	   "OPEN"   TO	DSTAT
	 CMATCH	   "C"	    TO	INVSTAT
	 GOTO	   DDISP  IF  NOT  EQUAL
	 MOVE	   "CANCELLED"	TO  DSTAT
. 
DDISP	 DISPLAY   *P10:06,DTYPE,"NUMBER  ",*P26:06,INVKEY:
		   *P10:07,"ACCOUNT  ",INVCUST,*P26:07,CUSTNAME:
		   *P10:09,"WEIGHT IS ",*P26:09,INVWGHT:
		   *P10:10,"CARTONS   ",*P26:10,CARTONS:
		   *P10:11,"PO NUMBER ",*P26:11,INVORDNO
. 
. 
. NOW ACCEPT ANY CHANGES
. 
. 
HKEYIN	 KEYIN	   *P26:09,*RV,INVWGHT:
		   *P26:10,*RV,CARTONS:
		   *P26:11,*RV,INVORDNO
. 
	 DISPLAY   *P26:09,INVWGHT:
		   *P26:10,CARTONS:
		   *P26:11,INVORDNO
. 
. DETERMINE IF CHANGES HAVE BEEN MADE TO FRGTRATE OR INVWGHT
. 
	 MOVE	   " "	TO  HDRCHGSW
. 
HKYN1	 MOVE	   " " TO REPLY
	 KEYIN	   *P01:24,"DO YOU WISH	TO CANCEL THIS INVOICE ? ":
			   " (Y/N) ",REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   HDRCNCL IF EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   NXT1	IF EQUAL
	 BEEP
	 GOTO	   HKYN1
. 
HDRCNCL	 MOVE	   "C"	TO  INVSTAT
	 CMATCH	   "L",INVCOMP
	 GOTO	   SUBREP  IF NOT EQUAL
	 SUBTRACT  TOTDOLS FROM	LIBNET
	 GOTO	   HCLZERO
SUBREP	 SUBTRACT  TOTDOLS FROM	REPNET
HCLZERO	 MOVE	   "0"	TO  TOTDOLS
	 MOVE	   "0"	TO  INVTOTL
	 MOVE	   "0"	TO  IDOLSLFT
	 MOVE	   "0"	TO  IRECCNTR
	 MOVE	   "0"	TO  ILIVCNTR
	 MOVE	   "0"	TO  RECCNTR
	 MOVE	   "CANCELED BY	DATA ENTRY OPERATOR" TO	INVMESG
	 CALL	   WRTHDRIS
	 GOTO	   ENDREPLY
. 
. 
. NOTE AT THIS TIME....	I F   WE DONT WANT TO DO MORE
. 
.  WE  C O U L D  EXECUTE A PREPARE, FOLLOWED BY A CLOSE
.  TO PHYSICALLY DELETE	THIS DATA ENTRY	BATCH FILE
. 
. NOTE.....SINCE WE HAVE SAVED THE OLD FREIGHT ALLOWANCE (IF ANY)
.	   AND SINCE WE	ARE USING  *RV	FUNCTION ON THOSE FIELDS
.	   THEN	WE CAN	  I N C	R E M E	N T  THE TOTDOLS
.	   FIELD  BY OLDFRGT  (REMOVING	THE FREIGHT ALLOWANCE)
.	   AND THEN ..... D E C	R E M E	N T  THE TOTDOLS
.	   FIELD  USING	THE CALCDATA ROUTINE........
. 
.  THIS	WILL WORK EVEN IF WE  D	I D N `	T  CHANGE THE VALUES !
. 
. ...............................................................
. NOTE	  THE CALCDATA ROUTINE ACCESSES	 THE  TOTDOLS FIELD
.	  FOR ITS CALCULATIONS....SO ONLY INCREMENT   TOTDOLS
. 
. NOW DETERMINE	IF THE HEADER VALUES HAD BEEN CHANGED!
. 
NXT1	 GOTO	   NOCHNGS
NOCHNGS	 CALL	   PTRCZRO	   (WRITE IT NOW NOT BEFORE 3/13)
	 GOTO	   SHOWTOTS	   (GO BACK AND	VIEW THE RESULTS !)
. 
. NOW UPDATE ALL NECESSARY FILES WITH THE DOLLAR INFORMATION
. 
CUSTUPDS MOVE	   PASSACCT  TO	 CUSTKEY
	 CALL	   GETCUST
	 CMATCH	   "H"	     TO	 IOSW
	 GOTO	   ADDCUST   IF	 EQUAL
	 BEEP
	 DISPLAY   *P01:24,"CANNOT FIND	THE CUSTOMER RECORD TO UPDATE SALES"
	 GOTO	   CTRLUPDT
ADDCUST	 ADD	   TOTDOLS   TO	 CURNTDUE
	 MOVE	   TOTDOLS   TO	 LSTSALPR
	 MOVE	   INVDATE   TO	 LSTSALDT
	 CALL	   PUTCUST
. 
. CUSTOMER HAS BEEN UPDATED WITH DOLLAR	INFORMATION
. 
CTRLUPDT CMATCH	   "A"	     TO	 PASSCOMP
	 GOTO	   REPCTLS   IF	 NOT  EQUAL
	 MOVE	   "APP"     TO	 CTLKEY
.
PROCCTL	 TRAPCLR   IO
	 TRAP	   NOCTLSFD IF IO
	 CMOVE	   "M" TO IOSW
	 FILEPI	   19;CONTROLS
	 CALL	   GETCTL
	 CMATCH	   "H"	     TO	 IOSW
	 GOTO	   NOCTLSFD  IF	 NOT EQUAL
.
	 ADD	   TOTDOLS   TO	 CTLSOLD
	 MOVE	   TODAY     TO	 CTLDATE
	 CMOVE	   "M",IOSW
	 CALL	   PUTCTL
	 FILEPI	   0
.
	 CMATCH	   "H",IOSW
	 GOTO	   BUMCTLWT IF NOT EQUAL
. .
. CONTROLS HAVE	BEEN UPDATED
	 GOTO	   ENDREPLY
.
BUMCTLWT BEEP
	 DISPLAY   *P1:23,*EL,"UNABLE TO UPDATE	CONTROLS FILE",*W3
	 DISPLAY   *P1:23,*EL
	 GOTO	   ENDREPLY
. 
. 
NOCTLSFD BEEP
	 DISPLAY   *P01:24,"NO CONTROLS	RECORD ...... WHATS HAPPENING?"
	 GOTO	   NOCTLSFD
. 
. 
REPCTLS	 MOVE	   "APP"     TO	 CTLKEY
	 GOTO	   PROCCTL
. 
ENDREPLY BEEP
. 
. NOW DISPLAY SAME AS SHOWTOTS PARAGRAPH BUT NO	MORE RETRIES
. 
	 DISPLAY   *ES,*P01:12,"INVOICE	NUMBER ",INVKEY," TOTAL	NET ":
			       "INVOICE	IS: $",TOTDOLS:
		       *P01:14,"NET INCLUDES A DEDUCTED	FREIGHT	ALLOWANCE":
			       " OF $",FRTALLOW
. 
	 MOVE	   "00"	 TO  INVLINES
	 MOVE	   "0"	 TO  TOTDOLS
	 MOVE	   "0"	 TO  FRTALLOW
. 
. 
.  5/18/83 - MODIFICATION:
. 
. SINCE	INVOICE	IS COMPLETE AND	TROUBLES HAVE BEEN EXPERIENCED WITH
. THE RETENTION	OF THE DATA ENTRY FILE,	PURGE IT HERE AND REOPEN IT,
. IF NECESSARY FOR NEW DATA
. 
. ALSO CLEAR THE MULTIPLE DESCRIPTION FIELD
. 
....................	    PREPARE   DAFILE,DANAME
.
	 CLOSE	   DAFILE	     (THIS PURGES/KILLS	THE DATA FILE)
.
. 
CLEARMOV MOVE	   "0" TO MULTICNT
	 MOVE	   "0" TO RECCNTR
	 MOVE	   "0" TO ILIVCNTR
	 MOVE	   "0" TO IRECCNTR
	 MOVE	   " " TO MULTIFLG
	 MOVE	   " " TO MD1
	 MOVE	   " " TO MD2
	 MOVE	   " " TO MD3
	 MOVE	   " " TO MD4
	 MOVE	   " " TO MD5
	 MOVE	   " " TO MD6
	 MOVE	   " " TO MD7
	 MOVE	   " " TO MD8
	 MOVE	   " " TO MD9
	 MOVE	   " " TO MD10
.......................................................................
. 
	 KEYIN	   *P01:16,*EF,"DO YOU HAVE ANY	MORE INVOICES TO ADD":
			       " AT THIS TIME ?	 (Y)es or (N)o	",REPLY
	 CMATCH	   "Y" TO REPLY
	 GOTO	   CHAINHDR IF EQUAL
	 CMATCH	   "N" TO REPLY
	 GOTO	   TAPTERM IF EQUAL
.
	BEEP
	GOTO	   CLEARMOV
.
TAPTERM	 DISPLAY   *ES,*P01:12,"TOTAL INVOICE NET -   ",REPNET
	 BEEP
	 KEYIN	   *P01:20,"PRESS ENTER	TO TERMINATE THIS PROGRAM",KREPLY
. 
	 RELEASE
.
. 
. ............................................................
	 CHAIN	   "APPINVC1"
. 
CHAINHDR PREPARE   DAFILE,DANAME (NOW PREPARE THE DATA FILE AGAIN)
	 GOTO	   WHATKIND	 (GO BACK TO THE INVOICE HEADER	ENTRY)
. 
. 
. 
NOINVOIC  BEEP
	 DISPLAY   *ES,*P01:12,"DISK IO	ERROR ON ORDER TOTAL UPDATE":
		       *N,"SERIOUS PROBLEM, CALL BOB BACHARACH,	MSC"

	 GOTO	   NOINVOIC
. 
. 
	 INCLUDE   CONTRLIO.TXT
. 
. 
	 INCLUDE   CUSTIO.TXT
. 
	 INCLUDE   STDIOERR.TXT
. 
	 INCLUDE   STDFMTER.TXT
. 
	 INCLUDE   STDRANGE.TXT
. 
	 INCLUDE   STDPARER.TXT
. 
	 STOP
