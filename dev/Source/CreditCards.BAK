;=============================================================================;
;       CREATED BY:  CHIRON SOFTWARE & SERVICES, INC.                         ;
;                    4 NORFOLK LANE                                           ;
;                    BETHPAGE, NY  11714                                      ;
;                    (516) 935-0196                                           ;
;=============================================================================;
;                                                                             ;
;  PROGRAM:    CreditCards                                                    ;
;                                                                             ;
;   AUTHOR:    HARRY RATHSAM                                                  ;
;                                                                             ;
;     DATE:    05/07/2015 AT 2:54AM                                           ;
;                                                                             ;
;  PURPOSE:                                                                   ;
;                                                                             ;
; REVISION:    VER     DATE     INIT       DETAILS                            ;
;                                                                             ;
;              1.0   05/07/2015   HOR     INITIAL VERSION                     ;
;                                                                             ;
;                                                                             ;
;=============================================================================;
; Primary Object = Main Object when program first opens
; Secondary Object = Object that gets focus AFTER the record is viewed/created
;
; PrimaryyKey = Main Key used to retrieve the record
;
; PrimaryField = Main Field that is normally used in Primary Index
;
;
$SearchMethod       equ                2                    //1 = All, 2 = Specific
MajorMinorVersion   init               "1.005"

                    include            WORKVAR.INC

                    goto               STARTPGM
                    include            Cust.FD
                    include            Sequence.FD
                    include            CreditCards.FD

MAIN                plform             CreditCard4.PLF
DataMenu            plform             DataMenu.PLF
CustomerID          dim                6

STARTPGM            routine            CustomerID
                    include           SECURITY.INC
                    move              "1",ProgLoaded
...TESTING                    move               "5555      ",CustomerID
;
; If we're unable to find the Help file, then we're going to simply just not make
; the F1 Function key available to the Users
;
                    move              "AppDir;HELPDIR",EnvData
                    clock             INI,EnvData
                    if                NOT OVER
                     pack              EnvData,EnvData,CreditCardsHelp
                     trap              NOHELP IF OBJECT
                     setmode           *HELPLIB=EnvData
                     setmode           *F1HELP=ON
                     trapclr           OBJECT
NOHELP
                     noreturn
                   endif

                    call               OpenAllFiles

CreditCardsEDTColl        collection
CreditCardsResetColl      collection
CreditCardsActive         collection                                                        //Used for Enable/Disable objects only
;
; Includes ALL fields
;
                    listins           CreditCardsEDTColl,ECardNumber,ECVC,ECCName                    //Fields that are edited during New/Update

                    listins           CreditCardsResetColl,ECardNumber,ECVC,ECCName

                    listins           CreditCardsActive,DTExpirationDate,CBCCType

.                    winhide
                    formload          MAIN
                    formload          DataMenu,WMain

                    move               9999,RowSelected
                    clear              LVOptions

                    call              MainReset
                    call               MainValid
                    move               "-1",StartingRow
                    setfocus           LVCreditCards
                    LVCreditCards.SetItemState using *Index=0,*State=03,*StateMask=03
                    LVCreditCards.GetNextItem giving SelectedRow using *Start=StartingRow,*Flags=02
                    LVCreditCards.GetItemParam giving ItemParam using *Index=SelectedRow
                    display            "Item Param = ",ItemParam
                    packkey            CreditCardsKY from $Entity,CustomerID,ItemParam
                    call               RDCreditCards
                    if                 (ReturnFl = 0)
                    call               SetMain
                    endif
                    setfocus           LVCreditCards


                    loop
                      waitevent
                    repeat
;
; We never get here!!   Just in case though.... :-)
;
                    return
;==========================================================================================================
BROWSEFILE          routine
                    %if                $SearchMethod = 1
SEARCH              plform             SEARCHAll.PLF
                    %else
Search              plform             SearchSelection
                    %endif

                    call               OpenAllFiles
                    formload          SEARCH
                    return
;==========================================================================================================
INITSRCH
                    pack              SearchTitle,CreditCardsTitle," Search Window"
                    setprop           WSearch,Title=SearchTitle

                    ctaddcol          "Field 1",100,"Field 2",100

                    %if                $SearchMethod = 1                   //LoadAll
                    call               LoadAllRecords
                    %endif

                    return
;==========================================================================================================
                    %if                $SearchMethod = 1                   //LoadAll
LoadAllRecords
                    move              " ",CreditCardsKY
                    call              RDCreditCards
                    loop
                      call              KSCreditCards
                    until (ReturnFl = 1)
                      CTLoadRecord2     CreditCardsFL,CreditCards,CreditCards.PrimaryField,CreditCards.PrimaryField,CreditCards.SecondaryField
..                    CTLoadRecord2     &File,&Record,&Key,&Field1=,&Field2=
                    repeat
                    LVSearchPLB.SetItemState using *Index=0,*State=03,*StateMask=03
                    setprop                BSearchSelect,default=1
                    setfocus               LVSearchPLB
                    return
                    %endif
;==========================================================================================================
                    %if                $SearchMethod = 2                   //LoadAll
LoadSearchWindow
                    getprop           ESearchName,text=CreditCardsKY
                    SETMODE           *mcursor=*wait

                    COUNT             result,CreditCards
                    IF                (result = 0)
                      SETMODE           *mcursor=*Arrow
                      setprop                BSearchSearch,default=1
                      setfocus                 ESearchName
                      RETURN
                    ENDIF

                    clear             SearchCounter
                    Uppercase         CreditCardsKY
                    LVSearchPLB.DeleteAllItems
                    CALL              RDCreditCards
                    LOOP
                      CALL              KSCreditCards
                    UNTIL             (ReturnFl = 1)
...                      uppercase         CreditCards.PrimaryField
...                      MATCH             CreditCardsKY,CreditCards.PrimaryField
                      IF                not equal
                        SETMODE           *mcursor=*Arrow
                        BREAK
                      ENDIF
                      incr              SearchCounter
...                      CTLoadRecord2     CreditCardsFL,CreditCards,CreditCards.PrimaryField,CreditCards.PrimaryField,CreditCards.SecondaryField
..                    CTLoadRecord2     &File,&Record,&Key,&Field1=,&Field2=
                    REPEAT
                    SETMODE           *mcursor=*Arrow
                    if                (SearchCounter = 0)
                      beep
                      alert                    stop,"No CreditCards Found that match the Search Criteria",result,"NO VENDORS FOUND"
                      ESearchName.SelectAll
                      ESearchName.Clear
                      setprop                BSearchSearch,default=1
                      setfocus                 ESearchName
                    else
                      LVSearchPLB.SetItemState using *Index=0,*State=03,*StateMask=03
                      setprop                BSearchSelect,default=1
                      setfocus               LVSearchPLB
                    endif
                    RETURN
                    %endif
;==========================================================================================================
ItemSelected
                    LVSearchPLB.GetNextItem giving RowSelected using *Flags=02,*Start=FirstRow
                    if                 (RowSelected != -1)
                      LVSearchPLB.GetItemText giving $SearchKey using *Index=RowSelected,*SubItem=0
                      MOVE               "Y",PassedVar
                      DESTROY            WSearch
                    endif
                    RETURN
;==========================================================================================================
OnClickCreditCards
..                    F2SEARCH          LVCreditCards
..                    if                 (PassedVar="Y")
..                      call               MainValid
..                    endif
                    return
;==========================================================================================================
ErrorRoutines
;
; Routines that operate the Main program
;
                    OPEN               ErrorFile,"ErrorLog.PLD"
                    CLOCK              TimeStamp,ErrorDateTime
                    PACK               AlertString FROM "Error : ",FileNameError,AlertReturn:
                                                        "Actual Error : ",S$ERROR$
                    getmode            *ProgName=ProgramName
                    getmode            *ProgStamp=ProgramStamp
                    getmode            *ProgVer=ProgramVer

                    WRITE              ErrorFile,SeqEOF;*CDFON,ErrorDateTime,AlertString,ProgramName,ProgramStamp,ProgramVer,*CDFOFF
                    ALERT              stop,AlertString,result,"ERROR"
                    STOP
;==========================================================================================================
PrintPreviewReport
                    return
;==========================================================================================================
PrintReport
                    RETURN
;==========================================================================================================
NOPRINT
                   TRAPCLR           SPOOL
                   NORETURN
                   RETURN
;==========================================================================================================
MaintMenuOption
                   CALL                 GetMenuName
                   RETURN
;==========================================================================================================
MainValid
                   IF                (RecordStatus = "")
                     LVCreditCards.DeleteAllItems
                     packkey            CreditCardsKY from $Entity,CustomerID
                     call               RDCreditCards
                     loop
                       call               KSCreditCards
                     until (ReturnFl = 1 or  CustomerID != CreditCards.CustomerID)
                       call               InsertIntoListView
                     repeat
;
; OK, we've been able to read the record and now let's show it on the screen.
;
                     MOVE              "F",RecordStatus                   .We've found a record
                     setprop           BMainChange,enabled=1

                     ENABLETOOL        ID_Change
                     EnableDBMenu      MModifyRecord

                     setprop           BMainDelete,enabled=1
                     ENABLETOOL        ID_Delete
                     EnableDBMenu        MDeleteRecord

.                     setprop           LVCreditCards,enabled=0
                     setprop           Fill1,enabled=0
                     SETFOCUS          BMainChange
                   ENDIF
                   RETURN
;==========================================================================================================
InsertIntoListView
TypeDesc            dim                20
CreditCardNo        dim                20
ExpirationDate      dim                12
CardLength          form               2
CreditCard          dim                20
CVC                 dim                4

                    incr               CreditCards.Type
                    load               TypeDesc from CreditCards.Type with "MasterCard","Visa","American Express","Discover Card","Diners Club"
                    unpack             CreditCards.ExpirationDate into yyyy,mm
                    pack               ExpirationDate from mm,"/",yyyy
                    chop               CreditCards.NameOnCard,CreditCards.NameOnCard
                    call               BlockCreditCardInfo

                    PACK              DATALINE FROM TypeDesc,";":
                                                    CreditCard,";":
                                                    ExpirationDate,";":
                                                    CreditCards.NameOnCard

                    LVCreditCards.SetItemTextAll using *Index=SelectedRow,*Text=DataLine,*Delimiter=";",*Param=CreditCards.SeqNo,*Options=LVOptions  //HR 4.4.2016
                    return
;==========================================================================================================
BlockCreditCardInfo
                    chop               CreditCards.Number,CreditCards.Number
                    movelptr           CreditCards.Number,CardLength
                    subtract           "3",CardLength
                    reset              CreditCards.Number,CardLength
                    pack               CreditCard from "*************",CreditCards.Number
                    move               "****",CVC
                    return
;==========================================================================================================
MAININIT
;
; Set the SELECTALL property for the COLLECTION and then take care of
; any ActiveX controls.
; Initialize MAIN Form and setup the Menu's, Fields, Objects, Buttons, etc
;
                   SETPROP           CreditCardsEDTColl,SELECTALL=$SelectAll
.
                   CALL              MainReset
                   RETURN
;==========================================================================================================
MainNew
;
; New Button is pressed
;
                   IF                (RecordStatus = "N")      //Let's 'Save' this NEW Record
                     CALL              ValidateFields     //Validate the data first
;
; Something's not right...Let's just return and wait until all the fields
; have been validated
;
                     IF                (ValidFlag = 1)
                       RETURN
                     ENDIF
;
; Get all of the fields from the Form into the proper RECORD
;
                     CALL              GetMain
                     GetNextSeq        CC
                     move              Sequence.SeqNo,CreditCards.SeqNo
;
; Let's see if SOMEBODY else has entered/used this code before or let's just
; see if this Code already exists in the system
;
                     packkey           CreditCardsKY from $Entity,CustomerID,CreditCards.SeqNo
                     CALL              TSTCreditCards
                     IF                (RETURNFL = 1)
                       CALL              WRTCreditCards
                       call              InsertIntoListView
                       CALL              MAINRESET
                       RETURN
                     ELSE
                       BEEP
                       pack            AlertString from CreditCardsTITLE with code CreditCardsKY already exists. Please enter another code"
                       ALERT           Note,AlertString,result,"ERROR: Record already exists"
                       SETFOCUS        LVCreditCards
                     ENDIF                                //Valid record exists???
                   ELSE
                     CALL              MainReset
                     MOVE              "N",RecordStatus
                     CALL              DisableRecordButtons
;
; Enable all of the EditText fields and set the EditText fields
; to Non Read-Only
;
                     setprop           CreditCardsEDTColl,static=0,bgcolor=$Window
                     setprop           CreditCardsActive,enabled=1

                     setprop           Fill1,enabled=0
;
; Setup any DEFAULT values
;
                     call              LoadDefaultValues
;
; Disable & Enable the proper Buttons along with changing
; the description of the Button's (i.e. Exit --> Change)
;
                     setprop           BMainChange,enabled=0
                     DISABLETOOL       ID_Change
                     DisableDBMenu     MModifyRecord

                     setprop           BMainDelete,enabled=0
                     DISABLETOOL       ID_Delete
                     DisableDBMenu     MDeleteRecord

                     setprop           BMainNEW,title=SaveTitle
                     ENABLETOOL        ID_Save
                     EnableDBMenu      MSaveRecord

                     setprop           BMainCancel,title=CancelTitle
                     ENABLETOOL        ID_Cancel

                     DISABLETOOL       ID_New
                     DisableDBMenu     MNewRecord

;
; Set the Focus to the first field that we're going to be Entering
;
                     SETFOCUS          LVCreditCards
                   ENDIF
                   RETURN
;==========================================================================================================
MainChange
;
; Change/Save Button has been pressed
;
; I'm only getting here if the Change Button has been selected.  Soooooo....Either the
; Change Button has been pressed, or this button now reads 'Save'.  If it reads Save,
; the Status flag will have been set to 1 the first time that this routine has been
; reached.
                   IF                (RecordStatus = "U")                //'Save' button has been pressed
                     CALL              ValidateFields
                     IF                (ValidFlag = 0)           //Great..All fields ARE valid!!!
                       CALL              GETMAIN                 //Get all of the fields
;
; Let's see if SOMEBODY else has entered/used this code before or let's just
; see if this Code already exists in the system
;
                       debug
                       CALL              UPDCreditCards                //Update the record
                       move              "32",LVOptions
                       call              InsertIntoListView
                       CALL              MAINRESET               //Reset the objects & fields
                     ENDIF
                     RETURN                                      //Voila...Either way, we're RETURNING
                   ENDIF
..                   GETCOUNT          LVCreditCards
                     packkey           CreditCardsKY from $Entity,CustomerID,ItemParam
                     CALL              RDCreditCardsLK               //Lock the record so that nobody uses it
;
; Just for arguments sake, let's just make sure that the record hasn't been deleted
; by another user, AND...Let's make sure that it's not being used by another user
; as well!!                                                                                              b8fw56
;
                     IF                (RETURNFL = 1)          //WHAT!!! Somebody deleted this record
                       BEEP
                       ALERT             STOP,"Record deleted by another User!!",RESULT
                       CALL              MAINRESET
                       RETURN
                     else
                       setprop           ECardNumber,text=CreditCards.Number
.                       setprop           ECVC,Password=0
                     ENDIF
;
; Record is locked...Try again later
;
                     IF                (RETURNFL = 2)          //WHAT!!! Somebody's locked the record
                       BEEP
                       ALERT             NOTE,"Record locked by another User..."::
                                               "Try again later",RESULT,LOCKTITLE
                       RETURN
                     ENDIF
;
; OK, OK...We've gotten this far...The record is now locked and we
; can safely change the Status to "Modify"
;
                     MOVE              "U",RecordStatus                  //We've selected the Modify/Change Button
                     CALL              DisableRecordButtons
;
; Not only do we have a good record, but we've been able to 'Lock' the record
; and now we're ready to proceed
;
; Enable the Entire Collection of EditText fields as well as setting the
; background colors and making them Non Read-Only
;
                     setprop           Fill1,enabled=0

                     SETPROP           CreditCardsEDTColl,static=0,bgcolor=$Window
                     setprop           CreditCardsActive,enabled=1

;
; OK, OK...What do we do with any ActiveX components. We've got to handle
; them as well.  Let's change these to Non Read-Only and change the
; Background colors as well
;
; Change the Cancel button button to 'Save' and the 'Exit' button to Cancel
;
                     setprop           BMainCancel,title=CancelTitle
                     ENABLETOOL        ID_Cancel

                     setprop           BMainCHANGE,title=SaveTitle
                     ENABLETOOL        ID_Save
                     EnableDBMenu      MSaveRecord

                     ENABLETOOL        ID_Undo
                     DISABLETOOL       ID_Change
                     DisableDBMenu     MModifyRecord

                     DISABLETOOL       ID_New
                     setprop           BMainNew,enabled=0
                     DisableDBMenu     MDeleteRecord

..                     SETFOCUS          SecondaryObject               //Set the cursor to the next field
                     debug
                     setprop           LVCreditCards,enabled=0
                   RETURN
;==========================================================================================================
MainUndo                             // Routine to read the First record and display it
;
; If I've click on the Undo/Reset button, I've already got the 'Key' based on
; the fact that I'm changing a record that already exists and I loaded the
; key the first time.  Soooooo....Simply 'Re-read' the record, Calll the
; SetMain routine and Voila!!!!
;
                   BEEP
                   ALERT             PLAIN,"Revert back to the 'Original' record",RESULT:
                                           SureTitle
                   IF                (RESULT = 1)
                     CALL              RDCreditCardsLK
                     CALL              SetMain
                   ENDIF
                   RETURN
;==========================================================================================================
MainFind
...                   FindSearch        LVCreditCards
                   IF                (PassedVar = "Y")
..                     getprop           LVCreditCards,text=CreditCardsKY
                     MOVE              $SearchKey,CreditCardsKY
                     CALL              RDCreditCards
;
; We've got a record thanks to our Trusy Search/Browse window. Let's
; continue now by setting up the proper Code field and calling the
; MainValid subroutine, that will take care of it for us.
;
                     clear             RecordStatus
..                     setprop           LVCreditCards,text=CreditCards.PrimaryField
                     CALL              MainValid
                   else
                     setfocus          LVCreditCards
                   ENDIF
                   RETURN
;==========================================================================================================
MainFirst                            // Routine to read the First record and display it
;
                   CLEAR             CreditCardsKY
                   FILL              FirstASCII,CreditCardsKY
                   CALL              RDCreditCards
                   IF                (RETURNFL = 1)  . We didn't find a 'Blank' record
                     CALL              KSCreditCards         . Try the 'next' record
                     IF                (RETURNFL = 1)  . There are no records in the file
                       BEEP
                       ALERT             STOP,"No records exist in the system...",RESULT:
                                              FirstTitle
                       RETURN
                     ENDIF
                   ENDIF
;
; We've got a record (either on the READ or the READKS.  Let's now continue
; processing as if we just lost the Focus of the main field.  By calling the
; MainValid subroutine, that will take care of it for us.
;
                   clear               RecordStatus
..                   setprop           LVCreditCards,text=CreditCards.PrimaryField
                   CALL              MainValid
                   RETURN
;==========================================================================================================
MainLast                             // Routine to read the Last record and display it
;
                   CLEAR             CreditCardsKY
                   FILL              LastASCII,CreditCardsKY
                   CALL              RDCreditCards
                   IF                (RETURNFL = 1)  . We didn't find a 'Blank' record
                     CALL              KPCreditCards         . Try the 'Previous' record
                     IF                (RETURNFL = 1)  . There are no records in the file
                       BEEP
                       ALERT             STOP,"No records exist in the system...",RESULT:
                                              LastTitle
                       RETURN
                     ENDIF
                   ENDIF
;
; We've got a record (either on the READ or the READKP.  Let's now continue
; processing as if we just lost the Focus of the main field.  By calling the
; MainValid subroutine, that will take care of it for us.

                   clear               RecordStatus
..                   setprop           LVCreditCards,text=CreditCards.PrimaryField
                   CALL              MainValid
                   RETURN
;==========================================================================================================
MainNext                             // Routine to read the Next record and display it
;
; We can't just do a simple READKS/READKP because of certain conditions including
; 'Attempting' to read past the last record (Next --> EOF) and the reverse
; condition.  Due to this fact, we need to get the current code, and THEN
; do a READKS/READKP
;
..                   GETCOUNT          LVCreditCards
                   IF                (CharCount <> 0)
..                     getprop           LVCreditCards,text=CreditCardsKY
                     CALL              RDCreditCards
                   ENDIF

                   CALL              KSCreditCards         . Try the 'next' record
                   IF                (RETURNFL = 1)  . There are no records in the file
                     BEEP
                     ALERT             STOP,"End of file has been reached.",RESULT:
                                            NextTitle
                     RETURN
                   ENDIF
;
; We've got a record (either on the READ or the READKS.  Let's now continue
; processing as if we just lost the Focus of the main field.  By calling the
; MainValid subroutine, that will take care of it for us.
;
                   clear               RecordStatus
...                   setprop           LVCreditCards,text=CreditCards.PrimaryField
                   CALL              MainValid
                   RETURN
;==========================================================================================================
MainPrevious                         //  Routine to read the Previous record and display it
;
; We can't just do a simple 'READKS' because of certain conditions including
; 'Attempting' to read past the last record (Next --> EOF) and the reverse
; condition.  Due to this fact, we need to get the current code, and THEN
; do a READKS/READKP
;
..                   GETCOUNT          LVCreditCards
                   IF                (CharCount <> 0)
..                     getprop           LVCreditCards,text=CreditCardsKY
                     CALL              RDCreditCards
                   ENDIF

                   CALL              KPCreditCards         . Try the 'Previous' record
                   IF                (RETURNFL = 1)  . There are no records in the file
                     BEEP
                     ALERT             STOP,"Beginning of file has been reached...",RESULT:
                                            PrevTitle
                     RETURN
                   ENDIF

. We've got a record (either on the READ or the READKS.  Let's now continue
. processing as if we just lost the Focus of the main field.  By calling the
. MainValid subroutine, that will take care of it for us.

                   clear               RecordStatus
..                   setprop           LVCreditCards,text=CreditCards.PrimaryField
                   CALL              MainValid
                   RETURN
;==========================================================================================================
SaveMode                            // Save has been selected from the MENU vs. the Button
;
; OK...The 'Save' has been selected from the Menu rather than from the Save Button.
; What to do, What to do??  Is this a Save to a 'NEW' record or is it a Save to a
; 'CHANGED' record....
; Let's check the 'Status' flag...1 is a Change record, 2 is a New Record
;
                   BRANCH            Status,MainChange,MainNew
                   RETURN
;==========================================================================================================
ValidateFields                       // Routine to Validate the data from the Form
;
;  If we find an error below, set the ValidFlag = 1 and return
;
                   MOVE              "0",ValidFlag

; Everything's OK...Let's just return because the ValidFlag will be set to
; Zero from the top of this routine.
;
                   MOVE              "0",ValidFlag
                   RETURN
;==========================================================================================================
MainReset                            // Routine to 'Reset' everything which includes the Button's, Objects, fields, etc.
;
                   clear               RecordStatus       //reset the status to not updating
                   unlock            CreditCardsfl
;
; Reset the fields to 'Blank' and DISABLE all of those fields as well
;
                   clear             CreditCards
                   call              setmain

                   setprop           Fill1,enabled=1
;
; Reset the Buttons for the Next record
;
                   DISABLETOOL       ID_Change
                   DisableDBMenu     MModifyRecord

                   setprop           BMainDelete,enabled=0
                   DISABLETOOL       ID_Delete
                   DisableDBMenu     MDeleteRecord

                   setprop           BMainCHANGE,title=ChangeTitle,enabled=0

                   setprop           BMainNEW,title=NewTitle,enabled=1
                   ENABLETOOL        ID_New
                   EnableDBMenu      MNewRecord

                   setprop           BMainCancel,title=ExitTitle

                   DISABLETOOL       ID_Save
                   DISABLETOOL       ID_Undo
                   DISABLETOOL       ID_Cancel

                   DisableDBMenu     MSaveRecord

                   CALL              EnableRecordButtons
;
; Setup the Primary field that is used for Entry purposes
;
                   setprop           CreditCardsActive,enabled=0
                   setprop           CreditCardsEDTColl,static=1,BGColor=$BtnFace,text=""
                   SETPROP           LVCreditCards,enabled=1
                   SETFOCUS          LVCreditCards
                   RETURN
;==========================================================================================================
MainClose                              // Close Button has been Clicked
;
; Only display this message if I'm in either the Modify or New mode.  If not,
; simply exit the program and proceed as normal
;
                    if                (RecordStatus = "U" and RecordStatus = "N")
                      beep
                      alert              PLAIN,"By Exiting the program now, your operation will be Cancelled?",RESULT:
                                               "Are you sure?"
                      return           if (Result != 1)
                    endif
                    destroy            WMain                              //Get rid of the Bank Window
                    winshow
                    return
.=============================================================================
MainCancel                             // Cancel button has been pressed
;
                    if                (RecordStatus = "")                      // They want to exit the program
                      destroy         WMain                             // Get rid of the Main Window
                      winshow
                      return
                    else
                      if                (RecordStatus = "N" OR RecordStatus = "U")  . Change/New Mode
                        beep
                        alert              PLAIN,"Do you wish to cancel this operation?",RETURNFL:
                                                 "Are you sure?"
                        call               MainReset if (ReturnFl = 1)
                      else
                        call              MainReset
                      endif
                    endif
                    return
;==========================================================================================================
MainDelete                             // Delete Button has been Pressed
;
                    LVCreditCards.GetItemParam giving ItemParam using *Index=SelectedRow
                    packkey            CreditCardsKY from $Entity,CustomerID,ItemParam
                    call               RDCreditCards
                    if                 (ReturnFl = 1)
                      beep
                      alert              note,"Unable to locate the Credit Card record to delete",result,"ERROR: RECORD NOT FOUND"
                    else
                      pack             AlertString from "Do you wish to Delete the ",CreditCardsTitle,": ",CreditCards.Type,"?"
                      BEEP
                      ALERT            PLAIN,AlertString,AlertResult,DelTitle
                      IF               (AlertResult = 1)
                        CALL             DELCreditCards
                        pack             AlertString from "CreditCardsx has been deleted"
                        ALERT            NOTE,AlertString,AlertResult,DelOKTitle
                        LVCreditCards.DeleteItem
                        clear          RecordStatus
                        CALL             MAINRESET
                        call           MainValid
                      ENDIF
                    endif
                   RETURN
;==========================================================================================================
SetMain                                // Setup all of the fields in the Form based upon the data record

                   setprop            ECardNumber,text=CreditCard
                   setprop            DTExpirationDate,text=CreditCards.ExpirationDate
                   setprop            ECVC,text=CVC
                   setprop            ECCName,text=CreditCards.NameOnCard
                   CBCCType.SetCurSel using CreditCards.Type
                   setprop            CBPrimary,value=CreditCards.Primary
                   RETURN
;==========================================================================================================
GetMain                                // Retrieve all of the fields in the Form based upon the data record
                    move               $Entity,CreditCards.Entity
                    move               CustomerID,CreditCards.CustomerID
                    getprop            ECardNumber,text=CreditCards.Number
                    getprop            DTExpirationDate,text=CreditCards.ExpirationDate
                    getprop            ECVC,text=CreditCards.CVC
                    getprop            ECCName,text=CreditCards.NameOnCard
                    CBCCType.GetCurSel giving CreditCards.Type
                    getprop            CBPrimary,value=CreditCards.Primary
                    return
;==========================================================================================================
OnClickLVCreditCard
                    eventinfo          0,result=SelectedRow
.                    LVCreditCards.GetNextItem giving SelectedRow using *Start=StartingRow,*Flags=02
                    LVCreditCards.GetItemParam giving ItemParam using *Index=SelectedRow
                    display            "Item Param = ",ItemParam
                    packkey            CreditCardsKY from $Entity,CustomerID,ItemParam
                    call               RDCreditCards
                    call               BlockCreditCardInfo
                    call               SetMain

                    return
;==========================================================================================================
MainHelp
;
; Help Menu selection if required
;
                   RETURN
;==========================================================================================================
mainHelpContents
;
;Help Menu to bring up the Contents of the help file
;
                   RETURN
;==========================================================================================================
MainHelpSearch
;
; Help Menu to Search the help file
;
                   RETURN
;==========================================================================================================
onClickMainWinChangeMenu
                   PERFORM           RESULT OF  MAINNEW,MAINCHANGE,MainDelete,SAVEMODE
                   RETURN
;==========================================================================================================
onClickMainWinExitButton
;
; check to see if this is masquerading as a CANCEL button
;
                   CALL              MainCancel
                   RETURN
;==========================================================================================================
onClickMainWinFileMenu
;
; process a click on the file menu
;
                   PERFORM           result of MainPrint,,MAINCLOSE
                   RETURN
;==========================================================================================================
onClickMainWinHelpMenu
                   PERFORM           result of MAINHELP,MAINHELP,MAINAbout
                   RETURN
;==========================================================================================================
MainAbout                              // Display the Standard "About Box"
;
; Display an alert box describing the program
;
                   getmode           *ProgName=ProgramName
                   getmode           *ProgStamp=ProgramStamp
                   getmode           *ProgVer=ProgramVer
                   CALL              About using ProgramName,ProgramStamp,ProgramVer,MajorMinorVersion

                   SETFOCUS          WMain
                   RETURN
;==========================================================================================================
MainAboutChiron                        // Display the Standard "About Chiron"
;
; display an alert box describing the program
;
                   CALL              AboutChiron
                   SETFOCUS          WMain
                   RETURN
;==========================================================================================================
MainPrint                              // Print Report option
;
                   CALL              PrintReport
                   RETURN
;==========================================================================================================
MainPrintPreview
                   call              PrintPreviewReport
                   RETURN
;==========================================================================================================
MainToolBar
                   RETURN
;==========================================================================================================
DisableRecordButtons
;
; Disable the 'Record' Buttons because we're in the middle of Updating or
; Creating a New record.
;
                   DISABLETOOL       ID_First
                   DISABLETOOL       ID_Next
                   DISABLETOOL       ID_Previous
                   DISABLETOOL       ID_Last
                   DISABLETOOL       ID_Find

                   DisableGoToMenu   MFirst
                   DisableGoToMenu   MNext
                   DisableGoToMenu   MPrevious
                   DisableGoToMenu   MLast
                   DisableGoToMenu   MSearch
                   RETURN
;==========================================================================================================
EnableRecordButtons
;
; Enable the 'Record' Buttons because we're in the middle of Updating or
; Creating a New record.
;
                   ENABLETOOL        ID_First
                   ENABLETOOL        ID_Next
                   ENABLETOOL        ID_Previous
                   ENABLETOOL        ID_Last
                   ENABLETOOL        ID_Find

                   EnableGoToMenu    MFirst
                   EnableGoToMenu    MNext
                   EnableGoToMenu    MPrevious
                   EnableGoToMenu    MLast
                   EnableGoToMenu    MSearch
                   RETURN
;==========================================================================================================
OpenAllFiles
                    TRAP               ErrorRoutines if IO NORESET
                    CALL               OpenCreditCards
                    call               OpenSequence
                    TRAPCLR            IO
                    return
;==========================================================================================================
LoadDefaultValues                       // Setup the Default values when adding a new record
;
; Set the values and properties when adding new record
;
                    return
;==========================================================================================================
                   include           MenuDefs.INC
