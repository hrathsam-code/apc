	 INCLUDE   MSCBRAG.TXT
. .
. APTRNINQ MODULE - DISPLAY OPEN INVOICES BY VENDOR
. 
	 INCLUDE   COMMON
. 
	 INCLUDE   APTRANFD
. 
	 INCLUDE   APMASTFD
. 
APTRANFL IFILE	   KEYL=19,UNCOMPRESSED
APTRANNM INIT	   "APTRANFL.ISI"
. 
APMASTER IFILE	   KEYL=6,UNCOMPRESSED
APMASTNM INIT	   "APMASTER.ISI"
. 
VENDIN	 DIM	   6
DROW	 FORM	   "03"
REPLY	 DIM	   1
VENDTOT	 FORM	   "00000000.00"
. 
	 DISPLAY   *ES,"ACCOUNTS PAYABLE SYSTEM	- DISPLAY OPEN ":
		   "INVOICES BY	VENDOR"
. 
	 DISPLAY   *P01:03,"INVOICE   DATE    DESCRIPTION":
                   *P41:03,"  AMOUNT",*P52:03,"DATE POSTED"
	 OPEN	   APTRANFL,APTRANNM
	 OPEN	   APMASTER,APMASTNM
. 
WHO	 MOVE	   "	  " TO VENDIN
	 MOVE	   " " TO REPLY
. 
	 KEYIN	   *P01:23,*EL,"ENTER VENDOR NUMBER DESIRED or ":
		   "PRESS F5 TO	TERMINATE ",VENDIN
. 
	 GOTO	   EOJ IF F5
. 
	 MOVE	   VENDIN TO ACCKEY
. 
	 INCLUDE   APMASTRD
. 
	 GOTO	   NOGOOD IF OVER
. 
	 DISPLAY   *P01:02,"VENDOR IS ",NAME				 
. 
CORRVEND MOVE	   " " TO REPLY						      
	 KEYIN	   *P1:23,*EL,"CORRECT VENDOR ?	(Y/N) ",REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   OK IF EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   WHO IF EQUAL
	 BEEP
	 GOTO	   CORRVEND
. 
OK	 CLEAR	   TRNKEY						      
	 APPEND	   ACCKEY TO TRNKEY
	 APPEND	   "		  " TO TRNKEY
	 RESET	   TRNKEY TO 1
. 
	 INCLUDE   APTRANRD
. 
. 
READTRAN TRAPCLR   IO
. 
. 
	 INCLUDE   APTRANKS
. 
	 GOTO	   VENDBRK IF OVER
. 
	 MATCH	   TACCN TO VENDIN
	 GOTO	   VENDBRK IF NOT EQUAL
. 
	 ADD	   TAMT	 TO  VENDTOT
	 ADD	   "1"	 TO  DROW
	 COMPARE   "22"	 TO  DROW
	 CALL	   RESETDSP  IF	 EQUAL
. 
	 REP	   " 0"	IN TMON						      
. 
	 DISPLAY   *P1:DROW,TINVNO,*P9:DROW,TMON,"/",TDAY,"/",TYEAR:
		   *P19:DROW,TDESC,*P41:DROW,TAMT,*P55:DROW,TLDDAY
. 
	 GOTO	   READTRAN
. 
. 
VENDBRK	 DISPLAY   *P1:22,"END OF DATA FOR THIS	VENDOR - - TOTAL DUE":
		   " IS	",VENDTOT					      
. 
	 MOVE	   " " TO REPLY
MOREVEND KEYIN	   *P1:23,*EL,"MORE VENDORS ? (Y/N) ",REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   WHOM	IF EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   MOREVEND IF NOT EQUAL
	 GOTO	   EOJ
. 
WHOM	 DISPLAY   *P01:04,*EF
	 MOVE	   "03"	TO DROW
	 MOVE	   "0.00"  TO VENDTOT
	 GOTO	   WHO
. 
NOGOOD	 DISPLAY   *P1:23,"INVALID VENDOR - PLEASE RETRY ! ",*W2
	 GOTO	   WHO
. 
RESETDSP DISPLAY   *P1:22,*EL,"VENDOR TOTAL THUS FAR IS	",VENDTOT
	 KEYIN	   *P1:23,*EL,"TAP ENTER KEY FOR DISPLAY OF ADD":
		   "ITIONAL ITEMS ",REPLY
. 
	 MOVE	   "04"	TO DROW
ADDEM	 DISPLAY   *P1:DROW,*EL						      
	 ADD	   "1" TO DROW
	 COMPARE   "22"	TO DROW						      
	 GOTO	   RESETRTN IF EQUAL
	 GOTO	   ADDEM
. 
RESETRTN MOVE	   "04"	TO DROW
	 RETURN
. 
EOJ	 CHAIN	   "APMENU1"
. 
. 
