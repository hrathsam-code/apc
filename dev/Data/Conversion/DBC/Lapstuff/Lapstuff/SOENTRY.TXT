.   ORDER PROCESSING SYSTEM......INVOICE DATA ENTRY
. 
.  WRITTEN BY R. BACHARACH,  MICRO SYSTEMS CONSULTANTS
.                            41 TEELE DRIVE
.                            CORAM, NEW YORK 11727
.                            (516) 698-5577
. 
         INCLUDE   COMMON
. 
INVCNUMB DIM        5
PASSDT6  DIM       6
PASSACCT DIM       4
PASSCOMP DIM       1
PASSCUST DIM       30
. 
NINVCNO  DIM       5
NEWFKEY  DIM       5
.
INDX1    FORM      1
INDXX    DIM       1
RECCNTR  FORM      2
.
REPLY    DIM       1
.FIELD   TYPE      LENGTH    FROM-TO   DESCRIPTION
.-----   ----      ------    -------   -----------
WORK01   DIM        1
WORK02   DIM        2
WORK03   DIM        3
WORK04   DIM        4
WORK08   DIM        8
WORK10   DIM       10
WORK09   DIM       9
FIELD09  DIM       7
NFIELD09 FORM      7
.
DATE8    DIM       8            8 DIGIT DATE WORK FIELD
MM       DIM       2
DD       DIM       2
YY       DIM       2
XMM      DIM       2
XDD      DIM       2
XYY      DIM       2
FORM5    FORM      5
. 
DTYPE    DIM       8
DSTAT    DIM       10
INDX     FORM      2
STARTKEY DIM       5
PP       DIM       1
PC       DIM       1
LINES    FORM      2
PTERMS   DIM       18
HDRSW    DIM       1
PAGES    FORM      2
. 
         INCLUDE   PART
. 
. CONTROL FILE AREA
. 
         INCLUDE   CONTFILE
. 
NFIELD01 FORM       4                  (NUMERIC WORK FIELD)
FIELD01  DIM        4
. 
NFLD01   FORM      9
FLD01    DIM       9
. 
MULTIFLG DIM       1
MULTIROW FORM      2
MULTICNT FORM      2
.
MULTINDX FORM      2
MULTIDXX DIM       2
.
MULTIDSC DIM       25
MD1      DIM       25
MD2      DIM       25
MD3      DIM       25
MD4      DIM       25
MD5      DIM       25
MD6      DIM       25
MD7      DIM       25
MD8      DIM       25
MD9      DIM       25
MD10     DIM       25
EXTRA    INIT      "XXXXXXXXXXXXXXXXXXX"
COMPLETE DIM       1
UOM      DIM       2
PRODCODE DIM       2
SHIPNAME DIM       30
SHIPADDR DIM       25
SHIPYADR DIM       25
SHIPADR2 DIM       25
SHIPCITY DIM       18
SHIPSTAT DIM       2
SHIPZIP  DIM       5
COMPLET  DIM       1
. 
NORMTERM INIT      "2%15NET30"
.
SOHDRFL  IFILE     KEYL=5,UNCOMPRESSED
SOHDRNM  INIT      "SOHDRFL/ISAM"
.
SOHDRKY  DIM       5
SOLINES  DIM       2
SOHDRNO  DIM       5
.
.
SODTLFL  IFILE     KEYL=8,UNCOMPRESSED
SODTLNM  INIT      "SODTLFL/ISAM"
.
SODTLKY  DIM       8  (SO#/SOLN#/MULTI-DESC-SUFFIX [0-9])
.
SODTLSTA DIM       1
SODTLNO  DIM       5
SODTLLN  DIM       2
SODTLSFX DIM       1
NEWTERM  DIM       15
. ACCOUNT FILE READ/WORK AREA
. 
CUSTFILE IFILE     KEYL=4,UNCOMPRESSED
CSTNAME  INIT      "ACCOUNTS/ISAM"
. 
         INCLUDE   CUSTMAST
.
CLOSESW  INIT      " "
. 
ADDKEY   FORM      5           INCREMENT WORK FIELD FOR NUMBERS
. 
.  WORK  AREAS FOR INVOICE HEADER CREATION/WRITE
. 
         INCLUDE   RCVBLES
. 
SAVE     DIM         10
TOTDOLS  FORM        6.2
FRGHT    FORM        2.2
. 
OLDDET   DIM       1
IOSW     DIM       1
LIBNET   FORM      7.2
REPNET   FORM      7.2
. 
+...........................................................
. 
DATE2X   DIM       2
DATE2N   FORM      2
. 
. 
INVCKEGS FORM      7     (ORDER QTY)
PCSSOLD  FORM      7     (SHIPPED QTY)
INVPCSKG FORM      7     (BACKORDER QTY)
. 
BUILDPT  DIM       7
. 
. RECORD ASSEMBLY AREA
.           INVOICE DETAIL DETAIL RECORD = 45 BYTES
. 
INVCNO   DIM       5                   INVC NUMBER
INVCLINE DIM       2                   INVC LINE NUMBER SUFFIX FOR INDEXING
. 
.  NOTE THAT INVCLINE WILL BE A 2 DIGIT NUMERIC SUFFIX
.  COUNTER-CREATED IN THIS PROGRAM
. 
INVLINES FORM      "00"
IRECCNTR FORM      6
DTLSTAT  DIM       1
ILIVCNTR FORM      3
. 
ROW      FORM      2                USED TO ROLL DOWN THE KEYIN STMT
. 
NFIELD04 FORM       5.2                (NUMERIC WORK FIELD)
FIELD04  DIM        8         11- 18   PRICE LN 1                 5.2
NFIELD03 FORM       3
FIELD03  DIM        3
NFIELD02 FORM       4
FIELD02  DIM        4
. 
. EXTENSION CALCULATE WORK/DISPLAY AREA
. 
LNDOLS   FORM      5.2
KEGQTY   FORM      9
LINECOST FORM      6.2
KGSAVAIL FORM      6
CRAPMSG  DIM       18
CALCFLD  FORM      8.3
OLDFRGT  FORM      5.2
. 
FORM3    FORM      3
IPNSTDSX DIM       6
IPNSTDSN FORM      6
NONSTDSW DIM       1
SCANSW   DIM       1
PARTUPSW DIM       1
. 
KREPLY   DIM       1
. 
CHKPASS  DIM       5
.  
WEIGHT   FORM      6
HDRCHGSW DIM       1
. 
INVDTKEY DIM       7
. 
OLDXTEND FORM      6.2
. 
SAVDTLKY DIM       8
. 
CALCNET  FORM      6.2
FRTALLOW FORM      5.2
. 
TIMEDIM  DIM       8
. 
CUSTN    FORM      4
SPECMSG  DIM       80
MSGFLAG  DIM       1
PORTX    DIM       2
SPECSW   DIM       1
TIME     INIT      "HH/MM/SS"
. 
+........................................................................
. 
START    DISPLAY   *ES,*P01:12,*HON,"SALES ORDER DATA ENTRY PROGRAM":
                   *HOFF:
                       *P01:14,"IF YOU HAVE COME HERE ERRONEOUSLY FROM THE ":
                               "SYSTEM MENU PROGRAMS":
                       *P01:15,"AND DO  N O T  HAVE ANY  INVOICES TO ":
                               "ENTER........":
                       *P01:17,"YOU MAY TERMINATE THIS PROGRAM BY PRESSING ":
                               "THE   F5   KEY"
         KEYIN     *P01:20,*HON,"KEY     F5     TO  TERMINATE":
                   *P01:21,"KEY     ENTER  TO  ENTER ORDER ",REPLY:
                   *HOFF
. 
         GOTO      MAINLINE  IF  NOT  F5
. 
         BEEP
INDXIT   DISPLAY   *HOFF,*ES
         BEEP
         CHAIN     "APPINVC1"
.
MAINLINE MOVE      " " TO REPLY
.
.
MAINLIN1 DISPLAY   *HOFF,*ES,*P01:01,"OPENING DATA FILES FOR ENTRY"
         MOVE      " ",REPLY
         MOVE      " ",MSGFLAG
         MOVE      " ",SPECMSG
         GOTO      OPENFLS
. 
. FILE OPENING SEQUENCE
OPENFLS  TRAP      NOFILE  IF IO
. 
         OPEN      SOHDRFL,SOHDRNM
.
         OPEN      SODTLFL,SODTLNM
.
         MOVE      TODAY TO MM
         MOVE      TODAY TO XMM
         RESET     TODAY TO 4
         MOVE      TODAY TO DD
         MOVE      TODAY TO XDD
         RESET     TODAY TO 7
         MOVE      TODAY TO YY
         MOVE      TODAY TO XYY
         RESET     TODAY TO 1
. 
. SET UP SIX DIGIT DATE
. 
         MOVE      TODAY TO DATE8
         MOVE      DATE8 TO INVDATE
         BUMP      DATE8 BY 3
         RESET     INVDATE TO 2
         APPEND    DATE8 TO INVDATE
         BUMP      DATE8 BY 3
         RESET     INVDATE TO 4
         APPEND    DATE8 TO INVDATE
         RESET     INVDATE
         RESET     DATE8
         MOVE      INVDATE  TO  PASSDT6      PASS 6 DIGIT DATE TO DTL PGM
         GOTO      ACCTOPEN
.
NOFILE   DISPLAY   *ES,"ORDER PROCESSING FILES NOT ON LINE":
                   *N,"JOB IS ABORTING"
         BEEP
         CHAIN     "APPINVC1"        GO BACK TO DISPLAY INVOICE MENU
*...........................................................
. OPEN ISAM-LOOKUP FILE
.
ACCTOPEN TRAPCLR   IO
         TRAP      NOCUSTFL IF IO
.
         CALL      OPENCUST
.
         CMATCH    "H"  TO  IOSW
         GOTO      NOCUSTFL IF NOT EQUAL
.
         TRAPCLR   IO
         TRAP      NOCTRLS IF IO
.
         CALL      OPENCTLS
.
         CMATCH    "H"    TO IOSW
         GOTO      NOCTRLS IF NOT EQUAL
.
ZERONETS TRAPCLR   IO                (OKAY NOW LETS GO!)
         MOVE      "A" TO INVCOMP
         GOTO      DISPLAY
. 
NOCTRLS  DISPLAY   *ES," CONTROLS  FILE IS NOT ON-LINE",*W,*W
         BEEP
. 
MNUCHAIN CHAIN     "APPINVC1"
. 
NOCUSTFL DISPLAY   *ES,"CUSTOMER FILE IS NOT ON-LINE, ABORTING"
         BEEP
         GOTO      MNUCHAIN
. 
*...........................................................
. INITIAL SCREEN DISPLAY SEQUENCE
.
DISPLAY  CLOCK     TIME TO TIME
. 
         DISPLAY   *ES,*HON,*P11:1,"O R D E R   P R O C E S S I N G   D A T A":
                   "   E N T R Y     ":
                   *HOFF:
                   *P11:2,"(I)nvoice or (C)redit?  I       ":
                   *P43:2,"Document Number A_____":
                   *P1:3,"Sold to  ",*P43:3,"Ship to ":
                   *P1:4,"Acct No",*P3:5,"____":
                   *HOFF:
                   *P11:7,"P.O. ______________":
                   *P51:7,"P.O.               ":
                   *HON:
                   *P1:8,"PO NUMBER      SHIPPED  WGHT CTNS  SHIP VIA":
                    "            ":
                   *P54:8,"TERMS            ",*P71:8,"COMP/PART",*HOFF
LOOP     TRAPCLR   PARITY       (NOP)
. 
ORDERFOR MOVE      "A" TO INVCOMP
         GOTO      K01
. 
MOVREP   MOVE      "APP" TO CTLKEY
         GOTO      K01K
. 
*...........................................................
. 
.  SET NORMAL INVOICE CONSTANTS PRIOR TO DATA ENTRY
. 
............................................................
K01      MOVE      "O"  TO  INVSTAT
         MOVE      INVCOMP  TO  PASSCOMP
         MOVE      "I"  TO  INVTYPE
         MOVE      "C"  TO  INVFOB
         MOVE      " "  TO  INVCHECK
         MOVE      " "  TO  INVMESG
         CLEAR     INVORDNO
         MOVE      " "  TO  MOREORDR
         MOVE      "UPS          "  TO  INVSHIP
         DISPLAY   *P36:9,INVSHIP
         MOVE      "0"      TO   INVWGHT
         MOVE      "0"      TO   CARTONS
         MOVE      "000000.00"   TO   INVTOTL
         MOVE      "000000.00"   TO   IAMTRCVD
         MOVE      "0.00" TO LINEPRC
         MOVE      "0.00" TO LINECOST
         MOVE      "0.00" TO LNDOLS
         MOVE      "C"      TO   COMPLET
         DISPLAY   *P73:9,"(",COMPLET,")"
. KEYIN VARIABLES SEQUENCE
. 
INVTYPE  DISPLAY   *P35:2,INVTYPE
         KEYIN     *P35:2,*RV,INVTYPE
         CMATCH    "I" TO INVTYPE
         GOTO      K01KA  IF EQUAL
         CMATCH    "C" TO INVTYPE
         GOTO      K01KA  IF EQUAL
         BEEP
         MOVE      " " TO INVTYPE
         GOTO      INVTYPE
.
*...........................................................
. ACCOUNT ID NUMBER
. THIS FIELD IS "BOTH" REQUIRED & FILL-CONTROLLED
. FIELD TYPE IS NUMERIC
. FIELD VALIDATION TYPE IS ISAM-VERIFICATION
.
. 
K01KA    MOVE      INVCOMP TO PASSCOMP
         MOVE      "APP" TO CTLKEY
         CMATCH    "A" TO INVCOMP
         GOTO      MOVREP IF EQUAL
K01K     KEYIN     *P3:5,"____":
                   *P3:5,*DE,WORK04
         CMATCH    " " TO WORK04
         GOTO      K01K IF EOS
K01A     CLEAR     INVCUST
         MOVE      WORK04 TO INVCUST
K01B     RESET     INVCUST TO  4      (FILL-CONTROLLED)
         GOTO      K01K IF EOS
         RESET     INVCUST
         MOVE      INVCUST TO CUSTKEY
K01C     DISPLAY   *P3:5,INVCUST
         CALL      GETCUST
         CMATCH    "H"  TO  IOSW
         GOTO      VCUST    IF  EQUAL
         BEEP
         DISPLAY   *P10:23,"INVALID ACCOUNT NUMBER - PLEASE RETRY ",*W,*W:
                   *P10:23,*EL
         GOTO      K01K
. 
.  NOW DISPLAY THE VENDOR DATA FOR THE OPERATOR
. 
VCUST    MOVE      CUSTKEY TO CUSTN
. 
OKAYCOMP DISPLAY   *P11:3,CUSTNAME:
                   *P11:4,CUSTADDR:
                   *P11:5,CUSTCITY,*P32:5,CUSTSTAT,*P35:5,CUSTZIP
. 
         MOVE      CUSTNAME TO SHIPNAME
         MOVE      CUSTADDR TO SHIPADDR
         MOVE      CUSTCITY TO SHIPCITY
         MOVE      CUSTSTAT TO SHIPSTAT
         MOVE      CUSTZIP  TO SHIPZIP
         MOVE      "                         " TO SHIPADR2
         REP       " 0" IN SHIPZIP
. 
. NOW DISPLAY THE SHIP-TO DATA
. 
         DISPLAY   *P51:3,SHIPNAME:
                   *P51:4,SHIPADDR:
                   *P51:5,SHIPADR2:
                   *P51:6,SHIPCITY:
                   *P72:6,SHIPSTAT:
                   *P75:6,SHIPZIP
. 
ASKCUST  KEYIN     *P01:23,*EL,"CORRECT ACCOUNT ?  (Y/N) ",REPLY
         CMATCH    "Y",REPLY
         GOTO      PASSNAME  IF  EQUAL
         BEEP
         DISPLAY   *P01:23,*EL
         GOTO      K01K
. 
PASSNAME MOVE      CUSTNAME TO PASSCUST
         MOVE      CUSTKEY  TO PASSACCT   PASS THE ACCT NO. FOR $ UPDATE
* ***********************************
. 
. NOW ALLOW CHANGES TO SHIP-TO DATA
. 
         KEYIN     *P51:3,*RV,SHIPNAME
         DISPLAY   *P51:3,SHIPNAME
. 
         CLEAR     SHIPYADR
         KEYIN     *P51:4,SHIPYADR
         RESET     SHIPYADR
         GOTO      K03 IF EOS
         MOVE      SHIPYADR TO SHIPADDR
         DISPLAY   *P51:4,SHIPADDR
         KEYIN     *P51:5,*RV,SHIPADR2
         DISPLAY   *P51:5,SHIPADR2
         KEYIN     *P51:6,*RV,SHIPCITY
         DISPLAY   *P51:6,SHIPCITY
         KEYIN     *P72:6,*RV,SHIPSTAT
         DISPLAY   *P72:6,SHIPSTAT
         KEYIN     *P75:6,*RV,SHIPZIP
         DISPLAY   *P75:6,SHIPZIP
* ************************************
K03      DISPLAY   *P01:23,*EL
*...........................................................
. CUSTOMER ORDER NUMBER
. NO EDITING WILL BE PERFORMED ON THIS FIELD
.
         TRAPCLR   PARITY
K03K     KEYIN     *P16:7,"______________":
                   *P16:7,INVORDNO
. 
         RESET     INVORDNO TO 1
         GOTO      K03K IF EOS
. 
.
. DISPLAY IN THREE PLACES.
         DISPLAY   *P16:7,INVORDNO,*P56:7,INVORDNO,*P1:9,INVORDNO
         DISPLAY   *P15:9,TODAY,*P36:9,INVSHIP
. 
         GOTO      K08
. 
K09      DISPLAY   *P36:9,INVSHIP
         KEYIN     *P36:9,*RV,INVSHIP
         DISPLAY   *P36:9,INVSHIP
         GOTO      TERMSIN
. 
K08      MOVE      "1" TO INVWGHT
         DISPLAY   *P25:9,INVWGHT
         KEYIN     *P25:9,*RV,INVWGHT
         DISPLAY   *P25:9,INVWGHT
. 
K081     MOVE      "1" TO CARTONS
         DISPLAY   *P31:9,CARTONS
         KEYIN     *P31:9,*RV,CARTONS
         DISPLAY   *P31:9,CARTONS
         GOTO      K09
. 
TERMSIN  DISPLAY   *P51:9,NORMTERM
         MOVE      NORMTERM TO NEWTERM
         KEYIN     *P51:9,*RV,NEWTERM
. 
COMPLETE DISPLAY   *P73:9,"(",COMPLET,")"
         KEYIN     *P74:9,*RV,COMPLET
         CMATCH    "C" TO COMPLET
         GOTO      GETNUMB IF EQUAL
         CMATCH    "P" TO COMPLET
         GOTO      GETNUMB IF EQUAL
         BEEP
         GOTO      COMPLETE
. 
GETNUMB  TRAPCLR   PARITY            (WE ALREADY READ THE CONTROL FILE!)
         MOVE      PASSDT6  TO  INVDATE
. 
         FILEPI    19;CONTROLS
. 
         MOVE      "APP" TO CTLKEY
         CALL      GETCTL
         MOVE      CTLINVC TO ADDKEY
         ADD       "1"     TO ADDKEY
         MOVE      TODAY   TO CTLDATE
         MOVE      ADDKEY   TO  CTLINVC
         CALL      PUTCTL                  (UPDATE FILE, RESERVING NUMBER)
. 
. STOP THE PI HERE
. 
         FILEPI   0
         MOVE      ADDKEY   TO INVKEY
         REP       " 0"     IN INVKEY
         DISPLAY   *P60:2,*HON,INVKEY,*HOFF
         MOVE      " " TO SPECSW
         MOVE      INVKEY   TO  SOHDRKY
         MOVE      INVKEY   TO  SOHDRNO
         MOVE      INVKEY   TO  SODTLNO
.
.   4/26/91 - GOTO RECYCLE AND START DETAIL ENTRY
.
         MOVE      "00"  TO  INVLINES
         MOVE      "00"  TO  RECCNTR
.
         GOTO      RECYCLE
.............................................................
. DISK-WRITE SEQUENCE
.
WRTSOHDR TRAPCLR   IO
         TRAP      BADDSK IF IO
. 
. WRITE THE SO HEADER RECORD - ISAM
. 
W1A      TRAPCLR   IO
         TRAPCLR   FORMAT
         TRAPCLR   RANGE
         TRAPCLR   PARITY
         TRAP      BADDSK IF IO
         TRAP      BUMFMT IF FORMAT
. 
         WRITE     SOHDRFL,SOHDRKY;SOHDRNO,SOLINES,INVTYPE,INVSTAT,INVCUST:
                                   INVDATE,INVTOTL,INVORDNO,INVWGHT,NEWTERM:
                                   CARTONS,INVSHIP,COMPLET:
                                   SHIPNAME,SHIPADDR,SHIPADR2:
                                   SHIPCITY,SHIPSTAT,SHIPZIP:
                                   CALCNET,LIBNET
         MOVE      "H" TO IOSW
.
         RETURN
.
BUMFMT   BEEP
         DISPLAY   *P1:23,"FORMAT ERROR ON WRITE TO SOHDR FILE",*W3
         CHAIN     "APPINVC1"
. 
BADDSK   BEEP
         DISPLAY   *ES,*P01:01,"BAD WRITE TO DISK, SALES HEADER ON ":
                               "SOHDR FILE",*W3
.
         CHAIN     "APPINVC1"
. 
*...........................................................
. END OF JOB SEQUENCE
.
EOJ      GOTO      UPDTSEQ
. 
. 
EOJOUT   CHAIN     "APPINVC1"
.
         STOP
.
*...........................................................
. INITIAL SCREEN DISPLAY SEQUENCE
.
RECYCLE  MOVE      "10" TO ROW
         DISPLAY   *P01:10,*EF:
                   *HON:
                   "QTY ORD  QTY SHP  QTY B/O  PART NUMBER              ":
                   "PROD  PRICE  UOM EXTENSION  ",*HOFF
LOOP01  KEYIN     *P01:23,*EL,*HON,"Press ENTER to add data, F5 if ":
                          "done ",REPLY,*HOFF
         GOTO      BIGEOJ  IF  F5
         CMATCH    " " TO REPLY
         GOTO      LOOP01 IF NOT EOS
         DISPLAY   *P01:23,*EL
*...........................................................
. KEYIN VARIABLES SEQUENCE
         ADD       "01" TO ROW
         COMPARE   "21" TO ROW
         GOTO      RECYCLE IF NOT LESS
. 
         DISPLAY   *P01:ROW,"_______  _______  _______  ":
                            "_________________________    ":
                            "________ __  _________  "
. 
*...........................................................
. PART NO. 1
. THIS FIELD IS "BOTH" REQUIRED & FILL-CONTROLLED
. FIELD TYPE IS NUMERIC
.
         MOVE      "0" TO LNDOLS
         MOVE      "0" TO KEGQTY
         MOVE      "0" TO PCSSOLD
         MOVE      "0" TO INVCKEGS
         MOVE      "0" TO INVPCSKG
         MOVE      "0" TO LINECOST
*...........................................................
. QUANTITY ORDERED           4/23/83
. FIELD TYPE IS NUMERIC
.
B03      MOVE      FIELD03 TO SAVE
B03K     KEYIN     *P01:ROW,"_______  ":
                   *P01:ROW,WORK09
. 
B03A     CLEAR     FIELD09
         MOVE      WORK09 TO FIELD09
B03B     MOVE      "0" TO NFIELD09
         TYPE      FIELD09     (NUMERIC FIELD)
         GOTO      B03K IF NOT EQUAL
. 
.  BEEP IT HERE TO WAKE UP THE OPERATOR
. 
         MOVE      FIELD09 TO NFIELD09
B03C     MOVE      NFIELD09 TO FIELD09
         DISPLAY   *P01:ROW,FIELD09
. 
         MOVE      NFIELD09 TO INVCKEGS
. 
. CHECK IF ORDER IS COMPLETE AND DISPLAY ACCORDINGLY
. 
         CMATCH    "C",COMPLET
         GOTO      PCSKEG IF NOT EQUAL
. 
. FALLTHRU MEANS COMPLETE SO DISPLAY ORDER QUANTITY AS SHIPPED QTY
. AND DISPLAY ZERO IN BACKORDER FIELD
. 
         DISPLAY   *P10:ROW,INVCKEGS,*P19:ROW,"      0"
         MOVE      INVCKEGS TO PCSSOLD
         MOVE      "0"      TO INVPCSKG     (ZERO OUT B/O FIELD)
         GOTO      GETPARTZ
. ........................................
. 
. NOTE USE INVCKEGS FOR QUANTITY ORDERED
.      USE PCSSOLD  FOR QUANTITY SHIPPED
.      USE INVPCSKG FOR QUANTITY BACKORDERED
. 
. 
PCSKEG   KEYIN     *P10:ROW,"_______  ",*P10:ROW,PCSSOLD
. 
         DISPLAY   *P10:ROW,PCSSOLD
. 
         MOVE      INVCKEGS TO KEGQTY
         SUBTRACT  PCSSOLD  FROM  KEGQTY
         MOVE      KEGQTY   TO    INVPCSKG
         DISPLAY   *P19:ROW,INVPCSKG
         GOTO      GETPARTZ
. 
AHA      BEEP
         GOTO      AHA
. 
*.........................................................
GETPARTZ MOVE      "0" TO MULTICNT
         MOVE      " " TO MULTIFLG
         DISPLAY   *P1:23,*HON,"PRESS F1 FOR MULTI-LINE PART NUMBER":
                   *HOFF
         KEYIN     *P28:ROW,"_________________________":
                   *P28:ROW,MULTIDSC
         DISPLAY   *P28:ROW,MULTIDSC
         MOVE      MULTIDSC TO MD1
         MOVE      "1" TO MULTICNT
         GOTO      MULTISET IF F1    (IF F1..THERE IS MULTI-LINE DESCRIPTION)
.
         GOTO      B04
. 
*..........................................................
. 
MULTISET MOVE      "X" TO MULTIFLG
         BEEP
         DISPLAY   *P1:23,*EL
         DISPLAY   *P1:23,*HON,"MULTI-LINE (MAX 10) PART NUMBER",*W2,*HOFF
.
         GOTO      B04
.
...........................................................
. 
MULTIDSC MOVE      ROW TO MULTIROW
         MOVE      "1" TO MULTICNT
MULTILP  ADD       "1" TO MULTIROW
         COMPARE   "22"   TO  MULTIROW
         CALL      RESETROW   IF  NOT LESS
         DISPLAY   *P1:23,*EL,"CONTINUE TO PRESS F1 IF YOU WISH TO ADD MORE":
                   "ELSE PRESS ENTER"
         KEYIN     *P28:MULTIROW,"_________________________":
                   *P28:MULTIROW,MULTIDSC
         DISPLAY   *P28:MULTIROW,MULTIDSC
         RESET     MULTIDSC
         GOTO      MULTIRTN IF EOS       (IF NOTHING ENTERED...FORGET IT!)
         ADD       "1" TO MULTICNT
         STORE     MULTIDSC INTO MULTICNT OF MD1,MD2,MD3,MD4,MD5:
                                             MD6,MD7,MD8,MD9,MD10
. 
         CLEAR     SODTLKY
         APPEND    SOHDRNO  TO  SODTLKY
         APPEND    SODTLLN  TO  SODTLKY
........         APPEND    MULTICNT TO  SODTLKY
.
         MOVE      MULTICNT TO MULTINDX
         SUBTRACT  "1" FROM    MULTINDX
         MOVE      MULTINDX TO MULTIDXX
         RESET     MULTIDXX TO 2
         APPEND    MULTIDXX TO SODTLKY
         RESET     SODTLKY  TO  1
         RESET     MULTIDXX TO  1
.
         MOVE      MULTIDXX TO  SODTLSFX
         MOVE      "*"  TO  DTLSTAT
.
         CALL      WTSODTL
.
         COMPARE   "10" TO MULTICNT
         GOTO      MULTIRTN IF EQUAL
.
         COMPARE   "9",SECURITY
         GOTO      XS  IF NOT EQUAL
. 
         DISPLAY   *P1:23,*EL,MULTIDSC," MULTICNT=",MULTICNT,*W
.
XS       GOTO      MULTILP IF F1
. 
MULTIRTN MOVE      MULTIROW  TO  ROW    (LET PROGRAM KNOW!)
         RETURN                     (NO MORE!)
.
......        GOTO      B04
. 
..............................................................
. 
RESETROW MOVE      "10" TO MULTIROW
RESETLP  ADD       "1"  TO MULTIROW
         COMPARE   "21" TO MULTIROW
         GOTO      RESETOUT IF EQUAL
         DISPLAY   *P01:MULTIROW,*EL
         GOTO      RESETLP
RESETOUT MOVE      "12" TO MULTIROW
         MOVE      "12" TO ROW
         RETURN
. 
. 
EXTEND   RETURN
WRTHDRIS RETURN
. 
         INCLUDE   RCVBLSIO
. 
. 
. FALLTHRU IS NO MORE DESCRIPTIONS, SO GOTO B04
*...........................................................
. PRICE LN 1
. THIS FIELD IS REQUIRED
. FIELD TYPE IS NUMERIC
. 
B04      MOVE      FIELD04 TO SAVE
         GOTO      B04K
. 
         KEYIN     *P54:ROW,"__",*P54:ROW,PRODCODE
         DISPLAY   *P54:ROW,PRODCODE
.
B04K     KEYIN     *P57:ROW,"________":
                   *P57:ROW,WORK08
         CMATCH    "<" TO WORK08
         GOTO      B04K IF EOS
         CMATCH    ">" TO WORK08
         GOTO      B04A IF NOT EQUAL
         MOVE      SAVE TO FIELD04
         DISPLAY   *P57:ROW,FIELD04
         GOTO      B04K
B04A     CLEAR     FIELD04
         MOVE      WORK08 TO FIELD04
B04B     MOVE      "0" TO NFIELD04
         TYPE      FIELD04     (NUMERIC FIELD)
         GOTO      B04K IF NOT EQUAL
         MOVE      FIELD04 TO NFIELD04
B04C     MOVE      NFIELD04 TO FIELD04
         DISPLAY   *P57:ROW,FIELD04
         MOVE      NFIELD04 TO LNDOLS     (SELLING PRICE/UOM)
. 
. GET THE UNIT OF MEASURE
. 
UOM      MOVE      "  " TO  UOM
         KEYIN     *P66:ROW,UOM
. 
         MATCH     "GR" TO UOM
         GOTO      DUOM IF EQUAL
         MATCH     "EA" TO UOM
         GOTO      DUOM IF EQUAL
         MATCH     "M " TO UOM
         GOTO      DUOM IF EQUAL
         MATCH     "LT" TO UOM
         GOTO      DUOM IF EQUAL
         MATCH     "LB" TO UOM
         GOTO      DUOM IF EQUAL
         MATCH     "C " TO UOM
         GOTO      DUOM IF EQUAL
         MATCH     "FT" TO UOM
         GOTO      DUOM IF EQUAL
         MATCH     "PK" TO UOM
         GOTO      DUOM IF EQUAL
         MATCH     "BX" TO UOM
         GOTO      DUOM IF EQUAL
         MATCH     "RD" TO UOM
         GOTO      DUOM IF EQUAL
         MATCH     "NC" TO UOM
         GOTO      DUOM IF EQUAL
         BEEP
         DISPLAY   *P66:ROW,"  "
         GOTO      UOM
. 
DUOM     DISPLAY   *P66:ROW,UOM
. 
. 
CHK100X  MATCH     "C " TO UOM
         GOTO      CHK1000 IF NOT EQUAL
         MOVE      PCSSOLD TO CALCFLD
         DIVIDE    "100" INTO CALCFLD
         MULT      LNDOLS BY CALCFLD
         MOVE      CALCFLD TO LINECOST
         GOTO      DSPCOSTX
. 
. 
CHK1000  MATCH     "M " TO UOM
         GOTO      EACHX   IF NOT EQUAL
         MOVE      PCSSOLD TO CALCFLD
         DIVIDE    "1000"  INTO CALCFLD
         MULT      LNDOLS  BY   CALCFLD
         MOVE      CALCFLD TO   LINECOST
         GOTO      DSPCOSTX
. 
EACHX    MOVE      PCSSOLD TO LINECOST
. 
EACHXX   MULT      LNDOLS BY LINECOST
. 
DSPCOSTX DISPLAY   *P70:ROW,LINECOST
. 
. FIRST CHECK IF THE LINE IS OKAY!
. 
CHKLINE  BEEP
         MOVE      " " TO REPLY
         KEYIN     *P1:22,*EL,*HON,"IS DATA CORRECT ? (Y/N) ",REPLY,*HOFF
         DISPLAY   *P1:22,*EF
         CMATCH    "Y",REPLY
         GOTO      CONTXX IF EQUAL
. 
         CMATCH    "N",REPLY
         GOTO      LNRESET IF EQUAL
         BEEP
         GOTO      CHKLINE
. 
LNRESET  DISPLAY   *P1:ROW,*EL
         SUBTRACT  "1" FROM ROW
         GOTO      LOOP01
. 
CONTXX   ADD       LINECOST TO TOTDOLS
         ADD       LINECOST TO INVTOTL
. 
. 
. NOTE IF EXTRA DESCRIPTIVE LINES EXIST, THEN WRITE CONTINUATION RECORDS
.      FOR DESCRIPTIVE DATA
. 
. DISK-WRITE SEQUENCE
.
LINEINCR ADD       "01"   TO INVLINES
         MOVE      INVLINES  TO  RECCNTR
         MOVE      INVLINES  TO  SODTLLN
         REP       " 0" IN  SODTLLN
         MOVE      "0"  TO  SODTLSFX
         MOVE      "X"  TO  DTLSTAT
         MOVE      MD1  TO  MULTIDSC
.
         CLEAR     SODTLKY
         APPEND    SOHDRNO  TO  SODTLKY
         APPEND    SODTLLN  TO  SODTLKY
         APPEND    "0"      TO  SODTLKY
         RESET     SODTLKY  TO  1
         MOVE      SODTLKY  TO  SAVDTLKY
. 
.  WRITE THE SO DETAIL RECORD
.
         CALL      WTSODTL
.
. NOW CHECK FOR MULTI-DESCRIPTIVE LINES AND WRITE MULTIPLE RECORDS NOW!
. 
         CMATCH    "X",MULTIFLG
         GOTO      COMP18 IF NOT EQUAL
. 
. FALLTHRU MEANS WRITE CONTINUATION RECORDS
. 
. FIRST ZERO OUT THE QTY AND PRICE FIELDS
. 
DOMULTIS MOVE      "0" TO INVCKEGS
         MOVE      "0" TO INVPCSKG
         MOVE      "0" TO PCSSOLD
         MOVE      ".00"  TO LNDOLS
         MOVE      ".00"  TO LINECOST
. 
         CALL      MULTIDSC
.
         GOTO      COMP18
..........................................
         MOVE      "1"  TO  INDX
. 
LOADLOOP ADD       "1"  TO  INDX    (STARTS AT MD2, INDX VALUE=2)
         LOAD      MULTIDSC FROM INDX OF MD1,MD2,MD3,MD4,MD5,MD6:
                                         MD7,MD8,MD9,MD10
. 
         MOVE      INDX1    TO  INDXX
.
         COMPARE   INDX TO  MULTICNT
         GOTO      COMP18 IF  LESS
. 
         MOVE      "        "  TO  SODTLKY
         CLEAR     SODTLKY
         APPEND    SOHDRNO  TO  SODTLKY
         RESET     SODTLKY  TO  5
         APPEND    SODTLLN  TO  SODTLKY
         RESET     SODTLKY  TO  7
         APPEND    INDXX    TO  SODTLKY
         LENSET    SODTLKY
         RESET     SODTLKY  TO  1
         DISPLAY   *P60:23,*EL,SODTLKY,*W1
.
.      ADD THE ONE DIGIT MULTIDSC SUFFIX
.
         MOVE      SOHDRNO TO SODTLNO
         MOVE      INDXX   TO SODTLSFX
         MOVE      "*"  TO DTLSTAT
.
         DISPLAY   *P60:23,*EL,*HON,SODTLKY,*HOFF,*W3
         CALL      WTSODTL
.
         GOTO      LOADLOOP
.
...............................................................
.
WTSODTL  TRAPCLR   IO
         TRAP      BDDTL IF IO
.
         WRITE     SODTLFL,SODTLKY;SODTLNO,SODTLLN,SODTLSFX,DTLSTAT:
                                   MULTIDSC,INVCKEGS,INVPCSKG,PCSSOLD:
                                   LNDOLS,LINECOST,UOM,PRODCODE
.
         MOVE      "H" TO IOSW
         RETURN
.............................................................
BDDTL    BEEP
         DISPLAY   *P1:23,*EL,"UNABLE TO WRITE SO DETAIL ! ABORTING ! ",*W3
         CHAIN     "APPINVC1"
. 
COMP18   COMPARE   "18"  TO  INVLINES
         GOTO      NINENINE  IF  EQUAL       (END THIS INVOICE...NOW!)
         GOTO      LOOP01
. 
DETIOERR BEEP
         DISPLAY   *ES,*P01:12,"WRITE ERROR ON APP S/O DETAIL FILE":
                       *P01:14,"PROGRAM ABORTING",*W5
         BEEP
         CHAIN     "APPINVC1"
. 
NINENINE BEEP
         DISPLAY   *P01:23,*EL,"18 ITEMS KEYED FOR  INVC# ",INVCNO:
                     " I AM CLOSING THIS INVOICE",*W,*W,*W
         BEEP
         GOTO      BIGEOJ
. 
NOTFND   BEEP
         DISPLAY   *P01:ROW,*EL
         DISPLAY   *P01:ROW,"INVALID PART NUMBER - PLEASE RESUBMIT",*W2
         SUBTRACT  "1" FROM ROW
         GOTO      LOOP01
. 
*...........................................................
. END OF JOB SEQUENCE
.
BIGEOJ   TRAPCLR   IO
         TRAP      NOINVOIC  IF IO
         TRAPCLR   FORMAT
         TRAP      STDFMTER  IF FORMAT
. 
. 
. NOW SHOW THE SUBTOTAL AND ENTER THE SHIP/HANDLING AMOUNT
. 
DSPTOTS  DISPLAY   *P1:21,*EL,*P50:21,"SUBTOTAL",*P70:21,INVTOTL
         MOVE      ".00" TO CALCNET
         DISPLAY   *P1:22,*EL,*P50:22,"SHIP/HAND CHGS",*P70:22,CALCNET
         KEYIN     *P1:22,"ENTER SHIPPING AND HANDLING CHARGE":
                   *P70:22,*RV,CALCNET
         DISPLAY   *P70:22,CALCNET
         MOVE      INVTOTL TO LIBNET
         ADD       CALCNET TO   LIBNET
         DISPLAY   *P1:23,*EL,*P50:23,"ORDER NET",*P69:23,LIBNET
         KEYIN     *P1:23,*HON,"TAP ENTER TO PROCEED ",REPLY,*HOFF
         MOVE      LIBNET TO INVTOTL
         MOVE      LIBNET TO IDOLSLFT
         MOVE      INVTOTL TO TOTDOLS
         DISPLAY   *P1:23,"                          "
. 
. TEST FOR NEGATIVE INVOICE.
. 
         CMATCH    "I",INVTYPE
         GOTO      XCRED IF NOT EQUAL
. 
         COMPARE   ".00" TO INVTOTL
         GOTO      XCRED IF EQUAL
. 
         COMPARE   ".01" TO INVTOTL
         GOTO      XCRED IF NOT LESS
         BEEP
         DISPLAY   *P1:24,*EL,*HON,"NEGATIVE INVOICE DISALLOWED!",*W4:
                   *P1:24,*EL
         GOTO      DSPTOTS
. 
XCRED    MOVE      LIBNET  TO  INVTOTL
         MOVE      INVTOTL TO  IDOLSLFT
         MOVE      RECCNTR TO  SOLINES
         REP       " 0"    IN  SOLINES
.
         CALL      WRTSOHDR
.
         GOTO      SHOWTOTS      (DISPLAY FINAL INVOICE TOTALS)
. 
.......... .....................................................
. 
SHOWTOTS DISPLAY   *ES,*P01:12,"ORDER NUMBER ",INVKEY," TOTAL NET ":
                               "ORDER IS: $",TOTDOLS
CHECKIT  MOVE      " ",REPLY
.
         GOTO      UPDTSEQ
. 
UPDTSEQ  TRAPCLR   IO                (NOW ADD THE UPDATE LOGIC)
. 
         GOTO      ENDREPLY
......................................................................
. 
CUSTUPDS MOVE      PASSACCT  TO  CUSTKEY
         CALL      GETCUST
         CMATCH    "H"       TO  IOSW
         GOTO      ADDCUST   IF  EQUAL
         BEEP
         DISPLAY   *P01:24,"CANNOT FIND THE CUSTOMER RECORD TO UPDATE SALES"
         GOTO      CTRLUPDT
ADDCUST  ADD       TOTDOLS   TO  CURNTDUE
         MOVE      TOTDOLS   TO  LSTSALPR
         MOVE      INVDATE   TO  LSTSALDT
         CALL      PUTCUST
. 
. CUSTOMER HAS BEEN UPDATED WITH DOLLAR INFORMATION
. 
CTRLUPDT CMATCH    "A"       TO  PASSCOMP
         GOTO      REPCTLS   IF  NOT  EQUAL
         MOVE      "APP"     TO  CTLKEY
.
PROCCTL  TRAPCLR   IO
         TRAP      NOCTLSFD IF IO
         CMOVE     "M" TO IOSW
         FILEPI    19;CONTROLS
         CALL      GETCTL
         CMATCH    "H"       TO  IOSW
         GOTO      NOCTLSFD  IF  NOT EQUAL
.
         ADD       TOTDOLS   TO  CTLSOLD
         MOVE      TODAY     TO  CTLDATE
         CMOVE     "M",IOSW
         CALL      PUTCTL
         FILEPI    0
.
         CMATCH    "H",IOSW
         GOTO      BUMCTLWT IF NOT EQUAL
. .
. CONTROLS HAVE BEEN UPDATED
         GOTO      ENDREPLY
.
BUMCTLWT BEEP
         DISPLAY   *P1:23,*EL,"UNABLE TO UPDATE CONTROLS FILE",*W3
         DISPLAY   *P1:23,*EL
         GOTO      ENDREPLY
. 
. 
NOCTLSFD BEEP
         DISPLAY   *P01:24,"NO CONTROLS RECORD ...... WHATS HAPPENING?"
         GOTO      NOCTLSFD
. 
. 
REPCTLS  MOVE      "APP"     TO  CTLKEY
         GOTO      PROCCTL
. 
ENDREPLY BEEP
. 
. NOW DISPLAY SAME AS SHOWTOTS PARAGRAPH BUT NO MORE RETRIES
. 
         DISPLAY   *ES,*P01:12,"ORDER NUMBER ",INVKEY," TOTAL NET ":
                               "ORDER IS: $",TOTDOLS
. 
         MOVE      "00"  TO  INVLINES
         MOVE      "0"   TO  TOTDOLS
         MOVE      "0"   TO  FRTALLOW
. 
. 
CLEARMOV MOVE      "0" TO MULTICNT
         MOVE      "0" TO RECCNTR
         MOVE      "0" TO ILIVCNTR
         MOVE      "0" TO IRECCNTR
         MOVE      " " TO MULTIFLG
         MOVE      " " TO MD1
         MOVE      " " TO MD2
         MOVE      " " TO MD3
         MOVE      " " TO MD4
         MOVE      " " TO MD5
         MOVE      " " TO MD6
         MOVE      " " TO MD7
         MOVE      " " TO MD8
         MOVE      " " TO MD9
         MOVE      " " TO MD10
. 
         KEYIN     *P01:16,*EF,"DO YOU HAVE ANY MORE ORDERS TO ADD":
                               " AT THIS TIME ?  (Y)es or (N)o  ",REPLY
         CMATCH    "Y" TO REPLY
         GOTO      DISPLAY IF EQUAL
         CMATCH    "N" TO REPLY
         GOTO      TAPTERM IF EQUAL
.
          BEEP
          GOTO       CLEARMOV
.
TAPTERM  CHAIN     "APPINVC1"
. 
NOINVOIC  BEEP
          DISPLAY   *ES,*P01:12,"DISK IO ERROR ON ORDER TOTAL UPDATE":
                       *N,"SERIOUS PROBLEM, CALL BOB BACHARACH, MSC":
                       *N,"516-698-5577",*W,*W,*W
         GOTO      NOINVOIC
. 
         INCLUDE   CONTRLIO
. 
         INCLUDE   CUSTIO
. 
         INCLUDE   STDIOERR
. 
         INCLUDE   STDFMTER
. 
         INCLUDE   STDRANGE
. 
         INCLUDE   STDPARER
. 
         STOP
