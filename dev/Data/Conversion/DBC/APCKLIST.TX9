. 
. MODIFIED 3/13/83 TO TAKE THE ACTUAL CHECK NUMBER FROM	THE DATA FILE
. RATHER THAN REQUIRING	THAT THE FIRST NUMBER BE INPUT
. 
	 INC	   COMMON
	 INC	   APMASTFD
APCHECKS FILE
CKM	 DIM	   2
CKD	 DIM	   2
CKY	 DIM	   2
AMT	 FORM	   8.2
DISA	 FORM	   5.2
SEQ	 FORM	   "-1"
LINES	 FORM	   "51"
PAGE	 FORM	   2
APMASTER IFILE	   KEYL=6,UNCOMPRESSED
CNAME	 DIM	   25
RM	 DIM	   2
RD	 DIM	   2
RY	 DIM	   2
TOT	 FORM	   10.2
TOTDIS	 FORM	   6.2
CKNO	 FORM	   6
CKNUMBER DIM	   6
DATEDESC DIM	   15
DATERROR DIM	   1
MM	 DIM	   2
DD	 DIM	   2
YY	 DIM	   2
FM	 FORM	   2
FD	 FORM	   2
FY	 FORM	   2
BKUPFLG	 DIM	   1
DATFLG	 DIM	   1
.
ROLLNAME INIT	   "ROLLFILE/SYST:W"
REPLY	 DIM	   1
. 
DISPLAY	 DISPLAY   *ES,*P1:1,*HON,"ACCOUNTS PAYABLE SYSTEM - PRINT CHECK ":
		   "LISTING ",*HOFF
	 BEEP
	 MOVE	   " " TO REPLY
	 KEYIN	   *P01:10,"MOUNT STOCK	PAPER ON THE PRINTER - PRESS ":
		   "ENTER KEY WHEN READY! ",REPLY
	 MOVE	   " " TO REPLY
. 
	 OPEN	   APCHECKS,"C:\DBC\APCHECKS.TXT"
	 OPEN	   APMASTER,"C:\DBC\APMASTER.ISI"
.
	 MOVE	   "000000",ACCKEY
	 READ	   APMASTER,ACCKEY;ACCNO,CNAME
	 MATCH	   ACCKEY TO ACCNO
	 GOTO	   A0 IF EQUAL
	 BEEP
	 DISPLAY   *P01:24,*HON,"I WILL	AUTOMATICALLY RE-INDEX THE ACCOUNTS":
		   " PAYABLE MASTER FILE NOW!",*W2
	 ROLLOUT   "CHAIN APMSTIDX",ROLLNAME
	 GOTO	   DISPLAY
. 
A0	 MOVE	   "X",BKUPFLG
. 
	 MOVE	   TODAY TO MM
	 RESET	   TODAY TO 4
	 MOVE	   TODAY TO DD
	 RESET	   TODAY TO 7
	 MOVE	   TODAY TO YY
	 RESET	   TODAY TO 1
	 REP	   " 0"	 IN MM
	 MOVE	   MM TO RM
	 MOVE	   DD TO RD
	 MOVE	   YY TO RY
. 
........ KEYIN	   *R,*P1:24,"BEGINNING	CHECK ## = ",CKNO
. 
A1	 READ	   APCHECKS,SEQ;ACCNO,CKM,CKD,CKY,NAME,AMT,DISA,CKNUMBER
. 
. NOTE THAT WE ARE READING THE CHECK NUMBER !
. 
	 GOTO	   EOJ IF OVER
	 COMPARE   "51",LINES
	 GOTO	   A2 IF LESS
	 ADD	   "1",PAGE
	 PRINT	   *F,*28,CNAME
	 PRINT	   *26,"ACCOUNTS PAYABLE CHECK LISTING"
	 PRINT	   *34,"FOR: ",RM,"/",RD,"/",RY,*70,"PAGE: ",PAGE
	 PRINT	   " "
	 PRINT	   *3,"VENDOR",*14,"CHECK##",*27,"DATE",*48,"NAME":
		   *70,"AMOUNT",*82,"DISCOUNT"
	 PRINT	   " "
	 MOVE	   "0",LINES
A2	 PRINT	   *3,ACCNO,*14,CKNUMBER,*25,CKM,"/",CKD,"/",CKY,*38,NAME:
		   *68,AMT,*82,DISA
	 ADD	   "1",CKNO
	 ADD	   "1",LINES
	 ADD	   AMT,TOT
	 ADD	   DISA,TOTDIS
	 GOTO	   A1
EOJ	 PRINT	   *66,"-------------",*81,"---------"
	 PRINT	   *66,TOT,*81,TOTDIS
	 PRINT	   *F
	 CLOSE	   APCHECKS
	 RELEASE
. 
	 MOVE	   "2" TO TESTONE
	 CHAIN	   "APCASHRQ"
. 
	 INC	   GETDATE
