...................................................................
.    BTCHDEPS/TEXT - DISPLAY TOTAL CASH	DEPOSITS BY DATE RANGE	  .
.    FEB 5, 1991 - MICRO SYSTEMS CONSULTANTS			  .
...................................................................
.
	 INCLUDE   COMMON.TXT
.
	 INCLUDE   BATCHFD2.TXT
.
	 INCLUDE   CUSTMAST.TXT
.
CUSTFILE IFILE	   KEYL=4,UNCOMPRESSED
CSTNAME  INIT      "ACCOUNTS.ISI"
.
REPLY	 DIM	   1
ROW	 FORM	   2
DROW	 FORM	   2
COL	 FORM	   2
COLSW	 DIM	   1
CHKSUM	 FORM	   6.2
NEWBATCH FORM	   5
DATE8	 DIM	   8
TEST2	 FORM	   2
ASTER12	 INIT	   "************"
MM	 DIM	   2
DD	 DIM	   2
YY	 DIM	   2
PDATE	 DIM	   8
IOSW	 DIM	   1
ACCTIN	 DIM	   4
KEYLINE	 DIM	   2
LINENO	 FORM	   2
CAMTIN	 FORM	   6.2
ERRTYPE	 DIM	   10
CHKCNT	 FORM	   2
LASTROW	 FORM	   2
DATE1	 DIM	   8
DATE1X	 DIM	   6
DATE1N	 FORM	   6
DATE2	 DIM	   8
DATE2X	 DIM	   6
DATE2N	 FORM	   6
FORM6	 FORM	   6
PTIME	 DIM	   9
PAGES	 FORM	   3
LINES	 FORM	   2
BTCHDONE INIT	   "N"
MONTHTOT FORM	   8.2
DAYTOT	 FORM	   8.2
EOJSW	 INIT	   "N"
FIRSTSW	 INIT	   "Y"
SAVEDATE DIM	   6
SVPDATE	 DIM	   8
.
	 DISPLAY   *ES,*HON,*P08:01,"ACCOUNTS RECEIVABLE":
			       " SYSTEM	- DISPLAY CASH DEPOSITS	BY DATE	RANGE":
		       *P01:04,"IF YOU ENTERED THIS PROGRAM ERRONEOUSLY	":
			       "YOU MAY	EXIT BY	PRESSING F5 NOW",*HOFF
.
	 BEEP
	 KEYIN	   *P01:10,"PRESS ENTER	TO DISPLAY DEPOSIT TOTALS ",REPLY
	 GOTO	   EOJ	IF  F5
.
	 CALL	   OPENCUST
	 CLOCK	   TIME	 TO  PTIME
	 MOVE	   " "	 TO  COLSW
	 GOTO	   START
.
	 INCLUDE   DATESCHK.TXT
.
NOZERO	 BEEP
	 DISPLAY   *P1:23,*EL,*HON,"NO BATCH CONTROL RECORD - ABORTING!":
		   *W2,*HOFF
	 CHAIN	   "CASHLDGR"
.
START	 CALL	   BTCHOPEN
.
	 DISPLAY   *P1:6,*EF
.
WHCHBTCH CALL	   CLRBATCH
	 MOVE	   "0.00"  TO  MONTHTOT
.
DATE1	 DISPLAY   *P1:10,*EF:
		   *P1:10,*HON,"ENTER  START DATE FOR CASH SUM (MM/DD/YY) ":
		   *HOFF
.
	 CALL	   ENTERDT
.
	 MOVE	   BTCHDATE  TO	 DATE1X
	 MOVE	   BTCHDATE  TO	 DATE1N
.
	 CLEAR	   DATE1
	 APPEND	   MM  TO DATE1
	 APPEND	   "/" TO DATE1
	 APPEND	   DD  TO DATE1
	 APPEND	   "/" TO DATE1
	 APPEND	   YY  TO DATE1
	 RESET	   DATE1  TO  1
	 DISPLAY   *P50:10,*HON,DATE1,*HOFF
.
	 DISPLAY   *P1:12,*EF,*HON,"ENTER ENDING DATE FOR CASH SUM ":
		   "(MM/DD/YY) ",*HOFF
.
	 CALL	   ENTERDT
.
	 MOVE	   BTCHDATE  TO	 DATE2X
	 MOVE	   BTCHDATE  TO	 DATE2N
.
	 CLEAR	   DATE2
	 APPEND	   MM  TO DATE2
	 APPEND	   "/" TO DATE2
	 APPEND	   DD  TO DATE2
	 APPEND	   "/" TO DATE2
	 APPEND	   YY  TO DATE2
	 RESET	   DATE2  TO  1
.
	 DISPLAY   *P50:12,*HON,DATE2,*HOFF,*P1:17,*EF
.
	 MOVE	   " " TO REPLY
	 KEYIN	   *P1:20,"DATES CORRECT? (Y/N)	",REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   SETKEY IF EQUAL
	 BEEP
	 GOTO	   DATE1
.
.
SETKEY	 CLEAR	   BTCHRDKY
         APPEND    "00010100000" TO BTCHRDKY
	 RESET	   BTCHRDKY    TO  1
.
	 READ	   BATCHFL,BTCHRDKY;THISBTCH,BSTATUS
.
	 DISPLAY   *P1:23,*HON,*BLINKON,"CALCULATING ...",*HOFF
RDLOOP	 CALL	   BTCHRDKS
	 GOTO	   EOJ	 IF  OVER
.
CHKDATE1 MOVE	   BTCHDATE TO	FORM6
.
	 COMPARE   DATE1N   TO	FORM6
	 GOTO	   MAKEDTLS   IF  EQUAL
	 GOTO	   RDLOOP   IF	LESS	(CAN'T REALLY HAPPEN!)
.
CHKDATE2 COMPARE   DATE2N   TO	FORM6
	 GOTO	   MAKEDTLS   IF  EQUAL
	 GOTO	   EOJ	 IF  NOT LESS
.
	 GOTO	   MAKEDTLS
.
................................................................
	 INCLUDE   BATCHIO.TXT
	 INCLUDE   STDIOERR.TXT
	 INCLUDE   STDPARER.TXT
	 INCLUDE   STDFMTER.TXT
	 INCLUDE   STDRANGE.TXT
	 INCLUDE   CUSTIO.TXT
.
MAKEDTLS MOVE	   BCHECKS  TO	NEWBATCH
	 ADD	   BATCHTOT TO	MONTHTOT
.
	 GOTO	   RDLOOP
.
.........................................................
.
CLRBATCH TRAPCLR   PARITY
	 MOVE	   "0" TO BTCHINDX
.
CLR1	 ADD	   "1"	 TO  BTCHINDX
	 COMPARE   "21"	 TO  BTCHINDX
.
	 GOTO	   CLREXIT   IF	 EQUAL
.
. BLANK	THE ACCOUNT AND	ZERO THE AMOUNT	FIELDS
.
	 MOVE	   "0.00"  TO  CHECKAMT
	 MOVE	   "	"  TO  ACCTARG
	 CALL	   STORBTCH
	 GOTO	   CLR1
.
CLREXIT	 MOVE	   "00"	   TO  BTCHINDX
    	 MOVE	   "03"	   TO  LASTROW
	 RETURN
.........................................
.
SETDATE	 MOVE	   BTCHDATE TO YY
	 RESET	   BTCHDATE TO 3
	 MOVE	   BTCHDATE TO MM
	 REP	   " 0"	    IN MM
	 RESET	   BTCHDATE TO 5
	 MOVE	   BTCHDATE TO DD
	 RESET	   BTCHDATE TO 1
.
	 CLEAR	   SVPDATE
	 APPEND	   MM  TO SVPDATE
	 APPEND	   "/" TO SVPDATE
	 APPEND	   DD  TO SVPDATE
	 APPEND	   "/" TO SVPDATE
	 APPEND	   YY  TO SVPDATE
	 RESET	   SVPDATE TO 1
.
	 RETURN
.
EOJ	 DISPLAY   *P1:04,*EF:
		   *HON:
		   *P04:10,ASTER12,ASTER12,ASTER12,ASTER12,ASTER12,ASTER12:
		   *P04:11,"*	   ":
		   *P11:11," TOTAL CHECK DEPOSITS FROM ",DATE1," TO ":
			   DATE2," = $":
			   MONTHTOT:
			   "  *":
		   *P04:12,ASTER12,ASTER12,ASTER12,ASTER12,ASTER12,ASTER12:
		   *HOFF
	 MOVE	   " " TO REPLY
	 KEYIN	   *P1:23,*EL,"PRESS  F1 FOR MORE DATES,  F5 WHEN DONE",REPLY
.
	 GOTO	   WHCHBTCH  IF	F1
	 GOTO	   EOJEND IF F5
	 GOTO	   EOJ
.
EOJEND	 CHAIN	   "CASHLDGR"
.
