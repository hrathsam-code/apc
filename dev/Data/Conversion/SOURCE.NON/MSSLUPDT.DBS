.....................................................................
. MSSLUPDT/TXT - MODIFY	PARTS/PURCHASE/SALES HISTORY FILES  .
.								    .
	 INCLUDE   MSCBRAG.TXT
.								    .
. THIS PROGRAM PERMITS INQUIRY MODIFICATIONS OF	PART NUMBERS, AND .
. ASSOCIATED PURCHASE/SALE/QUOTATION HISTORY . .
.								    .
.....................................................................
. 
. MODIFY 4/18/84 TO REQUIRE THAT A VALID PART NUMBER BE	ENTERED
.		 AND DISPLAYED PRIOR TO	ALLOWING GENERIC DISPLAYS
. 
. 
	 INCLUDE   COMMON
. 
IOSW	 DIM	   1
ALLSW	 DIM	   1
XREF1	 DIM	   20
XREF2	 DIM	   20
. 
APMASTER IFILE	    KEYL=6,UNCOMPRESSED
. 
CUSTFILE IFILE	    KEYL=4,UNCOMPRESSED
CSTNAME	 INIT	   "C:\DBC\ACCOUNTS.ISI"
. 
	 INCLUDE   MSPARTSF.TXT
. 
	 INCLUDE   MSPURCHF.TXT
. 
	 INCLUDE   MSSALESF.TXT
. 
. 
CINDX1	 FORM	   3
DT6	 DIM	   6
MIN	 DIM	   2
DIN	 DIM	   2
YIN	 DIM	   2
FORM2	 FORM	   2
WHCH	 DIM	   1
REPLY	 DIM	   1
. 
TYPEIN	 DIM	   1
POIN	 DIM	   18
UOPIN	 DIM	   2
QTYIN	 FORM	   5.2
PRCIN	 FORM	   5.2
DATEIN	 DIM	   6
VENDIN	 DIM	   6
NAMEIN	 DIM	   20
DNAME	 DIM	   20
. 
PORTX	 DIM	   2
. 
NROW	 FORM	   2
NCOL	 FORM	   2
. 
	 INCLUDE   APMASTFD
. 
	 INCLUDE   CUSTMAST
. 
BLANK30	 INIT	   "				  "
SAVEPART DIM	   20
DISPQTY	 FORM	   5
. 
. KEYTABLE - THIS TABLE	STORES 14 KEYS FOR ACCESS / UPDATE OF RECORDS
.	     IN	THE MSSALES / MSPURCHS/ISI FILES
. 
KEY01	 DIM	   30
KEY02	 DIM	   30
KEY03	 DIM	   30
KEY04	 DIM	   30
KEY05	 DIM	   30
KEY06	 DIM	   30
KEY07	 DIM	   30
KEY08	 DIM	   30
KEY09	 DIM	   30
KEY10	 DIM	   30
KEY11	 DIM	   30
KEY12	 DIM	   30
KEY13	 DIM	   30
KEY14	 DIM	   30
. 
STOREKEY DIM	   30
STORINDX FORM	   2
LETTERS	 INIT	   "ABCDEFGHIJKLMN"
LETRINDX FORM	   2
DLETTER	 DIM	   1
. 
SCANKEY	 DIM	   30
SCANINDX FORM	   2
LASTKEY	 DIM	   30
LOOPSW	 DIM	   1
.....................................................
. 
DISPLAY	DISPLAY	   *ES,*P09:01,"INVENTORY HISTORY SYSTEM - MODIFY HISTO":
		   "RY PROGRAM",*P73:01,TODAY:
		   *P01:02,"PART NUMBER",*P31:02,"CROSS-REF PART NO.1":
		   *P61:02,"CROSS-REF PART NO.2":
		   *P01:04,"SIZE",*P38:04,"DESCRIPTION":
		   *P25:05,"PURCHASE/SALE/QUOTATION HISTORY":
		   *P01:06,"TYP	PURCHASE ORDER##    UOP	QUANTITY    PRICE   ":
			   "DATE VND/CST##  NAME":
		   *P13:20,"TYPE CODES",*P50:20,"UNIT OF PURCHASE":
		   *P01:21,"P=PURCHASE	R=VENDOR QUOTATION":
		   *P41:21,"EA=EACH,C=HUND,M=THOU,OZ=OUNCE,LB=POUND":
		   *P01:22,"S=SALE	Q=QUOTE	TO CUSTOMER":
		   *P41:22,"BG=BAG,BX=BOX,FT=FOOT"
. 
	 GOTO	   START
. 
	 INCLUDE   MSPARTIO
. 
	 INCLUDE   MSSALEIO
. 
	 INCLUDE   MSPOIO
. 
	 INCLUDE   STDIOERR
. 
	 INCLUDE   STDFMTER
. 
	 INCLUDE   STDPARER
. 
	 INCLUDE   STDRANGE
. 
	 INCLUDE   CUSTIO
. 
START	 CALL	   CLRTRAPS
. 
	 TRAP	   STDIOERR IF IO
	 TRAP	   STDPARER IF PARITY
	 TRAP	   STDFMTER IF FORMAT
	 TRAP	   STDRANGE IF RANGE
. 
	 MOVE	   PORTN  TO  PORTX
	 REP	   " 0"	  IN  PORTX
. 
NAMEOK	 CALL	   OPENCUST
	 CALL	   MSPTOPEN
	 CALL	   MSSLOPEN
	 CALL	   MSPOOPEN
. 
	 OPEN	   APMASTER,"C:\DBC\APMASTER.ISI"
. 
DOMORE	 CALL	   CLRPART
	 KEYIN	   *P1:23,*EL,*HON,"PRESS F1 TO	DISPLAY	PART / F5 TO ":
		   "TERMINATE ",*HOFF,REPLY
. 
	 MOVE	   " " TO ALLSW
	 GOTO	   EOJ IF F5
	 GOTO	   PARTIN IF F1
	 BEEP
	 GOTO	   DOMORE
. 
PARTIN	 CALL	   CLRPART
	 CALL	   CLRTABLE
. 
PARTIN1	 KEYIN	   *P01:03,MSPARTNO
	 GOTO	   EOJ	IF  F5
. 
...............	   MOVE	     MSPARTNO  TO  SAVEPART
. 
	 DISPLAY   *P01:03,MSPARTNO
.
	 DISPLAY   *P1:23,*EL
. 
	 MOVE	   MSPARTNO  TO	 MSPARTKY
. 
................... CALL      MSPTREAD
. 
	 INCLUDE   MSPTSIO.TXT
. 
	 GOTO	   INVALID  IF	OVER
. 
.  4/18/84 SAVE	PART NUMBER HERE TO TRIGGER GENERIC SEARCH
. 
	 MOVE	   MSPARTKY TO	SAVEPART
. 
	 CMATCH	   "H",IOSW
	 GOTO	   DISPPART  IF	 EQUAL
. 
INVALID	 BEEP
	 DISPLAY   *P1:23,*EL,*HON,"INVALID PART NUMBER!  TRY AGAIN! ":
		   *HOFF,*W3,*P1:23,*EL
	 GOTO	   DOMORE
. 
RDKSALL	 CMOVE	   "A"	TO  ALLSW
. 
READKS	 CALL	   MSPTRDKS
	 CMATCH	   "M",IOSW
	 GOTO	   EOJ IF EQUAL	   (THAT MEANS THERE AREN'T ANY	MORE PARTS)
. 
. NOW CHECK IF THE PART	WHICH HAS BEEN READ KEY	SEQUENTIALLY IS	A MATCH
. WITH THE PORTION OF THE KEY VALUE WHICH WAS ENTERED!
. 
	 CMATCH	   "A",ALLSW
	 GOTO	   DISPPART IF EQUAL
. 
	 MATCH	   SAVEPART TO MSPARTNO
	 GOTO	   DISPPART IF EQUAL
. 
. FALLTHRU MEANS THERE IS A BREAK ON THE GENERIC KEY VALUE
. SO GO	TO DETERMINE IF	USER IS	DONE
. 
	 GOTO	   DOMORE
. 
. 
DISPPART DISPLAY   *P01:03,MSPARTNO:
		   *P31:03,MSXRFPT1:
		   *P61:03,MSXRFPT2:
		   *P06:04,MSSIZE:
		   *P50:04,MSDESC
. 
	 MOVE	   MSXRFPT1  TO	 XREF1
	 MOVE	   MSXRFPT2  TO	 XREF2
. 
	 MOVE	   "6"	TO  NROW
	 GOTO	   DATAIN
. 
. 
CLRPART	 DISPLAY   *P01:03,*HOFF,*EL,*P06:04,BLANK30,*P50:04,BLANK30
	 MOVE	   " " TO MSPARTNO
	 MOVE	   " " TO MSXRFPT1
	 MOVE	   " " TO MSXRFPT2
	 MOVE	   " " TO MSSIZE
	 MOVE	   " " TO MSDESC
	 MOVE	   " " TO XREF1
	 MOVE	   " " TO XREF2
	 CALL	   RECYCLE
	 RETURN
. 
CLRTABLE MOVE	   "0"	TO  STORINDX
CLRT	 ADD	   "1"	TO  STORINDX
	 COMPARE   "15"	TO  STORINDX
	 GOTO	   CLREXIT  IF	EQUAL
. 
	 MOVE	   BLANK30  TO	STOREKEY
	 STORE	   STOREKEY INTO STORINDX OF KEY01,KEY02,KEY03,KEY04,KEY05:
					     KEY06,KEY07,KEY08,KEY09,KEY10:
					     KEY11,KEY12,KEY13,KEY14
	 GOTO	   CLRT
. 
CLREXIT	 MOVE	   "0" TO STORINDX
	 RETURN
. 
DATAIN	 ADD	   "1"	TO  NROW
	 COMPARE   "20"	TO  NROW
	 CALL	   RECYCLE  IF NOT LESS
	 GOTO	   GETDATA
. 
RECYCLE	 MOVE	   "6"	TO  NROW
	 RESET	   LETTERS
	 MOVE	   " "	TO  DLETTER
	 CALL	   CLRTABLE  (THIS IS TO RESET FOR 2ND AND SUBSEQUENT PAGES)
RCYCLE	 ADD	   "1"	TO  NROW
	 COMPARE   "20"	TO  NROW
	 GOTO	   CYCLOUT  IF	EQUAL
	 DISPLAY   *P01:NROW,*HOFF,*EL
	 GOTO	   RCYCLE
. 
CYCLOUT	 MOVE	   "7"	TO  NROW
	 RETURN
. 
. 
. NOTE READ THE	PURCHASES FILE,	FOLLOWED BY THE	SALES FILE
. FOR ALL OCCURRENCES, BEGINNING WITH THE KEYED	PART NUMBER
. AND FOLLOWED BY THE FIRST CROSS REFERENCE PART NUMBER,
. THEN THE SECOND CROSS	REFERENCE PART NUMBER
. 
GETDATA	 CLEAR	   MSPOKEY
	 APPEND	   MSPARTNO  TO	 MSPOKEY
	 APPEND	   "	     "	 TO  MSPOKEY
	 RESET	   MSPOKEY
	 READ	   MSPURCHS,MSPOKEY;MSPOPART
. 
POLOOP1	 CALL	   MSPORDKS
. 
	 CMOVE	   "P" TO LOOPSW
	 CMATCH	   "H",IOSW
	 GOTO	   GETSALES IF NOT EQUAL
. 
	 MATCH	   MSPOPART TO MSPARTNO
	 GOTO	   GETSALES IF NOT EQUAL   (NO MORE FOR	THIS PART)
.
. NOTE 2/5/86 -	BYPASS RECORD IF CODED AS LOGICAL DELETE
.		WITH AN	ASTERISK IN THE	POTYPE FIELD
.
	 CMATCH	   "*" TO MSPOTYPE
	 GOTO	   POLOOP1 IF EQUAL
................................................
.
. 
. NOW STORE THE	KEY VALUE IN TABLE FOR LATER ACCESS AND	UPDATE
. 
SETTABLE CLEAR	   STOREKEY
. 
ST1	 APPEND	   MSPOTYPE  TO	 STOREKEY
	 APPEND	   MSPOPART  TO	 STOREKEY
	 APPEND	   MSPOJULD  TO	 STOREKEY
	 APPEND	   MSPOSUFX  TO	 STOREKEY
	 RESET	   STOREKEY  TO	 1
	 MOVE	   STOREKEY  TO	 LASTKEY
. 
	 MOVE	   NROW	     TO	 STORINDX
	 SUBTRACT  "6"	   FROM	 STORINDX
. 
	 STORE	   STOREKEY  INTO  STORINDX  OF	 KEY01,KEY02,KEY03,KEY04:
						 KEY05,KEY06,KEY07,KEY08:
						 KEY09,KEY10,KEY11,KEY12:
						 KEY13,KEY14
. 
	 MOVE	   LETTERS  TO	DLETTER
	 BUMP	   LETTERS	  (SET IT TO NEXT ONE)
. 
	 MOVE	   MSPOQTY  TO DISPQTY
. 
	 DISPLAY   *P1:NROW,DLETTER,"*",MSPOTYPE,*P5:NROW,MSPONUMB:
		   *P25:NROW,MSPOUOP,*P29:NROW,DISPQTY:
		   *P37:NROW,MSPOPRC,*P47:NROW,MSPODATE:
		   *P54:NROW,MSPOVEND,*P61:NROW,MSPOVNAM
. 
	 ADD	   "1" TO NROW
	 COMPARE   "20"	  TO  NROW
. 
. STOP AT NROW = 21
. 
	 GOTO	   STOPDISP IF EQUAL
	 GOTO	   POLOOP1
. 
STOPDISP KEYIN	   *P1:22,*EL,*HON,"TO DISPLAY ADDITIONAL DATA PRESS F1	":
		   *P1:23,*EL,"TO SELECT ITEM FOR MAINTENANCE  PRESS F3	":
		   *HOFF,REPLY
	 GOTO	   CHANGEIT IF F3
	 GOTO	   SHWMOR IF F1
	 BEEP
	 GOTO	   STOPDISP
.
SHWMOR	 CALL	   RECYCLE
	 CMATCH	   "P" TO LOOPSW
	 GOTO	   POLOOP1 IF EQUAL
	 CMATCH	   "S" TO LOOPSW
	 GOTO	   SLLOOP1 IF EQUAL
	 RETURN
. 
GETSALES CLEAR	   MSSALEKY
. 
	 APPEND	   MSPARTNO  TO	 MSSALEKY
	 APPEND	   "	     "	TO  MSSALEKY
	 RESET	   MSSALEKY
	 READ	   MSSALEFL,MSSALEKY;MSSLPART
. 
SLLOOP1	 CALL	   MSSLRDKS
	 CMOVE	   "S" TO LOOPSW
. 
	 MATCH	   MSSLPART TO MSPARTNO
	 GOTO	   CHANGEIT IF NOT EQUAL
. 
. NOTE 2/5/86 -	BYPASS RECORD IF AN ASTERISK EXISTS IN THE SLTYPE FIELD
.
	 CMATCH	   "*" TO MSSLTYPE
	 GOTO	   SLLOOP1 IF EQUAL
.............................................
.
SETBLE	 CLEAR	   STOREKEY
ST2	 APPEND	   MSSLTYPE TO STOREKEY
	 APPEND	   MSSLPART TO STOREKEY
	 APPEND	   MSSLJULD TO STOREKEY
	 APPEND	   MSSLSUFX TO STOREKEY
	 RESET	   STOREKEY TO 1
	 MOVE	   STOREKEY TO LASTKEY
. 
	 MOVE	   NROW	    TO STORINDX
	 SUBTRACT  "6"	  FROM STORINDX
. 
	 MOVE	   LETTERS  TO	DLETTER
	 BUMP	   LETTERS	(INCREMENT LETTER)
.
	 STORE	   STOREKEY INTO STORINDX OF KEY01,KEY02,KEY03,KEY04,KEY05:
					     KEY06,KEY07,KEY08,KEY09,KEY10:
					     KEY11,KEY12,KEY13,KEY14
. 
	 MOVE	   MSSLQTY  TO	DISPQTY
. 
. 
	 DISPLAY   *P1:NROW,DLETTER,*P3:NROW,MSSLTYPE,*P5:NROW,MSSLNUMB:
		   *P25:NROW,MSSLUOP,*P29:NROW,DISPQTY:
		   *P37:NROW,MSSLPRC,*P47:NROW,MSSLDATE:
		   *P54:NROW,MSSLCUST,*P61:NROW,MSSLCNAM
. 
	 ADD	   "1"	TO  NROW
	 COMPARE   "20"	TO  NROW
	 GOTO	   STOPDISP IF EQUAL
....	 CALL	   RECYCLE  IF	NOT  LESS
	 GOTO	   SLLOOP1
. 
. 
CHANGEIT BEEP
	 MOVE	   "00"	TO NCOL
.
	 COMPARE   "9",SECURITY
	 GOTO	   WHICHPT IF NOT EQUAL
. 
.	PRINT	  *1,"KEY01=",KEY01," KEY02=",KEY02," KEY03=",KEY03:
.		     " KEY04=",KEY04
.	PRINT	  *1,"KEY05=",KEY05," KEY06=",KEY06," KEY07=",KEY07:
.		     " KEY08=",KEY08
.	PRINT	  *1,"KEY09=",KEY09," KEY10=",KEY10," KEY11=",KEY11:
.		     " KEY12=",KEY12
.	PRINT	  *1,"KEY13=",KEY13," KEY14=",KEY14
. 
WHICHPT	 MOVE	   " " TO REPLY
	 DISPLAY   *P1:22,*EL,*P1:23,*EL
	 KEYIN	   *P1:23,"Select line by letter, press	F5 to finish":
		   " or	F1 to display more ",*P70:23,REPLY
	 GOTO	   DISPMORE IF F1
	 GOTO	   DOMORE   IF F5
. 
	 MOVE	   "00"	TO SCANINDX
	 RESET	   LETTERS TO 1
. 
LOOPSCAN ADD	   "1"	TO SCANINDX
. 
	 COMPARE   "15"	TO SCANINDX
	 GOTO	   BADLTR  IF NOT LESS
. 
	 CMATCH	   LETTERS TO REPLY
	 GOTO	   SCANLOAD IF EQUAL
. 
	 BUMP	   LETTERS
	 GOTO	   LOOPSCAN
. 
. FALLTHRU IS A	MATCH ON LETTER, SO TAKE SCANINDX AND USE IT TO
. LOAD KEY FROM	KEY TABLE
. 
SCANLOAD LOAD	   SCANKEY FROM	SCANINDX OF KEY01,KEY02,KEY03,KEY04:
					    KEY05,KEY06,KEY07,KEY08:
					    KEY09,KEY10,KEY11,KEY12:
					    KEY13,KEY14
. 
CMARND	 MOVE	   SCANKEY TO TYPEIN
. 
	 COMPARE   "9",SECURITY
	 GOTO	   CMARNDX IF NOT EQUAL
	 DISPLAY   *P40:23,*EL,SCANKEY
	 DISPLAY   *P75:23,TYPEIN,*W4
	 KEYIN	   *P78:23,REPLY
.
CMARNDX	 MATCH	   BLANK30 TO SCANKEY
	 GOTO	   BADLTR  IF EQUAL
. 
	 MOVE	   SCANINDX TO NROW
	 ADD	   "6"	    TO NROW
. 
	 GOTO	   DELETE
.....................................................................
. 
DISPMORE MOVE	   LASTKEY TO SCANKEY
	 CMATCH	   "S",SCANKEY
	 GOTO	   SETSALES IF EQUAL
	 CMATCH	   "P",SCANKEY
	 GOTO	   SETPURCH IF EQUAL
	 BEEP
	 KEYIN	   *P1:23,*HON,"E R R O	R  - TAP ENTER TO TERMINATE! ",REPLY:
		   *HOFF
	 CHAIN	   "MSHISTRY"
. 
SETSALES RESET	   SCANKEY TO 2
	 MOVE	   SCANKEY TO MSSALEKY
	 CALL	   MSSLREAD
	 CALL	   RECYCLE
	 RESET	   SCANKEY TO 1
	 GOTO	   SLLOOP1     (THIS GETS US GOING AGAIN!)
. 
SETPURCH RESET	   SCANKEY TO 2
	 MOVE	   SCANKEY TO MSPOKEY
	 CALL	   MSPOREAD
	 CALL	   RECYCLE
	 RESET	   SCANKEY TO 1
	 GOTO	   POLOOP1     (THIS GETS US GOING AGAIN!)
. 
. 
BADLTR	 BEEP
	 DISPLAY   *P1:23,*EL,"INVALID LETTER SELECTED,	RETRY! ",*W4
	 GOTO	   WHICHPT
. 
. 
. NOTE THAT THERE IS NO	NEED TO	RE-READ	THE PART FILE AS
. THE PART IS THE SAME....
. TO RESTORE THE READKS, MERELY	RESTORE	KEY14 AND READ,	FOLLOWED
. BY READKS TO THE HISTORY FILE, EITHER	SALES OR PURCHASES
. 
. 
. NOW CHECK IF ANY CROSS-REFERENCE PARTS EXIST!
. 
PO2	 MOVE	   MSPARTNO  TO	 SAVEPART
	 CMATCH	   " " TO XREF1
	 GOTO	   CHECKPT2 IF EOS
	 GOTO	   CHECKPT2 IF EQUAL
. 
. FALLTHRU MEANS THERE IS A CROSS-REFERENCE PART NUMBER!
. SO GET CHASE IT!
. 
	 MOVE	   XREF1  TO  MSPARTKY
	 CALL	   MSPTREAD
	 CMATCH	   "H",IOSW
	 GOTO	   CHECKPT2  IF	 NOT  EQUAL
	 CALL	   POSLLOOP
	 GOTO	   CHECKPT2
. 
. FALLTHRU MEANS WE HAVE FOUND A VALID CROSS-REFERENCE PART NUMBER
. 
POSLLOOP CLEAR	   MSPOKEY
	 APPEND	   MSPARTNO  TO	 MSPOKEY
	 APPEND	   "	     "	 TO  MSPOKEY
	 RESET	   MSPOKEY
	 READ	   MSPURCHS,MSPOKEY;MSPOPART
. 
POLOOP2	 CALL	   MSPORDKS
	 CMATCH	   "H",IOSW
	 GOTO	   SL2 IF NOT EQUAL  (IE END OF	FILE)
	 MATCH	   MSPOPART TO MSPARTNO
	 GOTO	   SL2 IF NOT EQUAL
.
.NOTE 2/5/86 - BYPASS RECORD IF	MSPOTYPE = "*"
.
	 CMATCH	   "*" TO MSPOTYPE
	 GOTO	   POLOOP2 IF EQUAL
. 
	 MOVE	   MSPOQTY  TO	DISPQTY
. 
. 
	 DISPLAY   *P1:NROW,*HON,"*",MSPOTYPE,*P5:NROW,MSPONUMB:
		   *P25:NROW,MSPOUOP,*P29:NROW,DISPQTY:
		   *P37:NROW,MSPOPRC,*P47:NROW,MSPODATE:
		   *P54:NROW,MSPOVEND,*P61:NROW,MSPOVNAM,*HOFF
. 
	 ADD	   "1"	TO  NROW
	 COMPARE   "20"	TO  NROW
	 GOTO	   STOPDISP IF EQUAL
....	 CALL	   RECYCLE  IF	NOT  LESS
	 GOTO	   POLOOP2
. 
SL2	 CLEAR	   MSSALEKY
	 APPEND	   MSPARTNO  TO	 MSSALEKY
	 APPEND	   "	     "	 TO  MSSALEKY
	 RESET	   MSSALEKY
	 READ	   MSSALEFL,MSSALEKY;MSSLPART
. 
SLLOOP2	 CALL	   MSSLRDKS
	 CMATCH	   "H",IOSW
	 GOTO	   POSLRTRN  IF	 NOT  EQUAL  (IE END OF	FILE)
	 MATCH	   MSSLPART  TO	 MSPARTNO
	 GOTO	   POSLRTRN  IF	 NOT  EQUAL
.
.NOTE 2/5/86 - BYPASS RECORD IF	MSSLTYPE = "*"
.
	 CMATCH	   "*" TO MSSLTYPE
	 GOTO	   SLLOOP2 IF EQUAL
.
..............................................
. 
	 MOVE	   MSSLQTY  TO	DISPQTY
. 
	 DISPLAY   *P2:NROW,MSSLTYPE,*P5:NROW,MSSLNUMB:
		   *P25:NROW,MSSLUOP,*P29:NROW,DISPQTY:
		   *P37:NROW,MSSLPRC,*P47:NROW,MSSLDATE:
		   *P54:NROW,MSSLCUST,*P61:NROW,MSSLCNAM
. 
	 ADD	   "1"	TO  NROW
	 COMPARE   "20"	TO  NROW
	 GOTO	   STOPDISP IF EQUAL
....	 CALL	   RECYCLE  IF	NOT  LESS
	 GOTO	   SLLOOP2
. 
POSLRTRN RETURN
. 
CHECKPT2 CMATCH	   " " TO XREF2
	 CALL	   STOPDISP  IF	 EOS
	 CALL	   STOPDISP  IF	 EQUAL
	 CMATCH	   " " TO XREF2
	 GOTO	   RESTRPRT  IF	EQUAL
. 
	 MOVE	   XREF2    TO	MSPARTKY
. 
	 CALL	   MSPTREAD
	 CMATCH	   "H",IOSW
	 GOTO	   RESTRPRT  IF	 NOT  EQUAL
. 
	 CALL	   POSLLOOP
	 CALL	   STOPDISP
. 
RESTRPRT MOVE	   SAVEPART  TO	 MSPARTKY
	 CALL	   MSPTREAD	 (TO RESTORE THE POSITION IN FILE)
. 
. NOW FALLTHRU TO DOMORE AND CHECK THE GENERIC SEARCH!
. 
	 GOTO	   DOMORE
. ...........
	 GOTO	   EOJ	IF F5
.
.
.
	 CMATCH	   "P",TYPEIN
	 GOTO	   GPO IF EQUAL
	 CMATCH	   "S",TYPEIN
	 GOTO	   GPO IF EQUAL
	 CMATCH	   "R",TYPEIN
	 GOTO	   GPO IF EQUAL
	 CMATCH	   "Q",TYPEIN
	 GOTO	   GPO IF EQUAL
	 BEEP
	 DISPLAY   *P02:NROW," "
	 GOTO	   GETDATA
. 
DELETE	 MOVE	   " " TO REPLY
	 KEYIN	   *P1:23,*EL,"DELETE THIS HISTORY RECORD ? (Y/N) ",REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   GETRID IF EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   GPO IF EQUAL
	 BEEP
	 GOTO	   DELETE
. 
GETRID	 MOVE	   " " TO REPLY
	 KEYIN	   *P1:23,*EL,"YOU REALLY WANT TO DELETE IT? (Y/N) ",REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   DELOK IF EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   GETRID IF NOT EQUAL
	 BEEP
	 DISPLAY   *P1:23,*EL,"I DIDN'T	THINK SO! ",*W3,*P1:23,*EL
	 GOTO	   GPO
. 
DELOK	 CMATCH	   "S",TYPEIN
	 GOTO	   DELETS IF EQUAL
	 CMATCH	   "Q",TYPEIN
	 GOTO	   DELETS IF EQUAL
	 CMATCH	   "P",TYPEIN
	 GOTO	   DELETP IF EQUAL
	 CMATCH	   "R",TYPEIN
	 GOTO	   DELETP IF EQUAL
DELBEEP	 BEEP
	 DISPLAY   *P1:23,*EL,"I DON'T KNOW WHAT TO DO!",*W2
	 GOTO	   DELBEEP
. 
DELETS	 RESET	   SCANKEY TO 2
	 MOVE	   SCANKEY TO MSSALEKY
........	DELETE	  MSSALEFL,MSSALEKY
	 CMOVE	   "*" TO MSSLTYPE
	 CALL	   MSSLUPDT
.
	 BEEP
	 DISPLAY   *P60:23,*EL,*HON,"RECORD DELETED",*HOFF,*W2:
		   *P60:23,*EL
.
......................................
	 RESET	   SCANKEY TO 1
	 GOTO	   WHATNEXT
. 
DELETP	 RESET	   SCANKEY TO 2
	 MOVE	   SCANKEY TO MSPOKEY
........	DELETE	  MSPURCHS,MSPOKEY
	 CMOVE	   "*" TO MSPOTYPE
	 CALL	   MSPOUPDT
................................
	 BEEP	   
	 DISPLAY   *P60:23,*EL,*HON,"RECORD DELETED",*HOFF,*W2:
		   *P60:23,*EL
.
	 RESET	   SCANKEY TO 1
	 GOTO	   WHATNEXT
. 
GPO	 DISPLAY   *P65:23,*EL,*P70:23,"TYPE=",TYPEIN,*W4
	 CMATCH	   "S",TYPEIN
	 GOTO	   MOVESALE IF EQUAL
	 CMATCH	   "Q",TYPEIN
	 GOTO	   MOVESALE IF EQUAL
	 GOTO	   MOVEPO
. 
MOVESALE RESET	   SCANKEY TO 2
	 MOVE	   SCANKEY TO MSSALEKY
	 CALL	   CLRTRAPS
	 CALL	   MSSLREAD
	 RESET	   SCANKEY TO 1
. 
	 MOVE	   MSSLNUMB TO POIN
	 MOVE	   MSSLUOP  TO UOPIN
	 MOVE	   MSSLQTY  TO QTYIN
	 MOVE	   MSSLQTY  TO DISPQTY
	 MOVE	   MSSLPRC  TO PRCIN
	 MOVE	   MSSLDATE TO DATEIN
	 MOVE	   MSSLCUST TO VENDIN
	 MOVE	   MSSLCNAM TO NAMEIN
	 GOTO	   GPO1
. 
MOVEPO	 CMATCH	   "P",TYPEIN
	 GOTO	   MVPO	IF EQUAL
	 CMATCH	   "R",TYPEIN
	 GOTO	   MVPO	IF EQUAL
	 GOTO	   MOVESALE
. 
MVPO	 RESET	   SCANKEY TO 2
	 MOVE	   SCANKEY TO MSPOKEY
	 CALL	   CLRTRAPS
	 CALL	   MSPOREAD
	 RESET	   SCANKEY TO 1
. 
	 MOVE	   MSPONUMB TO POIN
	 MOVE	   MSPOUOP  TO UOPIN
	 MOVE	   MSPOQTY  TO QTYIN
	 MOVE	   MSPOQTY  TO DISPQTY
	 MOVE	   MSPODATE TO DATEIN
	 MOVE	   MSPOVEND TO VENDIN
	 MOVE	   MSPOPRC  TO PRCIN
	 MOVE	   MSPOVNAM TO NAMEIN
	 GOTO	   GPO1
. 
GPO1	 DISPLAY   *P01:23,*EL
	 DISPLAY   *P05:NROW,POIN
	 KEYIN	   *P05:NROW,*RV,POIN
	 DISPLAY   *P05:NROW,POIN
. 
UOP	 DISPLAY   *P25:NROW,UOPIN
	 KEYIN	   *P25:NROW,*RV,UOPIN
. 
. VALIDATE UNIT	OF PURCHASE CODE
. 
	 MATCH	   "EA"	TO UOPIN
	 GOTO	   QTYIN   IF  EQUAL
	 CMATCH	   "C"	TO UOPIN
	 GOTO	   QTYIN   IF  EQUAL
	 CMATCH	   "M"	TO UOPIN
	 GOTO	   QTYIN   IF  EQUAL
	 MATCH	   "OZ"	TO UOPIN
	 GOTO	   QTYIN   IF  EQUAL
	 MATCH	   "LB"	TO UOPIN
	 GOTO	   QTYIN   IF  EQUAL
	 MATCH	   "BG"	TO UOPIN
	 GOTO	   QTYIN   IF  EQUAL
	 MATCH	   "BX"	TO UOPIN
	 GOTO	   QTYIN   IF  EQUAL
	 MATCH	   "FT"	TO UOPIN
	 GOTO	   QTYIN   IF  EQUAL
	 BEEP
	 DISPLAY   *P1:23,*EL,*HON,"UNIT OF PURCHASE INVALID",*W2:
		   *HOFF,*P1:23,*EL
	 DISPLAY   *P25:NROW,"	"
	 GOTO	   UOP
. 
QTYIN	 DISPLAY   *P29:NROW,DISPQTY
	 KEYIN	   *P29:NROW,*RV,QTYIN
	 MOVE	   QTYIN TO DISPQTY
	 DISPLAY   *P29:NROW,DISPQTY
. 
PRCIN	 DISPLAY   *P37:NROW,PRCIN
	 KEYIN	   *P37:NROW,*RV,PRCIN
	 DISPLAY   *P37:NROW,PRCIN
. 
DATEIN	 DISPLAY   *P47:NROW,DATEIN
	 MOVE	   DATEIN TO MIN
	 RESET	   DATEIN TO 3
	 MOVE	   DATEIN TO DIN
	 RESET	   DATEIN TO 5
	 MOVE	   DATEIN TO YIN
	 RESET	   DATEIN TO 1
	 KEYIN	   *P47:NROW,*RV,*DE,*+,MIN,*RV,*DE,*+,DIN,*RV,*DE,*-,YIN
. 
	 MOVE	   MIN	TO  FORM2
	 COMPARE   "01"	TO  FORM2
	 GOTO	   DATERR   IF	LESS
         COMPARE   "13" TO  FORM2
	 GOTO	   DATERR   IF	NOT  LESS
. 
	 MOVE	   DIN	TO  FORM2
	 COMPARE   "01"	TO  FORM2
	 GOTO	   DATERR   IF	LESS
	 COMPARE   "32"	TO  FORM2
	 GOTO	   DATERR   IF	NOT  LESS
. 
...         MOVE      YIN  TO  FORM2
...         COMPARE   "80" TO  FORM2
...         GOTO      DATERR  IF  LESS
. 
	 CLEAR	   DATEIN
	 APPEND	   MIN	TO  DATEIN
	 APPEND	   DIN	TO  DATEIN
	 APPEND	   YIN	TO  DATEIN
	 LENSET	   DATEIN
	 RESET	   DATEIN
	 REPLACE   " 0"	IN  DATEIN
. 
. NOW SET UP KEY FIELD KNOWN AS	DATEIN
. 
	 CLEAR	   DT6
. 
         RESET     TODAY  TO  5
         APPEND    TODAY  TO  DT6
. 
	 RESET	   DT6	  TO  2
	 RESET	   TODAY  TO  1
	 APPEND	   TODAY  TO  DT6
. 
	 RESET	   DT6	  TO  4
         RESET     TODAY  TO  3
	 APPEND	   TODAY  TO  DT6
. 
	 RESET	   DT6	  TO  6
	 LENSET	   DT6
. 
	 RESET	   DT6	  TO  1
	 RESET	   TODAY  TO  1
	 REP	   " 0"	  IN  DT6
	 GOTO	   GVEND
. 
DATERR	 BEEP
	 DISPLAY   *P1:23,*EL,*HON,"INVALID DATE",*W2,*HOFF,*P1:23,*EL
	 DISPLAY   *P47:NROW,"	    "
	 GOTO	   DATEIN
. 
GVEND	 DISPLAY   *P54:NROW,VENDIN
	 KEYIN	   *P54:NROW,*RV,*DE,VENDIN
. 
. NOTE ...  IF	ZERO IS	IN THE VENDOR CODE FIELD, IT MEANS THAT	NO VENDOR
.	    CODE IS AVAILABLE, SO PERMIT KEYIN OF THE VENDOR NAME.
. 
	 MATCH	   "000000"  TO	 VENDIN
	 GOTO	   NAMEIN    IF	 EQUAL
. 
. FALLTHRU MEANS THEY KEYED A VENDOR/CUSTOMER NUMBER
. FIND OUT WHICH BY TYPE CODE
. 
WHICH	 CMATCH	   "P",TYPEIN
	 GOTO	   VENDOR IF EQUAL
	 CMATCH	   "R",TYPEIN
	 GOTO	   VENDOR IF EQUAL
. 
. FALLTHRU IS CUSTOMER
. 
	 MOVE	   "C" TO WHCH
     	 MOVE	   VENDIN TO CUSTKEY
	 CALL	   GETCUST
	 CMATCH	   "H",IOSW
	 GOTO	   DCUST IF EQUAL
	 BEEP
	 DISPLAY   *P1:23,*EL,*HON,"CUSTOMER NOT ON FILE",*W2,*HOFF,*P1:23,*EL
	 GOTO	   GVEND
. 
DCUST	 MOVE	   CUSTNAME TO NAMEIN
	 DISPLAY   *P61:NROW,NAMEIN
	 GOTO	   CHKDATA
. 
VENDOR	 MOVE	   "V" TO WHCH
	 MOVE	   VENDIN TO ACCKEY
. 
	 INCLUDE   APMASTRD
. 
	 GOTO	   VNDNG IF OVER
	 MOVE	   NAME	 TO NAMEIN
	 DISPLAY   *P61:NROW,NAMEIN
	 GOTO	   CHKDATA
. 
VNDNG	 BEEP
	 DISPLAY   *P1:23,*EL,*HON,"VENDOR NOT ON FILE",*W2,*HOFF,*P1:23,*EL
	 GOTO	   GVEND
. 
. 
NAMEIN	 KEYIN	   *P60:NROW,*RV,NAMEIN
	 CMATCH	   "P",TYPEIN
	 GOTO	   NM1 IF EQUAL
	 CMATCH	   "R",TYPEIN 
	 GOTO	   NM1 IF EQUAL
. 
	 MOVE	   "C" TO WHCH
	 GOTO	   CHKDATA
. 
NM1	 MOVE	   "V" TO WHCH
. 
CHKDATA	 MOVE	   " " TO REPLY
	 KEYIN	   *P1:23,*EL,*HON,"IS ALL DATA	CORRECT	(Y/N) ?	",REPLY:
		   *HOFF
	 CMATCH	   "Y",REPLY
	 GOTO	   RECORDS IF EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   CHKDATA IF NOT EQUAL
. 
. FALLTHRU IS BAD DATA
. 
	 DISPLAY   *P1:23,*EL
	 DISPLAY   *P5:NROW,*EL
	 GOTO	   GPO
. 
RECORDS	 CMATCH	   "V",WHCH
	 GOTO	   PORECS IF EQUAL
. 
. FALLTHRU IS SLRECS - SALES OR	SALES QUOTATIONS
. 
CUSTOMER MOVE	   POIN	     TO	 MSSLNUMB
	 MOVE	   UOPIN     TO	 MSSLUOP
	 MOVE	   QTYIN     TO	 MSSLQTY
	 MOVE	   PRCIN     TO	 MSSLPRC
	 MOVE	   DATEIN    TO	 MSSLDATE
	 MOVE	   VENDIN    TO	 MSSLCUST
	 MOVE	   NAMEIN    TO	 MSSLCNAM
. 
WCUSTREC CALL	   CLRTRAPS
	 MOVE	   " " TO IOSW
	 CALL	   MSSLUPDT
	 BEEP
	 DISPLAY   *P1:23,*EL,*HON,"RECORD UPDATED SUCCESSFULLY! ",*W3:
		   *HOFF,*P1:23,*EL
	 MOVE	   "00"	TO SCANINDX
	 MOVE	   " " TO SCANKEY
	 RESET	   SCANKEY TO 1
	 GOTO	   WHICHPT
. 
MOREDATA MOVE	   " " TO REPLY
	 KEYIN	   *P1:23,*EL,*HON,"PRESS F1 FOR MORE HISTORY /	F5 IF ":
		   "COMPLETE ",REPLY,*HOFF
. 
	 GOTO	   DATAIN   IF F1
	 GOTO	   DOMORE   IF F5
	 BEEP
	 GOTO	   MOREDATA
. 
WHATNEXT GOTO	   MOREDATA
. 
CLEANUP	 DISPLAY   *P1:23,*EL
	 MOVE	   " ",REPLY
	 GOTO	   DATAIN
. 
PORECS	 TRAPCLR   PARITY
	 MOVE	   POIN	   TO MSPONUMB
	 MOVE	   UOPIN   TO MSPOUOP
	 MOVE	   QTYIN   TO MSPOQTY
	 MOVE	   DATEIN  TO MSPODATE
	 MOVE	   VENDIN  TO MSPOVEND
	 MOVE	   PRCIN   TO MSPOPRC
	 MOVE	   NAMEIN  TO MSPOVNAM
. 
	 CALL	   CLRTRAPS
	 MOVE	   " " TO IOSW
	 CALL	   MSPOUPDT
	 DISPLAY   *P1:23,*EL,*HON,"RECORD UPDATED SUCCESSFULLY	!",*W3:
		   *HOFF,*P1:23,*EL
	 MOVE	   "00"	TO SCANINDX
	 MOVE	   " " TO SCANKEY
	 RESET	   SCANKEY TO 1
	 GOTO	   WHICHPT
. 
GETSTUFF TRAPCLR   PARITY
EOJ	 DISPLAY   *P1:23,*EL,*HON,"END	OF INVENTORY HISTORY UPDATE",*W2:
		   *HOFF
. 
	 CHAIN	   "MSHISTRY"
. 
