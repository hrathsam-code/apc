.......................................................................
. AGED RECEIVABLES DETAIL REPORT:   MODULE NAME   "COMMLIST/TXT"
. 
. ACCESSES "ARFILEDT/ISAM:ACCOUNTS"  KEY SEQUENTIALLY
. PRODUCING REPORT FOR INPUT ACCOUNT HAVING OPEN INVOICES/CREDITS
. 
. NOTE THAT ARFILE/ISI CONSISTS OF THE INVOICE HEADER RECORDS
. MODIFIED FOR RECEIVABLES PURPOSES AND INDEXED IN THE FOLLOWING MANNER
. 
. KEY COMPONENTS:
.                 MAJOR  IS  ACCOUNT  (INVCUST)  POSITIONS 9 - 12
.                 MINOR  IS  INVC NO. (INVKEY)   POSITIONS 1 - 5
. 
.......................................................................
. 
. WRITTEN  9/26/81 BY  R. BACHARACH
.                     MICRO SYSTEMS CONSULTANTS
.                     41 TEELE DRIVE
.                     CORAM, NEW YORK 11727
.                     (516) 698-5577
.......................................................................
. 
         INCLUDE   COMMON
. 
         INCLUDE   RCVBLES
. 
ARFILES  IFILE     KEYL=10,UNCOMPRESSED
ARFILEXX INIT      "ARFILEXX/ISAM"
. 
         INCLUDE   DATETBLZ
. 
CUSTFILE IFILE     KEYL=6,UNCOMPRESSED
CSTNAME  INIT      "ACCTSTAT/ISAM"
. 
CURRSTAT DIM       2
.
. 
         INCLUDE   CUSTMAST
. 
CURRACCT DIM       4
FIRSTSW  INIT      "1"
PAGEZ    FORM      3
SUMMARY  INIT      "S"
LINEZ    FORM      2
RECCNT   FORM      "00"
RLIMIT   FORM      "71"
SHORTRUN DIM       1
REPLY    DIM       1
COMPANY  DIM       1
IOSW     DIM       1
STARTKEY DIM       10
NUMCUST  FORM      4
PDTOTL   FORM      6.2
CREDTOTL FORM      6.2
SHAZBAT  DIM       1
. 
ASTER45  INIT      "*********************************************"
. 
PTYPE    DIM       7
WKSUB    FORM      6.2
WKPRINT  FORM      6.2
NETBAL   FORM      6.2
UPFLAG   INIT      "N"
ZEROS    INIT      "000000.00"
TOTCURNT FORM      "0000000.00"
TOT130   FORM      "0000000.00"
TOT3160  FORM      "0000000.00"
TOT6190  FORM      "0000000.00"
TOTOV90  FORM      "0000000.00"
TOTNET   FORM      "00000000.00"
.
STATEKEY DIM       6
DUMMY2   DIM       20
.
MONTH    DIM       2
NMONTH   FORM      2
MM       DIM       2
.
TESTDATE DIM       6
TMON     DIM       2
TYR      DIM       2
YY       DIM       2
ADDTOTL  FORM      7.2
ACCTTOTL FORM      7.2
STATTOTL FORM      9.2
. 
.
COMPDISP INIT      "AMERICAN PRECISION PRODUCTS"
. 
INITIATE MOVE      "0",PAGEZ
         MOVE      "0",LINEZ
         MOVE      " ",REPLY
. 
         CALL      OPENCUST
         OPEN      ARFILES,ARFILEXX
. 
         MOVE      TODAY  TO  MM
         RESET     TODAY  TO  7
         MOVE      TODAY  TO  YY
         RESET     TODAY  TO  1
.
         REP       " 0"  IN MM
. 
KEY1     DISPLAY   *ES,*P01:04,*HON,"COMMISSION CALCULATION PROGRAM":
                   *HOFF
. 
         KEYIN     *P1:6,*HON,"MOUNT STANDARD STOCK PAPER AND PRESS ":
                   "ENTER WHEN READY ",*HOFF,REPLY
.
         MOVE      " " TO REPLY
.
WHATMTH  MOVE      "00" TO MONTH
.
         DISPLAY    *P1:8,*EF,*HON,"REPORTING PERIOD IS ",MM,"/",YY,*HOFF
         KEYIN      *P1:9,*HON,"PRESS F1 IF OK, F5 TO CHANGE REPORTING ":
                    "PERIOD ",*HOFF,REPLY
         GOTO      GOTMTH IF F1
.
         GOTO      GETMTH  IF F5
         BEEP
         GOTO      WHATMTH
.
GETMTH   KEYIN     *P1:8,*EF,*HON,"ENTER REPORTING MONTH AND YEAR ":
                   *+,MM,"/",YY,*HOFF
         MOVE      " " TO REPLY
         GOTO      WHATMTH
.
.
GOTMTH   MOVE      MM TO MONTH
         REP       " 0"  IN MONTH
         REP       " 0"  IN MM
         MOVE      MONTH TO NMONTH 
         CALL      HDR
.
SUMMARY  MOVE      "S" TO SUMMARY
.......       KEYIN     *P1:10,"PRINT (D)etail or (S)ummary REPORT ? ",SUMMARY
         CMATCH    "S",SUMMARY 
         GOTO      CUST1 IF EQUAL
         CMATCH    "D",SUMMARY
         GOTO      CUST1 IF EQUAL
         BEEP
         GOTO      SUMMARY
.
CUST1    CLEAR     STATEKEY
         APPEND    "AL    " TO STATEKEY
         RESET     STATEKEY TO 1
         READ      CUSTFILE,STATEKEY;DUMMY2
.
. ABOVE READ POSITIONS US AT ALABAMA, BYPASSING CANADA
.
. NOW INITIATE THINGS BY READING THE CUSTOMER FILE KEY SEQUENTIALLY
. UNTIL END-OF-FILE STOPS THE WORKS!
.
CUSTOMR  INCLUDE   GTCSTKS
         DISPLAY   *P01:23,CUSTSTAT," ",CUSTKEY
.
         GOTO      EOJ IF OVER
.
         CMATCH    "1",FIRSTSW
         GOTO      FLOOP IF NOT EQUAL
.
         MOVE      " "  TO  FIRSTSW
         MOVE      CUSTKEY  TO  CURRACCT
         MOVE      CUSTSTAT TO  CURRSTAT
. 
. PUSH THE START KEY
. 
FLOOP    MATCH     CURRSTAT TO CUSTSTAT
         CALL      STATBRK  IF NOT EQUAL
.
         MOVE      CUSTSTAT TO CURRSTAT
         MOVE      CUSTKEY  TO CURRACCT
.
         MOVE      CURRACCT   TO  STARTKEY
         RESET     STARTKEY   TO  4
         APPEND    "      "    TO  STARTKEY
         RESET     STARTKEY   TO  10
         LENSET    STARTKEY
         RESET     STARTKEY   TO  1     (HUTCH ALSO!  STARTKEY IS LONELY!)
. 
         READ      ARFILES,STARTKEY;INVKEY,INVTYPE,INVCOMP,INVSTAT
. 
LIBIT    MOVE      "A"    TO  COMPANY
. 
RDLOOP   TRAPCLR   IO
         TRAP      RCVIOERR IF IO
         READKS    ARFILES;INVKEY,INVTYPE,INVCOMP,INVSTAT,INVCUST:
                           INVDATE,INVTOTL,IPMNTS:
                           IAMTPD1,IAMTPD2,IAMTPD3,IAMTPD4,IAMTPD5:
                           ICHK1,ICHK2,ICHK3,ICHK4,ICHK5:
                           IDTPD1,IDTPD2,IDTPD3,IDTPD4,IDTPD5:
                           IDOLSLFT,IPRGDTE
.
         GOTO      ACCTBRK IF OVER
.
         MATCH     "00000" TO INVKEY
         GOTO      RDLOOP  IF EQUAL
.
         MATCH     INVCUST TO CURRACCT
         GOTO      ACCTBRK IF NOT EQUAL
.
. ...................
.NOW CHECK INVOICE/CREDIT DATE FOR INCLUSION
.
         MOVE      INVDATE TO TESTDATE
         REP       " 0"    IN TESTDATE
         MOVE      TESTDATE   TO  TMON
         RESET     TESTDATE   TO  5
         MOVE      TESTDATE   TO  TYR
         RESET     TESTDATE   TO  1
.
         MATCH     MM  TO  TMON
         GOTO      TESTYR  IF  EQUAL
         GOTO      RDLOOP
.
TESTYR   MATCH     YY  TO  TYR
         GOTO      FSTCHK IF EQUAL
         GOTO      RDLOOP
.
FSTCHK   CMATCH    "1",FIRSTSW
         GOTO      RDLP1  IF  NOT  EQUAL
. 
......................................................
RDLP1    CMATCH    "1",FIRSTSW
         CALL      FIRSTONE IF EQUAL
. 
         MOVE      INVTOTL  TO  ADDTOTL
.
         CMATCH    "I",INVTYPE
         GOTO      DEBIT IF  EQUAL
         CMATCH    "D",INVTYPE
         GOTO      DEBIT IF  EQUAL
.
         MULT      "-1"  BY  ADDTOTL
.
DEBIT    ADD       ADDTOTL  TO  ACCTTOTL
         ADD       ADDTOTL  TO  STATTOTL
. 
         DISPLAY   *P1:23,*CLICK
C3       GOTO      RDLOOP
. 
. 
FIRSTONE MOVE      INVCUST  TO  CUSTKEY
         CALL      GETCUST
         MOVE      INVCUST  TO  CURRACCT
         CALL      HDR
         MOVE      " ",FIRSTSW
         RETURN
. 
HDR      ADD       "1"  TO  PAGEZ
         PRINT     *F:
                   *51,"COMMISSIONS CALCULATION LISTING":
                   *111,"AS OF ",TODAY
         PRINT     *52,"FOR REPORTING PERIOD OF ",MM,"/",YY
. 
LIBHEAD  PRINT     *51,"AMERICAN PRECISION PRODUCTS",*111,"PAGE ",PAGEZ
TITPRNT  PRINT     " "
         MOVE      "4"  TO  LINEZ
.
         PRINT     *3,"ACCOUNT ",*55,"COMMISSIONABLE TOTAL"
         PRINT     *1,"NUMBER    NAME"
         RETURN
. 
ACCTBRK  ADD       ACCTTOTL    TO  TOTNET
.
. DON'T PRINT A ZERO ACCOUNT
.
         COMPARE   "0.00"    TO  ACCTTOTL
         GOTO      CUSTOMR   IF  EQUAL
.
         COMPARE   "54",LINEZ
         CALL      HDR IF NOT LESS
.
. 
         PRINT     *1,CUSTKEY,*10,CUSTNAME:
                   *60,ACCTTOTL
.
         ADD       "1"  TO  LINEZ
.
         MOVE      "0.00" TO ACCTTOTL
.
         GOTO      CUSTOMR
. 
. 
         INCLUDE   CUSTIO
. 
         INCLUDE   RCVBLSIO
. 
         INCLUDE   DATECALC
. 
EOJ      BEEP
         CALL      STATBRK
         CALL      REPTOTS
.
         DISPLAY   *ES,*P01:04,"END OF COMMISSIONS CALC. LISTING",*W5
.
         RELEASE
         CHAIN     "APMENU1 "
. 
INVCRTRN RETURN
. 
REPTOTS  CALL      HDR
         PRINT     *L,*L
         PRINT     *1,ASTER45,ASTER45,ASTER45
         PRINT     " "
         PRINT     *10,"COMPANY TOTALS:"
RPT1     PRINT     *10,COMPDISP,*68,TOTNET
         PRINT     " "
         PRINT     *1,ASTER45,ASTER45,ASTER45
         RETURN
. 
STATBRK  COMPARE   "54" TO LINEZ
         CALL      HDR IF NOT LESS
         PRINT     " "
         PRINT     *10,"STATE TOTALS FOR: ",CURRSTAT:
                   *60,"---------------> ",STATTOTL
         PRINT     *1,ASTER45,ASTER45,ASTER45
         PRINT     " "
         ADD       "4" TO LINEZ
         MOVE      "0.00" TO STATTOTL
         MOVE      CUSTSTAT TO CURRSTAT
         RETURN
.
