.   ORDER PROCESSING SYSTEM......PACKING LIST UPDATE
. 
.  WRITTEN BY R. BACHARACH,  MICRO SYSTEMS CONSULTANTS
.                            41 TEELE DRIVE
.                            CORAM, NEW YORK 11727
.                            (516) 698-5577
. 
         INCLUDE   COMMON
. 
INVCNUMB DIM        5
PASSDT6  DIM       6
PASSACCT DIM       4
PASSCOMP DIM       1
PASSCUST DIM       30
. 
NINVCNO  DIM       5
NEWFKEY  DIM       5
.
INDX1    FORM      1
RECCNTR  FORM      2
.
REPLY    DIM       1
.FIELD   TYPE      LENGTH    FROM-TO   DESCRIPTION
TESTLNS  FORM      2
.-----   ----      ------    -------   -----------
WORK01   DIM        1
WORK02   DIM        2
WORK03   DIM        3
WORK04   DIM        4
WORK08   DIM        8
WORK10   DIM       10
WORK09   DIM       9
FIELD09  DIM       7
NFIELD09 FORM      7
.
DATE8    DIM       8            8 DIGIT DATE WORK FIELD
MM       DIM       2
DD       DIM       2
YY       DIM       2
XMM      DIM       2
XDD      DIM       2
XYY      DIM       2
FORM5    FORM      5
. 
DTYPE    DIM       8
DSTAT    DIM       10
INDX     FORM      2
STARTKEY DIM       5
PP       DIM       1
PC       DIM       1
LINES    FORM      2
PTERMS   DIM       18
HDRSW    DIM       1
PAGES    FORM      2
. 
         INCLUDE   PART
. 
NFIELD01 FORM       4                  (NUMERIC WORK FIELD)
FIELD01  DIM        4
. 
NFLD01   FORM      9
FLD01    DIM       9
. 
MULTIFLG DIM       1
MULTIROW FORM      2
MULTICNT FORM      2
MULTIDSC DIM       25
MD1      DIM       25
MD2      DIM       25
MD3      DIM       25
MD4      DIM       25
MD5      DIM       25
MD6      DIM       25
MD7      DIM       25
MD8      DIM       25
MD9      DIM       25
MD10     DIM       25
EXTRA    INIT      "XXXXXXXXXXXXXXXXXXX"
COMPLETE DIM       1
UOM      DIM       2
PRODCODE DIM       2
SHIPNAME DIM       30
SHIPADDR DIM       25
SHIPYADR DIM       25
SHIPADR2 DIM       25
SHIPCITY DIM       18
SHIPSTAT DIM       2
SHIPZIP  DIM       5
COMPLET  DIM       1
. 
NORMTERM INIT      "2%15NET30"
.
SOHDRFL  IFILE     KEYL=5,UNCOMPRESSED
SOHDRNM  INIT      "SOHDRFL/ISAM"
.
SOHDRKY  DIM       5
SOLINES  DIM       2
SOHDRNO  DIM       5
.
NEWLINES FORM      2
NEWLINEX DIM       2
.
.
SODTLFL  IFILE     KEYL=8,UNCOMPRESSED
SODTLNM  INIT      "SODTLFL/ISAM"
.
SODTLKY  DIM       8  (SO#/SOLN#/MULTI-DESC-SUFFIX [0-9])
.
SODTLSTA DIM       1
SODTLNO  DIM       5
SODTLLN  DIM       2
SODTLSFX DIM       1
NEWTERM  DIM       15
PTERM    DIM       9
. ACCOUNT FILE READ/WORK AREA
. 
CUSTFILE IFILE     KEYL=4,UNCOMPRESSED
CSTNAME  INIT      "ACCOUNTS/ISAM"
. 
         INCLUDE   CUSTMAST
.
CLOSESW  INIT      " "
. 
.  WORK  AREAS FOR INVOICE HEADER CREATION/WRITE
. 
         INCLUDE   RCVBLES
. 
SAVE     DIM         10
TOTDOLS  FORM        6.2
FRGHT    FORM        2.2
. 
OLDDET   DIM       1
IOSW     DIM       1
LIBNET   FORM      7.2
REPNET   FORM      7.2
INVNET   FORM      7.2
. 
+...........................................................
. 
DATE2X   DIM       2
DATE2N   FORM      2
. 
. 
INVCKEGS FORM      7     (ORDER QTY)
PCSSOLD  FORM      7     (SHIPPED QTY)
INVPCSKG FORM      7     (BACKORDER QTY)
. 
BUILDPT  DIM       7
. 
. RECORD ASSEMBLY AREA
.           INVOICE DETAIL DETAIL RECORD = 45 BYTES
. 
INVCNO   DIM       5                   INVC NUMBER
INVCLINE DIM       2                   INVC LINE NUMBER SUFFIX FOR INDEXING
. 
.  NOTE THAT INVCLINE WILL BE A 2 DIGIT NUMERIC SUFFIX
.  COUNTER-CREATED IN THIS PROGRAM
. 
INVLINES FORM      "00"
IRECCNTR FORM      6
DTLSTAT  DIM       1
ILIVCNTR FORM      3
. 
ROW      FORM      2                USED TO ROLL DOWN THE KEYIN STMT
. 
NFIELD04 FORM       5.2                (NUMERIC WORK FIELD)
FIELD04  DIM        8         11- 18   PRICE LN 1                 5.2
NFIELD03 FORM       3
FIELD03  DIM        3
NFIELD02 FORM       4
FIELD02  DIM        4
. 
. EXTENSION CALCULATE WORK/DISPLAY AREA
. 
LNDOLS   FORM      5.2
KEGQTY   FORM      9
LINECOST FORM      6.2
KGSAVAIL FORM      6
CRAPMSG  DIM       18
CALCFLD  FORM      8.3
OLDFRGT  FORM      5.2
. 
FORM3    FORM      3
IPNSTDSX DIM       6
IPNSTDSN FORM      6
NONSTDSW DIM       1
SCANSW   DIM       1
PARTUPSW DIM       1
. 
KREPLY   DIM       1
. 
CHKPASS  DIM       5
.  
WEIGHT   FORM      6
HDRCHGSW DIM       1
DUMMY    DIM       40
. 
INVDTKEY DIM       7
. 
OLDXTEND FORM      6.2
. 
. 
CALCNET  FORM      6.2
FRTALLOW FORM      5.2
. 
TIMEDIM  DIM       8
EOJSW    DIM       1
. 
CUSTN    FORM      4
SPECMSG  DIM       80
MSGFLAG  DIM       1
PORTX    DIM       2
SPECSW   DIM       1
TIME     INIT      "HH/MM/SS"
ALIGNSW  DIM       1
. 
+........................................................................
. 
START    DISPLAY   *ES,*P01:12,*HON," PACKING LIST UPDATE PROGRAM":
                   *HOFF:
                       *P01:14,"IF YOU HAVE COME HERE ERRONEOUSLY FROM THE ":
                               "SYSTEM MENU PROGRAMS":
                       *P01:15,"AND DO  N O T  HAVE ANY  INVOICES TO ":
                               "ENTER........":
                       *P01:17,"YOU MAY TERMINATE THIS PROGRAM BY PRESSING ":
                               "THE   F5   KEY"
         KEYIN     *P01:20,*HON,"KEY     F5     TO  TERMINATE":
                   *P01:21,"KEY  ENTER  TO UPDATE PACKING LISTS ",*HOFF,REPLY
. 
         GOTO      MAINLINE  IF  NOT  F5
. 
         BEEP
INDXIT   DISPLAY   *ES
         BEEP
         CHAIN     "APPINVC1"
.
MAINLINE MOVE      " " TO REPLY
.
         MOVE      " ",REPLY
         MOVE      " ",MSGFLAG
         MOVE      " ",SPECMSG
         GOTO      OPENFLS
. 
. FILE OPENING SEQUENCE
OPENFLS  TRAP      NOFILE  IF IO
. 
         OPEN      SOHDRFL,SOHDRNM
.
         OPEN      SODTLFL,SODTLNM
.
         MOVE      TODAY TO MM
         MOVE      TODAY TO XMM
         RESET     TODAY TO 4
         MOVE      TODAY TO DD
         MOVE      TODAY TO XDD
         RESET     TODAY TO 7
         MOVE      TODAY TO YY
         MOVE      TODAY TO XYY
         RESET     TODAY TO 1
. 
. SET UP SIX DIGIT DATE
. 
         MOVE      TODAY TO DATE8
         MOVE      DATE8 TO INVDATE
         BUMP      DATE8 BY 3
         RESET     INVDATE TO 2
         APPEND    DATE8 TO INVDATE
         BUMP      DATE8 BY 3
         RESET     INVDATE TO 4
         APPEND    DATE8 TO INVDATE
         RESET     INVDATE
         RESET     DATE8
         MOVE      INVDATE  TO  PASSDT6      PASS 6 DIGIT DATE TO DTL PGM
         GOTO      ACCTOPEN
.
NOFILE   DISPLAY   *ES,"ORDER PROCESSING FILES NOT ON LINE":
                   *N,"JOB IS ABORTING"
         BEEP
         CHAIN     "APPINVC1"        GO BACK TO DISPLAY INVOICE MENU
*...........................................................
.
ACCTOPEN TRAPCLR   IO
         TRAP      NOCUSTFL IF IO
.
         CALL      OPENCUST
.
         CMATCH    "H"  TO  IOSW
         GOTO      NOCUSTFL IF NOT EQUAL
.
         GOTO      DISPLAY
. 
.................................................
.
MNUCHAIN CHAIN     "APPINVC1"
. 
NOCUSTFL DISPLAY   *ES,"CUSTOMER FILE IS NOT ON-LINE, ABORTING"
         BEEP
         GOTO      MNUCHAIN
. 
*...........................................................
. INITIAL SCREEN DISPLAY SEQUENCE
.
DISPLAY  CLOCK     TIME TO TIME
.
         DISPLAY   *ES,*P1:1, "MODIFY OR DELETE PACKING LIST DATA PRIOR TO":
                              " INVOICE PRINT",*HOFF
.
GETNUMB  DISPLAY   *P1:23,*EL
         KEYIN     *P1:23,"ENTER PACKING LIST NUMBER - OR  F5  TO TERMINATE ":
                   SOHDRKY
.
         GOTO      EOJ  IF  F5
.
         CMATCH    " " TO SOHDRKY
         GOTO      DISPLAY IF EOS
.
         DISPLAY   *P1:3,"PACKING LIST## ",*P20:3,SOHDRKY
.
HDRLOOP  READ      SOHDRFL,SOHDRKY;SOHDRNO,SOLINES,INVTYPE,INVSTAT,INVCUST:
                           INVDATE,INVTOTL,INVORDNO,INVWGHT,NEWTERM:
                           CARTONS,INVSHIP,COMPLET:
                           SHIPNAME,SHIPADDR,SHIPADR2:
                           SHIPCITY,SHIPSTAT,SHIPZIP:
                           CALCNET,LIBNET
. 
         GOTO      NOTHERE  IF  OVER
.
         MOVE      INVCUST  TO  CUSTKEY
         CALL      GETCUST
.
SHOWPO   DISPLAY   *P30:3,"CUSTOMER PO## ",*P44:3,INVORDNO:
                   *P65:3,"DATE: ",INVDATE
.
         DISPLAY   *P10:5,"B I L L   T O",*P50:5,"S H I P   T O":
                   *P5:7,"ACCT NO. ",*P15:7,INVCUST:
                   *P5:8,CUSTNAME,*P45:8,SHIPNAME:
                   *P5:9,CUSTADDR,*P45:9,SHIPADDR:
                   *P5:10,CUSTCITY," ",CUSTSTAT," ",CUSTZIP:
                   *P45:10,SHIPCITY," ",SHIPSTAT," ",SHIPZIP:
                   *P1:12,"WEIGHT",*P9:12,"CARTONS",*P18:12,"SHIP VIA":
                   *P38:12,"COMPLT?",*P50:12,"FREIGHT",*P61:12,"NET TOTAL":
                   *P2:13,INVWGHT,*P13:13,CARTONS,*P18:13,INVSHIP:
                   *P41:13,COMPLET,*P48:13,CALCNET,*P60:13,LIBNET
.
ISITOPEN CMATCH    "D",INVSTAT
         GOTO      CANCELIT IF NOT EQUAL
.
         BEEP
         MOVE      " " TO REPLY
         KEYIN     *P1:23,*EL,"THIS ORDER IS DELETED! - REINSTATE IT? ":
                              "(Y/N) ",REPLY
         CMATCH    "Y",REPLY
         GOTO      REINSTAT IF EQUAL
         CMATCH    "N",REPLY
         GOTO      FORGETIT IF EQUAL
         BEEP
         GOTO      ISITOPEN
.
FORGETIT BEEP
         DISPLAY   *P1:23,*EL,"ORDER SHALL REMAIN DELETED! ",*W2,*P1:23,*EL
         GOTO      DISPLAY
.
REINSTAT MOVE      "P" TO INVSTAT
.
         INCLUDE   SOHDUPDT/TEXT
.
         DISPLAY   *P1:23,*EL,"ORDER HAS BEEN REINSTATED! ",*W2,*P1:23,*EL
         GOTO      DISPLAY
.
CANCELIT MOVE      " " TO REPLY
.
         MATCH     "I",INVSTAT
         GOTO      CANC1 IF NOT EQUAL
.
         BEEP      
         DISPLAY   *P1:22,*EF,*HON,"YOU MAY  N O T  MODIFY THIS ORDER - IT ":
                   "HAS ALREADY BEEN INVOICED!",*HOFF,*W3
         KEYIN     *P1:23,"PRESS ENTER WHEN READY TO PROCEED! ",REPLY
         GOTO      DISPLAY
.
CANC1    KEYIN     *P1:23,*EL,*HON,"CANCEL THE ENTIRE ORDER? (Y/N)",REPLY,*HOFF
         DISPLAY   *P1:23,*EL
         CMATCH    "Y",REPLY
         GOTO      GIVEITAD IF EQUAL
         CMATCH    "N",REPLY
         GOTO      MODHDR IF EQUAL
         BEEP
         GOTO      CANCELIT
.
GIVEITAD BEEP
         MOVE      "D"  TO  INVSTAT    (DO NOT PRINT OR ADD A/R RECORD!)
.
         INCLUDE   SOHDUPDT/TEXT
.
         BEEP
         DISPLAY   *P1:23,*EL,"PACKING LIST ",SOHDRNO," HAS BEEN DELETED!":
                   *W2,*P1:23,*EL
         GOTO      DISPLAY
.
NOTHERE  BEEP
         DISPLAY   *P1:23,*EL,"INVALID PACKING LIST NUMBER! - RETRY! ",*W2
         GOTO      DISPLAY
.
MODHDR   BEEP
         DISPLAY   *P1:23,*EL,*HON,"MODIFY DATA AS NECESSARY! ",*HOFF,*W2:
                   *P1:23,*EL
.
         KEYIN     *P44:3,*RV,INVORDNO
         DISPLAY   *P44:3,INVORDNO
.
         KEYIN     *P45:8,*RV,SHIPNAME
         DISPLAY   *P45:8,SHIPNAME
.
         KEYIN     *P45:9,*RV,SHIPADDR
         DISPLAY   *P45:9,SHIPADDR
.
         KEYIN     *P45:10,*RV,SHIPCITY
         DISPLAY   *P45:10,SHIPCITY
.
         KEYIN     *P64:10,*RV,SHIPSTAT
         DISPLAY   *P64:10,SHIPSTAT
.
         KEYIN     *P67:10,*RV,SHIPZIP
         DISPLAY   *P67:10,SHIPZIP
.
         KEYIN     *P2:13,*RV,INVWGHT
         DISPLAY   *P2:13,INVWGHT
.
         KEYIN     *P13:13,*RV,CARTONS
         DISPLAY   *P13:13,CARTONS
.
         KEYIN     *P18:13,*RV,INVSHIP
         DISPLAY   *P18:13,INVSHIP
.
CHGCMPL  KEYIN     *P41:13,*RV,COMPLET
         DISPLAY   *P41:13,COMPLET
.
         KEYIN     *P48:13,*RV,CALCNET
         DISPLAY   *P48:13,CALCNET
.
HDROKAY  MOVE      " " TO REPLY
         KEYIN     *P1:23,"HEADER DATA CORRECT? (Y/N) ",REPLY
         DISPLAY   *P1:23,*EL
.
         CMATCH    "Y",REPLY
         GOTO      WRTHDMOD IF EQUAL
         CMATCH    "N",REPLY
         GOTO      MODHDR IF EQUAL
.
         BEEP
         GOTO      HDROKAY
.
WRTHDMOD INCLUDE   SOHDUPDT/TEXT
.
MODDTLS2 MOVE      " " TO REPLY
         KEYIN     *P1:23,*EL,"MODIFY DETAILS? (Y/N) ",REPLY
.
         CMATCH    "Y",REPLY
.....        GOTO      SHOWDTLS IF EQUAL
         GOTO      SETDTLS  IF  EQUAL
.
         CMATCH    "N",REPLY
         GOTO      DISPLAY IF EQUAL
.
         BEEP
         GOTO      MODDTLS2
.
SHOWDTLS KEYIN     *P1:23,*EL,"MODIFY OR DELETE INDIVIDUAL LINE ITEMS? ":
                   "(Y/N) ",REPLY
         DISPLAY   *P1:23,*EL
         CMATCH    "Y",REPLY
         GOTO      SETDTLS IF EQUAL
         CMATCH    "N",REPLY
         GOTO      DONOTHIN IF  EQUAL
         BEEP
         GOTO      SHOWDTLS
.
DONOTHIN BEEP
         DISPLAY   *P1:23,"YOU DONT WANT TO DELETE NOR MODIFY! ":
                          " RESTARTING!",*W2
         GOTO      DISPLAY
.
.
. SET KEY FOR DETAIL READS
.
SETDTLS  CLEAR     SODTLKY
         APPEND    SOHDRNO  TO  SODTLKY
         APPEND    "000"    TO  SODTLKY
         RESET     SODTLKY  TO  1
         READ      SODTLFL,SODTLKY;DUMMY    (REC NOT FND)
.
         DISPLAY   *P1:15,"ORDER QTY",*P11:15,"SHIP QTY":
                   *P21:15,"B/O QTY",*P31:15,"DESCRIPTION":
                   *P60:15,"UNIT PRC",*P70:15," UOM"
.
DETLOOP  READKS    SODTLFL;SODTLNO,SODTLLN,SODTLSFX,DTLSTAT,MULTIDSC:
                           INVCKEGS,INVPCSKG,PCSSOLD:
                           LNDOLS,LINECOST,UOM,PRODCODE
.
         GOTO      NODETS  IF  OVER
.
MTCHNOS  MATCH     SOHDRNO TO SODTLNO
         GOTO      NODETS  IF  NOT  EQUAL
. 
DSPDTL   DISPLAY   *P1:16,INVCKEGS:
                   *P11:16,PCSSOLD:
                   *P21:16,INVPCSKG:
                   *P31:16,MULTIDSC:
                   *P60:16,LNDOLS:
                   *P71:16,UOM:
                   *P1:23,*EL,"ENTER CHANGES AS REQUIRED - OR TAP ENTER"
.
ISDTLOP  MATCH     "D",DTLSTAT
         GOTO      DELDTLS IF NOT EQUAL
.
         MOVE      " " TO REPLY
         KEYIN     *P1:23,"THIS LINE ITEM IS CANCELED! REINSTATE IT? ":
                   "(Y/N) ",REPLY
         DISPLAY   *P1:23,*EL
.
         CMATCH    "Y",REPLY
         GOTO      REINSDTL IF EQUAL
.
         CMATCH    "N",REPLY
         GOTO      DETLOOP IF EQUAL
.
         BEEP
         GOTO      ISDTLOP
.
REINSDTL MOVE      "O"  TO  DTLSTAT
         UPDATE    SODTLFL;SODTLNO,SODTLLN,SODTLSFX,DTLSTAT,MULTIDSC:
                           INVCKEGS,INVPCSKG,PCSSOLD:
                           LNDOLS,LINECOST,UOM,PRODCODE
.
         GOTO      DETLOOP
.
...........................................................
.
DELDTLS  MOVE      " " TO REPLY
         KEYIN     *P1:23,*EL,"DELETE THIS LINE ITEM? (Y/N) ",REPLY
         DISPLAY   *P1:23,*EL
         CMATCH    "Y",REPLY
         GOTO      KILLDTL IF EQUAL
         CMATCH    "N",REPLY
         GOTO      GETDATA IF EQUAL
         BEEP
         GOTO      DELDTLS
.
KILLDTL  MOVE      "D"  TO  DTLSTAT    (BYPASSED IN INVOICE PRT PGM)
         UPDATE    SODTLFL;SODTLNO,SODTLLN,SODTLSFX,DTLSTAT,MULTIDSC:
                           INVCKEGS,INVPCSKG,PCSSOLD:
                           LNDOLS,LINECOST,UOM,PRODCODE
.
         GOTO      DETLOOP
.
...................................................................
.
GETDATA  KEYIN     *P01:16,*RV,INVCKEGS
         DISPLAY   *P01:16,INVCKEGS
.
         KEYIN     *P11:16,*RV,PCSSOLD
         DISPLAY   *P11:16,PCSSOLD
.
         KEYIN     *P21:16,*RV,INVPCSKG
         DISPLAY   *P21:16,INVPCSKG
.
         KEYIN     *P31:16,*RV,MULTIDSC
         DISPLAY   *P31:16,MULTIDSC
.
         KEYIN     *P60:16,*RV,LNDOLS
         DISPLAY   *P60:16,LNDOLS
.
         KEYIN     *P71:16,*RV,UOM
         DISPLAY   *P71:16,UOM
.
ASKOKAY  MOVE      " " TO REPLY
         KEYIN     *P1:23,*EL,"ALL DATA CORRECT? (Y/N) ",REPLY
. 
         CMATCH    "Y",REPLY
         GOTO      UPDATEIT IF EQUAL
.
         CMATCH    "N",REPLY
         GOTO      DSPDTL IF EQUAL
.
         BEEP
         GOTO      ASKOKAY
.
UPDATEIT MOVE      PCSSOLD  TO  LINECOST
         MULTIPLY  LNDOLS   BY  LINECOST
.
         MOVE      "P"  TO  DTLSTAT
.
         UPDATE    SODTLFL;SODTLNO,SODTLLN,SODTLSFX,DTLSTAT,MULTIDSC:
                           INVCKEGS,INVPCSKG,PCSSOLD:
                           LNDOLS,LINECOST,UOM,PRODCODE
.
         GOTO      DETLOOP
.
NODETS   BEEP
         DISPLAY   *P1:23,*EL,"*** END OF DATA FOR THIS PO ***",*W2:
                   *P1:23,*EL
.
ADDMORE  MOVE      " " TO REPLY
         KEYIN     *P1:23,*EL,"ADD ANOTHER LINE ITEM? (Y/N) ",REPLY
         CMATCH    "Y",REPLY
         GOTO      GETNWONE IF EQUAL
         CMATCH    "N",REPLY
         GOTO      DISPLAY  IF EQUAL
         BEEP
         GOTO      ADDMORE
.
GETNWONE MOVE      SOLINES TO TESTLNS
         COMPARE   "18"    TO TESTLNS
         GOTO      GETNW   IF LESS
         BEEP
         DISPLAY   *P1:23,*EL,"YOU HAVE 18 LINE ITEMS ON THIS ORDER - ":
                   "NO MORE ARE PERMITTED! ",*W2
         GOTO      DISPLAY
.
GETNW    DISPLAY   *P1:16,*EL,*P1:23,*EL,"ENTER DATA FOR NEW LINE ITEM"
.
         KEYIN     *P1:16,INVCKEGS
         DISPLAY   *P1:16,INVCKEGS
.
         KEYIN     *P11:16,PCSSOLD
         DISPLAY   *P11:16,PCSSOLD
.
         KEYIN     *P21:16,INVPCSKG
         DISPLAY   *P21:16,INVPCSKG
.
         KEYIN     *P31:16,MULTIDSC
         DISPLAY   *P31:16,MULTIDSC
.
         KEYIN     *P60:16,LNDOLS
         DISPLAY   *P60:16,LNDOLS
.
         KEYIN     *P71:16,UOM
         DISPLAY   *P71:16
.
NWDTLOK  MOVE      " " TO REPLY
         KEYIN     *P1:23,*EL,"NEW DETAIL DATA OKAY? (Y/N) ",REPLY
         DISPLAY   *P1:23,*EL
.
         CMATCH    "Y",REPLY
         GOTO      DTLSETWR IF EQUAL
.
         CMATCH    "N",REPLY
         GOTO      GETNWONE IF EQUAL
.
         BEEP
         GOTO      NWDTLOK
.
DTLSETWR MOVE      SOLINES  TO  NEWLINES
         ADD       "1"      TO  NEWLINES
.
         MOVE      NEWLINES TO  NEWLINEX
         REP       " 0"     IN  NEWLINEX
.
         MOVE      NEWLINEX TO  SOLINES
.
         CLEAR     SODTLKY
         APPEND    SOHDRKY  TO  SODTLKY
         APPEND    NEWLINEX TO  SODTLKY
         APPEND    "0"      TO  SODTLKY
         RESET     SODTLKY  TO  1
.
         INCLUDE   SOHDUPDT/TEXT
.
         MOVE      SOHDRKY  TO  SODTLNO
         MOVE      NEWLINEX TO  SODTLLN
         MOVE      "0"      TO  SODTLSFX
         MOVE      "O"      TO  DTLSTAT
         MOVE      "  "     TO  PRODCODE
.
         MOVE      LNDOLS   TO  LINECOST
         MULTIPLY  PCSSOLD  BY  LINECOST
.
         WRITE     SODTLFL,SODTLKY;SODTLNO,SODTLLN,SODTLSFX,DTLSTAT,MULTIDSC:
                                   INVCKEGS,INVPCSKG,PCSSOLD:
                                   LNDOLS,LINECOST,UOM,PRODCODE
.
         BEEP
         DISPLAY   *P1:23,*EL,*HON,"NEW LINE ITEM ADDED! ",*HOFF,*W2
.
         GOTO      ADDMORE
.
............................................................
.
EOJ      DISPLAY   *P1:23,*EL,"*** END OF PACKING LIST MODIFICATIONS ***":
                   *W2
         CHAIN     "APPINVC1"
         STOP
.
. 
         INCLUDE   CUSTIO
. 
         INCLUDE   STDIOERR
. 
         INCLUDE   STDFMTER
. 
         INCLUDE   STDRANGE
. 
         INCLUDE   STDPARER
.
         INCLUDE   RCVBLSIO
. 
         STOP
