************************* DATASHARE ANSWER PROGRAM EXAMPLE ***************
.
.   THIS IS AN EXAMPLE OF A DATASHARE ANSWER PROGRAM ROUTINE WHICH
.   WILL LOAD A CHARACTER SET TO AN 8200.
.
.   THE USER MAY ADD,DELETE OR CHANGE ANY MESSAGES AND/OR ERROR
.   HANDLING ROUTINES TO SUIT THE SPECIFIC INSTALLATION.
.
......................
.
.   GENERAL NOTES ON CHARACTER SET LOADING:
.
.    1.  ALL '<' CHARACTERS IN THE FILE MUST BE CHANGED TO OCTAL 034.
.
.    2.  THE CONFIGURATION RECORD IN THE CHARACTER SET FILE MUST BE SENT FIRST.
.
.    3.  THE ANSWER PROGRAM MUST READ (KEYIN) AN 'ACK' SEQUENCE AFTER
.        EVERY TRANSMISSION BEFORE CONTINUING. IF THE READ TIMES-OUT,
.        THE ENTIRE LOAD SEQUENCE MUST BE RE-INITIATED.
.
.    4.  THE ANSWER PROGRAM SHOULD VERIFY THE THE 1ST RECORD OF THE FILE
.        CONTAINS '..CHAR8200..' BEFORE READING ANY ADDITIONAL RECORDS
.
.    5.  THIS PROGRAM WILL RE-INTIATE THE LOAD SEQUENCE IN AN ENDLESS LOOP
.        IF AN ERROR IS ENCOUNTERED AND NOT CORRECTED. INDIVIDUAL USERS
.        MAY WISH TO ATTEMPT TO DISPLAY AN ERROR MESSAGE ON THE HOST CONSOLE,
.        OR THE PRINTER, OR WRITE AN ERROR RECORD IN A SYSTEM LOG FILE.
.
.    6.  THE RECORD CONTAINING AN '*' IN THE FIRST POSITION IS THE END OF
.        FILE RECORD. THERE IS ONE MORE RECORD IN THE FILE BUT IT IS NOT
.        USED. IF A RANGE ERROR OCCURS, THE FILE IS INCOMPLETE.
.
.    7.  THE ANSWER PROGRAM SHOULD IGNORE ALL RECORDS NOT BEGINNING WITH
.        AN '<' EXCEPT AS NOTED ABOVE.
.
.    8.  DISPLAY INSTRUCTION NOTES:::
.        -- SPACE TRUNCATION (*+) MUST BE SET
.        -- CARRIAGE RETURN,LINE FEED MUST BE DISABLED (;)
.
.    9.  KEYIN INSTRUCTION NOTES:::
.        -- CURSOR MUST BE DISABLED (*EOFF)
.        -- CHARACTER INVERSION MUST BE SET (*IT) BEFORE KEYIN
.           AND RESET (*IN) AFTER THE KEYIN IS COMPLETED. USE *T
.           TO TIME OUT AFTER 2 SECONDS
.        -- CARRIAGE RETURN,LINE FEED MUST BE DISABLED (;)
.
........................
+
.....    DATA AREAS AND CONSTANTS USED IN THIS EXAMPLE
FILE     FILE
SEQ      FORM      "-1"
CONFIG   DIM       15
DATA     DIM       125
NAME     INIT      "FUNCTION/SYS"
RECCNT   FORM      3
REPLER   INIT      074,034
ACK      DIM       1
CFGRSP   DIM       14
NAKCNT   FORM      2
START    DISPLAY   *ES,"I AM NOW LOADING YOUR CHARACTER SET";
OPENCH
         OPEN      FILE,NAME                OPEN THE CHARACTER SET FILE
         READ      FILE,SEQ;DATA            OBTAIN THE FIRST RECORD
         GOTO      BADEOF IF OVER           IF EOF UNRECOVERABLE ERROR
         MATCH     "..CHAR8200.." TO DATA   IS THIS A CHARACTER SET FILE
         GOTO      GOTFILE IF EQUAL         YES THEN GO PROCESS IT
         BEEP                               NO THE DISPLAY ERROR MESSAGE
         DISPLAY   *P1:24,"FILE IS NOT A 8200 CHARACTER SET FILE":
                   *W2;
         STOP
GOTFILE
         READ      FILE,SEQ;CONFIG          READ IN THE CONFIGURATION BLOCK
         GOTO      BADEOF IF OVER           IF EOF UNRECOVERABLE ERROR
         CMATCH    "<" TO CONFIG            IS THIS THE RIGHT RECORD
         GOTO      GOTFILE IF NOT EQUAL     NO THEN TRY AGAIN
         REPLACE   REPLER IN CONFIG         CONVERT ALL '<' TO 034's
         CALL      SENDCFG                  SEND THE CONFIGURATION BLOCK
LOADLOOP
         READ      FILE,SEQ;DATA            READ IN THE NEXT RECORD
         GOTO      BADEOF IF OVER           IF EOF UNRECOVERABLE ERROR
         CMATCH    "*" TO DATA              IS THIS END OF DATA
         GOTO      EOJ IF EQUAL             YES THEN GO TO END OF JOB ROUTINE
         CMATCH    "<" TO DATA              IS THIS A DATA RECORD
         GOTO      LOADLOOP IF NOT EQUAL    NO THEN TRY AGAIN
         ADD       "1" TO RECCNT            ADD 1 TO RECORD COUNT
         REPLACE   REPLER IN DATA           CONVERT ALL '<' TO 034's
RESEND
         CONSOLE   *P14:1,"LOADING ",RECCNT,"             "
         KEYIN     *CL;                     CLEAR THE KEY-AHEAD BUFFER
         DISPLAY   *+,DATA;                 SEND THE DATA
         KEYIN     *EOFF,*IT,*T,*+,ACK;    GET THE ACK
         CMATCH    "@" TO ACK               DID I GET AN ACK
         GOTO      LOADLOOP IF EQUAL        YES THEN PROCESS NEXT RECORD
         CALL      SENDCFG                  NO THEN RESEND CONFIG BLOCK
         GOTO      RESEND                   RESEND THE CURRENT BLOCK
SENDCFG
         MOVE      "5" TO NAKCNT            TRY FIVE TIMES
SENDER
         CONSOLE   *P14:1,*EL,"SENDING CFG ",NAKCNT
         KEYIN     *CL;                     CLEAR THE KEY-AHEAD BUFFER
         DISPLAY   *+,CONFIG;               SEND THE CONFIG BLOCK
         KEYIN     *EOFF,*IT,*T,*+,CFGRSP;    GET THE ACK
         CMATCH    "A" TO CFGRSP               DID I GET A RESPONSE
         RETURN    IF NOT EOS               YES THEN MUST BE ACK
         BEEP
         SUBTRACT  "1" FROM NAKCNT          SUBTRACT 1 FROM RETRY COUNT
         GOTO      SENDER IF NOT ZERO       TRY AGAIN IF NOT ZERO
         NORETURN                           JUSTIFY THE STACK
         DISPLAY   034,"@@@@@"
         DISPLAY   *P1:24,"CHARACTER LOAD WILL NOT WORK",*W2;
         STOP
BADEOF   
         DISPLAY   *P1:24,"ENCOUNTERED AN EOF",*W2;
         STOP
EOJ      DISPLAY   *ES,"LOAD SUCCESSFUL",*W2
         STOP
