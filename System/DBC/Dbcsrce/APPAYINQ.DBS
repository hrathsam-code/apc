. .
	 INCLUDE   MSCBRAG.TXT
. APPAYINQ MODULE - DISPLAY PAYMENTS HISTORY
. 
	 INCLUDE   COMMON
. 
	 INCLUDE   APPAIDFD
. 
	 INCLUDE   APMASTFD
. 
APPAIDFL IFILE	   KEYL=19
APPAIDNM INIT	   "C:\DBC\APPAIDFL.ISI"
. 
APMASTER IFILE	   KEYL=6
APMASTNM INIT	   "C:\DBC\APMASTER.ISI"
. 
VENDIN	 DIM	   6
DROW	 FORM	   "03"
REPLY	 DIM	   1
VENDTOT	 FORM	   "00000000.00"
. 
DYY	 DIM	   2
DMM	 DIM	   2
.
	 MOVE	   TODAY TO DMM
         REP       " 0"  IN DMM
         RESET     TODAY TO 5
	 MOVE	   TODAY TO DYY
         REP       " 0"  IN DYY
	 RESET	   TODAY TO 1
.
	 DISPLAY   *ES,"ACCOUNTS PAYABLE SYSTEM	- DISPLAY PAYMENTS ":
		   "HISTORY BY VENDOR"					      
.
	 OPEN	   APPAIDFL,APPAIDNM
	 OPEN	   APMASTER,APMASTNM
. 
ASKWHEN	 MOVE	   " " TO REPLY
	 DISPLAY   *P1:04,"ENTER MONTH/YEAR FROM WHICH YOU WISH	TO BEGIN ":
			  "DISPLAY ",*P60:04,DMM,"/",DYY
.
	 KEYIN	   *P60:04,*+,*RV,DMM,*P63:04,*-,*RV,DYY
.
	 DISPLAY   *P1:04,*EL
.
.
         DISPLAY   *P01:03,"INVOICE",*P11:03,"DATE",*P19:03,"DESCRIPTION":
                   *P43:03,"AMOUNT",*P55:03,"DATE PAID",*P66:03,"CHECK NO."

. 
WHO	 MOVE	   "	  " TO VENDIN
	 MOVE	   " " TO REPLY
. 
	 KEYIN	   *P01:23,*EL,"ENTER VENDOR NUMBER DESIRED or ":
		   "PRESS F5 TO	TERMINATE ",VENDIN
. 
	 GOTO	   EOJ IF F5
. 
	 MOVE	   VENDIN TO ACCKEY
. 
	 INCLUDE   APMASTRD
. 
	 GOTO	   NOGOOD IF OVER
. 
	 DISPLAY   *P01:02,"VENDOR IS ",NAME				 
. 
CORRVEND MOVE	   " " TO REPLY						      
	 KEYIN	   *P1:23,*EL,"CORRECT VENDOR ?	(Y/N) ",REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   OK IF EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   WHO IF EQUAL
	 BEEP
	 GOTO	   CORRVEND
. 
OK	 CLEAR	   TRNKEY						      
	 APPEND	   ACCKEY TO TRNKEY
	 APPEND	   "		  " TO TRNKEY
.
	 RESET	   TRNKEY TO 6
.
	 APPEND	   DYY	  TO TRNKEY
	 REP	   " 0"	  IN DMM
	 APPEND	   DMM	  TO TRNKEY
..	  DISPLAY   *P60:23,*HON,TRNKEY,*HOFF
.
	 RESET	   TRNKEY TO 1
. 
	 INCLUDE   APPAIDRD
. 
. 
READPAID TRAPCLR   IO
. 
. 
	 INCLUDE   APPAIDKS
. 
	 GOTO	   VENDBRK IF OVER
. 
	 MATCH	   TACCN TO VENDIN
	 GOTO	   VENDBRK IF NOT EQUAL
. 
	 ADD	   TAMT	 TO  VENDTOT
	 ADD	   "1"	 TO  DROW
	 COMPARE   "22"	 TO  DROW
	 CALL	   RESETDSP  IF	 EQUAL
. 
	 REP	   " 0"	IN TMON						      
. 
	 DISPLAY   *P1:DROW,TINVNO,*P9:DROW,TMON,"/",TDAY,"/",TYEAR:
		   *P19:DROW,TDESC,*P41:DROW,TAMT,*P55:DROW,TPAIDDT:
		   "	",CKNUMBER
. 
	 GOTO	   READPAID
. 
. 
VENDBRK	 DISPLAY   *P1:22,"END OF DATA FOR THIS	VENDOR - - TOTAL PAID":
		   " IS	",VENDTOT					      
. 
	 MOVE	   " " TO REPLY
MOREVEND KEYIN	   *P1:23,*EL,"MORE VENDORS ? (Y/N) ",REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   WHOM	IF EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   MOREVEND IF NOT EQUAL
	 GOTO	   EOJ
. 
WHOM	 DISPLAY   *P01:04,*EF
	 MOVE	   "03"	TO DROW
	 MOVE	   "0.00"  TO VENDTOT
.
SAMEDATE MOVE	   " " TO REPLY
	 DISPLAY   *P1:23,"USE ",DMM,"/",DYY," FOR DATA	SCAN ?	(Y/N) "
	 KEYIN	   *P40:23,REPLY
	 DISPLAY   *P1:23,*EL
.
	 CMATCH	   "Y",REPLY
	 GOTO	   WHO IF EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   ASKWHEN IF EQUAL
	 BEEP
	 GOTO	   SAMEDATE
.
	 GOTO	   WHO
. 
NOGOOD	 DISPLAY   *P1:23,"INVALID VENDOR - PLEASE RETRY ! ",*W2
	 GOTO	   WHO
. 
RESETDSP DISPLAY   *P1:22,*EL,"VENDOR TOTAL THUS FAR IS	",VENDTOT
	 KEYIN	   *P1:23,*EL,"TAP ENTER KEY FOR MORE DATA OR ":
		   "F5 TO TERMINATE ",REPLY
. 
	 GOTO	   EOJ IF F5
. 
	 MOVE	   "04"	TO DROW
ADDEM	 DISPLAY   *P1:DROW,*EL						      
	 ADD	   "1" TO DROW
	 COMPARE   "22"	TO DROW						      
	 GOTO	   RESETRTN IF EQUAL
	 GOTO	   ADDEM
. 
RESETRTN MOVE	   "04"	TO DROW
	 RETURN
. 
EOJ	 CHAIN	   "APMENU1"
. 
. 
