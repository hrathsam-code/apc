...................................................................
. INVCNTRY - INVOICE/CREDIT DATA ENTRY PROGRAM
. 
. WRITTEN 10/30/82 BY R. BACHARACH
.		      MICRO SYSTEMS CONSULTANTS
.		      41 TEELE DRIVE
.		      CORAM, NEW YORK 11727
.		      (516) 698-5577
. 
...................................................................
. 
	 INCLUDE   COMMON
. 
	 INCLUDE   RCVBLES
. 
ARFLXX	 IFILE	   KEYL=10,UNCOMPRESSED
ARFLXXNM INIT	   "C:\DBC\ARFILEXX.ISI"
. 
ARFILEDT IFILE	   KEYL=11,UNCOMPRESSED
ARFLDTNM INIT	   "C:\DBC\ARFILEDT.ISI"
.
NEWFILE	 IFILE	   KEYL=5,UNCOMPRESSED
NEWFLNM	 INIT	   "C:\DBC\NEWINVCS.ISI"
NINVCNO	 DIM	   5
.
ARFLDTKY DIM	   11
. 
XMM	 DIM	   2
XDD	 DIM	   2
XYY	 DIM	   2
. 
CUSTFILE IFILE	   KEYL=4,UNCOMPRESSED
CSTNAME	 INIT	   "C:\DBC\ACCOUNTS.ISI"
. 
	 INCLUDE   CUSTMAST
. 
	 INCLUDE   CONTFILE
. 
	 INCLUDE   BALFWDFL
. 
REPLY	 DIM	   1
ROW	 FORM	   "00"
PAGETOTL FORM	   "0000000.00"
RUNTOTL	 FORM	   "0000000.00"
INVCCNT	 FORM	   "0000"
DINVTOTL FORM	   6.2
DIDOLS	 FORM	   6.2
IOSW	 DIM	   1
MON	 DIM	   2
DAY	 DIM	   2
YR	 DIM	   2
NMON	 FORM	   2
NDAY	 FORM	   2
NYR	 FORM	   2
STARTKEY DIM	   5
ACCTINVC DIM	   10
ONECENT	 FORM	   ".01"
INVCNUMB FORM	   5
KMON	 DIM	   2
DOCNO	 DIM	   5
DOCTYPE	 DIM	   1
.
SAVTOTL	 FORM	   6.2
. 
	 TRAPCLR   IO
	 TRAP	   NOACCTS IF IO
	 CALL	   OPENCUST
	 CMATCH	   "H",IOSW
	 GOTO	   OK IF EQUAL
	 BEEP
NOACCTS	 DISPLAY   *P01:01,*ES,*HON,"ACCOUNTS FILE NOT FOUND - ":
		   "JOB	ABORTING! ",*HOFF,*W3
	 CHAIN	   "ARMENU1"
. 
OK	 TRAPCLR   IO
	 TRAP	   NOAR	IF IO
	 CALL	   OPENRCVB
	 CMATCH	   "H",IOSW
	 GOTO	   ARXX	IF EQUAL
NOAR	 BEEP
	 DISPLAY   *P01:01,*HON,"ACCOUNTS RECEIVABLE FILE NOT FOUND - ":
		   "JOB	ABORTING! ",*HOFF,*W3
	 CHAIN	   "ARMENU1"
. 
ARXX	 TRAPCLR   IO
	 TRAP	   NOINDX IF IO
	 OPEN	   ARFLXX,ARFLXXNM
	 OPEN	   ARFILEDT,ARFLDTNM
	 OPEN	   NEWFILE,NEWFLNM
. 
	 MOVE	   TODAY TO XMM
	 RESET	   TODAY TO 4
	 MOVE	   TODAY TO XDD
	 RESET	   TODAY TO 7
	 MOVE	   TODAY TO XYY
	 RESET	   TODAY TO 1
. 
. ALSO OPEN CONTROLS FILE AND BALFWD FILE
. 
	 CALL	   OPENCTLS
	 CALL	   OPENBALF
. 
. NOW READ EACH	RECORD
. 
	 MOVE	   "APP" TO CTLKEY
	 CALL	   GETCTL
	 CMATCH	   "H",IOSW
	 GOTO	   BADCTLS IF NOT EQUAL
	 CALL	   GTBALFWD
	 CMATCH	   "H",IOSW
	 GOTO	   BADCTLS IF NOT EQUAL
	 GOTO	   BEGIN
. 
BADCTLS	 BEEP
	 DISPLAY   *P01:23,*HON,"UNABLE	TO OPEN	CONTROLS/BALANCE FORWARD ":
				"FILES - ABORTING! ",*W3
	 BEEP	   
	 CHAIN	   "ARMENU1"
. 
NOINDX	 BEEP
	 DISPLAY   *P01:23,*HON,"UNABLE	TO OPEN	A/R FILE - ACCOUNT INDEX! ":
		   "ABORTING!",*HOFF,*W2
	 BEEP
	 CHAIN	   "ARMENU1"
. 
. 
BEGIN	 DISPLAY   *ES,*P01:01,*HON:
		   "ACCOUNTS RECEIVABLE	SYSTEM - INVOICE / ":
		   "CREDIT DATA	ENTRY PROGRAM	  ",TODAY:
		   *HOFF:
		   *P01:03,"DOCUMENT  CODE  TRANSACT  ACCT  NAME":
		   *P52:03,"ORIGINAL  BALANCE":
		   *P01:04," NUMBER   (I/C)   DATE    CODE":
		   *P52:04," AMOUNT	DUE  "
. 
RECYCLE	 DISPLAY   *P01:05,*EF
	 MOVE	   "04"	TO ROW
	 MOVE	   "0.00"    TO	 PAGETOTL
. 
NTRYLOOP ADD	   "1"	TO  ROW
	 COMPARE   "21"	TO  ROW
	 GOTO	   RECYCLE  IF NOT LESS
. 
. NOW CLEAR ALL	FIELDS PRIOR TO	ENTRY
. 
	 CLEAR	   INVKEY
	 RESET	   INVKEY TO 1
	 CLEAR	   INVTYPE
	 RESET	   INVTYPE TO 1
	 CLEAR	   INVCOMP
	 RESET	   INVCOMP TO 1
	 MOVE	   "000000" TO INVCUST
	 MOVE	   "00000"  TO DOCNO
	 MOVE	   "00000"  TO INVKEY
	 MOVE	   "0.00"   TO IMATLS
	 MOVE	   "0.00"   TO DIDOLS
	 MOVE	   "0.00"   TO DINVTOTL
	 MOVE	   "0.00"   TO INVTOTL
	 MOVE	   "0.00"   TO IDOLSLFT
	 CALL	   SETFLD
	 GOTO	   KEYDOCNO
.
SETFLD	 MOVE	   "S" TO INVCOMP
	 MOVE	   "O" TO INVSTAT
	 MOVE	   "0" TO IPMNTS
	 MOVE	   "0.00"  TO  IAMTPD1
	 MOVE	   "0.00"  TO  IAMTPD2
	 MOVE	   "0.00"  TO  IAMTPD3
	 MOVE	   "0.00"  TO  IAMTPD4
	 MOVE	   "0.00"  TO  IAMTPD5
. 
	 MOVE	   "	  " TO ICHK1
	 MOVE	   "	  " TO ICHK2
	 MOVE	   "	  " TO ICHK3
	 MOVE	   "	  " TO ICHK4
	 MOVE	   "	  " TO ICHK5
	 MOVE	   "	  " TO IDTPD1
	 MOVE	   "	  " TO IDTPD2
	 MOVE	   "	  " TO IDTPD3
	 MOVE	   "	  " TO IDTPD4
	 MOVE	   "	  " TO IDTPD5
	 RETURN
. 
KEYDOCNO DISPLAY   *P1:23,*EL,"PRESS F5	IF YOU ARE FINISHED ENTERING ":
			      "INVOICES	"
	 RESET	   INVKEY TO 1
	 KEYIN	   *P03:ROW,*DE,DOCNO
. 
. DETERMINE IF ENTRY IS	COMPLETE!
. 
	 GOTO	   EOJ IF F5	    (THE F5 KEY	IS THE WAY OUT)
	 DISPLAY   *P1:23,*EL
.
	 MOVE	   DOCNO TO INVCNUMB
	 MOVE	   INVCNUMB  TO	 DOCNO
	 RESET	   DOCNO    TO	5
	 GOTO	   KEYDOCNO IF EOS
	 RESET	   DOCNO TO 1
	 MOVE	   DOCNO TO INVKEY
. 
	 DISPLAY   *P03:ROW,INVKEY
.
	 MOVE	   "I" TO DOCTYPE
	 DISPLAY   *P13:ROW,DOCTYPE
. 
. NOW CHECK TO SEE IF THIS INVOICE ALREADY EXISTS!
. 
	 MOVE	   DOCNO TO STARTKEY
	 MOVE	   "M"	   TO  IOSW
	 CALL	   GETRCVB
	 CMATCH	   "H" TO IOSW
	 GOTO	   KEYCODE IF NOT EQUAL
. 
	 BEEP
	 DISPLAY   *P01:23,*EL,*HON,"THIS INVOICE NUMBER ALREADY EXISTS!":
		   *HOFF,*W2,*P1:23,*EL
	 GOTO	   DUMBLOOP
. 
KEYCODE	 KEYIN	   *P13:ROW,*RV,DOCTYPE
	 CMATCH	   "C",DOCTYPE
	 GOTO	   DOCOK IF EQUAL
	 CMATCH	   "I",DOCTYPE
	 GOTO	   DOCOK IF EQUAL
	 BEEP
	 DISPLAY   *P1:23,*EL,*HON,"INVALID DOCUMENT CODE! ",*HOFF:
		   *W4,*P1:23,*EL
	 GOTO	   KEYCODE
.
DOCOK	 MOVE	   DOCTYPE TO INVTYPE
. 
. DISPLAY TODAYS DATE FOR INVOICES AND ALLOW AN	OVERRIDE KEY
. 
DATES	 RESET	   MON	  TO  1
	 RESET	   DAY	  TO  1
	 RESET	   YR	  TO  1
	 MOVE	   TODAY  TO  MON
	 REP	   " 0"	  IN  MON
	 RESET	   TODAY  TO  4
	 MOVE	   TODAY  TO  DAY
	 RESET	   TODAY  TO  7
	 MOVE	   TODAY  TO  YR
	 RESET	   TODAY
	 DISPLAY   *P17:ROW,MON,"/",DAY,"/",YR
. 
.....KEYDATE  KEYIN	*P17:ROW,*RV,*DE,*+,MON,*P19:ROW,"/":
. 
KEYDATE	 CLEAR	   KMON
	 KEYIN	   *P17:ROW,*RV,*DE,*JR,KMON
	 RESET	   KMON	TO 2
	 GOTO	   KXXX	IF EOS
	 RESET	   KMON	TO 1
	 MOVE	   KMON	TO MON
	 KEYIN	   *P20:ROW,*RV,*DE,*+,DAY,*P22:ROW,"/":
		   *P23:ROW,*RV,*DE,*+,YR
. 
KXXX	 MOVE	   MON TO NMON
	 COMPARE   "13"	  TO NMON
	 GOTO	   BEEPDATE IF NOT LESS
	 COMPARE   "1"	  TO NMON
	 GOTO	   BEEPDATE IF LESS
. 
	 MOVE	   DAY	  TO NDAY
	 COMPARE   "32"	  TO NDAY
	 GOTO	   BEEPDATE  IF	NOT LESS
	 COMPARE   "1"	  TO NDAY
	 GOTO	   BEEPDATE  IF	LESS
. 
	 MOVE	   YR	  TO NYR
	 COMPARE   "81"	  TO NYR
	 GOTO	   DISPDATE  IF	NOT LESS
. 
BEEPDATE BEEP
	 DISPLAY   *P01:23,"INVALID DATE! PLEASE RETRY!	",*W2:
		   *P01:23,*EL
	 GOTO	   KEYDATE
. 
DISPDATE REP	   " 0"	IN MON
	 REP	   " 0"	IN DAY
	 DISPLAY   *P17:ROW,MON,"/",DAY,"/",YR
	 CLEAR	   INVDATE
	 APPEND	   MON TO INVDATE
	 APPEND	   DAY TO INVDATE
	 APPEND	   YR  TO INVDATE
	 RESET	   INVDATE
. 
KEYACCT	 CLEAR	   INVCUST
	 KEYIN	   *P26:ROW,*DE,*JR,INVCUST
	 RESET	   INVCUST TO 4
	 GOTO	   GOODCUST   IF NOT EOS
	 BEEP
	 DISPLAY   *P33:ROW,*HON,"INVALID DATA ENTERED",*HOFF,*W2
	 DISPLAY   *P33:ROW,*EL
	 CLEAR	   INVCUST
	 RESET	   INVCUST TO 1
	 GOTO	   KEYACCT
. 
GOODCUST RESET	   INVCUST TO 3
	 REP	   " 0"	IN INVCUST
	 RESET	   INVCUST TO 1
	 DISPLAY   *P26:ROW,INVCUST
	 MOVE	   INVCUST  TO	CUSTKEY
	 CALL	   GETCUST
	 CMATCH	   "H",IOSW
	 GOTO	   DISPNAME IF EQUAL
	 BEEP
	 DISPLAY   *P01:23,"INVALID ACCOUNT CODE - PLEASE RETRY! ":
		   *W2,*P01:23,*EL
	 GOTO	   KEYACCT
. 
DISPNAME DISPLAY   *P33:ROW,CUSTNAME
CUSTOK	 MOVE	   " " TO REPLY
	 BEEP
	 DISPLAY   *P1:23,*EL
	 KEYIN	   *P01:23,"IS THIS THE	CORRECT	ACCOUNT	? (Y/N)	":
		   REPLY,*P01:23,*EL
	 CMATCH	   "Y",REPLY
	 GOTO	   KEYACCT IF NOT EQUAL
. 
KEYTOTL	 DISPLAY   *P52:ROW,*EL
	 KEYIN	   *P52:ROW,INVTOTL
. 
. SAVE THE KEYED AMOUNT	FOR USE	IN UPDATING CONTROLS AND BALFWD	FILES
. 
	 MOVE	   INVTOTL  TO	SAVTOTL
. 
	 COMPARE   ONECENT TO INVTOTL
	 GOTO	   OKIN	IF NOT LESS
	 BEEP
	 DISPLAY   *P01:23,*HON,"YOU MAY NOT ENTER NEGATIVE NUMBERS!":
		   *HOFF,*W2,*P01:23,*EL
	 GOTO	   KEYTOTL
. 
OKIN	 MOVE	   INVTOTL TO DINVTOTL
	 CMATCH	   "C" TO INVTYPE
	 GOTO	   TYPOK  IF NOT EQUAL
	 MULTIPLY  "-1"	  BY DINVTOTL
. 
TYPOK	 DISPLAY   *P52:ROW,DINVTOTL
	 MOVE	   INVTOTL  TO	IDOLSLFT
	 DISPLAY   *P61:ROW,DINVTOTL
. 
BALTOTL	 KEYIN	   *P61:ROW,*RV,IDOLSLFT
	 COMPARE   ONECENT TO IDOLSLFT
	 GOTO	   TYPOK2 IF NOT LESS
	 BEEP
	 DISPLAY   *P01:23,*HON,"YOU MAY NOT ENTER NEGATIVE NUMBERS!":
		   *HOFF,*W2,*P1:23,*EL
	 GOTO	   TYPOK
. 
. 
......TYPOK2   KEYIN	 *P71:ROW,IMATLS
..........	  DISPLAY   *P71:ROW,IMATLS
TYPOK2	 CMATCH	   "C" TO INVTYPE
	 GOTO	   CTLSCRED IF EQUAL
. 
. FALLTHRU MEANS THAT WE ARE PROCESSING	INVOICES, SO ADD ACCORDINGLY
. 
	 ADD	   SAVTOTL  TO	CTLSOLD
	 GOTO	   TYPOK22
. 
CTLSCRED ADD	   SAVTOTL  TO	LBALCRED
.
TYPOK22	 MOVE	   IDOLSLFT TO DIDOLS
	 MATCH	   "C" TO INVTYPE
	 GOTO	   TYPXX IF NOT	EQUAL
	 MULTIPLY  "-1"	BY DIDOLS
TYPXX	 DISPLAY   *P61:ROW,DIDOLS
. 
ASKOKAY	 BEEP
	 KEYIN	   *P01:23,"IS ALL DATA	CORRECT	AS ENTERED ? ":
		   "(Y/N) ",REPLY,*P01:23,*EL
	 CMATCH	   "Y",REPLY
	 GOTO	   ADDTOTS IF EQUAL
. 
. FALLTHRU MEANS THE OPERATOR HAS SIGHTED DATA ENTRY ERRORS!
. SO ALLOW MEANS FOR CORRECTION	OF ENTIRE LINE
. 
DUMBLOOP BEEP
	 DISPLAY   *P01:ROW,*EL
	 SUBTRACT  "1" FROM ROW
	 DISPLAY   *P01:23,*EL
	 GOTO	   NTRYLOOP
. 
ADDTOTS	 ADD	   DIDOLS  TO  PAGETOTL	   (INCREMENT THE COUNTER)
	 ADD	   DIDOLS  TO  RUNTOTL
	 ADD	   "1"	     TO	 INVCCNT
. 
	 DISPLAY   *P01:22,*HON,INVCCNT," INVOICES ENTERED - BALANCE IS	":
		   *HOFF,RUNTOTL
. 
. NOW WRITE THE	ACCOUNTS RECEIVABLE RECORD
. 
	 CLEAR	   STARTKEY
	 APPEND	   DOCNO   TO STARTKEY
	 RESET	   STARTKEY TO	1
	 MOVE	   "M"	    TO IOSW
. 
	 CALL	   SETFLD
	 MOVE	   DOCNO TO INVKEY
	 MOVE	   DOCTYPE  TO INVTYPE
	 CALL	   ADDRCVB
	 CMATCH	   "H",IOSW
	 GOTO	   BADARWRT IF NOT EQUAL
. 
	 MOVE	   INVKEY  TO  NINVCNO
	 WRITE	   NEWFILE,NINVCNO;NINVCNO
.
. NOW INSERT THIS KEY INTO THE ARFILEXX/ISI FILE, 
. CONSTRUCTED AS FOLLOWS:
. 
. KEY NAME IS ACCTINVC	(DIM 10)  ACCT=4, DOCUMENT TYPE=1, INVC=5
. 
	 CLEAR	   ACCTINVC
	 APPEND	   INVCUST TO ACCTINVC
	 APPEND	   INVTYPE TO ACCTINVC
	 APPEND	   INVKEY  TO ACCTINVC
	 RESET	   ACCTINVC  TO	 1
. 
	 TRAPCLR   IO
	 TRAP	   NOINSERT IF IO
	 INSERT	   ARFLXX,ACCTINVC
. 
. NOW INSERT TO	THE ARFILEDT/ISI FILE 6/22/83
. 
	 CLEAR	   ARFLDTKY
	 APPEND	   XYY TO ARFLDTKY
	 APPEND	   XMM TO ARFLDTKY
	 APPEND	   XDD TO ARFLDTKY
	 APPEND	   INVKEY TO ARFLDTKY
	 RESET	   ARFLDTKY  TO	1
	 INSERT	   ARFILEDT,ARFLDTKY
. 
	 GOTO	   CHEKTYPE
. 
NOINSERT BEEP	  
	 DISPLAY   *P01:23,*HON,"UNABLE	TO WRITE TO ARFILEXX/ISI ":
		   "INDEX FILE!	- INDEXING FOLLOWS!",*HOFF,*W3
...	  ROLLOUT   "CHAIN ARINDX"
	 CHAIN	   "ARMENU1"
. 
. 
. NOW UPDATE THE ACCOUNT WITH THIS INVOICE/CREDIT"
. 
CHEKTYPE CMATCH	   "I",INVTYPE
	 GOTO	   INCRCRDT IF NOT EQUAL
. 
. FALLTHRU MEANS IT WAS	AN INVOICE
. 
	 ADD	   IDOLSLFT  TO	 CURNTDUE
	 MOVE	   IDOLSLFT  TO	 LSTSALPR
. 
PUTCST	 CLEAR	   LSTSALDT
	 APPEND	   MON TO LSTSALDT
	 APPEND	   DAY TO LSTSALDT
	 APPEND	   YR  TO LSTSALDT
	 RESET	   LSTSALDT
	 CALL	   PUTCUST	     (UPDATE THE CUSTOMER RECORD)
	 CMATCH	   "H",IOSW
	 GOTO	   BADCUST IF NOT EQUAL
. 
. ALL ENTRY AND	UPDATES	FOR THIS INVOICE ARE COMPLETED 
. 
	 GOTO	   NTRYLOOP
. 
INCRCRDT ADD	   IDOLSLFT  TO	 MTDCRED
	 MOVE	   IDOLSLFT  TO	 LSTSALPR
	 MULTIPLY  "-1"	     BY	 LSTSALPR
	 GOTO	   PUTCST
. 
BADARWRT BEEP
	 DISPLAY  *P01:23,*HON,"UNABLE TO WRITE	TO A/R FILE! - JOB ":
		  "ABORTING! ",*HOFF,*W2
	 CHAIN	   "ARMENU1"
. 
BADCUST	 BEEP
	 DISPLAY   *P01:23,*HON,"UNABLE	TO UPDATE CUSTOMER RECORD FOR ":
		   INVCUST," - JOB ABORTING! ",*HOFF,*W2
	 CHAIN	   "ARMENU1"
. 
. 
EOJ	 DISPLAY   *P01:23,*EL,*HON,"INVOICE/CREDIT DATA ENTRY IS ":
		   "COMPLETE! ",*HOFF,*W3
. 
. NOW UPDATE THE CONTROLS FILE AND THE BALFWD FILE !
.
	 MOVE	   "APP" TO CTLKEY
	 MOVE	   "M"	 TO IOSW
	 CALL	   GETCTL
	 CMATCH	   "H",IOSW
	 GOTO	   UGH IF NOT EQUAL
	 ADD	   RUNTOTL TO CTLSOLD
.
	 CALL	   PUTCTL
	 CMATCH	   "H",IOSW
	 GOTO	   BADPUTZ IF NOT EQUAL
.....	 CALL	   PTBALFWD
	 CMATCH	   "H",IOSW
	 GOTO	   BADPUTZ IF NOT EQUAL
.
	 CLOSE	   RCVBFILE
	 CLOSE	   NEWFILE
.
	 CLOSE	   CUSTFILE
.....	 CLOSE	   BALFWD
	 CLOSE	   CONTROLS
	 GOTO	   CHAINOUT
.
UGH	 BEEP
	 DISPLAY   *P1:22,*EL,*HON,"UNABLE TO UPDATE CORPORATE CONTROLS! ":
		   *W3
	 GOTO	   UGH
. 
BADPUTZ	 BEEP
	 DISPLAY   *P01:24,*HON,"UNABLE	TO UPDATE THE CONTROLS OR ":
		   "BALANCE FORWARD FILES !",*HOFF,*W3
	 GOTO	   BADPUTZ
. 
CHAINOUT CHAIN	   "ARMENU1"
. 
	 INCLUDE   CUSTIO
. 
	 INCLUDE   RCVBLSIO
. 
	 INCLUDE   CONTRLIO
.
	 INCLUDE   BALFWDIO
