. PRINT	DAILY INVOICE TRANSACTIONS
. 
	 INCLUDE   MSCBRAG.TXT
.
	 INCLUDE   COMMON
.
NEWFNAM	 INIT	   "C:\DBC\NEWINVCS.ISI"
NEWFILE	 IFILE	   KEYL=5,UNCOMPRESSED
NINVCNO	 DIM	   5
. 
. 
REPLY	 DIM	   1
SWMR	 FORM	   "0"	       '1'=MATCHING RECORD
SW1P	 FORM	   "1"	       '1'=FIRST CYCLE
LINECT	 FORM	   "000"	   LINE	COUNTER
PAGE	 FORM	   3	       PAGE NUMBER
RID01	 FORM	   "0"	       RECORD TYPE 01
LR	 FORM	   "0"	       '1'=LAST	RECORD PENDING
LRTOT	 FORM	   5		GRAND TOTAL
VOIDS	 FORM	   5
PAIDS	 FORM	   5
CRDTS	 FORM	   5
ORDRTOTS FORM	   7.2
MATLTOTS FORM	   7.2
. 
IOSW	 DIM	   1
COMP	 DIM	   1
REC1	 FORM	   "0001"
REC2	 FORM	   "0002"
. 
EOJTIME	 DIM	   9
STARTIME DIM	   9
EOJDATE	 DIM	   8
ENDTIME	 DIM	   9
FILLER	 INIT	   "........./........./........./........./........./...."
TIMEFILE FILE	   FIX=80,UNCOMPRESSED
TIMENAME INIT	   "C:\DBC\EOJTIME.TXT"
REC0	 FORM	   "0000"
SEQ	 FORM	   "-1"
. 
	 INCLUDE   CUSTMAST
. 
CUSTFILE IFILE	    KEYL=4,UNCOMPRESSED
CSTNAME	 INIT	   "C:\DBC\ACCOUNTS.ISI"
. 
	 INCLUDE   RCVBLES
. 
COMPVAR	 DIM	   27
. 
MM	 DIM	   2
DD	 DIM	   2
YY	 DIM	   2
DATE8	 DIM	   8
. 
STATUS	 DIM	   8
. 
ARFILE	 IFILE	   KEYL=5,UNCOMPRESSED
ARFILENM INIT	   "C:\DBC\ARFILE.ISI"
ARFILEKY DIM	   5
. 
ROLLNAME INIT	   "C:\DBC\ROLLFILE.SYS"
.
+............................................................
. MAINLINE FOLLOWS
.
. FILE OPENING SEQUENCE
        DISPLAY   *ES,*P1:1,"PRINTING DAILY INVOICE REPORT":
                  *W3

	 DISPLAY   *ES,*P1:10,*HON,"DAILY INVOICE TRANSACTION LISTING FOR ":
		   TODAY,*HOFF
. 
	 GOTO	   CONT1
. 
NOTIME	 BEEP
	 DISPLAY   *ES,"NO TIME	FILE FOUND - UNABLE TO COMPLETE	JOB"
	 GOTO	   NOTIME
. 
NOARFILE BEEP
	 DISPLAY   *P01:23,*EL,"ACCOUNTS RECEIVABLE FILE NOT ON	LINE",*W2
	 GOTO	    NOARFILE
. 
CONT1	 TRAPCLR   IO
	 TRAP	   NOARFILE IF IO
	 OPEN	   ARFILE,ARFILENM
	 OPEN	   NEWFILE,NEWFNAM
.
	 DISPLAY   *P1:23,*EL,*HON,"NEW	INVOICE	FILE OPENED",*HOFF,*W3:
		   *P1:23,*EL
.
. SET DATE6
. 
	 MOVE	   TODAY  TO  MM
	 REP	   " 0"	  IN  MM
         RESET     TODAY  TO  3
	 MOVE	   TODAY  TO  DD
	 REP	   " 0"	  IN  DD
         RESET     TODAY  TO  5
	 MOVE	   TODAY  TO  YY
	 RESET	   TODAY  TO  1
. 
	 CLEAR	   DATE6
	 APPEND	   MM	  TO  DATE6
.
	 APPEND	   DD	  TO  DATE6
	 APPEND	   YY	  TO  DATE6
	 RESET	   DATE6  TO  1
. 
	 TRAPCLR   IO
	 CALL	   OPENCUST
	 CMATCH	   "H"	TO  IOSW
	 GOTO	   NOFILE   IF NOT EQUAL
	 GOTO	   GETCOMP
. 
NOFILE	 BEEP
	 DISPLAY   *P01:12,*EL,"ACCOUNT	FILE NOT MOUNTED - ":
			       "JOB ABORTING",*W
	 GOTO	   CHAINOUT
. 
GETCOMP	 TRAPCLR   PARITY    (NOP)
. 
	 DISPLAY   *P1:23,*EL
.
MOVLIB   MOVE      "APC COMPONENTS" TO  COMPVAR
	 MOVE	   "A"	TO  COMP
	 CALL	   HEADER
.
..........................
.
. NOW USE THE NEWINVCS/ISAM FILE FOR DIRECTED READS TO TODAYS ACTIVITY
.
.    4/30/85
..........................
.
READINVC READKS	   NEWFILE;NINVCNO
.
	 GOTO	   EOJROUT IF OVER
.
	 MATCH	   "00000"   TO	 NINVCNO
	 GOTO	   READINVC  IF	 EQUAL
. 
	 MOVE	   NINVCNO   TO	 ARFILEKY
*............................................................
.
READ	 READ	   ARFILE,ARFILEKY;INVKEY,INVTYPE,INVCOMP,INVSTAT,INVCUST:
			    INVDATE,INVTOTL,IPMNTS:
			    IAMTPD1,IAMTPD2,IAMTPD3,IAMTPD4,IAMTPD5:
			    ICHK1,ICHK2,ICHK3,ICHK4,ICHK5:
			    IDTPD1,IDTPD2,IDTPD3,IDTPD4,IDTPD5:
			    IDOLSLFT,IPRGDTE
.
	 GOTO	   READINVC  IF	 OVER
*............................................................
. IS THE ITEM AN  O P E	N  RECEIVABLE  ?
	 CMATCH	   "X",INVSTAT
	 GOTO	   READINVC  IF	 EQUAL	       (ITEM HAS BEEN	P A I D)
	 CMATCH	   "V",INVSTAT
	 GOTO	   READINVC  IF	 EQUAL	       (ITEM HAS BEEN  V O I D E D)
	 CMATCH	   "W",INVSTAT
	 GOTO	   READINVC  IF	 EQUAL	       (ITEM HAS BEEN  WRITTEN OFF)
. 
MOVEDATA MOVE	   INVCUST TO CUSTKEY
	 CMATCH	   "O" TO INVSTAT
	 GOTO	   CHKCLS IF NOT EQUAL
	 MOVE	   "OPEN " TO STATUS
	 GOTO	   TRAPS
CHKCLS	 CMATCH	   "P" TO INVSTAT
	 GOTO	   MAKCANC IF NOT EQUAL
	 MOVE	   "PARTIAL" TO	STATUS
	 GOTO	   TRAPS
MAKCANC	 CMATCH	   "V"	  TO  INVSTAT
	 GOTO	   MKC1	  IF  NOT   EQUAL
	 MOVE	   "VOID"     TO    STATUS
	 GOTO	   TRAPS
MKC1	 CMATCH	   "X"	  TO  INVSTAT
	 GOTO	   MKC2	  IF  NOT   EQUAL
	 MOVE	   "PAID"     TO    STATUS
	 GOTO	   TRAPS
MKC2	 CMATCH	   "C"	  TO  INVSTAT
	 GOTO	   MKC3	  IF  NOT  EQUAL
	 MOVE	   "CRDT"     TO   STATUS
	 GOTO	   TRAPS
MKC3	 MOVE	   "DEL."     TO   STATUS
TRAPS	 TRAPCLR   IO
	 CALL	   GETCUST
	 CMATCH	   "H"	  TO IOSW
	 GOTO	   SETDATE   IF	 EQUAL
PNOVEND	 MOVE	   "*ACCOUNT NOT ON FILE*" TO CUSTNAME
*............................................................
. CLEAR	OUT ACCUMULATORS (DETAIL TIME)
.
SETDATE	 TRAPCLR   IO
	 MOVE	   INVDATE TO DATE8
	 MOVE	   DATE8  TO MM
	 RESET	   DATE8  TO 3
	 MOVE	   DATE8  TO DD
	 RESET	   DATE8  TO 5
	 MOVE	   DATE8  TO YY
	 RESET	   DATE8
	 CMATCH	   "C"	  TO INVTYPE
	 GOTO	   DETCALC IF NOT EQUAL
	 MULT	   "-1"	  BY INVTOTL
	 MULT	   "-1"	  BY IDOLSLFT
. 
DETCALC	 ADD	   "1" TO LRTOT
	 ADD	   IDOLSLFT TO ORDRTOTS
	 ADD	   IMATLS   TO MATLTOTS
*............................................................
. HEADING AND DETAIL OUTPUT
.
DETOUT	 COMPARE   "50"	TO LINECT
	 CALL	   HEADER IF NOT LESS
*............................................................
	 PRINT	   *3,INVKEY:
		   *9,MM,"/",DD,"/",YY:
		   *19,CUSTNAME:
		   *50,STATUS:
		   *68,IDOLSLFT
. 
	 ADD	   "1" TO LINECT
	 GOTO	   READINVC
*............................................................
. 
	 INCLUDE   CUSTIO
. PAGE HEADING ROUTINE
.
HEADER	 ADD	   "1" TO PAGE
	 PRINT	   *F:
		   *21,"DAILY INVOICE TRANSACTION LISTING":
		   *56,"FOR ",TODAY:
		   *70,"PG ",PAGE
	 PRINT	   *24,COMPVAR,*C,*L
	 MOVE	   "0" TO LINECT
	 PRINT	   *2,"INVOICE/CREDIT":
		   *19,"ACCOUNT	NAME":
		   *49,"STATUS":
		   *70,"INVOICE"
	 PRINT	   *2,"NUMBER":
		   *11,"DATE":
		   *71,"TOTAL":
		   *C,*L
	 RETURN
*............................................................
. TOTAL	OUTPUT
.
EOJROUT	 PRINT	   *56,"----------":
		   *67,"----------"
	 PRINT	   *67,ORDRTOTS
. 
	 CALL	   HEADER
	 PRINT	   " "
	 PRINT	   " "
	 PRINT	   *5,LRTOT," INVOICES ENTERED FOR ",COMPVAR:
		   *66,ORDRTOTS,"***",*C,*L
.
	 RELEASE
. 
CHAINOUT BEEP
.
. UPDATE THE TIME FILE AND PRINT THE TIME ELAPSED
.
	 DISPLAY   *ES,*P1:1,*HON,"NOW RESET NEW INVOICES INDEX	FILE FOR":
		   " NEXT DAY! ",*HOFF,*W3
.
         ROLLOUT   "CHAIN NEWINVCS.CHN",ROLLNAME
.
	 DISPLAY   *ES,*W3
	 DISPLAY   *ES,*P1:1,"NEW INVOICE FILE HAS BEEN	RESET!",*W3
.
. 
	 TRAPCLR   IO
	 TRAP	   NOTIME IF IO
	 OPEN	   TIMEFILE,TIMENAME
	 READ	   TIMEFILE,REC0;STARTIME,ENDTIME,EOJDATE
	 CLOCK	   TIME	TO ENDTIME
	 WRITE	   TIMEFILE,REC1;STARTIME,ENDTIME,EOJDATE
	 WEOF	   TIMEFILE,REC2
. 
	 PRINT	   *F,*25,"END-OF-DAY PROCEDURE	FOR ",EOJDATE:
		   " BEGAN AT ",STARTIME," AND ENDED AT	",ENDTIME
	 PRINT	   *F
. 
	 RELEASE
.
         CHAIN     "APPMENU1"
.
