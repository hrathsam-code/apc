KEYNSTDS KEYIN     *P44:ROW,IPNSTDSX
         CMATCH    "<"    TO IPNSTDSX
         GOTO      TAKESTDS  IF  EOS
         GOTO      B03K   IF EQUAL
. ...............................................................
. BACK ARROW  ALLOWS  CHANGE OF KEGS ORDERED INPUT
. EOS  MEANS THAT THIS ITEM HAS NON-STD INVENTORY BUT WE ARE SHIPPING STDS
. 
.  F A L L T H R U  MEANS THAT WE WANT TO SELL NON-STDS
...........................................................................
         TYPE      IPNSTDSX
         GOTO      KEYNSTDS  IF  NOT  EQUAL
         MOVE      IPNSTDSX  TO  IPNSTDSN       (THIS IS THE DESIRED SELL QTY)
         MOVE      "N"    TO   NONSTDSW
         MOVE      BUILDPT   TO  NSPART
         REPLACE   " 0"   IN     NSPART
         CALL      GTNONSTD
         CMATCH    "H"    TO     IOSW
         GOTO      NS1  EQUAL
         BEEP
         DISPLAY   *P01:23,*EF,*P01:23,"NON-STD RECORD DOES NOT EXIST - ":
                   "UPDATING PART RECORD ACCORDINGLY"
         MOVE      "Y"    TO     PSTD
         CALL      PARTUPDT
         GOTO      NOTFND         (TO RECYCLE THE LINE
NS1      CALL      NSTDSCAN       (CHECK THE PIECES TO SEE IF IN THE RECORD)
. 
. ALSO....IF RECORD IS FOUND CHECK IF SUFFICIENT INVENTORY AVAILABLE
. 
         CMATCH    "H"    TO     SCANSW     (DID WE FIND IT?)
         GOTO      NS2    IF   EQUAL
         BEEP
         DISPLAY   *P01:23,*EF,*P01:23,"THE PIECE COUNT YOU INPUT IS   NOT  ":
                   "CURRENTLY IN INVENTORY":
                   *P01:24,"YOU WILL BE ALLOWED TO GO TRY ANOTHER PIECE ":
                           "COUNT STARTING WITH STANDARDS",*W,*W,*W,*W,*W
         BEEP
         GOTO      NOTFND
. .....................................
. COMING HERE MEANS WE HAVE FOUND A RECORD AND SUFFICIENT KEGS
NS2      MOVE      NSTDPCS  TO  KEGQTY    (SET UP EXTENSION ROUTINE)
         MOVE      NSTDPCS  TO  INVPCSKG
         MOVE      NFIELD03 TO  INVCKEGS
. 
. 
.  1/31/82  SPECIAL BYPASS BECAUSE ALL THIS JUNK UPDATES
.        AT THE WRONG TIME
. 
         GOTO      B04NS
. 
. 
.  NOTE THAT NSTDSCAN MUST DETERMINE THE COMPANY TO BE ACCESSED
.  AND SCAN FOR THE PIECE COUNT INPUT AND LOAD THE KEGCOUNT
.  IN NSTDKEGS WHEN FOUND
. 
. .. I F   NO MATCH ON PIECE COUNT....MOVE "0"  TO  NSTDKEGS
.          AND "M"  (OR MISS) IN SCANSW
. 
         SUBTRACT  NFIELD03  FROM NSTDKEGS (DECREASE NON-STD INVENTORY)
. 
         CMATCH    "L"  TO  PASSCOMP
         GOTO      DECNSREP    IF NOT EQUAL
. 
DECNSLIB CALL      STNYNSTD           (PUT THE NEW QTY BACK)
         GOTO      B04NS
. 
DECNSREP CALL      STTXNSTD           (PUT THE TX QTY BACK)
         GOTO      B04NS
. 
. 
TAKESTDS MOVE      PKGQTY TO KEGQTY
         MOVE      PKGQTY TO INVPCSKG
         MULT      NFIELD03 BY KEGQTY
         DISPLAY   *P44:ROW,PKGQTY,*P52:ROW,KEGQTY
. 
. 
.  DETERMINE PO QTY FIELD TO UPDATE, NY OR TX
.  CALCULATE AND PREPARE TO UPDATE INVENTORY FILE ACCORDINGLY
.    JUST SET A SWITCH AND UPDATE IT AT THE END
. 
         CMATCH    "L" TO PASSCOMP
         GOTO      TXUD IF NOT EQUAL
         COMPARE   NFIELD03 TO PNYOH
         GOTO      NYOK IF NOT LESS
         BEEP
         DISPLAY   *P01:23,"DESIRED QUANTITY IS NOT AVAILABLE!":
                    "QUANTITY REMAINING IS ",PNYOH,*W,*W,*W
         BEEP
BKTOK03  DISPLAY   *P01:24,"YOU MAY KEY A NEW QUANTITY ",*W,*W
         DISPLAY   *P01:23,*EF
         GOTO      B03
. 
NYOK     GOTO      B04                      1/31/82 DONT DECREMENT
         SUBTRACT  NFIELD03 FROM PNYOH
         MOVE      "X"    TO  PARTUPSW
         GOTO      B04
. 
TXUD     CMATCH    "R" TO PASSCOMP
         GOTO      AHA IF NOT EQUAL
         COMPARE   NFIELD03 TO PTXOH
         GOTO      TXOK IF NOT LESS
         BEEP
         DISPLAY   *P01:23,"DESIRED QUANTITY IS NOT AVAILABLE!":
                   "QUANTITY REMAINING IS ",PTXOH,*W,*W,*W
         BEEP
         GOTO      BKTOK03
. 
TXOK     GOTO      B04                       1/31/82 DONT DECREMENT
         SUBTRACT  NFIELD03 FROM PTXOH
         MOVE      "X"  TO  PARTUPSW
         GOTO      B04
. .
. ...N O T E   YOU WANT TO UPDATE THE COMMODITY FILE WITH $ OF COST
.              GOODS SOLD...EXCEPT YOU HAVEN`T CALCULATED IT YET
.              RIGHT NOW YOU HAVE NOTHING BUT AN EXTENDED SELLING
.              PRICE
. 8/8/81  BYPASS ALL UPDATE CODE AT THIS POINT IN TIME UNTIL
.         IT IS DETERMINED THAT THE ENTIRE INVOICE IS CORRECT
.         AT WHICH TIME WE WILL WRITE THE INVOICE HEADER AND
.         ALL DETAILS FROM THE "DAINVCS/TXT  DA FILE" TO THE
.         INVCHDRS/ISI AND INVCDTLS/ISI FILES, UPDATING THE
.         INVENTF/ISI, CUSTMAST/ISI, USAGE/ISI, CONTROLS/ISI,
.         COMMODS/ISI, AND MAYBE THE NONSTDS/ISI FILES
. 
         GOTO      LINEINCR         (BYPASS THE UPDATES UNTIL LATER!)
*...........................................................
. 
. DECREASE THE COMMODITY RECORD BY THE DOLLARS INVOLVED
. 
COMMDOLS MOVE      PARTNO TO  PART3
         MOVE      PART3  TO  COMMKEY
         CALL      GETCOMM
         CMATCH    "H"    TO  IOSW
         GOTO      COMM1  IF  EQUAL
. 
. NO COMMODITY RECORD EXISTS SO CREATE ONE
. 
COMMCRT  MOVE      PDESC  TO  COMMDESC
         MOVE      "00"   TO  COMMCTLG
         CLEAR     COMPODS1
         CLEAR     COMPODS2
         CLEAR     COMPODS3
         MOVE      ".00"  TO  COMNYVAL
         MOVE      ".00"  TO  COMTXVAL
         TRAPCLR   IO
         TRAP      NOCMWRT    IF IO
         WRITE     COMODITY,COMMKEY;COMMKEY,COMMDESC,COMMCTLG,COMPODS1:
                                    COMPODS2,COMPODS3,COMNYVAL,COMTXVAL
         BEEP
         DISPLAY   *P01:24,"COMMODITY RECORD CREATED FOR COMM: ",COMMKEY,*W,*W
         GOTO      COMMDOLS
NOCMWRT  BEEP
         DISPLAY   *P01:24,"I/O ERROR ON COMMODITY RECORD CREATE FOR COMM: ":
                           COMMKEY
         GOTO      NOCMWRT
. 
. 
COMM1    CMATCH    "L"  TO  PASSCOMP
         GOTO      COMMREPS IF NOT EQUAL
         SUBTRACT  LINECOST FROM COMNYVAL
         GOTO      COMMPUTS
COMMREPS SUBTRACT  LINECOST FROM COMTXVAL
COMMPUTS CALL      PUTCOMM
         RETURN                             (GO BACK - SUBROUTINE)
. 
. 
. 
. COMMODITY RECORD HAS BEEN DECREMENTED BY LINE ITEM DOLLARS
. 
. NOW UPDATE THE USAGE FILE WITH THE SALE FOR THIS PART NUMBER
. 
SLSUSAGE MOVE      PARTNO   TO  USAGPART
         CALL      GETUSAGE
         CMATCH    "H"      TO  IOSW
         GOTO      NOUSEUD  IF  NOT  EQUAL
         CMATCH    "L"      TO  PASSCOMP
         GOTO      REPUSG   IF  NOT  EQUAL
         CALL      LOADNYUS                 (SELECT THIS MONTHS USAGE)
         ADD       INVCKEGS TO  NYSOLD
         CALL      STORNYUS
         MOVE      "0"      TO  NYSOLD
         GOTO      PTNWUSAG
REPUSG   CALL      LOADTXUS      
         ADD       INVCKEGS TO  TXSOLD
         CALL      STORTXUS
         MOVE      "0"      TO  TXSOLD
. 
PTNWUSAG MOVE      "M"  TO  IOSW
         CALL      PUTUSAGE
         CMATCH    "H"  TO  IOSW
         GOTO      PTUSRTRN  IF  EQUAL
         BEEP
         DISPLAY   "UNSUCCESSFUL AT USAGE UPDATE FOR PART ",USAGPART:
                   *W,*W,*W
         BEEP
         GOTO      PTNWUSAG
....................................................................
. 
. UPDATE THE NON-STDS INVENTORY RECORD IF NECESSARY HERE
. ELSE GO DOWN TO PUTSTDS AND UPDATE THE NORMAL INVENTORY FILE
. 
         CMATCH    "N" TO NONSTDSW
         GOTO      PUTSTDS   IF  NOT  EQUAL
. 
         CALL      PTNONSTD
         MOVE      " " TO NONSTDSW
. 
COMP99   COMPARE   "99"  TO  INVLINES
         GOTO      NINENINE  IF  EQUAL       (END THIS INVOICE...NOW!)
         GOTO      LOOP01
. 
DETIOERR BEEP
         DISPLAY   *ES,*P01:12,"WRITE ERROR ON INVOICE DETAIL FILE":
                       *P01:14,"PROGRAM ABORTING",*W5
         BEEP
         GOTO      INDXIT
. 
DADTLIO  BEEP
         DISPLAY   *ES,*P01:12,"WRITE ERROR ON RECORD ",RECCNTR:
                               " OF INVOICE DA FILE":
                       *P01:14,"UNRECOVERABLE ERROR - CALL MICRO SYSTEMS":
                               " CONSULTANTS!"
         GOTO      DADTLIO
. 
PUTSTDS  CALL      PARTUPDT
         MOVE      " "    TO  PARTUPSW
         GOTO      COMP99
. 
NINENINE BEEP
         DISPLAY   *P01:24,*EL,"99 ITEMS KEYED FOR  INVC# ",INVCNO:
                     " I AM CLOSING THIS INVOICE",*W,*W,*W
         BEEP
         GOTO      BIGEOJ
. 
NOTFND   BEEP
         DISPLAY  *P01:ROW,"INVALID PART NUMBER - PLEASE RESUBMIT",*W,*W,*W
         SUBTRACT  "1" FROM ROW
         GOTO      LOOP01
CUSTUPDS MOVE      PASSACCT  TO  CUSTKEY
         CALL      GETCUST
         CMATCH    "H"       TO  IOSW
         GOTO      ADDCUST   IF  EQUAL
         BEEP
         DISPLAY   *P01:24,"CANNOT FIND THE CUSTOMER RECORD TO UPDATE SALES"
         GOTO      CTRLUPDT
ADDCUST  ADD       TOTDOLS   TO  CURNTDUE
         MOVE      TOTDOLS   TO  LSTSALPR
         MOVE      INVDATE   TO  LSTSALDT
         CALL      PUTCUST
. 
. CUSTOMER HAS BEEN UPDATED WITH DOLLAR INFORMATION
. 
CTRLUPDT CMATCH    "L"       TO  PASSCOMP
         GOTO      REPCTLS   IF  NOT  EQUAL
         MOVE      "LIB"     TO  CTLKEY
PROCCTL  CALL      GETCTL
         CMATCH    "H"       TO  IOSW
         GOTO      NOCTLSFD  IF  NOT EQUAL
         ADD       TOTDOLS   TO  CTLSOLD
         MOVE      TODAY     TO  CTLDATE
         CALL      PUTCTL
. .
. CONTROLS HAVE BEEN UPDATED
         GOTO      ENDREPLY
. 
. 
NOCTLSFD BEEP
         DISPLAY   *P01:24,"NO CONTROLS RECORD ...... WHATS HAPPENING?"
         GOTO      NOCTLSFD
. 
. 
REPCTLS  MOVE      "REP"     TO  CTLKEY
         GOTO      PROCCTL
