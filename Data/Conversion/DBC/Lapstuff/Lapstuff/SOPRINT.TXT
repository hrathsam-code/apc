.   ORDER PROCESSING SYSTEM......PACKING LIST PRINT
. 
.  WRITTEN BY R. BACHARACH,  MICRO SYSTEMS CONSULTANTS
.                            41 TEELE DRIVE
.                            CORAM, NEW YORK 11727
.                            (516) 698-5577
. 
         INCLUDE   COMMON
. 
INVCNUMB DIM        5
PASSDT6  DIM       6
PASSACCT DIM       4
PASSCOMP DIM       1
PASSCUST DIM       30
. 
NINVCNO  DIM       5
NEWFKEY  DIM       5
.
INDX1    FORM      1
RECCNTR  FORM      2
.
REPLY    DIM       1
.FIELD   TYPE      LENGTH    FROM-TO   DESCRIPTION
.-----   ----      ------    -------   -----------
WORK01   DIM        1
WORK02   DIM        2
WORK03   DIM        3
WORK04   DIM        4
WORK08   DIM        8
WORK10   DIM       10
WORK09   DIM       9
FIELD09  DIM       7
NFIELD09 FORM      7
.
DATE8    DIM       8            8 DIGIT DATE WORK FIELD
MM       DIM       2
DD       DIM       2
YY       DIM       2
XMM      DIM       2
XDD      DIM       2
XYY      DIM       2
FORM5    FORM      5
. 
DTYPE    DIM       8
DSTAT    DIM       10
INDX     FORM      2
STARTKEY DIM       5
PP       DIM       1
PC       DIM       1
LINES    FORM      2
PTERMS   DIM       18
HDRSW    DIM       1
PAGES    FORM      2
. 
         INCLUDE   PART
. 
. CONTROL FILE AREA
. 
         INCLUDE   CONTFILE
. 
NFIELD01 FORM       4                  (NUMERIC WORK FIELD)
FIELD01  DIM        4
. 
NFLD01   FORM      9
FLD01    DIM       9
. 
MULTIFLG DIM       1
MULTIROW FORM      2
MULTICNT FORM      2
MULTIDSC DIM       25
MD1      DIM       25
MD2      DIM       25
MD3      DIM       25
MD4      DIM       25
MD5      DIM       25
MD6      DIM       25
MD7      DIM       25
MD8      DIM       25
MD9      DIM       25
MD10     DIM       25
EXTRA    INIT      "XXXXXXXXXXXXXXXXXXX"
COMPLETE DIM       1
UOM      DIM       2
PRODCODE DIM       2
SHIPNAME DIM       30
SHIPADDR DIM       25
SHIPYADR DIM       25
SHIPADR2 DIM       25
SHIPCITY DIM       18
SHIPSTAT DIM       2
SHIPZIP  DIM       5
COMPLET  DIM       1
. 
NORMTERM INIT      "2% 15 NET 30"
.
SOHDRFL  IFILE     KEYL=5,UNCOMPRESSED
SOHDRNM  INIT      "SOHDRFL/ISAM"
.
SOHDRKY  DIM       5
SOLINES  DIM       2
SOHDRNO  DIM       5
.
.
SODTLFL  IFILE     KEYL=8,UNCOMPRESSED
SODTLNM  INIT      "SODTLFL/ISAM"
.
SODTLKY  DIM       8  (SO#/SOLN#/MULTI-DESC-SUFFIX [0-9])
.
SODTLSTA DIM       1
SODTLNO  DIM       5
SODTLLN  DIM       2
SODTLSFX DIM       1
NEWTERM  DIM       15
. ACCOUNT FILE READ/WORK AREA
. 
CUSTFILE IFILE     KEYL=4,UNCOMPRESSED
CSTNAME  INIT      "ACCOUNTS/ISAM"
. 
         INCLUDE   CUSTMAST
.
CLOSESW  INIT      " "
. 
ADDKEY   FORM      5           INCREMENT WORK FIELD FOR NUMBERS
. 
.  WORK  AREAS FOR INVOICE HEADER CREATION/WRITE
. 
         INCLUDE   RCVBLES
. 
SAVE     DIM         10
TOTDOLS  FORM        6.2
FRGHT    FORM        2.2
. 
OLDDET   DIM       1
IOSW     DIM       1
LIBNET   FORM      7.2
REPNET   FORM      7.2
. 
+...........................................................
. 
DATE2X   DIM       2
DATE2N   FORM      2
. 
. 
INVCKEGS FORM      7     (ORDER QTY)
PCSSOLD  FORM      7     (SHIPPED QTY)
INVPCSKG FORM      7     (BACKORDER QTY)
. 
BUILDPT  DIM       7
. 
. RECORD ASSEMBLY AREA
.           INVOICE DETAIL DETAIL RECORD = 45 BYTES
. 
INVCNO   DIM       5                   INVC NUMBER
INVCLINE DIM       2                   INVC LINE NUMBER SUFFIX FOR INDEXING
. 
.  NOTE THAT INVCLINE WILL BE A 2 DIGIT NUMERIC SUFFIX
.  COUNTER-CREATED IN THIS PROGRAM
. 
INVLINES FORM      "00"
IRECCNTR FORM      6
DTLSTAT  DIM       1
ILIVCNTR FORM      3
. 
ROW      FORM      2                USED TO ROLL DOWN THE KEYIN STMT
. 
NFIELD04 FORM       5.2                (NUMERIC WORK FIELD)
FIELD04  DIM        8         11- 18   PRICE LN 1                 5.2
NFIELD03 FORM       3
FIELD03  DIM        3
NFIELD02 FORM       4
FIELD02  DIM        4
. 
. EXTENSION CALCULATE WORK/DISPLAY AREA
. 
LNDOLS   FORM      5.2
KEGQTY   FORM      9
LINECOST FORM      6.2
KGSAVAIL FORM      6
CRAPMSG  DIM       18
CALCFLD  FORM      8.3
OLDFRGT  FORM      5.2
. 
FORM3    FORM      3
IPNSTDSX DIM       6
IPNSTDSN FORM      6
NONSTDSW DIM       1
SCANSW   DIM       1
PARTUPSW DIM       1
. 
KREPLY   DIM       1
. 
CHKPASS  DIM       5
.  
WEIGHT   FORM      6
HDRCHGSW DIM       1
DUMMY    DIM       40
. 
INVDTKEY DIM       7
. 
OLDXTEND FORM      6.2
. 
. 
CALCNET  FORM      6.2
FRTALLOW FORM      5.2
. 
TIMEDIM  DIM       8
. 
CUSTN    FORM      4
SPECMSG  DIM       80
MSGFLAG  DIM       1
PORTX    DIM       2
SPECSW   DIM       1
TIME     INIT      "HH/MM/SS"
ALIGNSW  DIM       1
. 
+........................................................................
. 
START    DISPLAY   *ES,*P01:12,*HON,"PACKING LIST PRINT PROGRAM":
                   *HOFF:
                       *P01:14,"IF YOU HAVE COME HERE ERRONEOUSLY FROM THE ":
                               "SYSTEM MENU PROGRAMS":
                       *P01:15,"AND DO  N O T  HAVE ANY  INVOICES TO ":
                               "ENTER........":
                       *P01:17,"YOU MAY TERMINATE THIS PROGRAM BY PRESSING ":
                               "THE   F5   KEY"
         KEYIN     *P01:20,*HON,"KEY     F5     TO  TERMINATE":
                   *P01:21,"KEY  ENTER  TO PRINT PACKING LISTS ",*HOFF,REPLY
. 
         GOTO      MAINLINE  IF  NOT  F5
. 
         BEEP
INDXIT   DISPLAY   *HOFF,*ES
         BEEP
         CHAIN     "APPINVC1"
.
MAINLINE MOVE      " " TO REPLY
.
.
         MOVE      " ",REPLY
         MOVE      " ",MSGFLAG
         MOVE      " ",SPECMSG
         GOTO      OPENFLS
. 
. FILE OPENING SEQUENCE
OPENFLS  TRAP      NOFILE  IF IO
. 
         OPEN      SOHDRFL,SOHDRNM
.
         OPEN      SODTLFL,SODTLNM
.
         MOVE      TODAY TO MM
         MOVE      TODAY TO XMM
         RESET     TODAY TO 4
         MOVE      TODAY TO DD
         MOVE      TODAY TO XDD
         RESET     TODAY TO 7
         MOVE      TODAY TO YY
         MOVE      TODAY TO XYY
         RESET     TODAY TO 1
. 
. SET UP SIX DIGIT DATE
. 
         MOVE      TODAY TO DATE8
         MOVE      DATE8 TO INVDATE
         BUMP      DATE8 BY 3
         RESET     INVDATE TO 2
         APPEND    DATE8 TO INVDATE
         BUMP      DATE8 BY 3
         RESET     INVDATE TO 4
         APPEND    DATE8 TO INVDATE
         RESET     INVDATE
         RESET     DATE8
         MOVE      INVDATE  TO  PASSDT6      PASS 6 DIGIT DATE TO DTL PGM
         GOTO      ACCTOPEN
.
NOFILE   DISPLAY   *ES,"ORDER PROCESSING FILES NOT ON LINE":
                   *N,"JOB IS ABORTING"
         BEEP
         CHAIN     "APPINVC1"        GO BACK TO DISPLAY INVOICE MENU
*...........................................................
. OPEN ISAM-LOOKUP FILE
.
ACCTOPEN TRAPCLR   IO
         TRAP      NOCUSTFL IF IO
.
         CALL      OPENCUST
.
         CMATCH    "H"  TO  IOSW
         GOTO      NOCUSTFL IF NOT EQUAL
.
         TRAPCLR   IO
         TRAP      NOCTRLS IF IO
.
         CALL      OPENCTLS
.
         CMATCH    "H"    TO IOSW
         GOTO      NOCTRLS IF NOT EQUAL
.
ZERONETS TRAPCLR   IO                (OKAY NOW LETS GO!)
         MOVE      "A" TO INVCOMP
         GOTO      DISPLAY
. 
NOCTRLS  DISPLAY   *ES," CONTROLS  FILE IS NOT ON-LINE",*W,*W
         BEEP
. 
MNUCHAIN CHAIN     "APPINVC1"
. 
NOCUSTFL DISPLAY   *ES,"CUSTOMER FILE IS NOT ON-LINE, ABORTING"
         BEEP
         GOTO      MNUCHAIN
. 
*...........................................................
. INITIAL SCREEN DISPLAY SEQUENCE
.
DISPLAY  CLOCK     TIME TO TIME
         DISPLAY   *ES:
                   *P1:1,*HON,"PRINTING PACKING LISTS FOR SHIPMENT":
                   *P1:3,"LOAD PACKING LIST MAILER FORMS.....":
                   *P1:4,"SET LEFT MARGIN AT MINUS 5 (-5)":
                         " AND TOP OF PRINT HEAD AT THE FORM PERFORATION":
                   *HOFF
         KEYIN     *P1:23,"PRESS ENTER KEY WHEN READY TO START ",REPLY
         DISPLAY   *P1:23,*EL
.
ALIGNCK  MOVE      "100.00" TO LINECOST
         MOVE      "1.00"   TO LNDOLS
         MOVE      "100"    TO INVCKEGS
         MOVE      "100"    TO PCSSOLD
         MOVE      "EA"     TO UOM
         MOVE      "TESTPART"  TO MULTIDSC
         MOVE      "C"      TO COMPLET
.
         MOVE      "XXXXX"  TO INVKEY
         MOVE      "TESTORDER" TO INVORDNO
         MOVE      "10"     TO INVWGHT
         MOVE      "1"      TO CARTONS
         MOVE      NORMTERM TO NEWTERM
         MOVE      "UPS"    TO INVSHIP
.
         MOVE      "0001"   TO  CUSTKEY
         CALL      GETCUST
.
         MOVE      "X"  TO  ALIGNSW
         MOVE      CUSTNAME TO SHIPNAME
         MOVE      CUSTADDR TO SHIPADDR
         MOVE      CUSTADDR TO SHIPADR2
         MOVE      CUSTCITY TO SHIPCITY
         MOVE      CUSTSTAT TO SHIPSTAT
         MOVE      CUSTZIP  TO SHIPZIP
.
         CALL      HDINVCS
         MOVE      "X"  TO  ALIGNSW
.
         CALL      P1
.
ALIGNASK MOVE      " " TO REPLY
         KEYIN     *P1:23,*EL,"ANOTHER ALIGNMENT TRY? (Y/N) ",REPLY
         DISPLAY   *P1:23,*EL
         CMATCH    "Y",REPLY
         GOTO      ALIGNCK IF EQUAL
         CMATCH    "N",REPLY
         GOTO      ORDERFOR IF EQUAL
         BEEP
         GOTO      ALIGNASK
.
ORDERFOR MOVE      "A" TO INVCOMP
         MOVE      " " TO ALIGNSW
.
HDRLOOP  READKS    SOHDRFL;SOHDRNO,SOLINES,INVTYPE,INVSTAT,INVCUST:
                           INVDATE,INVTOTL,INVORDNO,INVWGHT,NEWTERM:
                           CARTONS,INVSHIP,COMPLET:
                           SHIPNAME,SHIPADDR,SHIPADR2:
                           SHIPCITY,SHIPSTAT,SHIPZIP:
                           CALCNET,LIBNET
.
         GOTO      LASTEOJ IF OVER
.
.......................................................................
.   5/25/91  -  DETERMINE IF THIS IS A REPRINT RUN
.               IF TESTONE = 'R' THEN REPRINT ALL PACKING LISTS
.......................................................................
.
REPRINT  CMATCH    "R",TESTONE
         GOTO      SETDTLS  IF  EQUAL
.
         MATCH     "O"  TO  INVSTAT
         GOTO      HDRLOOP  IF  NOT  EQUAL
.
. SET KEY FOR DETAIL READS
.
SETDTLS  CLEAR     SODTLKY
         APPEND    SOHDRNO  TO  SODTLKY
         APPEND    "000"    TO  SODTLKY
         RESET     SODTLKY  TO  1
         READ      SODTLFL,SODTLKY;DUMMY    (REC NOT FND)
.
MOVREP   MOVE      "APP" TO CTLKEY
. 
PRNTINVC MOVE      "000000.00" TO IAMTPD1
         MOVE      "000000.00" TO IAMTPD2
         MOVE      "000000.00" TO IAMTPD3
         MOVE      "000000.00" TO IAMTPD4
         MOVE      "000000.00" TO IAMTPD5
         MOVE      "      "    TO ICHK1
         MOVE      "      "    TO ICHK2
         MOVE      "      "    TO ICHK3
         MOVE      "      "    TO ICHK4
         MOVE      "      "    TO ICHK5
         MOVE      "      "    TO IDTPD1
         MOVE      "      "    TO IDTPD2
         MOVE      "      "    TO IDTPD3
         MOVE      "      "    TO IDTPD4
         MOVE      "      "    TO IDTPD5
         MOVE      "00000000"  TO IPRGDTE
         MOVE      "0"         TO IPMNTS
         MOVE      SOHDRNO     TO INVKEY
         MOVE      INVKEY      TO STARTKEY  (SET THE ISAM ADD KEY)
         MOVE      " "         TO HDRSW
         MOVE      "0"         TO PAGES
         GOTO      PRNTS
. 
. NOW PRINT THE INVOICE
. 
PRNTS    MOVE      SOHDRNO  TO  INVKEY
         MOVE      INVCUST  TO  CUSTKEY
         CALL      GETCUST
.
         CMATCH    "C",INVTYPE
....        CALL      HDCREDS IF EQUAL
         CALL      HDINVCS IF EQUAL    (DON'T WANT DIFFERENT HDR!)
. 
         CMATCH    "I",INVTYPE
         CALL      HDINVCS IF EQUAL
. 
         CMATCH    "H",HDRSW
         RETURN    IF EQUAL
.
         GOTO      DETPRINT
. 
HDCREDS  PRINT     *74,"XXXXXXXXXX",*FLUSH
         PRINT     *60,"CREDIT NO.",*75,"A",INVKEY,*FLUSH
         PRINT     " ",*FLUSH
         GOTO      ADRPRINT
. 
HDINVCS  PRINT     *73,"A",INVKEY,*FLUSH
         PRINT     " ",*FLUSH
         PRINT     " ",*FLUSH
. 
ADRPRINT PRINT     " ",*FLUSH
         PRINT     " ",*FLUSH
         PRINT     *8,"ACCT. ",CUSTKEY
         PRINT     " ",*FLUSH
         PRINT     *8,CUSTNAME,*54,SHIPNAME,*FLUSH
         PRINT     *8,CUSTADDR,*54,SHIPADDR,*FLUSH
         PRINT     *8,CUSTCITY," ",CUSTSTAT," ",*ZF,CUSTZIP:
                   *54,SHIPADR2,*FLUSH
         PRINT     *8,"ACCOUNTS PAYABLE":
                   *54,SHIPCITY," ",SHIPSTAT," ",SHIPZIP,*FLUSH
.
         PRINT     " ",*FLUSH
         PRINT     *6,INVORDNO,*58,INVORDNO,*FLUSH
         PRINT     " ",*FLUSH
         PRINT     " ",*FLUSH
. 
         CMATCH    "C",COMPLET
         GOTO      PARTIAL IF NOT EQUAL
. 
         MOVE      "X" TO PC
         MOVE      " " TO PP
         GOTO      NXTPRNT
. 
PARTIAL  MOVE      " " TO PC
         MOVE      "X" TO PP
. 
NXTPRNT  PRINT     *2,INVORDNO,*17,TODAY,*26,INVWGHT," ",CARTONS:
                   *37,INVSHIP,*57,NEWTERM,*73,PC,*80,PP,*FLUSH
. 
         PRINT     " ",*FLUSH
         PRINT     " ",*FLUSH     (19 LINES PRINTED SO FAR!)
         PRINT     " ",*FLUSH     (ADDED 12/16/88 R.B.)
         MOVE      "19" TO LINES
. 
. END HEADER PRINT
. 
         RETURN
. 
DETPRINT CLEAR     SODTLKY
         APPEND    SOHDRNO  TO SODTLKY
         APPEND    "000"     TO SODTLKY
         RESET     SODTLKY  TO 1
         READ      SODTLFL,SODTLKY;DUMMY    (REC NOT FND)
. 
DETLOOP  READKS    SODTLFL;SODTLNO,SODTLLN,SODTLSFX,DTLSTAT,MULTIDSC:
                           INVCKEGS,INVPCSKG,PCSSOLD:
                           LNDOLS,LINECOST,UOM,PRODCODE
.
         GOTO      NODETS  IF  OVER
.
MTCHNOS  MATCH     SOHDRNO TO SODTLNO
         GOTO      NODETS  IF  NOT  EQUAL
. 
. 
         CMATCH    "D",DTLSTAT
         GOTO      DETLOOP IF EQUAL  (BYPASS DELETED RECORDS)
. 
         CMATCH    "*",DTLSTAT
         GOTO      CONTPRNT IF EQUAL
. 
P1       PRINT     *3,INVCKEGS,*13,PCSSOLD,*23,INVPCSKG,*33,MULTIDSC,*FLUSH
.....                  *62,LNDOLS,*71,UOM,*74,LINECOST,*FLUSH
. 
. NOW INCREMENT THE TOTALS
. 
         ADD       LINECOST  TO  INVTOTL
. 
         ADD       "1" TO LINES
.
         COMPARE   "40",LINES
         CALL      HEADER  IF  NOT  LESS
.
         MATCH     "X" TO ALIGNSW
         GOTO      NODETS  IF  EQUAL
.
         GOTO      DETLOOP
. 
CONTPRNT PRINT     *34,MULTIDSC
.
         ADD       "1" TO LINES
.
         COMPARE   "40",LINES
         CALL      HEADER  IF  NOT  LESS
.
         GOTO      DETLOOP
. 
HEADER   PRINT     " ",*FLUSH
         PRINT     " ",*FLUSH
         PRINT     " ",*FLUSH
.
         ADD       "1",PAGES
         PRINT     *1,"*** PAGE ",PAGES," ***",*FLUSH
.
         PRINT     " ",*FLUSH
         PRINT     " ",*FLUSH
         PRINT     " ",*FLUSH
         PRINT     " ",*FLUSH
         PRINT     " ",*FLUSH
         PRINT     " ",*FLUSH
         PRINT     " ",*FLUSH
. 
. PRINTED  11 BLANK LINES FOR SPACE DOWN TO NEXT INVOICE
. 
         MOVE      "H" TO HDRSW
         CALL      PRNTS
         MOVE      " " TO HDRSW
         RETURN
. 
NODETS   COMPARE   "40",LINES
         GOTO      SPCLOOP IF LESS
         GOTO      SUBTOTS IF EQUAL
. 
SPCLOOP  PRINT     " ",*FLUSH
         ADD       "1" TO LINES
         GOTO      NODETS
. 
SUBTOTS  MOVE      INVTOTL TO LIBNET
         ADD       CALCNET TO LIBNET
.
. 10/27/90  ADD TWO BLANK LINES
.
         PRINT     " ",*FLUSH
         PRINT     " ",*FLUSH
         PRINT     " ",*FLUSH
         PRINT     " ",*FLUSH
         PRINT     " ",*FLUSH
         PRINT     " ",*FLUSH
. 
         MOVE      LIBNET TO INVTOTL
         ADD       INVTOTL TO REPNET
         MOVE      INVTOTL TO IDOLSLFT
. 
         PRINT     " ",*FLUSH
         PRINT     " ",*FLUSH
         PRINT     " ",*FLUSH
....         PRINT     *15,"D. HASTINGS",*FLUSH
         PRINT     " ",*FLUSH
         PRINT     " ",*FLUSH
.
         MATCH     "X" TO ALIGNSW
         GOTO      ALGNRTRN IF EQUAL
.
.  NOW UPDATE THE SOHDRFL RECORD SO WE DON'T PRINT IT AGAIN!
.
         MOVE      "P"  TO  INVSTAT
.
         INCLUDE   SOHDUPDT/TEXT
.
.
         GOTO      HDRLOOP
.
ALGNRTRN RETURN
. 
LASTEOJ  BEEP
         DISPLAY   *P1:23,*EL,"END OF PACKING LIST PRINT",*W2
         RELEASE
         CHAIN     "APPINVC1"
         INCLUDE   CONTRLIO
. 
. 
         INCLUDE   CUSTIO
. 
         INCLUDE   STDIOERR
. 
         INCLUDE   STDFMTER
. 
         INCLUDE   STDRANGE
. 
         INCLUDE   STDPARER
. 
         STOP
