. PRINT	ACCOUNTS RECEIVABLE DETAIL LISTING
. 
	 INCLUDE   MSCBRAG.TXT
.
	 INCLUDE   COMMON.TXT
.
NAME	 INIT	   "C:\DBC\ARFILE.ISI"
INPUT	 IFILE	   KEYL=5,UNCOMPRESSED	      (ISAM INPUT )
. 
. 
REPLY	 DIM	   1
SWMR	 FORM	   "0"	       '1'=MATCHING RECORD
SW1P	 FORM	   "1"	       '1'=FIRST CYCLE
LINECT	 FORM	   "000"	   LINE	COUNTER
PAGE	 FORM	   3	       PAGE NUMBER
RID01	 FORM	   "0"	       RECORD TYPE 01
LR	 FORM	   "0"	       '1'=LAST	RECORD PENDING
LRTOT	 FORM	   5		GRAND TOTAL
VOIDS	 FORM	   5
PAIDS	 FORM	   5
CRDTS	 FORM	   5
ORDRTOTS FORM	   8.2
. 
MATLTOTS FORM	   8.2
IOSW	 DIM	   1
COMP	 DIM	   1
. 
. 
	 INCLUDE   CUSTMAST
. 
CUSTFILE IFILE	   KEYL=4,UNCOMPRESSED
CSTNAME	 INIT	   "C:\DBC\ACCOUNTS.ISI"
. 
	 INCLUDE   RCVBLES.TXT
. 
COMPVAR	 DIM	   30
. 
MM	 DIM	   2
DD	 DIM	   2
YY	 DIM	   2
DATE8	 DIM	   8
. 
STATUS	 DIM	   8
+............................................................
. MAINLINE FOLLOWS
.
. FILE OPENING SEQUENCE
	 DISPLAY   *ES,*P01:10,"PRINT ACCOUNTS RECEIVABLE MASTER LIST":
		   *P01:12,"NOTE..THIS REPORT MAY BE PRINTED ON	NARROW PAPER":
		   *P01:16,"P L	E A S E	  BE PATIENT!  THIS IS A VERY LARGE":
			   " FILE!"
. 
	 GOTO	   CONT1
. 
NOARFILE BEEP
	 DISPLAY   *P01:24,*EL,"ACCOUNTS RECEIVABLE FILE NOT ON	LINE",*W2
	 GOTO	    NOARFILE
. 
CONT1	 TRAPCLR   IO
	 TRAP	   NOARFILE IF IO
	 OPEN	   RCVBFILE,RCVBNAME
.
	 TRAPCLR   IO
	 CALL	   OPENCUST
	 CMATCH	   "H"	TO  IOSW
	 GOTO	   NOFILE   IF NOT EQUAL
	 GOTO	   GETCOMP
. 
NOFILE	 BEEP
	 DISPLAY   *P01:12,*EL,"ACCOUNT	FILE NOT MOUNTED - ":
			       "JOB ABORTING",*W
	 GOTO	   CHAINOUT
. 
GETCOMP	 TRAPCLR   PARITY    (NOP)
. 
MOVLIB	 MOVE	   "AMERICAN PRECISION COMPONENTS " TO	COMPVAR
	 MOVE	   "A"	TO  COMP
	 CALL	   HEADER
. 
*............................................................
. READ A RECORD	FROM THE FILE JUST PROC'D
. AT START: READ RECORD	FROM EACH FILE
.
READ	 READKS	   RCVBFILE;INVKEY,INVTYPE,INVCOMP,INVSTAT,INVCUST:
			    INVDATE,INVTOTL,IPMNTS:
			    IAMTPD1,IAMTPD2,IAMTPD3,IAMTPD4,IAMTPD5:
			    ICHK1,ICHK2,ICHK3,ICHK4,ICHK5:
			    IDTPD1,IDTPD2,IDTPD3,IDTPD4,IDTPD5:
			    IDOLSLFT,IPRGDTE
	 GOTO	   EOJROUT  IF	OVER
*............................................................
. CHECK	THE COMPANY CODE FOR MATCH TO TERMINAL INPUT
.	 MATCH	   COMP	   TO INVCOMP
.	 GOTO	   READ	IF NOT EQUAL
. 
. BYPASS THE ZERO RECORD
. 
	 MATCH	   "00000" TO INVKEY
	 GOTO	   READ	   IF EQUAL
. ........
*............................................................
. IS THE ITEM AN  O P E	N  RECEIVABLE  ?
	 CMATCH	   "X",INVSTAT
	 GOTO	   READ	 IF  EQUAL	   (ITEM HAS BEEN   P A	I D)
	 CMATCH	   "V",INVSTAT
	 GOTO	   READ	 IF  EQUAL	   (ITEM HAS BEEN  V O I D E D)
	 CMATCH	   "W",INVSTAT
	 GOTO	   READ	 IF  EQUAL	   (ITEM HAS BEEN  WRITTEN OFF)
.
MOVEDATA TRAPCLR   PARITY   (NOP)
	 MOVE	   INVCUST TO CUSTKEY
	 CMATCH	   "O" TO INVSTAT
	 GOTO	   CHKCLS IF NOT EQUAL
	 MOVE	   "OPEN " TO STATUS
	 GOTO	   TRAPS
CHKCLS	 CMATCH	   "P" TO INVSTAT
	 GOTO	   MAKCANC IF NOT EQUAL
	 MOVE	   "PARTIAL" TO	STATUS
	 GOTO	   TRAPS
MAKCANC	 CMATCH	   "V"	  TO  INVSTAT
	 GOTO	   MKC1	  IF  NOT   EQUAL
	 MOVE	   "VOID"     TO    STATUS
	 GOTO	   TRAPS
MKC1	 CMATCH	   "X"	  TO  INVSTAT
	 GOTO	   MKC2	  IF  NOT   EQUAL
	 MOVE	   "PAID"     TO    STATUS
	 GOTO	   TRAPS
MKC2	 CMATCH	   "C"	  TO  INVSTAT
	 GOTO	   MKC3	  IF  NOT  EQUAL
	 MOVE	   "CRDT"     TO   STATUS
	 GOTO	   TRAPS
MKC3	 MOVE	   "DEL."     TO   STATUS
TRAPS	 TRAPCLR   IO
	 CALL	   GETCUST
	 CMATCH	   "H"	  TO IOSW
	 GOTO	   SETDATE   IF	 EQUAL
PNOVEND	 MOVE	   "*ACCOUNT NOT ON FILE*" TO CUSTNAME
*............................................................
. CLEAR	OUT ACCUMULATORS (DETAIL TIME)
.
SETDATE	 TRAPCLR   IO
	 MOVE	   INVDATE TO DATE8
	 MOVE	   DATE8  TO MM
	 RESET	   DATE8  TO 3
	 MOVE	   DATE8  TO DD
	 RESET	   DATE8  TO 5
	 MOVE	   DATE8  TO YY
	 RESET	   DATE8
	 CMATCH	   "C"	  TO INVTYPE
	 GOTO	   DETCALC IF NOT EQUAL
	 MULT	   "-1"	  BY INVTOTL
	 MULT	   "-1"	  BY IDOLSLFT
. 
DETCALC	 ADD	   "1" TO LRTOT
	 ADD	   IDOLSLFT TO ORDRTOTS
	 ADD	   IMATLS   TO MATLTOTS
*............................................................
. HEADING AND DETAIL OUTPUT
.
DETOUT	 COMPARE   "50"	TO LINECT
	 CALL	   HEADER IF NOT LESS
*............................................................
	 PRINT	   *3,INVKEY:
		   *9,MM,"/",DD,"/",YY:
		   *19,CUSTNAME:
		   *50,STATUS:
		   *58,INVTOTL:
		   *68,IDOLSLFT
......		       *83,IMATLS
. 
	 ADD	   "1" TO LINECT
	 GOTO	   READ
*............................................................
. 
	 INCLUDE   CUSTIO
. PAGE HEADING ROUTINE
.
HEADER	 ADD	   "1" TO PAGE
	 PRINT	   *F:
		   *21,"RECEIVABLES MASTER LISTING":
		   *56,"A/O ",TODAY:
		   *70,"PG ",PAGE
	 PRINT	   *21,COMPVAR,*C,*L
	 MOVE	   "0" TO LINECT
	 PRINT	   *2,"INVOICE/CREDIT":
		   *19,"ACCOUNT	NAME":
		   *49,"STATUS":
		   *58,"BEGINNING":
		   *71,"OPEN"
	 PRINT	   *2,"NUMBER":
		   *11,"DATE":
		   *60,"TOTAL":
		   *71,"TOTAL":
		   *C,*L
	 RETURN
*............................................................
. TOTAL	OUTPUT
.
EOJROUT	 CALL	   HEADER
	 PRINT	   *C,*L,*L
	 PRINT	   *5,LRTOT," OPEN INVOICES FOR	",COMPVAR:
		   *65,ORDRTOTS,"*",*C,*L,*L
	 PRINT	   "NOTE: THE TOTALS SHOWN INDICATED AMOUNTS WHICH REMAIN ":
		   "OPEN AT THIS TIME"
.
	 RELEASE
CHAINOUT CHAIN	   "ARMENU1 "
