.....................................................................
. MSPARTDE/TXT - CREATE	INITIAL	PARTS/PURCHASE/SALES HISTORY FILES  .
.								    .
	 INCLUDE   MSCBRAG.TXT
.								    .
. THIS PROGRAM PERMITS ISAM-ADD	DATA ENTRY OF NEW PART NUMBERS,	AND .
. ASSOCIATED PURCHASE/SALE/QUOTATION HISTORY TO	TEMPORARY ISAM WORK .
. FILES	AS FOLLOWS:						    .
.								    .
. NPARTS(nn)/ISI, NPURCH(nn)/ISI, NSALES(nn)/ISI..NOTE (nn) = PORTN .
.								    .
. THESE	ISAM FILES WILL	BE SAPPED BY AN	AUTOMATED PROCEDURE TO THE  .
. FOLLOWING PERMANENT MASTER FILES:				    .
.								    .
. MSPARTFL/ISI,	  MSPURCHS/ISI,	  MSSALES/ISI			    .
.								    .
. THE TEMPORARY	DATA ENTRY FILES WILL THEN BE LISTED BY	PROGRAM	    .
. MSPLIST/DBC, AND WILL	THEN BE	KILLED,	PREPPED	AND REINDEXED	    .
.....................................................................
. 
. MODIFIED 4/7/84 TO MOVE THE UOP FIELD	LEFT ONE POSITION
. 
. 
	 INCLUDE   COMMON
. 
IOSW	 DIM	   1
. 
APMASTER IFILE	    KEYL=6,UNCOMPRESSED
. 
CUSTFILE IFILE	    KEYL=4,UNCOMPRESSED
CSTNAME	 INIT	   "C:\DBC\ACCOUNTS.ISI"
. 
	 INCLUDE   MSPARTSF.TXT
. 
	 INCLUDE   MSPURCHF.TXT
. 
	 INCLUDE   MSSALESF.TXT
. 
	 INCLUDE   DATETBLZ
. 
. 
DT6	 DIM	   6
MIN	 DIM	   2
DIN	 DIM	   2
YIN	 DIM	   2
FORM2	 FORM	   2
WHCH	 DIM	   1
REPLY	 DIM	   1
. 
TYPEIN	 DIM	   1
POIN	 DIM	   18
UOPIN	 DIM	   2
QTYIN	 FORM	   5.2
PRCIN	 FORM	   5.2
DATEIN	 DIM	   6
VENDIN	 DIM	   6
NAMEIN	 DIM	   20
DNAME	 DIM	   20
. 
NPARTS	 IFILE	    KEYL=20,UNCOMPRESSED
NPARTSNM INIT	   "C:\DBC\NEWPARTS.ISI"
. 
NPURCH	 IFILE	    KEYL=29,UNCOMPRESSED
NPURCHNM INIT	   "C:\DBC\NEWPURCH.ISI"
. 
NSALES	 IFILE	    KEYL=29,UNCOMPRESSED
NSALESNM INIT	   "C:\DBC\NEWSALES.ISI"
. 
WHO	 DIM	   25
PORTX	 DIM	   2
. 
Z28	 INIT	   "0000000000000000000000000000"
. 
NROW	 FORM	   2
NCOL	 FORM	   2
. 
	 INCLUDE   APMASTFD
. 
	 INCLUDE   CUSTMAST
. 
BLANK30	 INIT	   "				  "
DISPQTY	 FORM	   5
DUMMYRD	 DIM	   25
. 
.....................................................
. 
DISPLAY	 DISPLAY   *ES,*P09:01,"INVENTORY HISTORY SYSTEM - INITIAL DATA":
		   " ENTRY PROGRAM",*P73:01,TODAY:
		   *P01:02,"PART NUMBER",*P31:02,"CROSS-REF PART NO.1":
		   *P61:02,"CROSS-REF PART NO.2":
		   *P01:04,"SIZE",*P38:04,"DESCRIPTION":
		   *P25:05,"PURCHASE/SALE/QUOTATION HISTORY":
		   *P01:06,"TYP	PURCHASE ORDER##    UOP	QUANTITY    PRICE   ":
			   "DATE VND/CST##  NAME":
		   *P13:20,"TYPE CODES",*P50:20,"UNIT OF PURCHASE":
		   *P01:21,"P=PURCHASE	R=VENDOR QUOTATION":
		   *P41:21,"EA=EACH,C=HUND,M=THOU,OZ=OUNCE,LB=POUND":
		   *P01:22,"S=SALE	Q=QUOTE	TO CUSTOMER":
		   *P41:22,"BG=BAG,BX=BOX,FT=FOOT"
. 
WHOISIT	 MOVE	   " " TO WHO
	 MOVE	   "DATA ENTRY OPERATOR" TO WHO
	 GOTO	   START
. 
	 INCLUDE   MSPARTIO
. 
	 INCLUDE   MSSALEIO
. 
	 INCLUDE   MSPOIO
. 
	 INCLUDE   STDIOERR
. 
	 INCLUDE   STDFMTER
. 
	 INCLUDE   STDPARER
. 
	 INCLUDE   STDRANGE
. 
	 INCLUDE   CUSTIO
.
START	 CALL	   CLRTRAPS
. 
	 TRAP	   STDIOERR IF IO
	 TRAP	   STDPARER IF PARITY
	 TRAP	   STDFMTER IF FORMAT
	 TRAP	   STDRANGE IF RANGE
. 
	 MOVE	   "OPEN"    TO	 IOTYPE
	 MOVE	   NPARTSNM  TO	 IOERFILE
	 OPEN	   NPARTS,NPARTSNM
. 
	 MOVE	   NPURCHNM  TO	 IOERFILE
	 OPEN	   NPURCH,NPURCHNM
. 
	 MOVE	   NSALESNM  TO	 IOERFILE
	 OPEN	   NSALES,NSALESNM
. 
. NOW SET THE NAME RECORD ON EACH FILE
. 
	 MOVE	   Z28	TO  MSPARTKY
	 CALL	   CLRTRAPS
	 MOVE	   NPARTSNM TO	IOERFILE
	 MOVE	   "READ"   TO	IOTYPE
. 
	 TRAP	   STDIOERR IF	IO
	 TRAP	   STDRANGE IF	RANGE
	 TRAP	   STDFMTER IF	FORMAT
	 TRAP	   STDPARER IF	PARITY
. 
	 READ	   NPARTS,MSPARTKY;MSSTATUS,MSPARTNO,MSSIZE:
				   MSXRFPT1,MSXRFPT2
	 GOTO	   NAMEOK   IF	NOT  OVER  (RECORD ALREADY EXISTS!)
. 
. FALLTHRU MEANS NO ZERO RECORD	EXISTS,	THEREFORE, THIS	IS THE INITIAL ENTRY
. 
	 MOVE	   "X"	TO  MSSTATUS
	 MOVE	   Z28	TO  MSPARTNO
	 MOVE	   WHO	TO  MSSIZE
	 MOVE	   " "	TO  MSDESC
	 MOVE	   " "	TO  MSXRFPT1
	 MOVE	   " "	TO  MSXRFPT2
	 MOVE	   "WRITE"  TO	IOTYPE
. 
. NOW ADD PART RECORD HEADER
. 
	 TRAPCLR   IO
	 TRAP	   AAA IF IO
	 FILEPI	   1;NPARTS
	 WRITE	   NPARTS,MSPARTKY;MSSTATUS,MSPARTNO,MSSIZE,MSDESC:
				    MSXRFPT1,MSXRFPT2
. 
AAA	 MOVE	   Z28	TO  MSPOKEY
	 MOVE	   "0.00"   TO	MSPOQTY
	 MOVE	   "0.00"   TO	MSPOPRC
	 MOVE	   " "	    TO	MSPOTYPE
	 MOVE	   WHO	    TO	MSPONUMB
	 MOVE	   "  "	    TO	MSPOUOP
	 MOVE	   "000000" TO	MSPODATE
	 MOVE	   "000000" TO	MSPOVEND
. 
. NOW ADD PO FILE HEADER
. 
	 TRAPCLR   IO
	 TRAP	   BBB IF IO
	 FILEPI	   1;NPURCH
	 WRITE	   NPURCH,MSPOKEY;MSPOKEY,MSPOTYPE,MSPONUMB,MSPOUOP:
				  MSPOQTY,MSPODATE,MSPOPRC,MSPOVNAM
. 
BBB	 MOVE	   Z28	TO  MSSALEKY
	 MOVE	   "0.00"   TO	MSSLQTY
	 MOVE	   "0.00"   TO	MSSLPRC
	 MOVE	   " "	    TO	MSSLTYPE
	 MOVE	   WHO	    TO	MSSLNUMB
	 MOVE	   "  "	    TO	MSSLUOP
	 MOVE	   "000000" TO	MSSLDATE
	 MOVE	   "000000" TO	MSSLCUST
. 
. NOW ADD SALES	FILE HEADER
. 
	 TRAPCLR   IO
	 TRAP	   NAMEOK IF IO
	 FILEPI	   1;NSALES
	 WRITE	   NSALES,MSSALEKY;MSSALEKY,MSSLTYPE,MSSLNUMB,MSSLUOP:
				   MSSLQTY,MSSLDATE,MSSLPRC,MSSLCNAM
. 
NAMEOK	 CALL	   OPENCUST
	 CALL	   MSPTOPEN
	 CALL	   MSSLOPEN
	 CALL	   MSPOOPEN
. 
....	     OPEN      APMASTER,"APMASTER/ISAM"
	 OPEN	   APMASTER,"C:\DBC\APMASTER.ISI"
. 
DOMORE	 KEYIN	   *P1:23,*EL,*HON,"PRESS F1 TO	ENTER NEW PART / F5 TO ":
		   "TERMINATE ",*HOFF,REPLY
	 GOTO	   EOJ IF F5
	 GOTO	   PARTIN IF F1
	 BEEP
	 GOTO	   DOMORE
. 
PARTIN	 GOTO	   CLRPART
. 
PARTIN1	 KEYIN	   *P01:03,MSPARTNO
	 DISPLAY   *P01:03,MSPARTNO
. 
	 GOTO	   EOJ	IF  F5
	 DISPLAY   *P1:23,*EL
. 
	 MOVE	   " " TO MSXRFPT1
	 KEYIN	   *P31:03,*RV,MSXRFPT1
	 DISPLAY   *P31:03,MSXRFPT1
. 
	 MOVE	   " " TO MSXRFPT2
	 KEYIN	   *P61:03,*RV,MSXRFPT2
	 DISPLAY   *P61:03,MSXRFPT2
. 
	 MOVE	   " " TO MSSIZE
	 KEYIN	   *P06:04,MSSIZE
	 DISPLAY   *P06:04,MSSIZE
. 
	 MOVE	   " " TO MSDESC
	 KEYIN	   *P50:04,MSDESC
	 DISPLAY   *P50:04,MSDESC
. 
PARTOK	 MOVE	   " " TO REPLY
	 KEYIN	   *P1:23,*EL,*HON,"ALL	PART DATA CORRECT ? (Y/N) ",REPLY:
		   *HOFF
	 CMATCH	   "Y",REPLY
	 GOTO	   WRITPART  IF	 EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   CLRPART IF EQUAL
	 BEEP
	 GOTO	   PARTOK
. 
CLRPART	 DISPLAY   *P01:03,*EL,*P06:04,BLANK30,*P50:04,BLANK30
	 MOVE	   " " TO MSPARTNO
	 MOVE	   " " TO MSXRFPT1
	 MOVE	   " " TO MSXRFPT2
	 MOVE	   " " TO MSSIZE
	 MOVE	   " " TO MSDESC
	 CALL	   RECYCLE
	 GOTO	   PARTIN1
. 
WRITPART CALL	   CLRTRAPS
	 MOVE	   MSPARTNO  TO	 MSPARTKY
.
....................	  CALL	    MSPTREAD
.
	 FILEPI	   1;MSPARTFL
	 READ	   MSPARTFL,MSPARTKY;DUMMYRD
.
	 GOTO	   DUPEPART  IF	 NOT  OVER
	 CALL	   CLRTRAPS
	 TRAP	   DUPEPART IF IO
	 TRAP	   STDFMTER IF FORMAT
	 TRAP	   STDRANGE IF RANGE
	 TRAP	   STDPARER IF PARITY
. 
	 MOVE	   "WRITE"  TO	IOTYPE
	 MOVE	   MSPARTNM TO	IOERFILE
	 MOVE	   MSPARTNO TO MSPARTKY
	 MOVE	   "A"	    TO MSSTATUS
. 
	 FILEPI	   1;NPARTS
	 WRITE	   NPARTS,MSPARTKY;MSSTATUS,MSPARTNO,MSSIZE,MSDESC:
				   MSXRFPT1,MSXRFPT2
. 
	 CALL	   MSPTWRIT
. 
. 
	 MOVE	   "6"	TO  NROW
. 
	 GOTO	   MOREDATA
. 
DUPEPART BEEP
	 DISPLAY   *P1:23,*EL,*HON,"PART ",MSPARTKY," ALREADY ON FILE! ":
		   *HOFF,*W2,*EL
	 GOTO	   DOMORE
. 
. 
DATAIN	 ADD	   "1"	TO  NROW
	 COMPARE   "20"	TO  NROW
	 CALL	   RECYCLE  IF NOT LESS
	 GOTO	   GETDATA
. 
RECYCLE	 MOVE	   "6"	TO  NROW
RCYCLE	 ADD	   "1"	TO  NROW
	 COMPARE   "20"	TO  NROW
	 GOTO	   CYCLOUT  IF	EQUAL
	 DISPLAY   *P01:NROW,*EL
	 GOTO	   RCYCLE
. 
CYCLOUT	 MOVE	   "7"	TO  NROW
	 RETURN
. 
GETDATA	 KEYIN	   *P02:NROW,TYPEIN
	 GOTO	   EOJ	IF F5
	 CMATCH	   "P",TYPEIN
	 GOTO	   GPO IF EQUAL
	 CMATCH	   "S",TYPEIN
	 GOTO	   GPO IF EQUAL
	 CMATCH	   "R",TYPEIN
	 GOTO	   GPO IF EQUAL
	 CMATCH	   "Q",TYPEIN
	 GOTO	   GPO IF EQUAL
	 BEEP
	 DISPLAY   *P02:NROW," "
	 GOTO	   GETDATA
. 
GPO	 DISPLAY   *P01:23,*EL
	 KEYIN	   *P05:NROW,POIN
	 DISPLAY   *P05:NROW,POIN
. 
UOP	 MOVE	   "EA"	 TO  UOPIN
	 KEYIN	   *P25:NROW,*RV,UOPIN
. 
. VALIDATE UNIT	OF PURCHASE CODE
. 
	 MATCH	   "EA"	TO UOPIN
 	 GOTO	   QTYIN   IF  EQUAL
	 CMATCH	   "C"	TO UOPIN
	 GOTO	   QTYIN   IF  EQUAL
	 CMATCH	   "M"	TO UOPIN
	 GOTO	   QTYIN   IF  EQUAL
	 MATCH	   "OZ"	TO UOPIN
	 GOTO	   QTYIN   IF  EQUAL
	 MATCH	   "LB"	TO UOPIN
	 GOTO	   QTYIN   IF  EQUAL
	 MATCH	   "BG"	TO UOPIN
	 GOTO	   QTYIN   IF  EQUAL
	 MATCH	   "BX"	TO UOPIN
	 GOTO	   QTYIN   IF  EQUAL
	 MATCH	   "FT"	TO UOPIN
	 GOTO	   QTYIN   IF  EQUAL
	 BEEP
	 DISPLAY   *P1:23,*EL,*HON,"UNIT OF PURCHASE INVALID",*W2:
		   *HOFF,*P1:23,*EL
	 DISPLAY   *P25:NROW,"	"
	 GOTO	   UOP
. 
QTYIN	 MOVE	   "0.00"  TO  QTYIN
	 KEYIN	   *P30:NROW,*RV,QTYIN
. 
	 MOVE	   QTYIN   TO  DISPQTY
. 
. ......................................................
. 4/18/84 CLEAR	TO END OF LINE TO PROPERLY DISPLAY DATA
. 
	 DISPLAY   *P27:NROW,*EL
. ......................................................
. 
	 DISPLAY   *P28:NROW,DISPQTY,*P36:NROW,*EL
. 
PRCIN	 MOVE	   "0.00"  TO  PRCIN
	 KEYIN	   *P37:NROW,*RV,PRCIN
	 DISPLAY   *P37:NROW,PRCIN
. 
DATEIN   MOVE      "00"  TO  YIN
	 MOVE	   "01"	 TO  MIN
	 MOVE	   "01"	 TO  DIN
	 KEYIN	   *P47:NROW,*RV,*DE,*+,MIN,*RV,*DE,*+,DIN,*RV,*DE,*-,YIN
. 
	 MOVE	   MIN	TO  FORM2
	 COMPARE   "01"	TO  FORM2
	 GOTO	   DATERR   IF	LESS
	 COMPARE   "13"	TO  FORM2
	 GOTO	   DATERR   IF	NOT  LESS
. 
	 MOVE	   DIN	TO  FORM2
	 COMPARE   "01"	TO  FORM2
	 GOTO	   DATERR   IF	LESS
	 COMPARE   "32"	TO  FORM2
	 GOTO	   DATERR   IF	NOT  LESS
. 
...         MOVE      YIN  TO  FORM2
...         COMPARE   "80" TO  FORM2
...         GOTO      DATERR  IF  LESS
. 
	 CLEAR	   DATEIN
	 APPEND	   MIN	TO  DATEIN
	 APPEND	   DIN	TO  DATEIN
	 APPEND	   YIN	TO  DATEIN
	 LENSET	   DATEIN
	 RESET	   DATEIN
	 REPLACE   " 0"	IN  DATEIN
. 
. NOW SET UP KEY FIELD KNOWN AS	DATEIN
. 
	 CLEAR	   DT6
. 
	 RESET	   TODAY  TO  5
	 APPEND	   TODAY  TO  DT6
. 
	 RESET	   DT6	  TO  2
	 RESET	   TODAY  TO  1
	 APPEND	   TODAY  TO  DT6
. 
	 RESET	   DT6	  TO  4
	 RESET	   TODAY  TO  3
	 APPEND	   TODAY  TO  DT6
. 
	 RESET	   DT6	  TO  6
	 LENSET	   DT6
. 
	 RESET	   DT6	  TO  1
	 RESET	   TODAY  TO  1
	 REP	   " 0"	  IN  DT6
	 GOTO	   GVEND
. 
DATERR	 BEEP
	 DISPLAY   *P1:23,*EL,*HON,"INVALID DATE",*HOFF,*W2,*P1:23,*EL
	 DISPLAY   *P47:NROW,"	    "
	 GOTO	   DATEIN
. 
GVEND	 MOVE	   "000000"  TO	 VENDIN
	 KEYIN	   *P54:NROW,*RV,*DE,VENDIN
. 
. NOTE ...  IF	ZERO IS	IN THE VENDOR CODE FIELD, IT MEANS THAT	NO VENDOR
.	    CODE IS AVAILABLE, SO PERMIT KEYIN OF THE VENDOR NAME.
. 
	 MATCH	   "000000"  TO	 VENDIN
	 GOTO	   NAMEIN    IF	 EQUAL
. 
. FALLTHRU MEANS THEY KEYED A VENDOR/CUSTOMER NUMBER
. FIND OUT WHICH BY TYPE CODE
. 
WHICH	 CMATCH	   "P",TYPEIN
	 GOTO	   VENDOR IF EQUAL
	 CMATCH	   "R",TYPEIN
	 GOTO	   VENDOR IF EQUAL
. 
. FALLTHRU IS CUSTOMER
. 
	 MOVE	   "C" TO WHCH
	 MOVE	   VENDIN TO CUSTKEY
	 CALL	   GETCUST
	 CMATCH	   "H",IOSW
	 GOTO	   DCUST IF EQUAL
	 BEEP
	 DISPLAY   *P1:23,*EL,*HON,"CUSTOMER NOT ON FILE",*HOFF,*W2,*P1:23,*EL
	 GOTO	   GVEND
. 
DCUST	 MOVE	   CUSTNAME TO NAMEIN
	 DISPLAY   *P61:NROW,NAMEIN
	 GOTO	   CHKDATA
. 
VENDOR	 MOVE	   "V" TO WHCH
	 MOVE	   VENDIN TO ACCKEY
. 
	 INCLUDE   APMASTRD
. 
	 GOTO	   VNDNG IF OVER
	 MOVE	   NAME	 TO NAMEIN
	 DISPLAY   *P61:NROW,NAMEIN
	 GOTO	   CHKDATA
. 
VNDNG	 BEEP
	 DISPLAY   *P1:23,*EL,*HON,"VENDOR NOT ON FILE",*HOFF,*W2,*P1:23,*EL
	 GOTO	   GVEND
. 
. 
NAMEIN	 KEYIN	   *P61:NROW,*RV,NAMEIN
	 CMATCH	   "P",TYPEIN
	 GOTO	   NM1 IF EQUAL
	 CMATCH	   "R",TYPEIN 
	 GOTO	   NM1 IF EQUAL
. 
	 MOVE	   "C" TO WHCH
	 GOTO	   CHKDATA
. 
NM1	 MOVE	   "V" TO WHCH
. 
CHKDATA	 MOVE	   " " TO REPLY
	 KEYIN	   *P1:23,*EL,*HON,"IS ALL DATA	CORRECT	(Y/N) ?	",*HOFF,REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   RECORDS IF EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   CHKDATA IF NOT EQUAL
. 
. FALLTHRU IS BAD DATA
. 
	 DISPLAY   *P1:23,*EL
	 DISPLAY   *P1:NROW,*EL
	 SUBTRACT  "1" FROM NROW
	 GOTO	   DATAIN
. 
RECORDS	 CMATCH	   "V",WHCH
	 GOTO	   PORECS IF EQUAL
. 
. FALLTHRU IS SLRECS - SALES OR	SALES QUOTATIONS
. 
CUSTOMER MOVE	   MSPARTNO  TO	 MSSLPART
	 MOVE	   DT6	     TO	 MSSLJULD
	 MOVE	   "000"     TO	 MSSLSUFX
	 MOVE	   TYPEIN    TO	 MSSLTYPE
	 MOVE	   POIN	     TO	 MSSLNUMB
	 MOVE	   UOPIN     TO	 MSSLUOP
	 MOVE	   QTYIN     TO	 MSSLQTY
	 MOVE	   PRCIN     TO	 MSSLPRC
	 MOVE	   DATEIN    TO	 MSSLDATE
	 MOVE	   VENDIN    TO	 MSSLCUST
	 MOVE	   NAMEIN    TO	 MSSLCNAM
	 CLEAR	   MSSALEKY
	 APPEND	   MSSLPART  TO	 MSSALEKY
. 
. NOTE.... MSSLJULD / DT6  IN  YY/MM/DD	ORDER
. 
	 APPEND	   MSSLJULD  TO	 MSSALEKY
	 APPEND	   MSSLSUFX  TO	 MSSALEKY
	 LENSET	   MSSALEKY
	 RESET	   MSSALEKY  TO	 1
. 
WCUSTREC CALL	   CLRTRAPS
. 
	 TRAP	   STDFMTER IF FORMAT
	 TRAP	   STDPARER IF PARITY
	 TRAP	   STDRANGE IF RANGE
	 TRAP	   BUMPCSUF IF IO
.
	 FILEPI	   1;MSSALEFL
	 WRITE	   MSSALEFL,MSSALEKY;MSSLPART,MSSLJULD,MSSLSUFX,MSSLTYPE:
				     MSSLNUMB,MSSLUOP,MSSLQTY,MSSLDATE:
				     MSSLCUST,MSSLPRC,MSSLCNAM
.
	 FILEPI	   1;NSALES
	 WRITE	   NSALES,MSSALEKY;MSSLPART,MSSLJULD,MSSLSUFX,MSSLTYPE:
				     MSSLNUMB,MSSLUOP,MSSLQTY,MSSLDATE:
				     MSSLPRC,MSSLCNAM
	 CMOVE	   " " TO IOSW
. 
. 
MOREDATA MOVE	   " " TO REPLY
	 KEYIN	   *P1:23,*EL,*HON,"PRESS F1 FOR MORE HISTORY /	F5 IF ":
		   "COMPLETE ",*HOFF,REPLY
. 
	 GOTO	   DATAIN   IF F1
	 GOTO	   DOMORE   IF F5
	 BEEP
	 GOTO	   MOREDATA
. 
CLEANUP	 DISPLAY   *P1:23,*EL
	 MOVE	   " ",REPLY
	 GOTO	   DATAIN
. 
	 GOTO	   DATAIN
. 
BUMPCSUF MOVE	   MSSLSUFX  TO	 CINDX1
	 ADD	   "1"	     TO	 CINDX1
	 MOVE	   CINDX1    TO	 MSSLSUFX
	 REP	   " 0"	     IN	 MSSLSUFX
. 
	 RESET	   MSSALEKY  TO	 26
	 APPEND	   MSSLSUFX  TO	 MSSALEKY
	 RESET	   MSSALEKY  TO	 29
	 LENSET	   MSSALEKY
	 RESET	   MSSALEKY  TO	 1
. 
	 MOVE	   "X" TO IOSW
	 RETURN
. 
PORECS	 INCLUDE   MSPORECS.TXT
. 
EOJ	 DISPLAY   *P1:23,*EL,*HON,"END	OF INVENTORY HISTORY DATA ENTRY",*W2:
		   *HOFF
. 
	 CHAIN	   "MSHISTRY"
. 
