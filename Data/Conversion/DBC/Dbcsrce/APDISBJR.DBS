. .
. APDISBJR MODULE - A/P	DISBURSEMENTS JOURNAL
. 
	 INCLUDE   COMMON
. 
	 INCLUDE   APPAIDFD
. 
	 INCLUDE   APMASTFD
. 
APDISTFL IFILE	   KEYL=6,UNCOMPRESSED
. 
APPAIDFL IFILE	   KEYL=25,UNCOMPRESSED
APPAIDNM INIT	   "C:\DBC\PAIDBYDT.ISI"
. 
APMASTER IFILE	   KEYL=6,UNCOMPRESSED
APMASTNM INIT	   "C:\DBC\APMASTER.ISI"
. 
VENDIN	 DIM	   6
DROW	 FORM	   "03"
REPLY	 DIM	   1
VENDTOT	 FORM	   "	   0.00"					      
RUNTOTS	 FORM	   "000000000.00"
PAGES	 FORM	   "0000"
LINES	 FORM	   "00"
WHORPLY	 DIM	   1
ASTER	 INIT	   "---------------------------------------------"
LISTTYPE DIM	   1
PERIOD	 DIM	   1
LISTNAME DIM	   25
IPMONTH	 DIM	   2
IPYEAR   DIM       2
MONTHN	 FORM	   2
TESTMON	 DIM	   2
PTLINES  FORM      5 
SAVECHK	 DIM	   6
SAVEACCT DIM	   6
SAVEGLCD DIM	   6
PRTMON	 DIM	   2
PRTDAY	 DIM	   2
PRTYR	 DIM	   2
SAVEDTPD DIM	   8
SAVEDESC DIM	   20
SAVENAME DIM	   25
HOWTO	 DIM	   1
TRANNKEY DIM	   25
DESC2	 DIM	   20
PYR	 DIM	   2
YRCHKX   DIM       2
YRCHKN   FORM      2
TESTYR   FORM      2
. 
PAGETOTL FORM	   8.2
.
	 INCLUDE   DISTTABL.TXT
. 
	 INCLUDE   APCALNDR
. 
	 DISPLAY   *ES,"ACCOUNTS PAYABLE SYSTEM	- PRINT	DISBURSEMENTS ":
		   "JOURNAL"
. 
	 MOVE	   "	  " TO TDCODE
	 MOVE	   "	  " TO SRCHCODE
	 MOVE	   "0" TO SRCHINDX
	 MOVE	   "0" TO TAMT
	 MOVE	   "1" TO FIRSTIME
.
.  DATE	CORRECTION    1/23/99 TO CAPTURE REPORTING YEAR	 .
.
	 CLOCK	   DATE	 TO  TODAY
	 REP	   " 0"	 IN  TODAY
         DISPLAY   *P1:22,*EF,"TODAY=",TODAY,*W3
         MOVE      TODAY  TO  PRTMON
         REP       " 0" IN PRTMON
	 RESET	   TODAY  TO  3
	 MOVE	   TODAY  TO  PRTDAY
         REP       " 0" IN PRTDAY
	 RESET	   TODAY  TO  5
	 MOVE	   TODAY  TO  PRTYR
         REP       " 0"   IN  PRTYR
	 RESET	   TODAY  TO  1
.
BLNKLOOP ADD       "1" TO SRCHINDX
	 COMPARE   "51"	TO SRCHINDX
	 GOTO	   ENDBLP IF EQUAL
. 
	 INCLUDE   DISTSTOR
. 
...	  INCLUDE   DISTUPDT
. 
	 GOTO	   BLNKLOOP
. 
ENDBLP	 MOVE	   "0" TO SRCHINDX
. 
	 MOVE	   "* ALL PAYMENTS *" TO LISTNAME
	 MOVE	   "D" TO LISTTYPE
	 GOTO	   OPENFILZ
. 
HEADER	 PRINT	   *F
	 ADD	   "1" TO PAGES
. 
	 CMATCH	   "P" TO PERIOD
	 GOTO	   HDRX	IF NOT EQUAL
	 MOVE	   "*DETAIL/LAST 60 DAYS*" TO LISTNAME
. 
HDRX	 CMATCH	   "S" TO LISTTYPE					      
	 GOTO	   HDRCONT IF NOT EQUAL
	 MOVE	   "* S	U M M A	R Y *" TO LISTNAME
HDRCONT	 PRINT	   *1,LISTNAME:						      
		   *36,"** A P C   C O M P O N E N T S - DISBURSEMENTS":
		       " JOURNAL ":
		       " **",*100,"PAGE	",PAGES,"     AS OF ":
		       PRTMON,"/",PRTDAY,"/",PRTYR
         PRINT     *50,"FOR MONTH OF ",IPMONTH,"/",IPYEAR
         PRINT     *1,"CHECK NO.         DESCRIPTION              AMOUNT":
		      "	    DATE PAID		    VENDOR   NAME":
		      *115,"G/L	CODE"
	 PRINT	   " "
	 MOVE	   "3" TO LINES						      
	 RETURN								      
. 
OPENFILZ OPEN	   APPAIDFL,APPAIDNM					      
	 OPEN	   APMASTER,APMASTNM
	 OPEN	   APDISTFL,"C:\DBC\APDISTFL.ISI"
.
         MOVE      PRTMON TO TMON
         MOVE      PRTDAY TO TDAY
         MOVE      PRTYR  TO TYEAR
         CALL      CALCDATE
	 MOVE	   TESTDATE TO CURRDATE
	 SUBTRACT  "60"	FROM CURRDATE
. 
. NOW ALL WE NEED DO IS	SEE IF TESTDATE	OF TRANSACTION IS LESS THAN CURRDATE
. 
. IF LESS, AND OPTION IS PRINT 60, THEN	BYPASS
. 
. 
KEYMONTH MOVE	   " " TO HOWTO
	 MOVE	   "M" TO HOWTO
	 GOTO	   HOWOK
....
. 
HOWOK	 CMATCH	   "M",HOWTO
	 GOTO	   TAKEYEAR IF NOT EQUAL
         KEYIN     *P40:2,"ENTER MONTH (01) ",*DE,IPMONTH
	 MOVE	   IPMONTH TO MONTHN
	 COMPARE   "1" TO MONTHN
	 GOTO	   KEYMONTH IF LESS
	 COMPARE   "13",MONTHN
	 GOTO	   KEYMONTH IF NOT LESS
	 MOVE	   MONTHN TO IPMONTH
         REP       " 0" IN IPMONTH
DOYR     MOVE      PRTYR TO IPYEAR
         KEYIN     *P60:2,"ENTER YEAR (00) ",*RV,*DE,IPYEAR
         MOVE      IPYEAR TO TYEAR
         MOVE      IPYEAR TO YRCHKX
         MOVE      YRCHKX TO YRCHKN   (STORE REPORT YEAR FOR COMPARE)


. 
TAKEYEAR MOVE	   "D" TO LISTTYPE
	 MOVE	   "A" TO PERIOD
	 GOTO	   WHO1
. 
SUMMARY	 KEYIN	   *P1:4,*HON,"PRINT (S)ummary or (D)etail REPORT ?  ":
		   LISTTYPE
	 CMATCH	   "S" TO LISTTYPE
	 GOTO	   WHO IF EQUAL						      
	 CMATCH	   "D" TO LISTTYPE
	 GOTO	   WHO IF EQUAL
	 BEEP
	 GOTO	   SUMMARY
. 
. 
WHO	 MOVE	   "	  " TO VENDIN
	 MOVE	   " " TO REPLY
. 
. NOW DETERMINE	IF WE WANT ONLY	PAYMENTS OF LAST SIXTY DAYS
. 
	 KEYIN	   *P1:6,"PRINT	(A)ll items or ":
		   "(P)ast 60 DAYS PAYMENTS ? ",REPLY			      
. 
	 MOVE	   REPLY TO PERIOD
	 CMATCH	   "A",REPLY
	 GOTO	   WHO1	IF EQUAL
	 CMATCH	   "P",REPLY
	 GOTO	   WHO1	IF EQUAL
	 BEEP
	 DISPLAY   *P1:6,*EL
	 GOTO	   WHO
. 
WHO1	 CALL	   HEADER
. 
. 
...................... 7/23/83
. 
	 CLEAR	   TRANNKEY
	 APPEND	   TYEAR TO TRANNKEY
	 APPEND	   IPMONTH TO TRANNKEY
	 APPEND	   "			     " TO TRANNKEY
	 RESET	   TRANNKEY
	 TRAPCLR   IO
	 TRAP	   READPAID  IF	IO
	 DISPLAY   *P1:23,*EL,*HON,"READING THE	PAYMENTS HISTORY FILE ",*HOFF
	 READ	   APPAIDFL,TRANNKEY;TRANNKEY
	 GOTO	   READPAID IF OVER
	 GOTO	   READPAID
. 
. ........................
. 
. 
WHO1A	 KEYIN	   *P1:8,"PRINT	(A)ll vendors or by (V)endor ? ",WHORPLY
	 CMATCH	   "A",WHORPLY
	 GOTO	   PRNTALL IF EQUAL
	 CMATCH	   "V",WHORPLY
	 GOTO	   WHO2	IF EQUAL
	 BEEP
	 GOTO	   WHO1A
. 
PRNTALL	 MOVE	   "000000" TO ACCKEY
. 
	 INCLUDE   APMASTRD
. 
	 INCLUDE   APMASTKS
. 
	 MOVE	   ACCNO  TO  VENDIN
. 
	 MOVE	   ACCNO TO TRNKEY
	 RESET	   TRNKEY TO 6
	 APPEND	   "		 " TO TRNKEY				      
	 RESET	   TRNKEY TO 1
. 
	 INCLUDE   APPAIDRD
.									      
	 GOTO	   READPAID
. 
WHO2	 KEYIN	   *P01:24,*EL,"ENTER VENDOR NUMBER DESIRED or ":
		   "PRESS F5 TO	TERMINATE ",VENDIN
. 
	 GOTO	   EOJ IF F5
. 
	 MOVE	   VENDIN TO ACCKEY
. 
	 INCLUDE   APMASTRD
. 
	 GOTO	   NOGOOD IF OVER
. 
	 DISPLAY   *P01:02,"VENDOR IS ",NAME				 
. 
CORRVEND MOVE	   " " TO REPLY						      
	 KEYIN	   *P1:24,*EL,"CORRECT VENDOR ?	(Y/N) ",REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   OK IF EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   WHO IF EQUAL
	 BEEP
	 GOTO	   CORRVEND
. 
VENDSET	 INCLUDE   APMASTKS
	 MOVE	   ACCNO TO VENDIN
	 GOTO	   EOJ IF OVER
. 
	 MOVE	   ACCNO  TO  ACCKEY
. 
. 
OK	 CLEAR	   TRNKEY						      
	 APPEND	   ACCKEY TO TRNKEY
	 APPEND	   "		  " TO TRNKEY
	 RESET	   TRNKEY TO 1
. 
	 INCLUDE   APPAIDRD
. 
. 
READPAID TRAPCLR   IO
. 
. 
	 INCLUDE   APPAIDKS
. 
	 GOTO	   EOJ IF OVER
. 
.................................................
	 CMATCH	   "1" TO FIRSTIME
	 GOTO	   NOTFIRST IF NOT EQUAL
. 
	 MOVE	   " " TO FIRSTIME
	 CALL	   SAVERTN
	 GOTO	   NOTFIRST
. 
SAVERTN	 MOVE	   TACCN     TO	 SAVEACCT
	 MOVE	   CKNUMBER  TO	 SAVECHK
	 MOVE	   TDCODE    TO	 SAVEGLCD
	 MOVE	   TDESC     TO	 SAVEDESC
	 MOVE	   TPAIDDT   TO	 SAVEDTPD
. 
	 MOVE	   TACCN     TO	 ACCKEY
	 INCLUDE   APMASTRD
	 MOVE	   NAME	     TO	 SAVENAME
. 
	 RETURN
. 
. 
. TEST FOR TAKEYEAR 
. 
NOTFIRST CMATCH	   "Y",HOWTO
	 GOTO	   RDXX	IF EQUAL
. 
. TEST THE MONTH 7/23/83
. 
	 MOVE	   TPAIDDT TO TESTMON
         DISPLAY   *P1:23,"TESTMON=",TESTMON:
                   "  IPMONTH=",IPMONTH,*EL,*W
	 MATCH	   IPMONTH TO TESTMON
         GOTO      EOJ IF NOT EQUAL

         RESET     TPAIDDT TO 5
         MOVE      TPAIDDT TO YRCHKX
         MOVE      YRCHKX  TO TESTYR
         COMPARE   YRCHKN  TO TESTYR
         GOTO      EOJ IF NOT EQUAL

. ........................................
. 
. 
. CHECK	IF WE ARE SEEKING ONLY LAST 60 DAYS ACTIVITY
. AND IN DETAIL	PRINT
. 
	 CMATCH	   "S",LISTTYPE
	 GOTO	   RDXX	IF NOT EQUAL
. 
. FALLTHRU MEANS WE ARE	IN DETAIL PRINT
. SO ARE WE IN THE (P) PERIOD (60 DAYS ACTY?)
. 
	 CMATCH	   "P",PERIOD
	 GOTO	   RDXX	IF NOT EQUAL					      
. 
. OKAY LETS TEST THE DATES
. 
	 CALL	   CALCDATE
	 COMPARE   CURRDATE TO TESTDATE
	 GOTO	   READPAID IF LESS
. 
. 
RDXX	 MATCH	   SAVECHK  TO	CKNUMBER
	 GOTO	   PRINTCHK IF	NOT  EQUAL
. 
. FALLTHRU MEANS MULTIPLE LINES	ON SAME	CHECK SO ONLY INCREMENT	THE COUNT
. 
	 ADD	   TAMT	 TO  VENDTOT
. 
	 GOTO	   PBYPASS
. 
	 ADD	   "1"	 TO  DROW
. 
	 REP	   " 0"	IN TMON						      
. 
	 CMATCH	   "S" TO LISTTYPE
	 GOTO	   READPAID IF EQUAL
	 MOVE	   TACCN TO ACCKEY
. 
. 
	 INCLUDE   APMASTRD
. 
...................................................
. 
PAGETOTS PRINT	   *1,"--------------------------------------------------":
		      "--------------------------------------------------":
		      "---------------------------------------------------"
	 PRINT	   *19,"PAGE ",PAGES," TOTAL = ",*41,PAGETOTL
	 MOVE	   "0.00" TO PAGETOTL
	 RETURN
. 
. ..........
. 
PRINTCHK PRINT	   *1,SAVECHK:
		   *19,SAVEDESC,*41,VENDTOT,*55,SAVEDTPD:
		   *80,SAVEACCT,"  ",SAVENAME,*115,SAVEGLCD
	 ADD	   "1" TO LINES
         ADD       "1" TO PTLINES
         DISPLAY   *P55:22,*EL,"LINES PRINTED:",PTLINES
. 
	 ADD	   VENDTOT TO PAGETOTL
. 
	 COMPARE   "55"	TO LINES
	 CALL	   PAGETOTS IF NOT LESS
. 
	 COMPARE   "55"	TO LINES
	 CALL	   HEADER IF NOT LESS
. 
	 MOVE	   TAMT	 TO  VENDTOT	 (SET NEW AMOUNT)
. 
	 CALL	   SAVERTN
	 GOTO	   PBYPASS
. 
. NOTE... IT WOULD HAVE	BEEN ALRIGHT JUST TO FALL THRU HERE  8/8/83
. ......................................................
. 
. NOW SUMMARIZE	THE DATA
. 
PBYPASS	 MOVE	   "0" TO SRCHINDX
	 MOVE	   TDCODE  TO  SRCHCODE
. 
	 INCLUDE   DISTSRCH.TXT
. 
	 GOTO	   NEWCODE IF OVER
	 COMPARE   "0" TO SRCHINDX
	 GOTO	   NEWCODE IF EQUAL
. 
. FALLTHRU MEANS WE HAVE A MATCH ON THE	CODE
. 
	 INCLUDE   DISTLOAD	     (GET THE EXISTING NUMBERS!)
. 
	 INCLUDE   DISTUPDT.TXT	     (NOW INCREMENT THEM!)
. 
	 GOTO	   READPAID
. 
. 
NEWCODE	 MOVE	   "	  " TO SRCHCODE	  (FIND	THE FIRST BLANK	CODE)
	 MOVE	   "0" TO SRCHINDX
. 
	 INCLUDE   DISTSRCH.TXT
. 
. SRCHINDX NOW CONTAINS	THE INDEX VALUE	NEEDED
. 
	 MOVE	   TDCODE  TO  SRCHCODE	 (LOAD THE CODE	AND UPDATE)
. 
	 INCLUDE   DISTSTOR		  (LOAD	THE CODE!)
. 
. 
. INSURE THAT WE DO NOT	HOLD OVER ANY FALSE VALUES IN COUNTERS
	 MOVE	   "0.00" TO DISTAMT
	 MOVE	   "0.00" TO DISTAMT
. ........................................
. 
	 INCLUDE   DISTUPDT.TXT
. 
	 GOTO	   READPAID
. 
. 
. 
VENDBRK	 TRAPCLR   PARITY
	 COMPARE   "57",LINES						      
	 CALL	   HEADER IF NOT LESS
. 
. CHECK	IF SUMMARY LISTING
. IF SO	AND IF VENDTOT = ZERO BYPASS PRINT
. 
	 CMATCH	   "S",LISTTYPE
	 GOTO	   VNDPRT IF NOT EQUAL					      
. 
. FALLTHRU MEANS WE ARE	PRINTING A SUMMARY LISTING
. 
	 COMPARE   "0.00" TO VENDTOT					      
	 GOTO	   VNDBRK IF EQUAL
. 
VNDPRT	 PRINT	   *80,VENDIN,"	 ",NAME,*115,VENDTOT			      
	 ADD	   "1" TO LINES
. 
. IF THIS IS A SUMMARY PRINT AND VENDOR	TOTAL >	ZERO
. THEN SINGLE SPACE ONLY!
. 
	 CMATCH	   "S",LISTTYPE
	 GOTO	   ADDVNDS IF EQUAL
. 
	 PRINT	   " "
	 ADD	   "1" TO LINES
. 
ADDVNDS	 ADD	   VENDTOT TO RUNTOTS
	 MOVE	   "0.00" TO VENDTOT					      
. 
VNDBRK	 CMATCH	   "A",WHORPLY
	 GOTO	   VENDSET IF EQUAL
.									      
.									      
	 MOVE	   " " TO REPLY
MOREVEND KEYIN	   *P1:23,*EL,"MORE VENDORS ? (Y/N) ",REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   WHOM	IF EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   MOREVEND IF NOT EQUAL
	 GOTO	   EOJ
. 
WHOM	 DISPLAY   *P01:04,*EF
	 MOVE	   "03"	TO DROW
	 MOVE	   "0.00"  TO VENDTOT
	 GOTO	   WHO
. 
NOGOOD	 DISPLAY   *P1:23,"INVALID VENDOR - PLEASE RETRY ! ",*W2
	 GOTO	   WHO
. 
RESETDSP DISPLAY   *P1:22,*EL,"VENDOR TOTAL THUS FAR IS	",VENDTOT
	 KEYIN	   *P1:23,*EL,"TAP ENTER KEY FOR DISPLAY OF ADD":
		   "ITIONAL ITEMS ",REPLY
. 
	 MOVE	   "04"	TO DROW
. 
ADDEM	 DISPLAY   *P1:DROW,*EL						      
	 ADD	   "1" TO DROW
	 COMPARE   "22"	TO DROW						      
	 GOTO	   RESETRTN IF EQUAL
	 GOTO	   ADDEM
. 
RESETRTN MOVE	   "04"	TO DROW
	 RETURN
. 
	 INCLUDE   APDATES
. 
. 
EOJ	 PRINT	   *1,SAVECHK:
		   *19,SAVEDESC,*41,VENDTOT,*55,SAVEDTPD:
		   *80,SAVEACCT,"  ",SAVENAME,*115,SAVEGLCD
	 ADD	   VENDTOT TO PAGETOTL
	 CALL	   PAGETOTS
	 CALL	   MONTHTOT
	 PRINT	   *C,*L,*L
	 PRINT	   "** TOTAL PAYMENTS MADE = ",RUNTOTS," **"
	 PRINT	   " ",*FLUSH
	 RELEASE
	 CHAIN	   "APMENU1"
. 
. 
MONTHTOT CALL	   HEADER
	 MOVE	   "0" TO SRCHINDX
	 MOVE	   "	  "  TO	 SRCHCODE  (LOAD THE BLANK DELIMITER)
. 
	 INCLUDE   DISTSRCH.TXT		(FIND THE BLANK	SLOT)
. 
	 MOVE	   SRCHINDX  TO	 TDINDX1
	 MOVE	   "0"	     TO	 SRCHINDX
. 
MONLOOP	 ADD	   "1" TO SRCHINDX
	 COMPARE   SRCHINDX  TO	 TDINDX1  (IS THIS THE BLANK SLOT?)
	 GOTO	   MONEXIT IF EQUAL
. 
	 INCLUDE   DISTLOAD.TXT
. 
	 MOVE	   DCODE  TO  TDCODE
. 
	 MOVE	   "BAD	READ ON	DIST FILE" TO DESC2
.....	  DISPLAY   *P1:24,*HON,"DCODE = ",DCODE,"  TDCODE = ",TDCODE
. 
	 TRAPCLR   IO
	 TRAP  	   PRTIT IF IO
	 READ	   APDISTFL,TDCODE;TDCODE,DESC2
. 
PRTIT	 PRINT	   *1,TDCODE,*10,DESC2,*35,DISTAMT
	 ADD	   DISTAMT TO RUNTOTS
. 
	 GOTO	   MONLOOP
. 
MONEXIT	 MOVE	   "0" TO SRCHINDX
	 RETURN
. 
