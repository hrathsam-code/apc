.................................................................
. MSPARTLS/TXT - PRINTS INVENTORY PART MASTER LISTING
.................................................................
. 
         INCLUDE   COMMON
. 
         INCLUDE   MSPARTSF/TEXT
. 
PAGES    FORM      4
. 
NPARTSFL IFILE      KEYL=20,UNCOMPRESSED
NPARTS01 INIT      "NPARTS01/ISAM"
NPARTS02 INIT      "NPARTS02/ISAM"
NPARTS03 INIT      "NPARTS03/ISAM"
NPARTS04 INIT      "NPARTS04/ISAM"
PARTS    INIT      "MSPARTFL/ISAM"
. 
TERMINAL DIM       2
TIME     INIT      "HH/MM/SSS"
LINES    FORM      2
REPLY    DIM       1
WHO      DIM       20
WHOCNT   FORM      6
.
STARTKEY DIM       20
ROLLNAME INIT      "ROLLFILE/SYST"
. 
START    DISPLAY   *ES,*P1:1,*HON,"INVENTORY HISTORY MASTER LISTING",*HOFF
. 
         MOVE      "000" TO PAGES
         CLOCK     TIME  TO TIME
. 
         OPEN      NPARTSFL,PARTS
         MOVE      "01"  TO  TERMINAL
         MOVE      "0"   TO WHOCNT
         CALL      HEADER
.
PRINTWAT MOVE      " " TO REPLY
         KEYIN     *P1:23,*EL,"PRINT (A)ll or (S)tart from P/N ",REPLY
         MATCH     "A",REPLY
         GOTO      DOIT IF EQUAL
.
         MATCH     "S",REPLY
         GOTO      GIMMEKEY IF EQUAL
.
         BEEP
         GOTO      PRINTWAT
.
GIMMEKEY MOVE      "                    "  TO  STARTKEY
         KEYIN     *P1:20,"ENTER PART NUMBER TO START FROM: ",STARTKEY
         RESET     STARTKEY TO 20
         LENSET    STARTKEY
         RESET     STARTKEY TO 1
.
         READ      NPARTSFL,STARTKEY;MSSTATUS,MSPARTNO,MSSIZE,MSDESC:
                            MSXRFPT1,MSXRFPT2
         GOTO      PRTS
.
DOIT     CALL      LP1
         GOTO      BYPASS
.
..............................................................
.
         OPEN      NPARTSFL,NPARTS02
         MOVE      "02"  TO  TERMINAL
         MOVE      "0"   TO  WHOCNT
         CALL      LOOP01
. 
         OPEN      NPARTSFL,NPARTS03
         MOVE      "03"  TO  TERMINAL
         MOVE      "0"   TO  WHOCNT
         CALL      LOOP01
. 
         OPEN      NPARTSFL,NPARTS04
         MOVE      "04"  TO  TERMINAL
         MOVE      "0"   TO  WHOCNT
         CALL      LOOP01
. 
         COMPARE   "9",SECURITY
         GOTO      BYPASS IF NOT EQUAL
. 
WHAT     KEYIN     *P1:24,*HON,"CREATE NEW DATA ENTRY FILES ? ",*HOFF,REPLY
         CMATCH    "N",REPLY
         GOTO      BYPASS IF EQUAL
         CMATCH    "Y",REPLY
         GOTO      XX IF  EQUAL
         BEEP
         GOTO      WHAT
. 
XX       ROLLOUT   "CHAIN MSFLBILD/CHN",ROLLNAME
. 
BYPASS   CHAIN     "MSHISTRY"
. 
.......................................................................
. 
LOOP01   READKS    NPARTSFL;MSSTATUS,MSPARTNO,MSSIZE,MSDESC,MSXRFPT1,MSXRFPT2
         MOVE      MSSIZE  TO  WHO
         CALL      HEADER
. 
LP1      READKS    NPARTSFL;MSSTATUS,MSPARTNO,MSSIZE,MSDESC,MSXRFPT1,MSXRFPT2
         GOTO      LOOPEND IF OVER
.
PRTS     PRINT     *1,MSPARTNO,*22,MSSIZE,*55,MSDESC,*88,MSXRFPT1:
                   *110,MSXRFPT2
         ADD       "1"  TO  WHOCNT
         ADD       "1"  TO  LINES
         COMPARE   "53",LINES
         CALL      HEADER  IF  EQUAL
         GOTO      LP1
         RETURN
. 
LOOPEND  PRINT     *C,*L,*L,"** END OF INVENTORY MASTER LIST ":
                   " ** TOTAL RECORDS ENTERED = ":
                   WHOCNT," ** "
         RETURN
. 
HEADER   ADD       "1"  TO  PAGES
         PRINT     *F,*20,"INVENTORY MASTER FILE LISTING":
                   *88," AS OF ",TODAY,"  TIME ",TIME," PAGE ",PAGES
. 
         PRINT     " "
         PRINT     *1,"PART NUMBER",*22,"SIZE",*55,"DESCRIPTION":
                   *88,"CROSS-REFERENCE 1",*110,"CROSS-REFERENCE 2"
         PRINT     " "
         MOVE      "4"  TO  LINES
         RETURN
. 
. 
