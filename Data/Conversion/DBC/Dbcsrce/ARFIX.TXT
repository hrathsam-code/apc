.......................................................................
. AGED RECEIVABLES REPORT PROGRAM:   MODULE NAME   "AGELIST/TXT"
. 
. ACCESSES "ARFILE/ISI:ACCOUNTS"  KEY SEQUENTIALLY 
. PRODUCING REPORT FOR ALL ACCOUNTS HAVING OPEN INVOICES/CREDITS
. 
. NOTE THAT ARFILE/ISI CONSISTS OF THE INVOICE HEADER RECORDS
. MODIFIED FOR RECEIVABLES PURPOSES AND INDEXED IN THE FOLLOWING MANNER
. 
. KEY COMPONENTS:
.                 MAJOR  IS  ACCOUNT  (INVCUST)  POSITIONS 9 - 12
.                 MINOR  IS  INVC NO. (INVKEY)   POSITIONS 1 - 5
. 
.......................................................................
. 
. WRITTEN  9/13/81 BY  R. BACHARACH
.                     MICRO SYSTEMS CONSULTANTS
.                     41 TEELE DRIVE
.                     CORAM, NEW YORK 11727
.                     (516) 698-5577
.......................................................................
. 
         INCLUDE   COMMON
. 
         INCLUDE   RCVBLES
. 
ARFILES  IFILE     KEYL=5,UNCOMPRESSED
ARFILEXX INIT      "ARFILEXX/ISAM"
. 
         INCLUDE   DATETBLZ
. 
CUSTFILE IFILE     KEYL=6,UNCOMPRESSED
CSTNAME  INIT      "ACCOUNTS/ISAM"
. 
. 
         INCLUDE   CUSTMAST
. 
CURRACCT DIM       6
FIRSTSW  INIT      "1"
PAGEZ    FORM      3
LINEZ    FORM      2
RECCNT   FORM      "00"
RLIMIT   FORM      "71"
SHORTRUN DIM       1
REPLY    DIM       1
COMPANY  DIM       1
INRECS   FORM      5
OUTRECS  FORM      5
IOSW     DIM       1
STARTKEY DIM       8
NUMCUST  FORM      4
PDTOTL   FORM      6.2
CREDTOTL FORM      6.2
. 
ASTER45  INIT      "*********************************************"
. 
PTYPE    DIM       7
WKSUB    FORM      6.2
WKPRINT  FORM      6.2
NETBAL   FORM      6.2
UPFLAG   INIT      "N"
ZEROS    INIT      "000000.00"
TOTCURNT FORM      "0000000.00"
TOT130   FORM      "0000000.00"
TOT3160  FORM      "0000000.00"
TOT6190  FORM      "0000000.00"
TOTOV90  FORM      "0000000.00"
TOTNET   FORM      "00000000.00"
. 
COMPDISP INIT      "SUPERIOR WASHER & GASKET"
. 
INITIATE MOVE      "0",PAGEZ
         MOVE      "0",LINEZ
         MOVE      " ",REPLY
         MOVE      "?"  TO  COLUMNSW  (SET THE DATE CORRECTOR)
. 
         CALL      OPENCUST
. 
         TRAPCLR   IO
         TRAP      NOARFL IF IO
         OPEN      RCVBFILE,"ARFILE/ISAM:W"
         GOTO      CLCRNT
. 
NOARFL   BEEP
         DISPLAY   *P1:24,*HON,"A/R FILE NOT AVAILABLE - RE-INDEX"
         GOTO      NOARFL
. 
. 
. FIRST CALCULATE THE AGEING DATE RANGES!
. 
CLCRNT   READKS    RCVBFILE;INVKEY,INVTYPE,INVCOMP,INVSTAT,INVCUST:
                          INVDATE,INVTOTL
         ADD       "1" TO INRECS
. 
         GOTO      NEWEOJ IF OVER
         MOVE      "0.00" TO IAMTPD1
         MOVE      "0.00" TO IAMTPD2
         MOVE      "0.00" TO IAMTPD3
         MOVE      "0.00" TO IAMTPD4
         MOVE      "0.00" TO IAMTPD5
         MOVE      INVTOTL TO IDOLSLFT
         MOVE      "0"    TO IPMNTS
.
         CALL      PUTRCVB
         ADD       "1" TO OUTRECS
         GOTO      CLCRNT
.
NEWEOJ   DISPLAY   *P1:23,*EL,*HON,"RECS IN=",INRECS,"  RECS OUT=":
                   OUTRECS
         KEYIN     *P60:23,REPLY
         CHAIN     "ARMENU1"
.
.
RDLOOP   TRAPCLR   IO
         TRAP      RCVIOERR IF IO
         READKS    ARFILES;INVKEY,INVTYPE,INVCOMP,INVSTAT,INVCUST:
                           INVDATE,INVTOTL,IPMNTS:
                           IAMTPD1,IAMTPD2,IAMTPD3,IAMTPD4,IAMTPD5:
                           ICHK1,ICHK2,ICHK3,ICHK4,ICHK5:
                           IDTPD1,IDTPD2,IDTPD3,IDTPD4,IDTPD5:
                           IMATLS,IDOLSLFT,IPRGDTE
.
         GOTO      EOJ IF OVER
.
. 
. 
. BYPASS THE ZERO RECORD
. 
         MATCH     "00000" TO INVKEY
         GOTO      RDLOOP  IF EQUAL
.....................................
. 
. 
. BYPASS ITEM WHICH ARE:  PAID        (INVSTAT = "X")
.                         VOID        (INVSTAT = "V")
.                         WRITEOFF    (INVSTAT = "W")
. 
         CMATCH    "X",INVSTAT
         GOTO      RDLOOP IF EQUAL
         CMATCH    "W",INVSTAT
         GOTO      RDLOOP  IF EQUAL
         CMATCH    "V",INVSTAT
         GOTO      RDLOOP  IF EQUAL
. 
......................................................
RDLP1    CMATCH    "1",FIRSTSW
         CALL      FIRSTONE IF EQUAL
. 
C1       MATCH     INVCUST,CURRACCT
         CALL      ACCTBRK  IF  NOT  EQUAL    (PRINT TRAILER/THEN NEW HEADER)
. 
         MOVE      INVDATE  TO  WKDATE6       (SET UP TO AGE THE ITEM)
         MOVE      INVTOTL  TO  IDOLSLFT
         CALL      CALCTOTL    (DETERMINE AMOUNT OUTSTANDING AND ITEM TYPE)
. 
C2       CALL      AGEIT      (AGEING BUCKETS ARE INCREMENTED HR)
. 
C3       GOTO      RDLOOP
. 
. 
CALCTOTL MOVE      IDOLSLFT  TO  WKPRINT
         MOVE      "INVOICE"  TO  PTYPE
.
         CMATCH    "^",INVTYPE
         GOTO      PUTRCVBS IF NOT EQUAL
         MOVE      "I" TO INVTYPE
PUTRCVBS CALL      PUTRCVB
. 
MOVE0    MOVE      "0"      TO  WKSUB
. 
         CMATCH    "C",INVTYPE             (IS IT A CREDIT ?)
         GOTO      CALCB1   IF  NOT  EQUAL
         MOVE      "CREDIT "    TO   PTYPE
         MULT      "-1"  BY  WKPRINT         (YES...REVERSE THE AMOUNT)
         ADD       WKPRINT  TO  CREDTOTL
. 
CALCB1   MOVE      WKPRINT  TO  WKAMT
CB1RTRN  RETURN
. 
. 
. 
AGETOTS  ADD       AMTCURNT   TO  TOTCURNT
         ADD       AMT130P    TO  TOT130
         ADD       AMT3160P   TO  TOT3160
         ADD       AMT6190P   TO  TOT6190
         ADD       AMTOV90P   TO  TOTOV90
         RETURN
. 
PRINTDTL PRINT     *6,PTYPE,*18,INVKEY,*28,CCMON,"/",CCDAY,"/",CCYR:
                   *41,WKPRINT,*53,WKSUB,*66,WKAMT
         ADD       "1",LINEZ
         COMPARE   "55",LINEZ
         CALL      HDR  IF  NOT  LESS   
         RETURN
. 
FIRSTONE MOVE      INVCUST  TO  CUSTKEY
         CALL      GETCUST
         MOVE      INVCUST  TO  CURRACCT
         CALL      HDR
         MOVE      " ",FIRSTSW
         RETURN
. 
HDR      ADD       "1"  TO  PAGEZ
         PRINT     *F:
                   *51,"AGED ACCOUNTS RECEIVABLE REPORT":
                   *111,"AS OF ",TODAY
. 
LIBHEAD  PRINT     *52,"SUPERIOR WASHER & GASKET CORP.",*111,"PAGE ",PAGEZ
         GOTO      TITPRNT
. 
REPHEAD  PRINT     *55,"XXXXXXXX XXXXXXXXX INC.",*111,"PAGE ",PAGEZ
. 
TITPRNT  PRINT     " "
         PRINT     *2,"ACCOUNT   NAME",*47,"CURRENT",*60,"PAST DUE":
                   *74,"PAST DUE",*88,"PAST DUE",*102,"PAST DUE":
                   *117,"ACCOUNT"
         PRINT     *2,"NUMBER",*48,"1-30",*62,"31-60",*76,"61-90":
                   *89,"91-120",*102,"OVER 120",*118,"TOTAL"                  
         MOVE      "6"  TO  LINEZ
         RETURN
. 
ACCTBRK  CALL      AGETOTS
         ADD       "1"       TO  RECCNT
         MOVE      AMT130P   TO  PDTOTL
         ADD       AMT3160P  TO  PDTOTL
         ADD       AMT6190P  TO  PDTOTL
         ADD       AMTOV90P  TO  PDTOTL
         MOVE      AMTCURNT  TO  NETBAL
         ADD       PDTOTL    TO  NETBAL
         ADD       NETBAL    TO  TOTNET
. 
. ...................................................................
. NOTE THAT CREDTOTL IS ALREADY NEGATIVE...HENCE ADD VICE SUBTRACT
. ...................................................................
....     ADD       CREDTOTL  TO  NETBAL
. ...................................................................
         PRINT     " "
         PRINT     *3,CUSTKEY,*10,CUSTNAME:
                   *44,AMTCURNT,*58,AMT130P,*72,AMT3160P,*86,AMT6190P:
                   *100,AMTOV90P,*115,NETBAL                                  
         MOVE      "0"  TO  PDTOTL
         MOVE      "0"  TO  NETBAL
         MOVE      "0"  TO  AMTCURNT
         MOVE      "0"  TO  AMT130P
         MOVE      "0"  TO  AMT3160P
         MOVE      "0"  TO  AMT6190P
         MOVE      "0"  TO  AMTOV90P
         MOVE      "0"  TO  CREDTOTL
         MOVE      "0"  TO  WKSUB
         MOVE      "0"  TO  WKAMT
         ADD       "2"  TO  LINEZ
         COMPARE   "58" TO  LINEZ                                             
         CALL      HDR  IF  NOT  LESS
         MOVE      INVCUST  TO  CUSTKEY
         CALL      GETCUST
         MOVE      CUSTKEY  TO  CURRACCT
         RETURN
. 
. NOTE.....MODIFY THIS IF YOU WANT TO PRINT DETAILS AND GET A
.          FINAL ACCOUNT TOTAL LINE !
. 
         INCLUDE   CUSTIO
. 
         INCLUDE   RCVBLSIO
. 
         INCLUDE   DATECALC
. 
EOJ      CALL      ACCTBRK
         CALL      REPTOTS
         BEEP
         DISPLAY   *ES,*P01:04,"END OF AGED RECEIVABLES PRINT",*W5
         RELEASE
         CHAIN     "ARMENU1 "
. 
INVCRTRN RETURN
. 
REPTOTS  COMPARE   "54",LINEZ
         CALL      HDR IF LESS
         PRINT     *L,*L
         PRINT     *1,ASTER45,ASTER45,ASTER45
         PRINT     " "
         PRINT     *10,"COMPANY TOTALS:"
         CMATCH    "R",COMPANY
         GOTO      RPT1 IF NOT EQUAL
         MOVE      "REPUBLIC FASTENERS INC."  TO  COMPDISP
RPT1     PRINT     *10,COMPDISP,*44,TOTCURNT,*58,TOT130,*72,TOT3160:
                   *86,TOT6190,*100,TOTOV90,*113,TOTNET
         PRINT     " "
         PRINT     *1,ASTER45,ASTER45,ASTER45
         RETURN
. 
