. CASH ENTRY PROGRAM
. 
. WRITTEN BY R BACHARACH, MICRO	SYSTEMS	CONSULTANTS
.			   41 TEELE DRIVE
.			   CORAM, NEW YORK 11727
. 
. REWRITTEN  8/30/81 TO	ACCESS NEWLY DESIGNED RECEIVABLES FILE
.		     WHOSE KEY IS:  ACCT 4/INVOICE 5
.			   (516) 698-5577
. 
	 INCLUDE   COMMON.TXT
. 
.  WORK	AREAS FOR CASH CALCULATIONS
. 
IPCHECK	 DIM	   6	    CHECK NUMBER INPUT
IPDTPAY	 DIM	   6	    DATE PAID (ie. CHECK RECEIVED)
IPCHKAMT FORM	   6.2	    CHECK AMOUNT  (MAY BE PAYMENT FOR MULTIPLE ITEMS)
IPACCT	 DIM	   4	    CUSTOMER KEY
. 
DOLSPRGD FORM	   6.2	    DOLLARS KEYED OFF FROM CHECK
IPAID	 FORM	   6.2	    DOLLARS PAID THIS INVOICE
ANSWER	 DIM	   1	    RESPONSE FIELD  (Y OR N)
. 
INVC	 INIT	   "INVC"
CRED	 INIT	   "CRED"
. 
CTLSW	 DIM	   1
. 
	 INCLUDE   CONTFILE.TXT    
. 
ARFILE	 IFILE	   KEYL=5,UNCOMPRESSED
.
. 
. 
. 10/3/81    SUBSTITUTE	THESE WORK FIELDS FOR 
.	     THE PREVIOUS COPY OF "INVOICES/TXT"
. 
INVFOB	 DIM	   1
INVCHECK DIM	   6
INVORDNO DIM	   10
MOREORDR DIM	   30
INVSHIP	 DIM	   10
INVBLNO	 DIM	   10
FRGTRATE FORM	   2.2
INVWGHT	 FORM	   6
INVDTPAY DIM	   6
IAMTRCVD FORM	   6.2
INVMESG	 DIM	   80
. 
	 INCLUDE   RCVBCASH.TXT
. 
WOAMOUNT FORM	   5.2
.
ARFILES	 IFILE	   KEYL=10,UNCOMPRESSED
ARFILEXX INIT	   "ARFILEXX.ISI"
. 
CUSTFILE IFILE	   KEYL=4,UNCOMPRESSED
CSTNAME	 INIT	   "ACCOUNTS.ISI"
. 
	 INCLUDE   CUSTMAST.TXT
. 
DAILYCSH FILE	   FIX=59,UNCOMPRESSED
.     (NOTE THAT DAILYCSH WAS 61 WHEN ACCT NO=L6)
.
CSHFILE	 INIT	   "DAILYCSH.TXT"
. 
SEQ	 FORM	   "-1"
. 
ROW	 FORM	   "06"	   START DETAILS WITH SIXTH ROW	ON THE SCREEN
. 
IOSW	 DIM	   1	   I/O INDICATOR
. 
ZERO	 FORM	   "000000.00"
. 
ITEMCNT	 FORM	   2
TESTCNT	 FORM	   2.2
PT2	 FORM	   .2
. 
WRKFLDS	 DIM	   1
. 
	 INCLUDE   INVCTBL2.TXT
. 
TBLKEY	 DIM	   6
.
.
INVCKEYZ DIM	   10	   KEY TO SPECIAL ISI FILE (ACCT 4/INVOICE 5)
. 
IPCOMP	 DIM	   1
DSPCOMP	 DIM	   8
ITYPE	 DIM	   4
LINENUMB DIM	   2
SELECTED FORM	   2
CASHLEFT FORM	   6.2
ICNTR	 FORM	   2
TOTBAL	 FORM	   7.2
DOLRWORK FORM	   7.2
FORM4	 FORM	   4
STARTKEY DIM	   10
PMTINDX	 FORM	   1
REPLY	 DIM	   1
CUSTN	 FORM	   4
ITEMN	 FORM	   5
RECONSW	 DIM	   1
CREDSW	 DIM	   1
FULLPAGE DIM	   1
D2	 DIM	   2
NUMBR	 FORM	   2
. 
PAYDATE	 DIM	   6
MM	 DIM	   2
DD	 DIM	   2
YY	 DIM	   2
. 
RECZERO	 FORM	   "0000"
RECCNT	 FORM	   "000000"
RECNO	 FORM	   4
BLNK10	 INIT	   "	      "
DATEKEY	 DIM	   11
. 
ARFILEDT IFILE	   KEYL=11,UNCOMPRESSED
. 
CHKFILE	 IFILE	   KEYL=21,UNCOMPRESSED
CHKFLNAM INIT	   "WOCHECKS.ISI"
.
. NOTE CHKFILE ... FOR RECORDING OF WRITEOFFS ONLY!
.
. RECORD LENGTH	= 40 BYTES
.
CHKFLKEY DIM	   21
.
CHKACCT	 DIM	   4
CHKDATE	 DIM	   6
CHKNUMB	 DIM	   6
CHKINVC	 DIM	   5
CHKAMNT	 FORM	   6.2
CHKWOAMT FORM	   5.2
.
NMM	 DIM	   2
NDD	 DIM	   2
NYY	 DIM	   2
.......................
.
.3/1/85	    ADD	LOGIC TO ADD RECS TO WRITEOFF FILE, FOR	RECONCILIATIONS
.	    ADD	LOGIC FOR SYSTEM CREATED CREDITS IN THIS FILE ???
.
+ .....................................................................
. PROCEDURE DIVISION FOR ALL YOU COBOL GUYS!
.
......FRIGGIN DATES
.
	 CLOCK	   DATE	 TO  TODAY   (MMDDYY FORMAT  1/23/99)
.
. .....................................................................
XSTART	 TRAPCLR   IO
	 TRAP	   NOCSHOPN IF IO
	 OPEN	   DAILYCSH,CSHFILE
	 DISPLAY   *P1:1,*ES
	 DISPLAY   *P1:23,*EL,"CASH FILE OPENED",*W2
 
. 
. ESTABLISH THE	ADDRESS	OF THE LAST RECORD WRITTEN !
. 
	 MOVE	   "0" TO RECNO
	 TRAPCLR   IO
	 TRAP	   NODLYCSH IF IO
	 READ	   DAILYCSH,RECNO;RECCNT
	 GOTO	   OPENZ
.
NOCSHOPN BEEP
	 DISPLAY   *P1:23,*EL,*HON,"UNABLE TO OPEN DAILY CASH PROCESSING ":
		   "FILE - JOB ABORTED !",*HOFF,*W4
	 GOTO	   CHAINBK
.
NODLYCSH BEEP
	 DISPLAY   *P1:23,*EL,*HON,"UNABLE TO READ DAILY CASH PROCESSING ":
		   "FILE - JOB ABORTED !",*HOFF,*W4
CHAINBK	 CHAIN	   "ARMENU1"
. 
. NOW POINTING AT EOF IN DAILYCSH FILE (IF OLD)	ELSE NEWLY PREPD FILE
. 
OPENZ	 CALL	   OPENCUST
	 DISPLAY   *P1:23,*EL,"CUSTOMER	FILE OPENED"
	 CALL	   OPENCTLS
	 DISPLAY   *P1:23,*EL,"CORPORATE CONTROLS OPENED"
. 
	 TRAPCLR   IO
	 TRAP	   NOCHKS IF IO
	 OPEN	   CHKFILE,CHKFLNAM
	 DISPLAY   *P1:23,*EL,"WO CHECK	FILE OPENED"
	 TRAPCLR   IO
	 GOTO	   OPENZZ
	 TRAP	   ARFILEOP IF IO
. 
NOCHKS	 BEEP
	 DISPLAY   *P1:23,*EL,*HON,"UNABLE TO OPEN W/O CHECK FILE !":
		   " JOB ABORTED !",*W4
	 CHAIN	   "ARMENU1"
.
OPENZZ	 OPEN	   RCVBFILE,ARFILEXX
	 DISPLAY   *P1:23,*EL,"RCVBFILE	OPENED"
	 OPEN	   ARFILE,"ARFILE.ISI"	 (SAME FILE FOR	ADD PURPOSES)
	 DISPLAY   *P1:23,*EL,"ARFILE.ISI OPENED"
	 OPEN	   ARFILEDT,"ARFILEDT.ISI" (SAME	FILE DATE PURPOSES)
	 DISPLAY   *P1:23,*EL,"ARFILEDT.ISI OPENED"
	 DISPLAY   *P1:23,*EL,*HON,"ALL	A/R FILES OPENED FOR PROCESSING":
		   *HOFF,*W2
	 GOTO	   FIRSTDSP
. 
ARFILEOP BEEP
	 DISPLAY   *P01:23,*HON,"UNABLE	TO OPEN	ARFILE IN BOTH ADD ":
		   "AND	UPDATE MODES!",*W3
	 CHAIN	   "ARMENU1"
. 
. 
FIRSTDSP DISPLAY   *ES,*P19:01,*HON,"ACCOUNTS RECEIVABLE - CASH	":
		   "ENTRY PROCESSING",*P71:01,TODAY,*HOFF
. 
	 BEEP
. 
SECNDDSP KEYIN	   *P01:10,"IF THIS PROGRAM WAS	ACCIDENTALLY CALLED ":
			   "YOU	MAY EXIT NOW......":
		   *P01:12,"DO YOU WISH	TO EXIT	? (Y/N)	",REPLY
	 DISPLAY   *P01:10,*EF
. 
. 
	 CMATCH	   "Y",REPLY
	 GOTO	   GETOUT  IF  EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   IPACCT  IF  EQUAL
	 BEEP
	 GOTO	   SECNDDSP
. 
GETOUT	 CHAIN	   "ARMENU1"
. 
IPACCT	 DISPLAY   *P01:03,*EF		   (ERASE IN CASE OF LOOPBACK)
. 
	 CLEAR	   PAYDATE
	 MOVE	   TODAY  TO  MM
	 RESET	   TODAY  TO  4	   (VICE 4)
	 MOVE	   TODAY  TO  DD
	 RESET	   TODAY  TO  7	   (VICE 7)
	 MOVE	   TODAY  TO  YY
	 RESET	   TODAY  TO  1
	 REP	   " 0"	  IN  MM
	 APPEND	   MM  TO  PAYDATE
	 APPEND	   DD  TO  PAYDATE
	 APPEND	   YY  TO  PAYDATE
	 RESET	   PAYDATE TO 1
.
	 KEYIN	   *P01:02,"ENTER CUSTOMER NO.	____",*P21:02,*DE,*JR,IPACCT
	 DISPLAY   *P21:02,IPACCT
	 CMATCH	   " ",IPACCT
	 GOTO	   IPACCT IF EOS
	 MOVE	   IPACCT TO CUSTKEY
	 CALL	   GETCUST
	 CMATCH	   "H"	  TO IOSW
	 GOTO	   DISPNAME  IF	EQUAL
	 BEEP
	 GOTO	   IPACCT
.
DISPNAME DISPLAY   *P31:02,"NAME  ",CUSTNAME
CUSTOKAY KEYIN	   *P01:03,"CORRECT CUSTOMER ?	(Y or N) ",ANSWER
	 CMATCH	   "Y"	TO ANSWER
	 GOTO	   WHATCOMP  IF	 EQUAL
	 CMATCH	   "N"	TO  ANSWER
	 GOTO	   IPACCT    IF	  EQUAL
	 BEEP
	 GOTO	   CUSTOKAY
. 
WHATCOMP MOVE	   CUSTKEY  TO	FORM4
	 DISPLAY   *P01:03,*EL		   (ERASE THE QUESTION)
LIBIT	 MOVE	  "A"	    TO	IPCOMP
. 
OKAYCOMP MOVE	   "A.P.C." TO DSPCOMP
. 
SHOWCOMP BEEP
	 DISPLAY   *P71:02,DSPCOMP:
		   *P01:14,"N O	T E ---	 YOU ARE PERMITTED TO ENTER A CHECK":
			   " VALUE OF 0.00 ":
		   *P01:15,"		 IN ORDER TO PERMIT YOU	TO DO RECON":
			   "CILIATIONS SUCH AS":
		   *P01:16,"		 PURGING CREDITS AGAINST DEBITS":
		   *P01:18,"W A	R N I N	G - THE	CREDITS	MUST BE	SELECTED ":
					  "B E F O R E	 THE DEBITS":
		   *P01:23,"PRESS   E N	T E R  TO PROCEED"
	 KEYIN	   *P40:23,REPLY
	 DISPLAY   *P01:14,*EF
. 
. 
KEYCHK	 KEYIN	   *P01:03,*EL,*P01:03,"ENTER CHECK NO.	 ______":
		   *P18:03,IPCHECK,*P25:03,"ENTER CHECK	AMOUNT	_________":
		   *P45:03,IPCHKAMT,*P56:03,"ENTER DATE	PAID  "
. 
	 DISPLAY   *P73:03,PAYDATE
	 MOVE	   PAYDATE  TO	IPDTPAY
	 KEYIN	   *P73:03,*RV,IPDTPAY
. 
	 RESET	   IPDTPAY TO 6
	 GOTO	   KEYCHK  IF EOS
	 CMATCH	   "<"	   TO  IPDTPAY
	 GOTO	   KEYCHK  IF  EQUAL
.
	 MOVE	   IPDTPAY  TO	NMM
	 REP	   " 0"	 IN  NMM
	 RESET	   IPDTPAY  TO	3
	 MOVE	   IPDTPAY  TO	NDD
	 REP	   " 0"	 IN  NDD
	 RESET	   IPDTPAY  TO	5
	 MOVE	   IPDTPAY  TO	NYY
	 RESET	   IPDTPAY  TO	1
.
	 CLEAR	   CHKDATE
	 APPEND	   NYY TO CHKDATE
	 APPEND	   NMM TO CHKDATE
	 APPEND	   NDD TO CHKDATE
	 RESET	   CHKDATE TO 1
.
. NOTE THAT CHKDATE IS NOW SET IN CASE WE HAVE TO WRITE	OFF ANY	INVOICE
.
	 CLEAR	   CHKFLKEY
	 APPEND	   CUSTKEY TO CHKFLKEY
	 APPEND	   CHKDATE TO CHKFLKEY
	 APPEND	   IPCHECK TO CHKFLKEY
.
. YOU WILL HAVE	TO APPEND THE INVOICE NUMBER LATER....
.
	 MOVE	   CUSTKEY TO CHKACCT
	 MOVE	   IPCHECK TO CHKNUMB
	 MOVE	   IPCHKAMT TO CHKAMNT
.
.
.NOTE ABOVE LOGIC STORES THE PAYMENT DATE IN SEPARATE MM, DD, YY FORMATS
.
	 RESET	   IPDTPAY TO 1
	 COMPARE   ZERO	   TO	IPCHKAMT
. 
	 GOTO	   NONEGS  IF LESS
	 GOTO	   TITLE
. 
NONEGS	 BEEP
	 DISPLAY   *P01:23,"NO NEGATIVE	DOLLARS	PLEASE!",*W,*W,*W
	 BEEP
	 DISPLAY   *P01:23,*EL
	 GOTO	   KEYCHK
. 
BUCKS	 BEEP
	 DISPLAY   *P01:12,"YOU	MUST ENTER CASH	IN THIS	PROGRAM!":
		   *P01:13,"IF YOU WANT	TO TERMINATE......PLEASE PRESS THE ":
			   "F5 KEY RIGHT NOW":
		   *P01:14,"ELSE PRESS ENTER FOR ANOTHER CHANCE"
	 BEEP
	 KEYIN	   ANSWER
	 CMATCH	   " ",ANSWER
	 GOTO	   TERMNAT IF F5
	 DISPLAY   *P01:12,*EF
	 GOTO	   KEYCHK
. 
TITLE	 MOVE	   " "	TO  REPLY
	 KEYIN	   *P01:23,"DO YOU WISH	TO MAKE	CORRECTIONS ?  (Y/N) ",REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   KEYCHK  IF  EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   SETCASH IF  EQUAL
	 BEEP
	 GOTO	   TITLE
. 
SETCASH	 MOVE	   IPCHKAMT  TO	 CASHLEFT
	 MOVE	   " "	TO  RECONSW
	 COMPARE   "0"	TO  CASHLEFT
	 GOTO	   STCSH1   IF	NOT  EQUAL
	 MOVE	   "R"	TO  RECONSW
. 
. NOW THAT WE HAVE ACKNOWLEDGED	THAT WE	HAVE A PROPER ACCOUNT NUMBER
. LETS TAKE THE	OPPORTUNITY TO UPDATE THE CORPORATE CONTROLS AND
. THE CUSTOMER MASTER RECORD WITH THE TOTAL CHECK AMOUNT !
. BECAUSE THERE	IS NO REASON TO	MAKE THE SYSTEM	WAIT TO	TALLY THE
. PAYMENTS APPLIED....SINCE WE WILL FORCE PAYMENTS TO BALANCE TO
. THE CHECK AMOUNT....EVEN WITH	THE CREATION OF	A COMPUTER-GENERATED
. CREDIT IF NECESSARY!
. 
STCSH1	 MOVE	   "X"	 TO   CTLSW
	 CALL	   UPDTCTLS
	 CMATCH	   "H",IOSW
	 GOTO	   BADCTLS  IF	NOT  EQUAL
	 MOVE	   " ",IOSW
	 CALL	   UPDTCUST
	 CMATCH	   "H",IOSW
	 GOTO	   TITDISP  IF	EQUAL
BADCUST	 BEEP
	 DISPLAY   *P01:23,"BAD	UPDATE ON CUSTOMER FILE"
	 GOTO	   BADCUST
BADCTLS	 BEEP
	 DISPLAY   *P01:23,"BAD	UPDATE ON CONTROLS FILE"
	 GOTO	   BADCTLS
. 
TITDISP	 MOVE	   " "	TO   CTLSW
	 DISPLAY   *P45:03,CASHLEFT
	 DISPLAY   *P30:04,"OPEN ITEMS THIS ACCOUNT":
		   *P01:05,"LN	INV #  INV DATE	 TYPE  AMOUNT OPEN   LN	 ":
			       "INV #  INV DATE	 TYPE  AMOUNT OPEN"
	 MOVE	   "0"	TO  ITEMCNT
	 MOVE	   "5"	TO  ROW
	 CLEAR	   INVCKEYZ
	 APPEND	   IPACCT   TO	 INVCKEYZ
	 APPEND	   "	  " TO	 INVCKEYZ
	 RESET	   INVCKEYZ TO	 10
	 LENSET	   INVCKEYZ
	 RESET	   INVCKEYZ TO	 1     POINT AT	THE BEGINNING
. 
. READ THE RCVBLE FILE KEY SEQUENTIALLY
. 
	 TRAPCLR   IO
	 MOVE	   INVCKEYZ  TO	 STARTKEY
. 
...	 INCLUDE   ARREAD
	 CALL	   GETRCVB
. 
. 
INVCLOOP TRAPCLR   IO
. 
. CONTROL THE NUMBER OF	ITEMS READ TO 30, BUT DO NOT BYPASS INADVERTANTLY
. THAT IS...DO NOT READ	FIRST!
. 
	 ADD	   "1"	TO  ITEMCNT
	 COMPARE   "31"	TO  ITEMCNT
	 GOTO	   NEWPAGE  IF	NOT  LESS
. 
	 CALL	   RCVBSKS
. 
	 CMATCH	   "E",IOSW
	 GOTO	   NOMORE  IF  EQUAL
. 
.  IS IT AN OPEN RECEIVABLE ?
. 
	 CMATCH	   "P"	TO  INVSTAT
	 GOTO	   PRTBYP   IF	EQUAL
	 CMATCH	   "O"	TO  INVSTAT
	 GOTO	   PRTBYP   IF	EQUAL
. 
LOOPBACK SUBTRACT  "1"	FROM  ITEMCNT
	 GOTO	   INVCLOOP
. 
PRTBYP	 TRAPCLR   PARITY  (NOP)
	 MATCH	   IPACCT   TO	 INVCUST
	 GOTO	   NOMORE IF NOT EQUAL
.	 CMATCH	   IPCOMP   TO	 INVCOMP
.	 GOTO	   INVCLOOP IF NOT EQUAL
. 
	 CMATCH	   "C"	    TO	 INVTYPE
	 GOTO	   NOTCRED  IF	NOT  EQUAL
	 CALL	   CREDDOLS
	 GOTO	   SETTYPE
. 
NOTCRED	 CALL	   DEBDOLS
. 
SETTYPE	 MOVE	   "INVC"   TO	ITYPE
	 CMATCH	   "C"	    TO	INVTYPE
	 GOTO	   INCRITEM IF	NOT EQUAL
	 MOVE	   "CRED"   TO	ITYPE
. 
INCRITEM ADD	   "0"	 TO  ITEMCNT
. 
. ADDED	11/30/81 R.BACHARACH
. 
	 COMPARE   "05"	 TO  ROW
	 GOTO	   INCR1 IF NOT	EQUAL
YES6	 DISPLAY   *P01:06,*EF
INCR1	 TRAPCLR   IO	  (NOP)
. 
	 CLEAR	   TBLKEY
	 APPEND	   INVTYPE TO TBLKEY
	 APPEND	   INVKEY  TO TBLKEY
	 RESET	   TBLKEY  TO 1
. 
	 STORE	   TBLKEY INTO ITEMCNT OF INVC1,INVC2,INVC3,INVC4,INVC5:
					  INVC6,INVC7,INVC8,INVC9,INVC10:
					  INVC11,INVC12,INVC13,INVC14,INVC15:
					  INVC16,INVC17,INVC18,INVC19,INVC20:
					  INVC21,INVC22,INVC23,INVC24,INVC25:
					  INVC26,INVC27,INVC28,INVC29,INVC30:
					  INVC31,INVC32,INVC33,INVC34
. 
	 MOVE	   ITEMCNT   TO	 TESTCNT
	 DIVIDE	   "2"	 INTO	 TESTCNT
	 MOVE	   TESTCNT   TO	 PT2
	 CALL	   SETDOLRS
	 MOVE	   ITEMCNT   TO	 D2
	 REP	   " 0"	     IN	 D2
	 COMPARE   ".00"     TO	 PT2
	 GOTO	   LEFTSIDE  IF	 NOT EQUAL
	 GOTO	   RITESIDE
. 
LEFTSIDE ADD	   "1"	TO  ROW	    (ONLY INCREMENT ON THE LEFT	SIDE!!)
	 DISPLAY   *P01:ROW,D2,"  ",INVKEY,"   ",INVDATE,"   ":
		   ITYPE,"   ",DOLRWORK
	 GOTO	   INVCLOOP
RITESIDE DISPLAY   *P42:ROW,D2,"  ",INVKEY,"   ",INVDATE,"   ":
		   ITYPE,"   ",DOLRWORK
	 GOTO	   INVCLOOP
. 
SETDOLRS MOVE	   IDOLSLFT  TO	 DOLRWORK
	 CMATCH	   "C"	     TO	 INVTYPE
	 GOTO	   SDLRTRN   IF	 NOT EQUAL
	 MULTIPLY  "-1"	     BY	 DOLRWORK
SDLRTRN	 RETURN
. 
NEWPAGE	 MOVE	   "X"	TO  FULLPAGE
. 
. FALLTHRU  TO	NOMORE !
. 
. 
NOMORE	 CALL	   DISPDOLS
	 DISPLAY  *P01:22,*EF
	 DISPLAY  *P01:22,"SELECT ITEM FOR PAYMENT BY LN NUMBER	       ":
		  *P01:23,"OR PRESS  F5	IF CASH	COMPLETE / F1 TO CREDIT	":
			  "EXCESS CASH"
. 
	 CMATCH	  "X",FULLPAGE
	 GOTO	  NOMORKY IF NOT  EQUAL
	 DISPLAY  *P50:22,"*MORE ITEMS*	Press F3 to view"
. 
NOMORKY	 KEYIN	  *P60:23,"__",*P60:23,NUMBR
. 
	 GOTO	   EOJ IF F5	   (NO MORE ENTRIES...GET OUT!)
. 
. ALLOW	A WAY OUT IF THE AMOUNT	IS A CREDIT AND	THERE ARE NO MORE DEBITS
. 
	 GOTO	   CREDEOJ   IF	  F1
. 
. BRANCH OUT TO	VIEW MORE ITEMS
. 
	 GOTO	   F3BRNCH   IF	  F3
. 
. ........................................................................
.....	 GOTO	   NOMORE IF EOS
. 
	 MOVE	   NUMBR  TO SELECTED
	 COMPARE   "0"	     TO	SELECTED
	 GOTO	   NOMORE    IF	EQUAL
. 
	 COMPARE   ITEMCNT   TO	SELECTED
	 GOTO	   DECREM    IF	LESS
	 GOTO	   DECREM    IF	EQUAL
	 BEEP
. 
BADSELCT DISPLAY   *P01:22,*EF:
		   *P01:23,"LN NUMBER ",LINENUMB," DOES	NOT EXIST...TRY	AGAIN":
		   *W,*W,*W,*P01:23,*EL
	 BEEP
	 GOTO	   NOMORE
. 
NOCRED	DISPLAY	   *P01:22,*EF:
		   *P01:22,"APPLICATION	OF A CREDIT TOWARDS PAYMENT FEATURE":
		   *P01:23,"WILL BE ALLOWED WHEN CASH PROCEDURES ARE ":
		   "FINALIZED":
		   *W,*W,*W,*W
		   BEEP
		   GOTO	 NOMORE
. 
F3BRNCH	 MOVE	   " "	TO  FULLPAGE
	 MOVE	   "05"	TO  ROW
	 MOVE	   "0"	TO  ITEMCNT
	 GOTO	   INVCLOOP
. 
........................................................................
. WHEN CREDITS ARE SELECTED FOR	PURGING	THE IDOLSLFT AMOUNT WILL BE
. ADDED	TO THE CASHLEFT	FIELD, THEREBY EFFECTIVELY INCREASING THE
. CHECK	AMOUNT KEYED........IN THE EVENT THAT THE CHECK	AMOUNT ENTERED
. WAS 0.00, THIS WILL BE A RECONCILIATION EFFORT  O N L	Y ! WITH THE
. NET EFFECT OF	PURGING	THE CREDIT AND APPLYING	ITS AMOUNT AGAINST A
. DEBIT/INVOICE	AMOUNT,	EITHER AS A COMPLETE, PARTIAL, OR WRITEOFF
. PAYMENT
........................................................................
PURGCRED ADD	   IDOLSLFT  TO	 CASHLEFT
	 ADD	   IDOLSLFT  TO	 CASHLEFT
	 MOVE	   "C" TO  CREDSW
. 
. NOTE THAT THE	AMOUNT IS ADDED	TWICE...........
.      THIS IS TO ALLOW	FOR THE	PURGING	OF THE CREDIT ITSELF
.      WHILE HAVING THE	DOLLARS	AVAILABLE FOR PURGING ADDITIONAL ITEMS
. 
	 GOTO	   KILLIT
. 
. 
DECREM	 LOAD	   TBLKEY FROM SELECTED	OF INVC1,INVC2,INVC3,INVC4,INVC5:
					   INVC6,INVC7,INVC8,INVC9,INVC10:
					   INVC11,INVC12,INVC13,INVC14,INVC15:
					   INVC16,INVC17,INVC18,INVC19,INVC20:
					   INVC21,INVC22,INVC23,INVC24,INVC25:
					   INVC26,INVC27,INVC28,INVC29,INVC30:
					   INVC31,INVC32,INVC33,INVC34
. 
	 CLEAR	   STARTKEY
	 APPEND	   CUSTKEY  TO	STARTKEY
	 APPEND	   TBLKEY   TO	STARTKEY
	 RESET	   STARTKEY TO	1
. 
	 CALL	   GETRCVB
. 
. 
	 CMATCH	   "H"	TO  IOSW
	 GOTO	   BADSELCT IF NOT EQUAL
	 CMATCH	   "X"	TO  INVSTAT
	 GOTO	   DEAD	IF  EQUAL
. 
.  MAKE	SURE ITS THE SAME ACCOUNT IN CASE THE TABLE IS LOADED FROM
.  PREVIOUS ACCT WITH MORE ITEMS  IN ITS LIST
. 
	 MATCH	   IPACCT   TO	  INVCUST
	 GOTO	   NOMORE   IF NOT EQUAL
. 
	 CMATCH	   "C"	    TO	  INVTYPE
	 GOTO	   PURGCRED   IF    EQUAL
. 
	 COMPARE   IDOLSLFT  TO	 CASHLEFT
	 GOTO	   NOTENUFF IF	LESS
	 DISPLAY   *P01:23,*EF,*P01:23,"SUFFICIENT CASH	IS AVAILABLE TO	PAY ":
		   "INVOICE NO.	",INVKEY
ASKPAY	 KEYIN	   *P01:23,"SHALL I PAY	THIS INVOICE (Y	OR N) ",ANSWER
	 RESET	   ANSWER
	 GOTO	   ASKPAY IF EOS
	 CMATCH	   "Y"	  TO ANSWER
	 GOTO	   KILLIT IF EQUAL
	 CMATCH	   "N"	  TO ANSWER
	 GOTO	   NOMORE   IF EQUAL
	 BEEP	   
	 GOTO	   ASKPAY
NOTENUFF DISPLAY   *P01:23,*EF,*P01:23,"INSUFFICIENT CASH TO PAY THIS ITEM - ":
		   " (P)artial or (W)riteoff Difference	? "
NTNFKEY	 KEYIN	   *P78:23,ANSWER
	 RESET	   ANSWER
	 GOTO	   NTNFKEY IF EOS
	 CMATCH	   "P"	TO  ANSWER
	 GOTO	   PARTIAL IF EQUAL
	 CMATCH	   "W"	TO  ANSWER
	 GOTO	   WRITEOFF IF EQUAL
	 BEEP	   
	 GOTO	   NTNFKEY
. 
CREDDOLS MOVE	   IDOLSLFT TO DOLRWORK
	 MULT	   "-1"	    BY DOLRWORK
	 ADD	   DOLRWORK TO TOTBAL
	 RETURN
. 
DEBDOLS	 ADD	   IDOLSLFT TO TOTBAL
	 RETURN
. 
DISPDOLS DISPLAY   *P01:21,*EL,*P01:21,"OPEN BALANCE = $",TOTBAL:
		   *P40:21,"CASH AVAILABLE = $",CASHLEFT
	 RETURN
. 
DSPASTER MOVE	   SELECTED TO TESTCNT
	 DIVIDE	   "2"	  INTO TESTCNT
	 MOVE	   TESTCNT  TO PT2
	 COMPARE   "0.00"   TO PT2
	 GOTO	   DLEFT    IF NOT EQUAL
DRITE	 MOVE	   TESTCNT  TO ROW
	 ADD	   "5"	    TO ROW
	 DISPLAY   *P51:ROW,"*"
	 RETURN
DLEFT	 MOVE	   TESTCNT  TO ROW
	 ADD	   "5"	    TO ROW
	 DISPLAY   *P11:ROW,"*"
	 RETURN
. 
. 
KILLIT	 MOVE	   "X"	    TO	 INVSTAT
. 
. NOTE CASH ALLOCATED TO DAILYCSH
. 
	 MOVE	   IDOLSLFT TO	 IPAID
	 SUBTRACT  IDOLSLFT FROM CASHLEFT
...................................................................
. NOTE	 IF WE ARE DOING RECONCILIATIONS, WE DONT WANT TO 
.	 MODIFY	THE CALCULATED NETBAL DISPLAY FIELD, AS	WE ARE NOT
.	 AFFECTING THE ACTUAL ACCOUNT BALANCE, MERELY PURGING
.	 ONE ITEM AGAINST THE OTHER
...................................................................
	 CMATCH	   "C",CREDSW
	 GOTO	   PURGITEM  IF	 EQUAL
	 CMATCH	   "R",RECONSW
	 GOTO	   PURGITEM  IF	 EQUAL
. 
SUBDOLS	 SUBTRACT  IDOLSLFT FROM TOTBAL
. 
PURGITEM CALL	   UPDTINVC
	 CMATCH	   "H",IOSW
	 GOTO	   ASTERZ  IF  EQUAL
. 
NGINVC	 BEEP
	 DISPLAY   *P01:23,"BAD	UPDATE ON RECEIVABLES FILE"
	 GOTO	   NGINVC
. 
ASTERZ	 CALL	   DSPASTER
	 DISPLAY   *P01:23,*EF
	 CALL	   WRITECSH
.
	 GOTO	   NOMORE
. 
DEAD	 BEEP
	 DISPLAY   *P01:23,*EF
	 DISPLAY   *P01:23,"INVOICE ",INVKEY," IS ALREADY PAID!",*W,*W:
		   "TRY	ANOTHER!",*W,*W
	 DISPLAY   *P01:23,*EF
	 BEEP
	 GOTO	   NOMORE
. 
UPDTCUST ADD	   IPCHKAMT TO MTDCASH
	 MOVE	   IPDTPAY TO LSTPAYDT
	 MOVE	   IPCHKAMT TO LSTPAYPR
	 CALL	   PUTCUST
	 RETURN
. 
UPDTCTLS MOVE	   "APP"    TO	CTLKEY
. 
. 6/29/83 - TO PREVENT ERRONEOUS UPDATE	OF CONTROLS RECORD
.	    EXECUTE  -	PI  - HERE UNTIL DONE WITH CONTROLS UPDATE
. 
	 FILEPI	   18;CONTROLS
. 
GETCTLRC CALL	   GETCTL
	 MOVE	   TODAY    TO	CTLDATE
. 
. ONLY ADD THE CHECK AMOUNT   O	N E   T	I M E
. 
	 CMATCH	   "X",CTLSW
	 GOTO	   PUTZCTL  IF	NOT  EQUAL
	 ADD	   IPCHKAMT TO	CTLCASH
. 
PUTZCTL	 CALL	   PUTCTL
. 
. 6/29/83 - NOW	CLEAR THE PROGRAM INTERRUPT BLOCK
. 
	 FILEPI	   0
	 RETURN
. 
UPDTINVC CALL	  RCVBLOAD	   (PUT	THE PAYMENT IN THE BUCKET)
	 CMATCH	  "X",INVSTAT	   (IS IT FINISHED)
	 GOTO	  PUTAR	IF NOT EQUAL
	 MOVE	  TODAY	TO IPRGDTE (LOAD THE LOGICAL PURGE DATE)
. 
PUTAR	 CALL	  PUTRCVB
	 RETURN
. 
RCVBLOAD TRAPCLR  PARITY    (NOP......SEARCH OUT THE BUCKETS)
.			    (UPDATE BUCKET AND IDOLSLFT!)
	 MOVE	  IPMNTS  TO  PMTINDX
	 ADD	  "1"	  TO  PMTINDX
	 COMPARE  "6"	  TO  PMTINDX
	 GOTO	  NOSLOTS IF  NOT  LESS
. 
. OKAY...WE HAVE SLOTS
. 
	 STORE	  IPCHECK INTO PMTINDX OF ICHK1,ICHK2,ICHK3,ICHK4,ICHK5
	 STORE	  IPDTPAY INTO PMTINDX OF IDTPD1,IDTPD2,IDTPD3,IDTPD4,IDTPD5
	 STORE	  IPAID	  INTO PMTINDX OF IAMTPD1,IAMTPD2,IAMTPD3,IAMTPD4:
					  IAMTPD5
	 MOVE	  PMTINDX TO   IPMNTS
NOSLOTS	 SUBTRACT IPAID	  FROM IDOLSLFT
	 RETURN
. .......................................
. 
EOJ	 COMPARE  ZERO	    TO	CASHLEFT
	 GOTO	  EOJ1	    IF	EQUAL
	 GOTO	  EOJ1	    IF	LESS
	 BEEP
	 DISPLAY  *P01:22,*EF,*P01:22,"YOU MUST	APPLY ALL THE AVAILABLE	CASH":
		  *P01:23,"PLEASE SELECT AND ENTER A PARTIAL PAYMENT",*W,*W,*W
	 BEEP
	 GOTO	  NOMORE
. 
EOJ1	 MOVE	  " "  TO  ANSWER
	 KEYIN	  *P01:23,*EL,"ANY MORE	PAYMENTS TO APPLY ? (Y/N) ",ANSWER
	 CMATCH	  "N",ANSWER
	 GOTO	  ENDIT	 IF  EQUAL
	 CMATCH	  "Y",ANSWER
	 GOTO	  CLEAREM  IF  EQUAL
	 BEEP
	 GOTO	  EOJ1
. 
. 
. HE ANSWERED -	MORE - SO CLEAR	THE ACCUMULATORS
. 
CLEAREM	 MOVE	  "0"  TO  IPCHKAMT
	 MOVE	  "0"  TO  DOLSPRGD
	 MOVE	  "0"  TO  IPAID
	 MOVE	  "0"  TO  IPMNTS
	 MOVE	  "0"  TO  IAMTPD1
	 MOVE	  "0"  TO  IAMTPD2
	 MOVE	  "0"  TO  IAMTPD3
	 MOVE	  "0"  TO  IAMTPD4
	 MOVE	  "0"  TO  IAMTPD5
	 MOVE	  "0"  TO  IDOLSLFT
	 MOVE	  "0"  TO  INVTOTL
	 MOVE	  "0"  TO  ITEMCNT
	 MOVE	  "0"  TO  WOAMOUNT
	 MOVE	  "0"  TO  TESTCNT
	 MOVE	  "0"  TO  PT2
	 MOVE	  "0"  TO  CASHLEFT
	 MOVE	  "0"  TO  TOTBAL
	 MOVE	  "0"  TO  DOLRWORK
	 MOVE	  "0"  TO  ICNTR
	 MOVE	  "0"  TO  SELECTED
	 MOVE	  "05" TO  ROW
	 GOTO	  FIRSTDSP	     (GO DO IT	 A G A I N !)
. 
ENDIT	 CHAIN	   "ARMENU1"
...................................................................
. WE HAVE A CREDIT BALANCE AND NO MORE DETAILS TO PAY,	THEREFORE
. IT IS	NECESSARY TO CREATE A NEW CREDIT MEMO ON THE INVCHDRS/ISI 
. FILE,	IN ORDER TO PRINT IT, FOR THE CUSTOMER`S ADVICE
...................................................................
. 
CREDEOJ	 MOVE	   CASHLEFT    TO  IPAID
. 
. 6/29/83 - PREVENT ERRONEOUS UPDATE OF	CONTROLS BY CONCURRENT USERS
. 
	 FILEPI	   17;CONTROLS
	 MOVE	   "APP" TO CTLKEY
	 CALL	   GETCTL      (TO GET A NEW CREDIT NUMBER)
. 
INCRNUMB MOVE	   CTLINVC     TO  ITEMN       (ITEMN FORM 5)
	 ADD	   "1"	       TO  ITEMN
	 MOVE	   ITEMN       TO  CTLINVC
	 CALL	   PUTCTL
. 
. RELEASE PI HERE
. 
	 FILEPI	   0
. 
	 MOVE	   ITEMN       TO  INVKEY
...	 REPLACE   " 0"	       IN  INVKEY
. 
.  CREATE A CREDIT RECORD AND WRITE IT TO THE ARFILE/TXT FILE
. 
	 MOVE	   IPCOMP    TO	 INVCOMP
. 
	 MOVE	   TODAY     TO	 INVDATE
	 RESET	   TODAY     TO	 4
	 RESET	   INVDATE   TO	 2
	 APPEND	   TODAY     TO	 INVDATE
	 RESET	   TODAY     TO	 7
	 RESET	   INVDATE   TO	 4
	 APPEND	   TODAY     TO	 INVDATE
	 RESET	   INVDATE   TO	 6
	 LENSET	   INVDATE
	 RESET	   INVDATE   TO	 1
	 RESET	   TODAY     TO	 1
	 REPLACE   " 0"	     IN	 INVDATE
. 
	 MOVE	   IPACCT    TO	 INVCUST
	 MOVE	   "C"	     TO	 INVTYPE
	 MOVE	   "O"	     TO	 INVSTAT       (LET IT PRINT)
	 MOVE	   "-CREDIT-"	 TO  INVORDNO
	 MOVE	   "*OVERPAYMENT OF ACCT BAL.*"	TO MOREORDR
	 CLEAR	   INVSHIP
	 CLEAR	   INVBLNO
	 MOVE	   "0.00"    TO	 FRGTRATE
	 MOVE	   "0"	     TO	 INVWGHT
	 MOVE	   IPCHECK   TO	 INVCHECK
	 MOVE	   IPCHKAMT  TO	 IAMTRCVD
	 MOVE	   CASHLEFT  TO	 INVTOTL
	 MOVE	   CASHLEFT  TO	 IDOLSLFT
. 
. 
. WRITE	THE NEW	CREDIT RECORD
. 
	 TRAPCLR   IO
	 TRAP	   DUPINVC  IF	IO
......................................................................
. NOTE	 WE WILL ADD A - CREDIT	- TO THE ARFILE/ISI FILE
. 
. NOTE	 IN THE	NEW ACCOUNTS RECEIVABLE	SYSTEM (AS OF OCT. 1981)
.	 THE DAILYEOJ PROGRAM PRINTS A NEW ACCOUNTS RECEIVABLE
.	 BALANCE FORWARD NUMBER	AND UPDATES THE	BALFWD FILE ON THE
.	 BASIS OF THE FOLLOWING	EQUATION:
. 
.	 NEWBALFWD = OLDBALFWD + AMOUNT	SOLD TODAY
.			       - CASH PROCESSED	TODAY	(TOTAL IPCHKAMT	[S])
.			       - CREDITS ISSUED	TODAY	(EXCLUDING A/R GEN`D)
. 
.	 THEREFORE, IT IS  N O T  NECESSARY TO INCREMENT THE CREDITS FIELD
.	 OF THE	BALFWD FILE, INASMUCH AS THE CORPORATE CONTROLS	FILE WILL
.	 ALREADY HAVE BEEN UPDATED BY THE   O R	G I N A	L  AMOUNT OF THE
.	 CHECK REMITTANCE.     ADDITIONALLY,  IF THE CREDIT GENERATED BY
.	 THE CASHNTRY PROGRAM IS AS A RESULT OF	A CREDIT BALANCE REMAINDER
.	 IN A RECONCILIATION EFFORT, IT	WOULD BE  E R R	O N E O	U S
.	 TO EFFECTIVELY	REDUCE THE CORPORATE A/R BALANCE FORWARD BY
.	 THE CREDIT AMOUNT, SINCE THIS SAME AMOUNT HAD PREVIOUSLY
.	 AFFECTED THE ACCOUNTS RECEIVABLE BALANCES!
......................................................................
.....	 WRITE	   INVCFILE,INVKEY;INVKEY,INVTYPE,INVCOMP,INVSTAT,INVCUST:
.....				   INVDATE,INVTOTL,INVORDNO,MOREORDR,INVSHIP:
.....				   INVBLNO,INVFOB,FRGTRATE,INVWGHT,INVDTPAY:
.....				   INVCHECK,IAMTRCVD,IDOLSLFT,INVMESG
. 
	 CLEAR	   STARTKEY
	 APPEND	   INVCUST TO  STARTKEY
	 APPEND	   INVKEY  TO  STARTKEY
	 RESET	   STARTKEY
. 
	 CALL	   ADDRCVB
. 
. NOW INSERT THE KEY FOR THE NEW CREDIT	IN ARFILEXX/ISI
. 
...............	2/4/84	  INSERT    ARFILE,STARTKEY
. 
	 INSERT	   ARFILE,INVKEY	 (2/4/84 - 5 DIGIT KEY ONLY)
. 
. 
. NOW INSERT THE KEY FOR THE CREDIT IN ARFILEDT/ISI
. 
	 CLEAR	   DATEKEY
	 APPEND	   YY  TO  DATEKEY
	 APPEND	   MM  TO  DATEKEY
	 APPEND	   DD  TO  DATEKEY
	 APPEND	   INVKEY  TO  DATEKEY
	 RESET	   DATEKEY TO  1
. 
	 INSERT	   ARFILEDT,DATEKEY
. 
	 BEEP
. 
	 DISPLAY   *P01:23,*EF,*P01:23,"CREDIT NUMBER ",INVKEY," CREATED":
		   " FOR ACCT NO. ",INVCUST," AMOUNT = ",IDOLSLFT,*W,*W,*W:
		   *W,*W,*W
	 BEEP
	 DISPLAY   *P01:23,*EF
. 
. NOW WRITE THE	PRINT RECORD/WE	ARE SURE OF NO DUPE ON CREDIT NUMBER NOW!
. 
	 CALL	   WRITECSH
	 GOTO	   EOJ1
. 
DUPINVC	 GOTO	   INCRNUMB
. 
.....	 INCLUDE   INVCIO
. 
	 INCLUDE   CUSTIO.TXT
	 INCLUDE   CONTRLIO.TXT
	 INCLUDE   RCVBLSIO.TXT
. 
PREPCSH	 PREPARE   DAILYCSH,CSHFILE
	 GOTO	   OPENZ
. 
READCSH	 READ	   DAILYCSH,SEQ;WRKFLDS
	 GOTO	   READCSH IF NOT OVER
	 RETURN
. 
CANTREAD BEEP
	 DISPLAY   *ES,*P01:12,"CANT READ THE SPECIAL ACCT/INVC/ISAM FILE"
	 BEEP
	 GOTO	   CANTREAD
TERMNAT	 STOP		    (FOR NOW ONLY!)
. 
PARTIAL	 MOVE	   "P"	    TO	  INVSTAT   (INSURE YOU	CAN GET	AT IT AGAIN)
PARTIAL1 MOVE	   CASHLEFT TO	  IPAID
	 CALL	   DSPASTER
	 SUBTRACT  CASHLEFT FROM  TOTBAL
...............	     CALL      UPDTINVC
	 CALL	   WRITECSH
.
.........	SUBTRACT  CASHLEFT FROM	IDOLSLFT
	 SUBTRACT  CASHLEFT FROM  CASHLEFT
.
	 CALL	   UPDTINVC    (DO INVOICE LAST!)
.
	 DISPLAY   *P01:22,*EF,*P01:23,"CHECK NUMBER ",IPCHECK," IS COMPL":
		   "ETE...ACCOUNT BALANCE = ",TOTBAL,*W,*W,*W
	 GOTO	   EOJ	   (NO MORE CASH TO APPLY ON THIS CHECK)
. 
WRITEOFF MOVE	   "W"	    TO	  INVSTAT
	 MOVE	   TODAY    TO	  IPRGDTE
	 MOVE	   IDOLSLFT TO	  WOAMOUNT
	 SUBTRACT  CASHLEFT FROM  WOAMOUNT   (REMAINDER	IS WRITEOFF AMNT)
.
	 APPEND	   INVKEY  TO  CHKFLKEY
	 RESET	   CHKFLKEY TO	1
	 MOVE	   WOAMOUNT TO CHKWOAMT
	 MOVE	   INVKEY  TO CHKINVC
.
	 TRAPCLR   IO
	 TRAP	   BADCKWRT IF IO
	 WRITE	   CHKFILE,CHKFLKEY;CHKACCT,CHKDATE,CHKNUMB,CHKINVC:
				    CHKAMNT,CHKWOAMT
	 DISPLAY   *P64:23,*EL,*HON,"WRITEOFF RECORDED",*W2,*HOFF,*P64:23,*EL
	 GOTO	   PARTIAL1
. 
. 3/3/85  -  WRITE PROCEDURE TO	TAKE TODAYS DAILYCSH/TEXT FILE AND
.	     SAPP IF TO	MONTHLY	PAYMENTS HISTORY FILE FOR REPORTING!
.
BADCKWRT BEEP
	 DISPLAY   *P1:23,*EL,*HON,"UNABLE TO RECORD WRITEOFF! ":
		   " JOB ABORTING!",*HOFF,*W5
	 CHAIN	   "ARMENU1"
.
. 
.  NOTE	CHECK ALL THESE	JUNK STOPS ASAP	FOR REQUIRED LOGIC
. 
....WRITECSH WRITE DAILYCSH,SEQ;IPACCT,INVSTAT,INVKEY,IPAID,IDOLSLFT,IPCHECK:
....			    IPCHKAMT,IPDTPAY,INVCOMP
. 
WRITECSH FILEPI	   17;DAILYCSH
. 
. DETERMINE THE	ADDRESS	OF LAST	DATA RECORD WRITTEN
. 
	 READ	   DAILYCSH,ZERO;RECCNT
	 ADD	   "1"	TO  RECCNT
. 
. WRITE	NEW DATA RECORD	AT LAST	ADDRESS	PLUS 1
. 
.........	 WRITE	   DAILYCSH,RECCNT;IPACCT,INVSTAT,INVTYPE,INVKEY:
	 WRITE	   DAILYCSH,RECCNT;IPACCT,INVKEY,INVSTAT,INVTYPE:
				   IPAID,IDOLSLFT:
				   IPCHECK,IPCHKAMT,IPDTPAY,INVCOMP:
				   WOAMOUNT
. 
. WRITE	AN END-OF-FILE AT CURRENT ADDRESS PLUS 1
. 
	 ADD	   "1"	TO  RECCNT
	 WEOF	   DAILYCSH,RECCNT
. 
. RECORD THE ADDRESS OF	THE NEW	RECORD JUST ADDED IN ZERO RECORD
.     (AFTER DECREMENTING RECCNT TO ALLOW FOR THE EOF MARK)
. 
	 SUBTRACT  "1"	FROM  RECCNT
	 WRITE	   DAILYCSH,ZERO;RECCNT,BLNK10,BLNK10,BLNK10,BLNK10,BLNK10
. 
. NOW RESET THE	PI INSTRUCTION
. 
	 FILEPI	   0
. 
. WRITE	THE INVSTAT TO KNOW IF COMPLETE,PARTIAL	OR WRITEOFF
. 
	 MOVE	   "0.00" TO WOAMOUNT
	 RETURN
. 
	 STOP
