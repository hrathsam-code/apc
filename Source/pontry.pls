.   PURCHASING SYSTEM ...  ENTER AND PRINT P.O.	DOCUMENT
.   POENTRY.DBS	    WRITTEN  AUGUST 1999, R. BACHARACH
.
	 INCLUDE   COMMON.TXT
.
PASSCUST DIM	   30
INVCNUMB DIM	    5
PASSDT6	 DIM	   6
PASSACCT DIM	   4
PASSCOMP DIM	   1
.
NINVCNO	 DIM	   5
NEWFKEY	 DIM	   5
RESULT	 FORM	   2
DASH20	 INIT	   "--------------------"
DDASH20	 INIT	   "===================="
PURCHBY	 DIM	   20
VIA	 DIM	   15
PRTPONO	 FORM	   8
.
REPLY	 DIM	   1
.FIELD	 TYPE	   LENGTH    FROM-TO   DESCRIPTION
.-----	 ----	   ------    -------   -----------
WORK01	 DIM	    1
WORK02	 DIM	    2
WORK03	 DIM	    3
WORK04	 DIM	    4
WORK08	 DIM	    8
WORK10	 DIM	   10
WORK09	 DIM	   9
FIELD09	 DIM	   7
NFIELD09 FORM	   7
.
DATE8	 DIM	   8		8 DIGIT	DATE WORK FIELD
MM	 DIM	   2
DD	 DIM	   2
YY	 DIM	   2
XMM	 DIM	   2
XDD	 DIM	   2
XYY	 DIM	   2
FORM5	 FORM	   5
.
DTYPE	 DIM	   8
DSTAT	 DIM	   10
INDX	 FORM	   2
STARTKEY DIM	   5
PP	 DIM	   1
PC	 DIM	   1
LINES	 FORM	   2
PTERMS	 DIM	   18
HDRSW	 DIM	   1
PAGES	 FORM	   2
.
	 INCLUDE   PART.TXT
.
. CONTROL FILE AREA
.
	 INCLUDE   CONTFILE.TXT
.
NFIELD01 FORM	    4		       (NUMERIC	WORK FIELD)
FIELD01	 DIM	    4
.
	 INCLUDE   SPLOPNFD.TXT
NFLD01	 FORM	   9
FLD01	 DIM	   9
.
MULTIFLG DIM	   1
MULTIROW FORM	   2
MULTICNT FORM	   2
MULTIDSC DIM	   35
MD1	 DIM	   35
MD2	 DIM	   35
MD3	 DIM	   35
MD4	 DIM	   35
MD5	 DIM	   35
MD6	 DIM	   35
MD7	 DIM	   35
MD8	 DIM	   35
MD9	 DIM	   35
MD10	 DIM	   35
EXTRA	 INIT	   "XXXXXXXXXXXXXXXXXXX"
COMPLETE DIM	   1
UOM	 DIM	   2
PRODCODE DIM	   2
SHIPNAME DIM	   30
SHIPADDR DIM	   25
SHIPYADR DIM	   25
SHIPADR2 DIM	   25
SHIPCITY DIM	   18
SHIPSTAT DIM	   2
SHIPZIP	 DIM	   5
COMPLET	 DIM	   1
.
NORMTERM INIT	   "2% 15 NET 30"
NEWTERM	 DIM	   15
.
	 INCLUDE   CUSTMAST.TXT

DIMZIP	 DIM	   5
.
CLOSESW	 INIT	   " "
.
PORTXX	 DIM	   12
.
ADDKEY	 FORM	   8
NEXTPO	 DIM	   8
APP	 INIT	   "APP	    "
DIM8	 DIM	   8

.
.  WORK	 AREAS FOR INVOICE HEADER CREATION/WRITE
.
	 INCLUDE   RCVBLES.TXT
.
SAVE	 DIM	     10
TOTDOLS	 FORM	     6.2
FRGHT	 FORM	     2.2
.
OLDDET	 DIM	   1
IOSW	 DIM	   1
LIBNET	 FORM	   7.2
REPNET	 FORM	   7.2
.
+...........................................................
.
DATE2X	 DIM	   2
DATE2N	 FORM	   2
.
.
INVCKEGS FORM	   7	 (ORDER	QTY)
PCSSOLD	 FORM	   7	 (SHIPPED QTY)
INVPCSKG FORM	   7	 (BACKORDER QTY)
.
BUILDPT	 DIM	   7
.
. RECORD ASSEMBLY AREA
.
INVCNO	 DIM	   5		       INVC NUMBER
INVCLINE DIM	   2		       INVC LINE NUMBER	SUFFIX FOR INDEXING
.
INVLINES FORM	   "00"
IRECCNTR FORM	   6
DTLSTAT	 DIM	   1
ILIVCNTR FORM	   3
.
ROW	 FORM	   2		    USED TO ROLL DOWN THE KEYIN	STMT
.
NFIELD04 FORM	    5.2		       (NUMERIC	WORK FIELD)
FIELD04	 DIM	    8	      11- 18   PRICE LN	1		  5.2
NFIELD03 FORM	    3
FIELD03	 DIM	    3
NFIELD02 FORM	    4
FIELD02	 DIM	    4
.
. EXTENSION CALCULATE WORK/DISPLAY AREA
.
LNDOLS	 FORM	   5.2
KEGQTY	 FORM	   9
LINECOST FORM	   6.2
KGSAVAIL FORM	   6
CRAPMSG	 DIM	   18
CALCFLD	 FORM	   8.3
OLDFRGT	 FORM	   5.2
.
FILLER	 INIT	   "........./........./........./........"
FILLER2	 INIT	   "..............."
.
FORM3	 FORM	   3
IPNSTDSX DIM	   6
IPNSTDSN FORM	   6
NONSTDSW DIM	   1
SCANSW	 DIM	   1
PARTUPSW DIM	   1
.
KREPLY	 DIM	   1
.
CHKPASS	 DIM	   5
.
WEIGHT	 FORM	   6
HDRCHGSW DIM	   1
INVDTKEY DIM	   7
.
OLDXTEND FORM	   6.2
.
.
CALCNET	 FORM	   6.2
FRTALLOW FORM	   5.2
.
. NEW DIRECT-ACCESS FILE ADDED HERE TO ALLOW WRITING THE ENTIRE	INVOICE
. TO ONE FILE FOR BALANCING AND	REVIEW/CORRECTION MODE
.
TIMEDIM	 DIM	   8
DAFILE	 FILE	   FIX=128,UNCOMPRESSED
DANAME	 INIT	   "DAFILEXX.TXT"
.
PONUMBFL IFILE	   KEYL=8,UNCOMPRESSED
PONAME	 INIT	   "NEXTPO.ISI"
BUYER	 DIM	   25
.
PFILE	 PFILE			           // (SPOOL FILE)
.
DARECNO	 FORM	   "  -1"
REC0	 FORM	   "0000"
REC1	 FORM	   "0001"
REC2	 FORM	   "0002"
RECZERO	 FORM 	   "0000"
RECCNTR	 FORM	   4
RECLIMIT FORM	   "0099"
CUSTN	 FORM	   4
SPECMSG	 DIM	   80
MSGFLAG	 DIM	   1
PORTX	 DIM	   2
SPECSW	 DIM	   1
TIME	 INIT	   "HH/MM/SS"
.
SEQ	 FORM	   "-1"
SEQ3	 FORM	   "-3"
.
+........................................................................
.
START	 DISPLAY   *ES,*P01:12,*HON,"PURCHASE ORDER ENTRY PROGRAM":
		   *HOFF:
		       *P01:14,"IF YOU HAVE COME HERE ERRONEOUSLY FROM THE ":
			       "SYSTEM MENU PROGRAMS":
		       *P01:15,"AND DO	N O T  HAVE ANY	 INVOICES TO ":
			       "ENTER........":
		       *P01:17,"YOU MAY	TERMINATE THIS PROGRAM BY PRESSING ":
			       "THE   F5   KEY"
	 KEYIN	   *P01:20,*HON,"KEY	 F5	TO  TERMINATE":
		   *P01:21,"KEY	    ENTER  TO  ENTER P.O. ",REPLY:
		   *HOFF
.
	 GOTO	   MAINLINE  IF	 NOT  F5
.
	 BEEP
INDXIT	 DISPLAY   *HOFF,*ES
	 BEEP
	 CHAIN	   "ANSWER"
.
MAINLINE MOVE	   " " TO REPLY
.
.
	 KEYIN	   *ES,*P1:4,*HON,"PURCHASE ORDER PROCESSING":
		       *P1:6,"PRESS   (F3)   WHEN READY	",REPLY,*HOFF
	 GOTO	   MAINLINE IF NOT F3
.
MAINLIN1 DISPLAY   *HOFF,*ES
.
...	 INCLUDE   SPLOPNPR.TXT

                     SPLOPEN           "LPT1:"

.
	 MOVE	   " ",REPLY
	 MOVE	   " ",MSGFLAG
	 MOVE	   " ",SPECMSG
.
	 MOVE	   TODAY TO MM
	 MOVE	   TODAY TO XMM
	 RESET	   TODAY TO 4
	 MOVE	   TODAY TO DD
	 MOVE	   TODAY TO XDD
	 RESET	   TODAY TO 7
	 MOVE	   TODAY TO YY
	 MOVE	   TODAY TO XYY
	 RESET	   TODAY TO 1
.
. SET UP SIX DIGIT DATE
.
	 MOVE	   TODAY TO INVDATE
	 MOVE	   INVDATE  TO	PASSDT6	     PASS 6 DIGIT DATE TO DTL PGM
	 GOTO	   ACCTOPEN
.
NOFILE	 DISPLAY   *ES,"FILES NOT ON LINE":
		   *N,"JOB IS ABORTING"
	 BEEP
	 CHAIN	   "ANSWER"	     GO	BACK TO	DISPLAY	INVOICE	MENU
*...........................................................
. OPEN ISAM-LOOKUP FILE
.
ACCTOPEN TRAPCLR   IO
.
. ........................................................................
. NOW SET UP TO	EITHER OPEN OR PREPARE A NEW DATA ENTRY	BATCH FILE
. WHOSE	NAME IS	IN THE FORM OF DAINVC##/TXT:NCLACCTS, WHERE ## IS A
. REPLACED BY THE PORT NUMBER (HIGH ORDER ZERO FILLED) IN ORDER	TO PERMIT
. BOTH TERMINALS TO DO DATA ENTRY
.
. ........................................................................
.
.
PREPDA	 CLOCK	   TIME	 TO  TIMEDIM
	 CLOCK	   PORT	 TO  PORTXX
	 TRAPCLR   IO
	 TRAP	   ENTRYFL IF IO
	 PREPARE   DAFILE,DANAME
.
	 OPEN	   PONUMBFL,PONAME

.
ZERONETS TRAPCLR   IO		     (OKAY NOW LETS GO!)
	 MOVE	   "A" TO INVCOMP
	 GOTO	   WHATKIND
.
ENTRYFL	 BEEP
	 DISPLAY   *P1:23,*EL,"UNABLE TO PREPARE PO ENTRY FILE ",*W5
.
MNUCHAIN CHAIN         "SaleMenu"
.
WHATKIND TRAPCLR IO
*...........................................................
. INITIAL SCREEN DISPLAY SEQUENCE
.
DISPLAY	 CLOCK	   TIME	TO TIME
.
	 DISPLAY   *ES,*HON,*P11:1,"PURCHASE ORDER DATA":
		   " ENTRY":
		   *HOFF:
		   *P1:2,"Buyer:":
		   *P1:3,"Vendor:":
		   *P1:4,"Addr1:":
		   *P1:5,"Addr2:":
		   *P1:6,"City":
		   *P43:3,"Ship	To:"
LOOP	 TRAPCLR   PARITY	(NOP)
.
ORDERFOR MOVE	   "A" TO INVCOMP
*...........................................................
.
.  SET NORMAL INVOICE CONSTANTS	PRIOR TO DATA ENTRY
.
............................................................
K01	 MOVE	   "O"	TO  INVSTAT
	 MOVE	   " "	TO  NEWTERM
	 MOVE	   " "	TO  PTERMS
	 MOVE	   INVCOMP  TO	PASSCOMP
	 MOVE	   "I"	TO  INVTYPE
	 MOVE	   "C"	TO  INVFOB
	 MOVE	   " "	TO  INVCHECK
	 MOVE	   " "	TO  INVMESG
	 CLEAR	   INVORDNO
	 MOVE	   " "	TO  MOREORDR
	 MOVE	   "0"	    TO	 INVWGHT
	 MOVE	   "0"	    TO	 CARTONS
	 MOVE	   "000000.00"	 TO   INVTOTL
	 MOVE	   "000000.00"	 TO   IAMTRCVD
	 MOVE	   "0.00" TO LINEPRC
	 MOVE	   "0.00" TO LINECOST
	 MOVE	   "0.00" TO LNDOLS
	 MOVE	   "C"	    TO	 COMPLET
	 MOVE	   " " TO PURCHBY
	 MOVE	   "UPS" TO VIA
. KEYIN	VARIABLES SEQUENCE
.
*...........................................................
. ACCOUNT ID NUMBER
. THIS FIELD IS	"BOTH" REQUIRED	& FILL-CONTROLLED
. FIELD	TYPE IS	NUMERIC
. FIELD	VALIDATION TYPE	IS ISAM-VERIFICATION
.
.
K01KA	 MOVE	   " " TO CUSTNAME
  	 MOVE	   " " TO BUYER
	 MOVE	   " " TO CUSTADDR
	 MOVE	   " " TO CUSTCITY
	 MOVE	   " " TO CUSTSTAT
	 MOVE	   "0" TO CUSTZIP

K01K	 KEYIN	   *P11:2,*RV,BUYER
	 DISPLAY   *P11:2,BUYER
	 MOVE	   BUYER TO PURCHBY
	 KEYIN	   *P11:3,CUSTNAME
	 CMATCH	   " " TO CUSTNAME
	 GOTO	   K01K	IF EOS
.
K01A	 KEYIN	   *P11:4,CUSTADDR
	 KEYIN	   *P11:5,CUSTADR2
.
	 DISPLAY   *P11:4,CUSTADDR:
		   *P11:5,CUSTADR2
.
K01B	 KEYIN	   *P11:6,CUSTCITY
	 DISPLAY   *P11:6,CUSTCITY
	 KEYIN	   *P32:6,CUSTSTAT
	 DISPLAY   *P32:6,CUSTSTAT
	 KEYIN	   *P35:6,DIMZIP
	 DISPLAY   *P35:6,DIMZIP
.
OKAYCOMP DISPLAY   *P11:3,CUSTNAME:
		   *P11:4,CUSTADDR:
		   *P11:5,CUSTADR2:
		   *P11:6,CUSTCITY,*P32:6,CUSTSTAT,*P35:6,DIMZIP
GETVIA	 DISPLAY   *P11:7,"SHIP	VIA",*P20:7,VIA
	 KEYIN	   *P20:7,*RV,VIA
	 DISPLAY   *P20:7,VIA
.
. NOW ENTER TERMS
.
GETTERMS MOVE	   "NET/30" TO PTERMS
	 DISPLAY   *P1:22,*EF,"NORMAL TERMS ARE	",*P18:22,PTERMS
	 KEYIN	   *P18:22,*RV,PTERMS
	 DISPLAY   *P18:22,PTERMS
.
. NOW DISPLAY THE SHIP-TO DATA
.
	 DISPLAY   *P51:3," APC	COMPONENTS, INC":
		   *P51:4," 75 TOLEDO STREET":
		   *P51:5," FARMINGDALE, NY 11735"
.
ASKCUST	 KEYIN	   *P01:23,*EL,"ALL DATA CORRECT? (Y/N)	",REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   PASSNAME  IF	 EQUAL
	 CMATCH	   "N",REPLY
	 GOTO	   K01K	IF EQUAL
	 BEEP
	 DISPLAY   *P01:23,*EL
	 GOTO	   ASKCUST
.
PASSNAME MOVE	   CUSTNAME TO PASSCUST
.
K03	 DISPLAY   *P01:23,*EL
*...........................................................
. CUSTOMER ORDER NUMBER
K03K
	 CALL	   NEXTPONO
*...........................................................
.     LOGIC CHANGE SEPTEMBER 20, 1999
.
COK	 GOTO	   RECYCLE
.
.--------------------------------------------------------------------------
.
NEXTPONO TRAPCLR   PARITY	     (WE ALREADY READ THE CONTROL FILE!)
.
	 FILEPI	   8;PONUMBFL
.
	 READ	   PONUMBFL,APP;DIM8,NEXTPO
	 MOVE	   NEXTPO  TO ADDKEY
	 ADD	   "1"	   TO ADDKEY
	 MOVE	   ADDKEY  TO  NEXTPO
	 REP	   " 0"	IN NEXTPO
	 UPDATE	   PONUMBFL;DIM8,NEXTPO
	 MOVE	   NEXTPO TO PRTPONO
	 DISPLAY   *P52:2,*HON,"PO NUMBER:",PRTPONO,*HOFF
.
	 WRITE	   PONUMBFL,NEXTPO;NEXTPO,NEXTPO,CUSTNAME
	 FILEPI	   0
	 RETURN
.
*...........................................................
. DISK-WRITE SEQUENCE
.
W1	 TRAPCLR   IO
	 MOVE	   "0000"  TO RECCNTR
	 TRAP	   BADDSK IF IO
	 MOVE	   INVKEY    TO	  INVCNUMB
	 MOVE	   CUSTNAME  TO	  PASSCUST
	 MOVE	   INVTOTL   TO	  IDOLSLFT
.
. CHECK	FOR OVERRIDE ON	SPECIAL	MESSAGE
.
	 CMATCH	   "X",SPECSW
	 GOTO	   MOVESPEC IF NOT EQUAL
	 MOVE	   " " TO SPECSW
	 GOTO	   W1A
.
MOVESPEC MOVE	   SPECMSG   TO	  INVMESG
.
. NOW WRITE TO THE NEW DA FILE FOR BATCH REVIEW/CORRECTION CAPABILITY
.
W1A	 TRAPCLR   IO
	 TRAPCLR   FORMAT
	 TRAPCLR   RANGE
	 TRAPCLR   PARITY
	 TRAP	   BADDSK IF IO
	 TRAP	   BUMFMT IF FORMAT
.
	 GOTO	   EOJ
.
BUMFMT	 BEEP
	 DISPLAY   *P1:23,"FORMAT ERROR	ON WRITE TO DAFILE"
	 GOTO	   EOJ
.
.
BADDSK	 BEEP
	 DISPLAY   *ES,*P01:01,"BAD WRITE TO DISK, RECORD ZERO ON ":
			       "INVOICE	DA FILE":
		       *P01:03,"UNRECOVERABLE ERROR...CALL MICRO ":
			       "SYSTEMS	CONSULTANTS",*W2
	 GOTO	   BADDSK
.
. ISAM DUPLICATE KEY TRAP
.
DUPKEY	 BEEP
	 DISPLAY   *P01:23,"KEY	",INVKEY," ALREADY EXISTS! ":
			   "ASSIGN A NEW ONE!",*W,*W,*W
	 GOTO	   DUPKEY
*...........................................................
. END OF JOB SEQUENCE
.
EOJ
.
	 CMATCH	   "S"	TO  INVTYPE
	 GOTO	   EOJ2	IF  NOT	EQUAL	(ITS NORMAL MERCHANDISE	INVOICE)
	 GOTO	   UPDTSEQ
.
. EOJ2 IS ONLY FOR NEW REGULAR MERCHANDISE PO'S
.
EOJ2
	 GOTO	   CHAINDET
.
EOJOUT	 RELEASE
                     CHAIN                "SaleMenu"
.
	 STOP
.
. FILE OPENING SEQUENCE
CHAINDET MOVE	   " "	TO  KREPLY
	 MOVE	   "0001" TO RECCNTR
	 MOVE	   INVCNUMB TO CHKPASS
	 RESET	   CHKPASS TO 5
	 CMATCH	   " " TO CHKPASS
	 GOTO	   CONTIN IF NOT EOS
.
BADINVC	 BEEP
	 DISPLAY   *ES,*P01:12,"SORRY..YOU HAVE	NOT GIVEN ME A INVOICE NUMBER":
		       *P01:14,"  I CANNOT ALLOW YOU TO	CONTINUE",*W,*W
    	 BEEP
	 GOTO	   MAINLINE
.
CONTIN	 RESET	   CHKPASS
	 TYPE	   CHKPASS
	 GOTO	   BADINVC IF NOT EQUAL
	 GOTO	   HSKPG1
.
HSKPG1	 TRAPCLR   IO
	 MOVE	   "00"	TO INVLINES
	 MOVE	   INVCNUMB TO INVKEY
	 MOVE	   INVCNUMB TO INVCNO
	 MOVE	   PASSDT6  TO	DATE2X
TYPIT	 TYPE	   DATE2X
	 GOTO	   DATEOK  IF  EQUAL
	 BEEP
KEYIT	 KEYIN	   *ES,*P01:12,"ENTER THE MONTH	AS TWO DIGITS,	SUCH AS	 09 ":
		   "FOR	SEPT. ",*DE,DATE2X
	 GOTO	   TYPIT
.
DATEOK	 MOVE	   DATE2X  TO  DATE2N
	 COMPARE   "0"	   TO  DATE2N
	 GOTO	   KEYIT   IF  EQUAL
	 GOTO	   KEYIT   IF  LESS
	 COMPARE   "13"	   TO  DATE2N
	 GOTO	   KEYIT   IF  NOT  LESS
.
.  GOT A GOOD MONTH THERE
.
	 MOVE	   DATE2N  TO  USAGINDX
.
. USAGE	IS NOW SET UP FOR UPDATE
.
*...........................................................
. INITIAL SCREEN DISPLAY SEQUENCE
.
RECYCLE	 MOVE	   "8"	TO ROW
	 DISPLAY   *P01:08,*EF:
		   *HON:
		   "QTY	ORD		       PART NUMBER		":
		   "	  PRICE	 UOM EXTENSION	",*HOFF
LOOP01	KEYIN	  *P01:23,*EL,*HON,"Press ENTER	to add data, F5	if ":
			  "done	",REPLY,*HOFF
	 GOTO	   BIGEOJ  IF  F5
	 CMATCH	   " " TO REPLY
	 GOTO	   LOOP01 IF NOT EOS
	 DISPLAY   *P01:23,*EL
*...........................................................
. KEYIN	VARIABLES SEQUENCE
	 ADD	   "01"	TO ROW
	 COMPARE   "21"	TO ROW
	 GOTO	   RECYCLE IF NOT LESS
	 DISPLAY   *P01:ROW,"_______":
			    *P15:ROW,"_________________________	  "

*...........................................................
. PART NO. 1
. THIS FIELD IS	"BOTH" REQUIRED	& FILL-CONTROLLED
. FIELD	TYPE IS	NUMERIC
.
	 MOVE	   "0" TO LNDOLS
	 MOVE	   "0" TO KEGQTY
	 MOVE	   "0" TO PCSSOLD
	 MOVE	   "0" TO INVCKEGS
	 MOVE	   "0" TO INVPCSKG
	 MOVE	   "0" TO LINECOST
*...........................................................
. QUANTITY ORDERED	     4/23/83
. FIELD	TYPE IS	NUMERIC
.
B03	 MOVE	   FIELD03 TO SAVE
B03K	 KEYIN	   *P01:ROW,"_______  ":
		   *P01:ROW,WORK09
.
B03A	 CLEAR	   FIELD09
	 MOVE	   WORK09 TO FIELD09
B03B	 MOVE	   "0" TO NFIELD09
	 TYPE	   FIELD09     (NUMERIC	FIELD)
	 GOTO	   B03K	IF NOT EQUAL
.
	 MOVE	   FIELD09 TO NFIELD09
B03C	 MOVE	   NFIELD09 TO FIELD09
	 DISPLAY   *P01:ROW,FIELD09
.
	 MOVE	   NFIELD09 TO INVCKEGS
	 MOVE	   INVCKEGS TO PCSSOLD
.
.
	 GOTO	   GETPARTZ
. ........................................
.
. NOTE USE INVCKEGS FOR	QUANTITY ORDERED
.      USE PCSSOLD  FOR	QUANTITY SHIPPED
.      USE INVPCSKG FOR	QUANTITY BACKORDERED
.
.
PCSKEG	 KEYIN	   *P10:ROW,"_______  ",*P10:ROW,PCSSOLD
.
	 DISPLAY   *P10:ROW,PCSSOLD
.
	 MOVE	   INVCKEGS TO KEGQTY
	 SUBTRACT  PCSSOLD  FROM  KEGQTY
	 MOVE	   KEGQTY   TO	  INVPCSKG
	 DISPLAY   *P19:ROW,INVPCSKG
	 GOTO	   GETPARTZ
.
.
AHA	 BEEP
	 GOTO	   AHA
.
*.........................................................
GETPARTZ MOVE	   "0" TO MULTICNT
	 MOVE	   " " TO MULTIFLG
	 DISPLAY   *P1:23,*HON,"PRESS F1 FOR MULTI-LINE	PART NUMBER":
		   *HOFF
	 KEYIN	   *P15:ROW,"___________________________________":
		   *P15:ROW,MULTIDSC
	 DISPLAY   *P15:ROW,MULTIDSC
	 MOVE	   MULTIDSC TO MD1
	 MOVE	   "1" TO MULTICNT
	 GOTO	   MULTISET IF F1    (IF F1..THERE IS MULTI-LINE DESCRIPTION)
.
	 GOTO	   B04
.
*..........................................................
.
MULTISET MOVE	   "X" TO MULTIFLG
	 BEEP
	 DISPLAY   *P1:23,*EL
	 DISPLAY   *P1:23,*HON,"MULTI-LINE (MAX	10) PART NUMBER",*W2,*HOFF
	 GOTO	   B04
...........................................................
.
MULTIDSC MOVE	   ROW TO MULTIROW
MULTILP	 ADD	   "1" TO MULTIROW
	 COMPARE   "22"	  TO  MULTIROW
	 CALL	   RESETROW   IF  NOT LESS
	 KEYIN	   *P15:MULTIROW,"___________________________________":
		   *P15:MULTIROW,MULTIDSC
	 DISPLAY   *P15:MULTIROW,MULTIDSC
	 RESET	   MULTIDSC
	 GOTO	   B04 IF EOS	    (IF	NOTHING	ENTERED...FORGET IT!)
	 ADD	   "1" TO MULTICNT
	 STORE	   MULTIDSC INTO MULTICNT OF MD1,MD2,MD3,MD4,MD5:
					     MD6,MD7,MD8,MD9,MD10
.
	 COMPARE   "10"	TO MULTICNT
	 GOTO	   MULTIRTN IF EQUAL
.
	 GOTO	   MULTILP IF F1
.
MULTIRTN MOVE	   MULTIROW  TO	 ROW	(LET PROGRAM KNOW!)
	 RETURN			    (NO	MORE!)
.
..............................................................
.
RESETROW MOVE	   "10"	TO MULTIROW
RESETLP	 ADD	   "1"	TO MULTIROW
	 COMPARE   "21"	TO MULTIROW
	 GOTO	   RESETOUT IF EQUAL
	 DISPLAY   *P01:MULTIROW,*EL
	 GOTO	   RESETLP
RESETOUT MOVE	   "12"	TO MULTIROW
	 MOVE	   "12"	TO ROW
	 RETURN
.
.
EXTEND	 RETURN
WRTHDRIS RETURN
.
. FALLTHRU IS NO MORE DESCRIPTIONS, SO GOTO B04
*...........................................................
. PRICE	LN 1
. THIS FIELD IS	REQUIRED
. FIELD	TYPE IS	NUMERIC
.
B04	 MOVE	   FIELD04 TO SAVE
	 GOTO	   B04K
.
.	 KEYIN	   *P54:ROW,"__",*P54:ROW,PRODCODE
.	 DISPLAY   *P54:ROW,PRODCODE
.
B04K	 KEYIN	   *P57:ROW,"________":
		   *P57:ROW,WORK08
	 CMATCH	   "<" TO WORK08
	 GOTO	   B04K	IF EOS
	 CMATCH	   ">" TO WORK08
	 GOTO	   B04A	IF NOT EQUAL
	 MOVE	   SAVE	TO FIELD04
	 DISPLAY   *P57:ROW,FIELD04
	 GOTO	   B04K
B04A	 CLEAR	   FIELD04
	 MOVE	   WORK08 TO FIELD04
B04B	 MOVE	   "0" TO NFIELD04
	 TYPE	   FIELD04     (NUMERIC	FIELD)
	 GOTO	   B04K	IF NOT EQUAL
	 MOVE	   FIELD04 TO NFIELD04
B04C	 MOVE	   NFIELD04 TO FIELD04
	 DISPLAY   *P57:ROW,FIELD04
	 MOVE	   NFIELD04 TO LNDOLS	  (SELLING PRICE/UOM)
.
. GET THE UNIT OF MEASURE
.
UOM	 MOVE	   "  "	TO  UOM
	 KEYIN	   *P66:ROW,UOM
.
	 MATCH	   "GR"	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 MATCH	   "EA"	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 MATCH	   "M "	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 MATCH	   "LT"	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 MATCH	   "LB"	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 MATCH	   "C "	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 MATCH	   "FT"	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 MATCH	   "PK"	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 MATCH	   "BX"	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 MATCH	   "RD"	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 MATCH	   "NC"	TO UOM
	 GOTO	   DUOM	IF EQUAL
	 BEEP
	 DISPLAY   *P66:ROW,"  "
	 GOTO	   UOM
.
DUOM	 DISPLAY   *P66:ROW,UOM
.
.
CHK100X	 MATCH	   "C "	TO UOM
	 GOTO	   CHK1000 IF NOT EQUAL
	 MOVE	   PCSSOLD TO CALCFLD
	 DIVIDE	   "100" INTO CALCFLD
	 MULT	   LNDOLS BY CALCFLD
	 MOVE	   CALCFLD TO LINECOST
	 GOTO	   DSPCOSTX
.
.
CHK1000	 MATCH	   "M "	TO UOM
	 GOTO	   EACHX   IF NOT EQUAL
	 MOVE	   PCSSOLD TO CALCFLD
	 DIVIDE	   "1000"  INTO	CALCFLD
	 MULT	   LNDOLS  BY	CALCFLD
	 MOVE	   CALCFLD TO	LINECOST
	 GOTO	   DSPCOSTX
.
EACHX	 MOVE	   PCSSOLD TO LINECOST
.
EACHXX	 MULT	   LNDOLS BY LINECOST
.
DSPCOSTX DISPLAY   *P70:ROW,LINECOST
.
. FIRST	CHECK IF THE LINE IS OKAY!
.
CHKLINE	 BEEP
	 MOVE	   " " TO REPLY
	 KEYIN	   *P1:22,*EL,*HON,"IS DATA CORRECT ? (Y/N) ",REPLY,*HOFF
	 DISPLAY   *P1:22,*EF
	 CMATCH	   "Y",REPLY
	 GOTO	   CONTXX IF EQUAL
.
	 CMATCH	   "N",REPLY
	 GOTO	   LNRESET IF EQUAL
	 BEEP
	 GOTO	   CHKLINE
.
LNRESET	 DISPLAY   *P1:ROW,*EL
	 SUBTRACT  "1" FROM ROW
	 GOTO	   LOOP01
.
CONTXX	 ADD	   LINECOST TO TOTDOLS
	 ADD	   LINECOST TO INVTOTL
.
CHKMULTI CMATCH	   "X",MULTIFLG
	 CALL	   MULTIDSC IF EQUAL	(GET THE EXTRA LINES)
.
. NOTE IF EXTRA	DESCRIPTIVE LINES EXIST, THEN WRITE CONTINUATION RECORDS
.      FOR DESCRIPTIVE DATA
.
*...........................................................
.
. DISK-WRITE SEQUENCE
.
LINEINCR ADD	   "01"	  TO INVLINES
	 MOVE	   INVLINES  TO	 RECCNTR
	 MOVE	   INVCNO    TO	 INVDTKEY
	 RESET	   INVDTKEY  TO	 5
	 APPEND	   INVLINES  TO	 INVDTKEY
	 RESET	   INVDTKEY
	 REP	   " 0"	IN INVDTKEY
	 MOVE	   "X"	TO  DTLSTAT
	 MOVE	   MD1	TO  MULTIDSC
	 CALL	   PTDTLBK
.
. NOW CHECK FOR	MULTI-DESCRIPTIVE LINES	AND WRITE MULTIPLE RECORDS NOW!
.
	 CMATCH	   "X",MULTIFLG
	 GOTO	   COMP99 IF NOT EQUAL
.
. FALLTHRU MEANS WRITE CONTINUATION RECORDS
.
. FIRST	ZERO OUT THE QTY AND PRICE FIELDS
.
	 MOVE	   "0" TO INVCKEGS
	 MOVE	   "0" TO INVPCSKG
	 MOVE	   "0" TO PCSSOLD
	 MOVE	   ".00"  TO LNDOLS
	 MOVE	   ".00"  TO LINECOST
.
. STORE	LOOP
.
	 MOVE	   "1"	TO  INDX
.
STORLOOP ADD	   "1"	TO  INDX    (STARTS AT MD2, INDX VALUE=2)
	 COMPARE   INDX	TO  MULTICNT
	 GOTO	   COMP99 IF  LESS
.
	 LOAD	   MULTIDSC FROM INDX OF MD1,MD2,MD3,MD4,MD5,MD6:
					 MD7,MD8,MD9,MD10
.
	 ADD	   "01"	TO INVLINES
	 MOVE	   INVLINES TO RECCNTR
	 RESET	   INVDTKEY TO 5
	 APPEND	   INVLINES TO INVDTKEY
	 RESET	   INVDTKEY TO 1
	 REP	   " 0"	IN INVDTKEY
	 MOVE	   "*"	TO DTLSTAT
	 CALL	   PTDTLBK
	 GOTO	   STORLOOP
.
............................................
PTDTLBK	 TRAPCLR   IO
	 TRAPCLR   PARITY
	 TRAPCLR   FORMAT
	 TRAPCLR   RANGE
	 MOVE	   "WRITE" TO IOTYPE
	 MOVE	   DANAME  TO IOERFILE
	 TRAP	   STDIOERR IF IO
	 TRAP	   STDFMTER IF FORMAT
	 TRAP	   STDRANGE IF RANGE
	 TRAP	   STDPARER IF PARITY
	 MOVE	   RECCNTR  TO DARECNO
	 ADD	   "1"	    TO DARECNO
.
	 WRITE	   DAFILE,SEQ3;INVDTKEY:
		   DTLSTAT:	     TEMPORARY STATUS CODE
		   MULTIDSC:	    PART DESCRIPTION
		   INVCKEGS:	    QTY	ORDERED
		   INVPCSKG:	  QTY BACKORDERED
		   PCSSOLD:	  TOTAL	PIECES SHIPPED
		   LNDOLS:	    SELLING PRICE
		   LINECOST:	    EXTENSION PRICE		    5.2
		   UOM:		    UNIT OF MEASURE
		   PRODCODE:	     PRODUCT CODE
		   FILLER:
		   FILLER2
.
. NOW CLEAR ALL	NECESSARY FIELDS
.
	 MOVE	   "0" TO LINECOST
PTDTLRTN RETURN
....................................................................
.
COMP99	 COMPARE   "99"	 TO  INVLINES
	 GOTO	   NINENINE  IF	 EQUAL	     (END THIS INVOICE...NOW!)
	 GOTO	   LOOP01
.
DETIOERR BEEP
	 DISPLAY   *ES,*P01:12,"WRITE ERROR ON APP INVOICE DETAIL FILE":
		       *P01:14,"PROGRAM	ABORTING",*W5
	 BEEP
	 GOTO	   INDXIT
.
DADTLIO	 BEEP
	 DISPLAY   *ES,*P01:12,"WRITE ERROR ON RECORD ",DARECNO:
			       " OF INVOICE DA FILE":
		       *P01:14,"UNRECOVERABLE ERROR - CALL MICRO SYSTEMS":
			       " CONSULTANTS!"
	 GOTO	   DADTLIO
.
PUTSTDS	 GOTO	   COMP99
.
NINENINE BEEP
	 DISPLAY   *P01:23,*EL,"99 ITEMS KEYED FOR PO##	",NEXTPO:
		     " I AM CLOSING THIS PURCHASE",*W,*W,*W
	 BEEP
	 GOTO	   BIGEOJ
.
NOTFND	 BEEP
	 DISPLAY   *P01:ROW,*EL
	 DISPLAY   *P01:ROW,"INVALID PART NUMBER - PLEASE RESUBMIT",*W2
	 SUBTRACT  "1" FROM ROW
	 GOTO	   LOOP01
.
*...........................................................
. END OF JOB SEQUENCE
.
BIGEOJ	 TRAPCLR   IO
	 TRAP	   NOINVOIC  IF	IO
	 TRAPCLR   FORMAT
	 TRAP	   STDFMTER  IF	FORMAT
.
. FIRST	WRITE AN END-OF-FILE MARK ON THE BATCH
.
	 ADD	    "01" TO DARECNO
	 WEOF	   DAFILE,SEQ3
.
.
. NOW SHOW THE SUBTOTAL	AND ENTER THE SHIP/HANDLING AMOUNT
.
DSPTOTS	 DISPLAY   *P1:21,*EL,*P50:21,"SUBTOTAL",*P70:21,INVTOTL
.
	 KEYIN	   *P1:23,*EL,"TAP ENTER TO PROCEED ",REPLY
	 DISPLAY   *P1:23,*EL
	 GOTO	   SHOWTOTS
.
XCRED	 CALL	   GTRCZRO
	 MOVE	   LIBNET  TO  INVTOTL
	 MOVE	   INVTOTL TO  IDOLSLFT
. ....	 CALL	   CALCDATA
	 SUBTRACT  "1"	      FROM  RECCNTR
	 MOVE	   RECCNTR    TO    IRECCNTR
	 MOVE	   RECCNTR    TO    ILIVCNTR
	 CALL	   PTRCZRO
.
	 GOTO	   SHOWTOTS	 (DISPLAY FINAL	INVOICE	TOTALS)
.
. THEN UPDATE RECORD ZERO WITH THE COUNTS PRIOR	TO REVIEW CYCLE
.
GTRCZRO	 MOVE	   "DAREAD"  TO	IOTYPE
	 READ	   DAFILE,SEQ3;INVKEY:
				  INVTYPE:
				  INVCOMP:
				  INVSTAT:
				  INVCUST:
				  INVDATE:
				  INVTOTL:
				  INVORDNO:
				  MOREORDR:
				  INVSHIP:
				  INVBLNO:
				  INVFOB:
				  FRGTRATE:
				  INVWGHT:
				  INVDTPAY:
				  INVCHECK:
				  IAMTRCVD:
				  IDOLSLFT:
				  INVMESG:
				  IRECCNTR:
				  ILIVCNTR
.
	 GOTO	   NOINVOIC  IF	OVER
	 MOVE	   "H"	TO   IOSW
	 RETURN
.
. ............................................
.
PTRCZRO	 WRITE	   DAFILE,SEQ3;INVKEY:
			   INVTYPE:
			   INVCOMP:
			   INVSTAT:
			   INVCUST:
			   INVDATE:
			   INVTOTL:
			   INVORDNO:
			   MOREORDR:
			   INVSHIP:
			   INVBLNO:
			   INVFOB:
			   FRGTRATE:
			   INVWGHT:
			   INVDTPAY:
			   INVCHECK:
			   IAMTRCVD:
			   IDOLSLFT:
			   INVMESG:
			   IRECCNTR:
			   ILIVCNTR
	 RETURN
. .....................................................
.
SHOWTOTS DISPLAY   *ES,*P01:12,"P.O. NUMBER ",INVKEY," TOTAL PURCHASE":
			       ": $",TOTDOLS
CHECKIT	 MOVE	   " ",REPLY
	 BEEP
	 KEYIN	   *P01:16,*EL,"IS THIS	P.O. CORRECT ?	(Y/N) ",REPLY
	 CMATCH	   "Y",REPLY
	 GOTO	   UPDTSEQ IF EQUAL	 (NOW UPDATE ALL FILES!	7/81)
.
	 CMATCH	   "N",REPLY
	 GOTO	   UPDTSEQ IF EQUAL
.
	 BEEP
	 GOTO	   CHECKIT
.
. THE FOLLOWING	ROUTINE	IS USED	TO RETRIEVE ALL	LINE ITEMS
. ON THIS INVOICE AND TO MODIFY	THE STATUS CODE	FROM "X" TO "O"
.
UPDTSEQ	 TRAPCLR   IO		     (NOW ADD THE UPDATE LOGIC)
.
. WHAT MUST NOW	BE DONE	IS THE FOLLOWING:
.
. 1.  WRITE THE	HEADER RECORD TO THE INVOICE MASTER FILE
. 2.  LOOP THROUGH ALL THE DETAIL RECORDS, SELECTING ONLY THOSE
.     WHICH HAVE A STATUS CODE OF "X", AND UPDATE THE INVENTORY	MASTER
.     BY DECREMENTING THE ON-HAND QTY ACCORDING	TO THE PASSCOMP,
.     THEN WRITE A RECORD ISAM TO THE INVOICE DETAIL FILE
.
	 INCLUDE   POUPDSQ.TXT
.
WHERENOW
	 CLOSE	   DAFILE
	 PREPARE   DAFILE,DANAME
	 CLOSE	   DAFILE      (THIS DELETES THE FILE)
.
CLEARMOV
	 MOVE	   " " TO MD1
	 MOVE	   " " TO MD2
	 MOVE	   " " TO MD3
	 MOVE	   " " TO MD4
	 MOVE	   " " TO MD5
	 MOVE	   " " TO MD6
	 MOVE	   " " TO MD7
	 MOVE	   " " TO MD8
	 MOVE	   " " TO MD9
	 MOVE	   " " TO MD10
.......................................................................
.
	 KEYIN	   *P01:16,*EF,"DO YOU HAVE ANY	MORE PURCHASES TO ADD":
			       " AT THIS TIME ?	 (Y)es or (N)o	",REPLY
	 CMATCH	   "Y" TO REPLY
	 GOTO	   CHAINHDR IF EQUAL
	 CMATCH	   "N" TO REPLY
	 GOTO	   TAPTERM IF EQUAL
.
	BEEP
	GOTO	   CLEARMOV
.
....TAPTERM  DISPLAY   *ES,*P01:12,"TOTAL PURCHASE AMOUNT:",REPNET
...	   BEEP
...	   KEYIN     *P01:20,"PRESS ENTER TO TERMINATE THIS PROGRAM",KREPLY
.
TAPTERM
............... RELEASE
	 SPLCLOSE
.
.
. ............................................................
	 CHAIN	   "NEWMASTR"
.
CHAINHDR PREPARE   DAFILE,DANAME (NOW PREPARE THE DATA FILE AGAIN)
	 PRINT	   *F,"	"
	 GOTO	   WHATKIND	 (GO BACK TO THE INVOICE HEADER	ENTRY)
.
.
NOINVOIC BEEP
	 DISPLAY   *ES,*P01:12,"DISK IO	ERROR ON ORDER TOTAL UPDATE":
		       *N,"SERIOUS PROBLEM, CALL BOB BACHARACH,	MSC":
		       *N,"803-366-3250",*W,*W,*W

	 GOTO	   NOINVOIC
.
.	 INCLUDE   STDIOERR.TXT
.
.	 INCLUDE   STDFMTER.TXT
.
.	 INCLUDE   STDRANGE.TXT
.
.	 INCLUDE   STDPARER.TXT
.
	 STOP

STDIOERR STOP
STDFMTER STOP
STDRANGE STOP

STDPARER
	 GOTO  STDIOERR
