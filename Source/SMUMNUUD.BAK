.........................................................
. SMUMNUUD - ADD/MODIFY/DELETE SYSTEM USER MENU RECORDS .
.........................................................
. LAST REVISION - 08/18/98
.
         INCLUDE   NCOMMON.TXT
.
REPLY    DIM       1
IOSW     DIM       1
DIM2     DIM       2
DIM3     DIM       3
DIM9     DIM       9
AWK2     INIT      "  "
NWK2     FORM      2
.
.
USERFIL  IFILE     "KEYL=9,UNCOMPRESSED,FIX=30,NODUP
USERFILN INIT      "SC\SRESUF.ISI"
USERKEY  DIM       9
.
USERID   DIM       9         1 -  9    User ID Number
USER     DIM       20       10 - 29    User Name
USERLOC  DIM       1        30 - 30    User Location
.                                      1=NY, 2=SC 3=NV
.
USERREC  DIM       31
USERNM   DIM       20
USERCOD  DIM       9
USERTABS INIT      "21"
.
USERMNUS IFILE     "KEYL=13,UNCOMPRESSED,FIX=61,NODUP
USERMNUN INIT      "SC\USERMNUS.ISI"
USERMKEY DIM       13
.
UMNUID   DIM       9         1 -  9    User ID Number
UMNUCD   DIM       2        10 - 11    Application System Code
UMNSELNO DIM       2        12 - 13    Menu Selection Number
UMNPRGNM DIM       8        14 - 21    Program Name
UMNPRGDS DIM       40       22 - 61    Program Description         
.
UMNUREC  DIM       67
UMNUDSC  DIM       40
UMNUPGM  DIM       8
UMNUSEL  DIM       2
UMNUCOD  DIM       2
UMTABSET INIT      "6;15;66"
.
.
USERSYS  IFILE     "KEYL=11,UNCOMPRESSED,FIX=13,NODUP
USERSYSN INIT      "SC\USERSYS.ISI"
USERSKEY DIM       11
.
USYSID   DIM       9         1 -  9    User ID Number
USYSCOD  DIM       2        10 - 11    Application System Code
USYSLVL  FORM      2        12 - 13    User Security Level for Application
.
USYSREC  DIM       36
USYSDSC  DIM       28
USYSCD   DIM       2
USYSLV   DIM       2
UTABSETS INIT      "4;33"
.
.
MENUSYS  IFILE     "KEYL=2,UNCOMPRESSED,FIX=30,NODUP
MENUSYSN INIT      "SC\MENUSYS.ISI"
MENUSKEY DIM       2
.
MSYSID   DIM       2         1 -  2    Application System Code
MSYSDSC  DIM       28        3 - 30    Application Description         
.
MENUREC  DIM       32
MNUDESC  DIM       28
MENUCOD  DIM       2
COMPUSER DIM       9
COMPSYS  DIM       2
SPACE    INIT      " "
SP1      INIT      " "
TAB      INIT      011
TABSETS  INIT      "4"
.
.
MENUMST  IFILE     "KEYL=4,UNCOMPRESSED,FIX=52,NODUP
MENUMSTN INIT      "SC\MENUMST.ISI"
MENUMKEY DIM       2
.
MNUSYSID DIM       2         1 -  2    Application System Code
MNUSELNO DIM       2         3 -  4    Menu Selection Number
MNUPRGNM DIM       8         5 - 12    Program Name
MNUPRGDS DIM       40       13 - 52    Program Description         
.
MASTREC  DIM       56
MSTDESC  DIM       40
MASTNAM  DIM       8
MTABSETS INIT      "6;15"
.
NDX      FORM      3
XDX      FORM      3
AFTRITM  FORM      3
.
MENUFLAG INIT      " "
CHGFLAG  INIT      " "
NAMFLAG  INIT      " "
EOJFLAG  INIT      " "
XITFLAG  INIT      " "
OPMODE   INIT      " "
NULLFLD  INIT      "                              ":
                   "                              "
CnclButton BUTTON
QuitButton BUTTON
MenuButton BUTTON
GetButton  BUTTON
AddButton  BUTTON
RemButton  BUTTON
.
.
TITLTXT  INIT      "USER MENU FILE MAINTENANCE"
PROGNTXT INIT      "SMUMNUUD"
FLD01TXT INIT      "User ID"
FLD02TXT INIT      "User Name"
FLD2ATXT INIT      "Location"
.
TITLE    STATTEXT
PROGNAME STATTEXT
CURRDATE STATTEXT
PRMT01   STATTEXT
PRMT02   STATTEXT
PRMT02A  STATTEXT
.
ENTRY01  EDITTEXT
ENTRY02  EDITTEXT
ENTRY02A EDITTEXT
.
MENULST  DATALIST
MASTLST  DATALIST
USERLST  DATALIST
USRAPPS  DATALIST
UMNULST  DATALIST
.
RESULT   FORM      4
RESULTR  FORM      4
RESULTUM FORM      4
DA       DIALOG
DDATA    INIT      "FONTSIZE=8,POS=5:5,SIZE=45:9,":
                   "TYPE=MODELESS,":
                   "TEXT=3:5:5:40:'IF YOU HAVE COME HERE ERRONEOUSLY ":
                   "FROM THE SYSTEM MENUS":
                   " AND DO NOT WANT TO RUN THIS PROGRAM, ":
                   "YOU MAY EXIT BY":
                   " CLICKING THE Quit BUTTON... ":
                   "OTHERWISE, CLICK Continue',":
                   "BUTTON=7:8:14:20:'&Continue',": 
                   "BUTTON=7:8:26:32:'&Quit'" 
.
SAVEBOX  DIALOG
SDATA    INIT      "FONTSIZE=8,POS=5:5,SIZE=45:9,":
                   "TYPE=MODELESS,":
                   "TEXT=3:5:5:40:'THIS RECORD HAS CHANGED!  DO YOU ":
                   "WANT TO SAVE YOUR ":
                   "CHANGES BEFORE CONTINUING?           ":
                   "     CLICK THE Save ":
                   "BUTTON TO SAVE YOUR CHANGES, or ":
                   "CLICK Discard TO CONTINUE WITHOUT SAVING.',":
                   "BUTTON=7:8:14:20:'&Save',": 
                   "BUTTON=7:8:26:32:'&Discard'" 
.
+........................................................................
.
         SETMODE   *LTGRAY=ON,*3D=ON 
         CREATE    DA,DDATA
         DISPLAY   *BGCOLOR=*YELLOW,*ES,*BLUE:
                   *HON,*P05:01,"USER MENU FILE MAINTENANCE":
                   *HOFF,*BLACK
         ACTIVATE  DA,TESTIT,RESULT
         LOOP
         WAITEVENT
         REPEAT
         STOP
.
TESTIT   BRANCH    RESULT OF NULL,MAINLINE,INDXIT
.
NULL     RETURN
.
INDXIT   DISPLAY   *ES,*BLUE
         BEEP
         CHAIN     "SMMENU"
. 
MAINLINE MOVE      " ",REPLY
         DEACTIVATE DA
         DESTROY   DA
         DISPLAY   *BLUE,*ES
   CREATE PROGNAME=1:2:1:11,PROGNTXT,"HELVETICA",BORDER,STYLE=3DOUT         
   CREATE TITLE=1:2:23:58,TITLTXT,"HELVETICA(10,BOLD)",CENTER,BORDER,STYLE=3DOUT
       CREATE  CURRDATE=1:2:72:80,TODAY,"HELVETICA",BORDER,STYLE=3DOUT
       CREATE  PRMT01=4:5:1:9,FLD01TXT,"HELVETICA",BORDER,STYLE=3DOUT
       CREATE  PRMT02=4:5:24:33,FLD02TXT,"HELVETICA",BORDER,STYLE=3DOUT  
       CREATE  PRMT02A=4:5:59:67,FLD2ATXT,"HELVETICA",BORDER,STYLE=3DOUT  
.
         DISPLAY   *BLACK
       CREATE  ENTRY01=4:5:11:21,FONT="HELVETICA",BORDER,PASSWORD,MAXCHARS=9
       CREATE  ENTRY02=4:5:35:56,FONT="HELVETICA",BORDER,MAXCHARS=20
       CREATE  ENTRY02A=4:5:69:71,FONT="HELVETICA",BORDER,MAXCHARS=1
.
       CREATE  AddButton=10:11:65:73,"&Add"
       CREATE  RemButton=18:19:65:73,"&Remove"
       CREATE  GetButton=23:24:25:33,"&GetUser"
       CREATE  MenuButton=23:24:45:53,"Get&Menu"  
       CREATE  QuitButton=23:24:55:63,"&Quit"
       CREATE  CnclButton=23:24:65:73,"Ca&ncel"
       CREATE  SaveBox,SDATA
.
.
.
. Open the Application Descriptions File
.
         CALL      MENUSOPN
.
. Open the Menu Master File
.
         CALL      MENUMOPN
.
. Open the System Users File
.
         CALL      USEROPEN
.
. Open the User/Application File
.
         CALL      USERSOPN
.
. Open the User Menus File File
.
         CALL      USERMOPN
.
.
. Create a DATALIST object which contains the application descriptions
.
         CREATE    MENULST=7:14:1:31,FONT="HELVETICA",TABSTOPS=TABSETS
.
. Insert the application descriptions into the DATALIST
.
         MOVE      "1" TO XDX
         MOVE      "0" TO AFTRITM
         LOOP
           READKS  MENUSYS;MSYSID,MSYSDSC
           GOTO    ENDLOOP IF OVER
           PACKKEY    MENUREC WITH MSYSID,TAB,MSYSDSC
           INSERTITEM MENULST,AFTRITM,MENUREC
BYPINS     MOVE    "999" TO AFTRITM
           ADD     "1",XDX
         REPEAT    
ENDLOOP
.
.
START
*............................................................
. INITIAL SCREEN DISPLAY SEQUENCE
LOOP
.
         ACTIVATE  TITLE
         ACTIVATE  PROGNAME
         ACTIVATE  CURRDATE
         ACTIVATE  PRMT01
         ACTIVATE  PRMT02
         ACTIVATE  PRMT02A
.
         ACTIVATE  ENTRY01
         ACTIVATE  ENTRY02
         ACTIVATE  ENTRY02A
         ACTIVATE  QuitButton,EOJ,RESULT
         ACTIVATE  GetButton,GETKEY,RESULT
         ACTIVATE  MenuButton,DISPVAR,RESULT
         ACTIVATE  CnclButton,CLRADDS,RESULT
         DISABLEITEM  MenuButton
.
         LOOP
           WAITEVENT
         REPEAT
         STOP
.
.
GETKEY   DISABLEITEM  MenuButton
         MATCH     "X" TO MENUFLAG
         CALL      CLRMENU IF EQUAL
.
. Open the System Users File
.
         CALL      USEROPEN
.
. Create a DATALIST object which contains the user records
.
         CREATE    USERLST=15:22:30:51,FONT="HELVETICA",TABSTOPS=USERTABS
.
. Insert the user records into the DATALIST
.
         MOVE      "1" TO XDX
         MOVE      "0" TO AFTRITM
         LOOP
           READKS  USERFIL;USERID,USER,USERLOC
           GOTO    ENDLOOPU IF OVER
           PACKKEY    USERREC WITH USER,TAB,USERID
           INSERTITEM USERLST,AFTRITM,USERREC
           MOVE    "999" TO AFTRITM
           ADD     "1",XDX
         REPEAT    
ENDLOOPU
.
. Show the DATALIST
.
         ACTIVATE  USERLST,GETUSER,RESULT
         LOOP
           WAITEVENT
         REPEAT
         STOP
.
.
. Retrieve an item from the DATALST object 
.
GETUSER 		                    ;
         DEACTIVATE USERLST
	GETITEM   USERLST,RESULT,USERREC
         UNPACK    USERREC INTO USERNM,SP1,USERCOD
.
         MOVE      USERCOD TO USERKEY
.
MODIFYIT          
.
.
         READ      USERFIL,USERKEY;USERID,USER,USERLOC
.
         GOTO      MODIFY IF NOT OVER
         BEEP
         ALERT     CAUTION,"INVALID USER CODE - CANNOT MODIFY!",RESULT
         GOTO      GETKEY
.
.
.
LOADAPPS
         DISABLEITEM  MenuButton
         MATCH     "X" TO MENUFLAG 
         CALL      CLRMENU IF EQUAL
         PACKKEY   USERSKEY WITH USERID
         READ      USERSYS,USERSKEY;DIM3;
         MOVE      "1" TO XDX
         MOVE      "0" TO AFTRITM
         LOOP
           READKS  USERSYS;USYSID,USYSCOD,USYSLVL
           GOTO    ENDLOOPS IF OVER
           MATCH   USYSID TO USERID
           GOTO    ENDLOOPS IF NOT EQUAL
           PACKKEY MENUSKEY WITH USYSCOD
           READ    MENUSYS,MENUSKEY;MSYSID,MSYSDSC
           MOVE    MSYSDSC TO USYSDSC
           PACKKEY USYSREC WITH USYSCOD,TAB,USYSDSC,TAB,USYSLVL
           INSERTITEM  USRAPPS,AFTRITM,USYSREC
           MOVE    "999" TO AFTRITM
           ADD     "1" TO XDX
         REPEAT
ENDLOOPS
         RETURN                    
.
MODIFY   
.
*............................................................
. DISPLAY VARIABLES
DISPVAR  NORETURN
         CREATE    USRAPPS=7:14:22:59,FONT="HELVETICA",SORTED,TABSTOPS=UTABSETS
         CALL      LOADAPPS
         SETITEM   ENTRY01,0,USERID
         SETITEM   ENTRY02,0,USER
         SETITEM   ENTRY02A,0,USERLOC
.
.
         ACTIVATE  ENTRY01
         ACTIVATE  ENTRY02
         ACTIVATE  ENTRY02A
         ACTIVATE  USRAPPS,PROCAPPS,RESULTR
.
         LOOP
           WAITEVENT
         REPEAT
         STOP
.
PROCAPPS
         GETITEM   USRAPPS,RESULTR,USYSREC
         UNPACK    USYSREC INTO USYSCD,REPLY,USYSDSC,REPLY,USYSLV
         DEACTIVATE  USRAPPS
.
. Create a DATALIST object which contains the program descriptions
.                          from the menu master file
.
       CREATE    MASTLST=7:14:01:62,FONT="HELVETICA",TABSTOPS=MTABSETS,SORTED
.
. Insert the program descriptions into the DATALIST
.
         MOVE      USYSCD TO MENUMKEY
         MOVE      USYSCD TO COMPSYS
         READ      MENUMST,MENUMKEY;DIM3
         MOVE      "1" TO XDX
         MOVE      "0" TO AFTRITM
         LOOP
           READKS  MENUMST;MNUSYSID,MNUSELNO,MNUPRGNM,MNUPRGDS
           GOTO    ENDLOOPM IF OVER
           MATCH   MNUSYSID TO COMPSYS
           GOTO    ENDLOOPM IF NOT EQUAL
           PACKKEY    MASTREC WITH MNUSYSID,MNUSELNO,TAB,MNUPRGNM,TAB,MNUPRGDS
           INSERTITEM MASTLST,AFTRITM,MASTREC
BYPINSM    MOVE    "999" TO AFTRITM
           ADD     "1",XDX
         REPEAT    
ENDLOOPM
.
. Create a DATALIST object which contains the program descriptions
.                   from the user menus file
.
     CREATE    UMNULST=15:22:01:62,FONT="HELVETICA",TABSTOPS=UMTABSET,SORTED
.
. Insert the program descriptions into the DATALIST
.
         CLEAR     USERMKEY
         APPEND    USERID TO USERMKEY
         APPEND    USYSCD TO USERMKEY
         RESET     USERMKEY
         MOVE      USYSCD TO COMPSYS
         MOVE      USERCOD TO COMPUSER
         READ      USERMNUS,USERMKEY;DIM3
         MOVE      "1" TO XDX
         MOVE      "0" TO AFTRITM
         LOOP
           READKS  USERMNUS;UMNUID,UMNUCOD,UMNSELNO,UMNPRGNM,UMNPRGDS
           GOTO    ENDLPUM IF OVER
           MATCH   UMNUID TO COMPUSER
           GOTO    ENDLPUM IF NOT EQUAL
           MATCH   UMNUCOD TO COMPSYS
           GOTO    ENDLPUM IF NOT EQUAL
           PACKKEY    UMNUREC WITH UMNUCOD,UMNSELNO,TAB,UMNPRGNM,TAB,UMNPRGDS:
                                   TAB,UMNUID
           INSERTITEM UMNULST,AFTRITM,UMNUREC
           MOVE    "999" TO AFTRITM
           ADD     "1",XDX
         REPEAT    
ENDLPUM
.
. Show the DATALISTS
.
         MOVE      "X" TO MENUFLAG
         ACTIVATE  MASTLST,GETMAST,RESULTR
         ACTIVATE  UMNULST,GETUMNU,RESULTUM
         ACTIVATE  AddButton,ADDPGM,RESULT
         ACTIVATE  RemButton,REMPGM,RESULT
         ENABLEITEM  MenuButton
         DISABLEITEM  AddButton
         DISABLEITEM  RemButton
         LOOP
           WAITEVENT
         REPEAT
         STOP
.
.
. Retrieve an item from the Master Menus DATALST object 
.
GETMAST 		                    ;
	GETITEM   MASTLST,RESULTR,MASTREC
         UNPACK    MASTREC INTO MNUSYSID,MNUSELNO,SP1,MNUPRGNM,SP1,MNUPRGDS
         ENABLEITEM  AddButton
         DISABLEITEM  RemButton
         RETURN
.
.
. Retrieve an item from the User Menus DATALST object 
.
GETUMNU 		                    ;
	GETITEM   UMNULST,RESULTUM,UMNUREC
         UNPACK    UMNUREC INTO UMNUCOD,UMNSELNO,SP1,UMNPRGNM,SP1,UMNPRGDS:
                                SP1,UMNUID
         ENABLEITEM  RemButton
         DISABLEITEM  AddButton                                
         RETURN
.
.                    
*............................................................
. Routine to add a program  to the User Menus List
.
ADDPGM
         PACKKEY   USERMKEY WITH USERID,MNUSYSID,MNUSELNO
         READ      USERMNUS,USERMKEY;DIM9
         GOTO      ENDADD IF NOT OVER
         MOVE      USERID TO UMNUID
         MOVE      USYSCD TO UMNUCOD
         MOVE      MNUSELNO TO UMNSELNO
         MOVE      MNUPRGNM TO UMNPRGNM
         MOVE      MNUPRGDS TO UMNPRGDS
         WRITE     USERMNUS,USERMKEY;UMNUID,UMNUCOD,UMNSELNO,UMNPRGNM,UMNPRGDS
         PACKKEY   UMNUREC WITH UMNUCOD,UMNSELNO,TAB,UMNPRGNM,TAB,UMNPRGDS:
                                   TAB,UMNUID
         MOVE      "999" TO AFTRITM
         INSERTITEM  UMNULST,AFTRITM,UMNUREC
         ADD       "1" TO XDX
ENDADD   DISABLEITEM  AddButton
         DISABLEITEM  RemButton           
         RETURN
.
*............................................................
. Routine to remove a program from the User Menus List
.
REMPGM  
         PACKKEY   USERMKEY WITH UMNUID,UMNUCOD,UMNSELNO
         READ      USERMNUS,USERMKEY;UMNUID,UMNUCOD,UMNSELNO,UMNPRGNM,UMNPRGDS
         GOTO      BADREM IF OVER
         DELETE    USERMNUS,USERMKEY
         DELETEITEM  UMNULST,RESULTUM
         GOTO      ENDREM
BADREM   BEEP
         ALERT     CAUTION,"INVALID PROGRAM CODE - CANNOT REMOVE!",RESULT
ENDREM   DISABLEITEM  AddButton
         DISABLEITEM  RemButton           
         RETURN
.
*............................................................
. Routine to remove program lists from the screen
.
CLRMENU  DEACTIVATE  MASTLST
         DEACTIVATE  UMNULST
         DESTROY   MASTLST
         DESTROY   UMNULST
         MOVE      " " TO MENUFLAG
         RETURN
.
.............................................................
.
SAVE     MATCH     "A" TO OPMODE
         GOTO      WRITE IF EQUAL
UPDAT    MATCH     "X" TO CHGFLAG
         GOTO      NOCHG IF NOT EQUAL
.
         TRAP      BDMNUIO IF IO
         UPDATE    USERFIL;USERID,USER,USERLOC
         TRAPCLR   IO
.
.
ENDUP    MOVE      " " TO CHGFLAG
         MOVE      " " TO OPMODE
         MOVE      " " TO NAMFLAG
         CALL      CLRENTRY
         MATCH     "X" TO EOJFLAG
         RETURN    IF NOT EQUAL
         NORETURN
         GOTO      EOJOK
.
NOCHG    ALERT     NOTE,"You didn't change anything!",RESULT
         GOTO      EXITPNT
.
USEROPEN TRAP      NOUSER IF IO
         OPEN      USERFIL,USERFILN
         TRAPCLR   IO
         GOTO      USEROPND
.         
NOUSER   ALERT     CAUTION,"Authorized User File NOT FOUND!",RESULT
         CHAIN     "SMMENU"
.
USEROPND RETURN         
.
.
USERSOPN TRAP      NOUSERS IF IO
         OPEN      USERSYS,USERSYSN
         TRAPCLR   IO
         GOTO      USRSOPND
.         
NOUSERS  ALERT     CAUTION,"User/Application File NOT FOUND!",RESULT
         CHAIN     "SMMENU"
.
USRSOPND RETURN         
.
.
USERMOPN TRAP      NOUSERM IF IO
         OPEN      USERMNUS,USERMNUN
         TRAPCLR   IO
         GOTO      USRMOPND
.         
NOUSERM  ALERT     CAUTION,"User Menus File NOT FOUND!",RESULT
         CHAIN     "SMMENU"
.
USRMOPND RETURN         
.
.
MENUSOPN TRAP      NOMENUS IF IO
         OPEN      MENUSYS,MENUSYSN
         TRAPCLR   IO
         GOTO      MENUOPND
.         
NOMENUS  ALERT     CAUTION,"Application Code Table NOT FOUND!",RESULT
         CHAIN     "SMMENU"
.
MENUOPND RETURN         
.
.
MENUMOPN TRAP      NOMENUM IF IO
         OPEN      MENUMST,MENUMSTN
         TRAPCLR   IO
         GOTO      MNUMOPND
.         
NOMENUM  ALERT     CAUTION,"Menu Master File NOT FOUND!",RESULT
         CHAIN     "SMMENU"
.
MNUMOPND RETURN         
.
BDMNUIO
         ALERT     CAUTION,"ERROR ON APPLICATION FILE!",RESULT
         CHAIN     "SMMENU"
.....................................................................
.
WRITE    MATCH     "X" TO CHGFLAG
         GOTO      NOCHG IF NOT EQUAL
.
         TRAP      BDMNUIO IF IO
         WRITE     USERFIL,USERKEY;USERID,USER,USERLOC
         TRAPCLR   IO
.
.
         MOVE      " " TO CHGFLAG
         MATCH     "X" TO EOJFLAG
         GOTO      ENDWT IF EQUAL
         MATCH     "X" TO XITFLAG
         GOTO      ENDWT IF EQUAL
ENDWT    MOVE      " " TO CHGFLAG
         MOVE      " " TO NAMFLAG
         MOVE      " " TO XITFLAG
         CALL      CLRADDS
         MATCH     "X" TO EOJFLAG
         RETURN    IF NOT EQUAL
         NORETURN
         GOTO      EOJOK
.
.
CLRADDS  MOVE      NULLFLD TO USERID
         MOVE      NULLFLD TO USER
         MOVE      NULLFLD TO USERLOC
CLRENTRY
         MOVE      " " TO CHGFLAG
         SETITEM   ENTRY01,0,NULLFLD
         SETITEM   ENTRY02,0,NULLFLD
         SETITEM   ENTRY02A,0,NULLFLD
EXITPNT
         DEACTIVATE  MenuButton
         DEACTIVATE  USRAPPS
         DESTROY     USRAPPS
         DEACTIVATE  MENULST
         DEACTIVATE  AddButton
         DEACTIVATE  RemButton
         ACTIVATE  GetButton,GETKEY,RESULT
         RETURN
.
.
CHKSAV   MATCH     "X" TO CHGFLAG
         RETURN    IF NOT EQUAL
         ACTIVATE  SAVEBOX,TESTSV1,RESULT
         LOOP
           WAITEVENT
         REPEAT
         STOP
.
TESTSV1  BRANCH    RESULT OF NULLSV1,SAVEIT,DUMPIT
.
NULLSV1  RETURN
.
SAVEIT   MATCH     "A" TO OPMODE
         GOTO      DOWRIT IF EQUAL
         MATCH     "U" TO OPMODE
         GOTO      DOUPDT IF EQUAL
         NORETURN
         GOTO      EXITSV1
.
DOWRIT   MOVE      "X" TO XITFLAG
         CALL      WRITE
         NORETURN
         GOTO      EXITSV1
.
DOUPDT   CALL      UPDAT
         NORETURN
         GOTO      EXITSV1
.
DUMPIT   NORETURN
.
EXITSV1  DEACTIVATE SAVEBOX
         MOVE      " " TO CHGFLAG
         MOVE      " " TO XITFLAG
         RETURN
.
*............................................................
. END OF JOB
EOJ      MATCH     "X" TO CHGFLAG
         GOTO      EOJOK IF NOT EQUAL
         ACTIVATE  SAVEBOX,TESTSAV,RESULT
         LOOP
           WAITEVENT
         REPEAT
         STOP
TESTSAV  BRANCH    RESULT OF NULLSAV,DOSAVE,EOJOK
.
NULLSAV  RETURN
.
DOSAVE   MATCH     "A" TO OPMODE
         CALL      WRITE IF EQUAL
         MATCH     "U" TO OPMODE
         CALL      UPDAT IF EQUAL  
EOJOK
.
         CHAIN     "SMMENU"
.

