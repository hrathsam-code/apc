;=============================================================================;
;       CREATED BY:  CHIRON SOFTWARE & SERVICES, INC.                         ;
;                    4 NORFOLK LANE                                           ;
;                    BETHPAGE, NY  11714                                      ;
;                    (516) 935-0196                                           ;
;=============================================================================;
;                                                                             ;
;  PROGRAM:    BANK.PLS                                                       ;
;                                                                             ;
;   AUTHOR:    Harry Rathsam                                                  ;
;                                                                             ;
;     DATE:    07/12/2005 AT 9:11AM                                           ;
;                                                                             ;
;  PURPOSE:    Maintenance program for updating Bank records                  ;
;                                                                             ;
; REVISION:    VER     DATE     INIT       DETAILS                            ;
;                                                                             ;
;              1.0   07/12/2005   HOR     INITIAL VERSION                     ;
;                                                                             ;
;=============================================================================;


              INCLUDE           NCOMMON.TXT
              INCLUDE           WORKVAR.INC
#RESULT       FORM              1
#RES1         FORM              1
#DIM1         DIM               1
#DIM2         DIM               2
#RES2         FORM              2
#LEN1         FORM              3
#MenuObj      Automation
#MenuItem     DIM               25

              GOTO              STARTPGM
              INCLUDE           Bank.FD
              INCLUDE           GLMast.FD
              INCLUDE           CNTRY.FD
              INCLUDE           STATE.FD

MAIN          PLFORM            Bank.PLF
DataMenu             plform             DataMenu.PLF

About         PLFORM            About.PLF

.X FileMenu    menu
.X ChangeMenu  menu
.X HelpMenu    menu

FileMenuTxt init      "&File;":
                      "&Print;":
                      "-;":
                      "E&xit"

ChngMenuTxt init      "&Edit;":
                      "&New record;":
                      "&Change Record;":
                      "&Delete Record;":
                      "&Save Record"

HelpMenuTxt init      "&Help;":
                      "F1 - Help with A//P Terms;":
                      "-;":
                      "&About Chiron Software"
.
STARTPGM   ROUTINE
           INCLUDE           SECURITY.INC
           MOVE              "1",ProgLoaded
.
. If we're unable to find the Help file, then we're going to simply just not make
. the F1 Function key available to the Users
.
           MOVE              "AppDir;HELPDIR",EnvData
           CLOCK             INI,EnvData
           IF                NOT OVER
             PACK              EnvData,EnvData,BankHelp
             TRAP              NOHELP IF OBJECT
             SETMODE           *HELPLIB=EnvData
             SETMODE           *F1HELP=ON
             TRAPCLR           OBJECT
             GOTO              OPENFILES
NOHELP
             NORETURN
           ENDIF
.
OPENFILES
           CALL              OPENBank
           TRAPCLR           IO

           CALL              OPENGLMast
           CALL              OPENCNTRY
           CALL              OPENSTATE
.
BankCOLL   COLLECTION
BankEDT    COLLECTION
.
           LISTINS           BankCOLL,EBankCode,EBankName,EBankAddr1,EBankAddr2,EBankAddr3:
                                     EBankCity,EBankSt,EBankZip,EBankCountry,EBankContact:
                                     EBankGLAcct,EBankRoute,EBankAcct,EBankLCheck

           LISTINS           BankEDT,EBankCode,EBankName,EBankAddr1,EBankAddr2,EBankAddr3:
                                    EBankCity,EBankSt,EBankZip,EBankCountry,EBankContact:
                                    EBankGLAcct,EBankRoute,EBankAcct,EBankLCheck

.           winhide
           FORMLOAD          MAIN
           FORMLOAD          DataMenu,WMain
           LOOP
             WAITEVENT
           REPEAT
.
. We never get here!!   Just in case though.... :-)
.
           STOP

BROWSEFILE ROUTINE
SEARCH     PLFORM            SEARCH4.PLF
           CALL                 OpenBank

           FORMLOAD          SEARCH
           RETURN

INITSRCH
           PACK              SearchTitle,BankTitle," Search Window"
           SETPROP           WSearch,Title=SearchTitle

           CTADDCOL          "Bank Code",100,"Bank Name",100

           move              " ",BankKY
           call              RDBank
           loop
             call              KSBank
           until (ReturnFl = 1)
           CTLoadRecord2     Bank,Bank,BankREC.Code,BankREC.Name
             repeat
           RETURN
;==========================================================================================================
OnClickARTRM
                    F2SEARCH          EBankCode
                    if                 (PassedVar="Y")
                      call               MainValid
                    endif
                    return

;==========================================================================================================
.
. Routines that operate the Main program
.
;==========================================================================================================
ItemSelected
                      LVSearchPLB.GetNextItem giving RowSelected using *Flags=02,*Start=FirstRow
                    if                 (RowSelected != -1)
                      LVSearchPLB.GetItemText giving $SearchKey using *Index=RowSelected,*SubItem=0
                      MOVE               "Y",PassedVar
                      DESTROY            WSearch
                    endif

                    RETURN

.X READOPT    DIM               12
.X PAGED      DIM               4
.X NEXTLINE   INIT              127
.X PRTFILE    PFILE
.X PRTCOUNT   FORM              6
.X PRTCOUNTD  FORM              6
.X PRGTITLE   INIT              "A/P Terms MAster Listing"
.X PAGETITLE  DIM               60
.X DIM500     DIM               500
.X PRTWIDTH   FORM              8
.X PRTHEIGHT  FORM              8
.X DIM8       DIM               8
.X COL        FORM              3
.X ROW        FORM              3
.X MAXROWS    FORM              3
.X ACCT       DIM               9
.X TODAY8     DIM               8
.X TIME8      DIM               8
.X NUM1       FORM              3
.X NUM2       FORM              3
.X ToCode     DIM               8
.X .
.X PRTREPORT
.X BankRPT   PLFORM            BankRPT.PLF
.X .           WINHIDE
.X
.X            CLOCK             DATE,TODAY8
.X            CLOCK             TIME,TIME8
.X
.X            MOVE              "60",MAXROWS
.X            MOVE              "0",PAGE
.X            MOVE              "90",LINE
.X
.X            FORMLOAD          BankRPT
.X            LOOP
.X              WAITEVENT
.X            REPEAT
.X
.X            RETURN
.X
.X .           alert             note,"how???",returnfl
.X .           LOOP                                              .Wait...and Wait...
.X .             WAITEVENT                                       .
.X .           REPEAT                                            .and wait
.X
.X START
.X .
.X .
.X . Since we're dealing with a sorted file, we're going to read it in
.X . Sequential order vs. Key Sequential.  Also, we must make sure that
.X . we don't read any records that we're not supposed to based on the
.X . code that was in the other program. (AP42.DBS)
.X .
.X
.X            CLOCK             DATE,TODAY8
.X            CLOCK             TIME,TIME8
.X
.X            MOVE              "60",MAXROWS
.X            MOVE              "0",PAGE
.X            MOVE              "90",LINE
.X            MOVE              "0",PRTCOUNT
.X .
.X . Process all Vendors in Sorted Order
.X .
.X            MOVE              " ",BankKY
.X
.X            GETITEM           RAll,0,RETURNFL
.X            BRANCH            RETURNFL OF ALL
.X
.X            GETITEM           EFromRange,0,BankKY
.X            GETITEM           EToRange,0,ToCode
.X
.X            IF                (ToCode < BankKY)
.X            BEEP
.X            ALERT             STOP,"From Code cannot be less than the To Code",RETURNFL,"Range Error"
.X            SETFOCUS          ETORANGE
.X            RETURN
.X            ENDIF
.X
.X ALL
.X            TRAP              NOPRINT IF SPOOL
.X            PRTOPEN           PRTFILE,"@?","A/P Term Code Listing"
.X            TRAPCLR           IO
.X
.X            CALL              PRTHEADER2
.X
.X
.X            CALL              RDBank
.X            LOOP
.X              CALL              KSBank
.X            UNTIL             (RETURNFL = 1 OR BankREC.Code > ToCode)
.X              CALL              PRTLINE
.X            REPEAT
.X .
.X . Print Totals line
.X .
.X            IF                (LINE + 4 > MAXROWS)
.X              CALL              PRTHEADER
.X            ENDIF
.X            PRTPAGE           PRTFILE;*N=3,PRTCOUNT," A/P Term Records Printed"
.X            PRTCLOSE          PRTFILE
.X            RETURN
.X .
.X . Print Each Detailed line
.X .
.X PRTLINE
.X            ADD               "1",PRTCOUNT
.X            MOVE              PRTCOUNT,PRTCOUNTD
.X
.X            IF                (LINE > MAXROWS)
.X              CALL              PRTHEADER
.X            ENDIF
.X            CHOP              BankREC.NETDAYS,BankREC.NETDAYS
.X            CHOP              BankREC.DISCDAYS,BankREC.DISCDAYS
.X
.X            MOVE              BankREC.NETDAYS,NUM1
.X            MOVE              BankREC.DISCDAYS,NUM2
.X
.X            PRTPAGE           PRTFILE;*1,BankREC.Code:
.X                                      *16,BankREC.Desc:
.X                                      *Alignment=*Right:
.X                                      *38,Num1:
.X                                      *50,Num2:
.X                                      *Alignment=*Decimal:
.X                                      *61,BankREC.DiscPerc:
.X                                      *Alignment=*Left
.X
.X            ADD       "2" TO LINE
.X            RETURN
.X
.X PRTHEADER
.X            PRTPAGE           PRTFILE;*NEWPAGE;
.X PRTHEADER2
.X            CALC              COL=PRTWIDTH-15
.X            ADD               "1",PAGE
.X            MOVE              PAGE,PAGED
.X .           SETITEM           EPage,0,PAGED
.X
.X            PRTPAGE           PRTFILE;*P1:63,*LINE=85:63:
.X                              *N:
.X                              *40,"Page :",*alignment=*left,PAGE:
.X                              *P1:1:
.X                              *P1:1,"Date : ",TODAY8:
.X                              *BOLDON:
.X                              *H=27,"Chiron Software & Services, Inc.",*BOLDOFF:
.X                              *72,"APTERM":
.X                              *N:
.X                              *H=1,"Time : ",TIME8:
.X                              *H=29,"A/P Term Code Master Listing":
.X                              *72,"Accounts Payable":
.X                              *N=2:
.X                              *ULON:
.X                              *H=1,"Term Code",*H=16,"Description":
.X                              *H=33,"Net Days",*H=44,"Disc. Days":
.X                              *H=57,"Disc. %":
.X                              *ULOFF:
.X                              *N
.X
.X            MOVE              "5",LINE
.X            RETURN

EXITASK
           ALERT             PLAIN,"Are you sure you wish to exit this report?",#RESULT
           IF                (#RESULT=1)
             STOP
           ENDIF
           RETURN

NOPRINT
           TRAPCLR           SPOOL
           NORETURN
           RETURN
.=============================================================================
MaintMenuOption
           CALL                 GetMenuName

MainValid
           IF                (Status = 0)
             GETCOUNT          EBankCODE
             IF                (CharCount > 0)
               GETITEM           EBankCODE,0,BankKY
               CALL              RDBank
               IF               (RETURNFL = 1)
                 PARAMTEXT        BankTITLE,BankKY,"",""
                 ALERT            CAUTION,"^0: ^1 Not Found",#RES2,"Record does not exist"
                 CALL             MAINRESET
                 RETURN
               ENDIF
.
. OK, we've been able to read the record and now let's show it on the screen.
.
               CALL              SETMAIN
               MOVE              "3",Status                   .We've found a record
               ENABLEITEM        BMainCHANGE
               ENABLETOOL        ID_Change

.               DISABLEITEM       BMainNEW
.               DISABLETOOL       ID_New

               ENABLEITEM        BMainDELETE
               ENABLETOOL        ID_Delete

               DISABLEITEM       EBankCODE
               DISABLEITEM       Fill1
.1               DISABLEITEM       ChangeMenu,1
.1               ENABLEITEM        ChangeMenu,2
.1               ENABLEITEM        ChangeMenu,3
.1               DISABLEITEM       ChangeMenu,4
               SETFOCUS          BMainChange
             ENDIF
           ENDIF
           RETURN

.=============================================================================
. Initialize MAIN Form and setup the Menu's, Fields, Objects, Buttons, etc
.
MAININIT
.           CREATE            WMAIN;HelpMenu,HelpMenuTxt
.           CREATE            WMAIN;ChangeMenu,ChngMenuTxt
.           CREATE            WMAIN;FileMenu,FileMenuTxt
.
.           ACTIVATE          HelpMenu,onClickMainWinHelpMenu,result
.           ACTIVATE          ChangeMenu,onClickMainWinChangeMenu,result
.           ACTIVATE          FileMenu,onClickMainWinFileMenu,result
.
. Set the SELECTALL property for the COLLECTION and then take care of
. any ActiveX controls.
.
           SETPROP           BankEDT,SELECTALL=$SelectAll
.
           CALL              MAINRESET
           RETURN
.=============================================================================
. New Button is pressed
.
MAINNEW
           IF                (Status = 2)      //Let's 'Save' this NEW Record
             CALL              VALIDATE1     //Validate the data first
.
. Something's not right...Let's just return and wait until all the fields
. have been validated
.
             IF                (ValidFlag = 1)
               RETURN
             ENDIF
.
. Get all of the fields from the Form into the proper RECORD
.
             CALL              GETMAIN
.
. Let's see if SOMEBODY else has entered/used this code before or let's just
. see if this Code already exists in the system
.
             GETITEM           EBankCode,0,BankKY
             CALL              TSTBank
             IF                (RETURNFL = 1)
               CALL              WRTBank
               CALL              MAINRESET
               RETURN
             ELSE
               PARAMTEXT       BankTITLE,BankKY,"",""
               BEEP
               ALERT           Note,"^0 with code ^1 already exists. Please enter another code",result:
                                    "Record already exists"
               SETFOCUS        EBankCode
             ENDIF                                //Valid record exists???
           ELSE
             CALL              MAINRESET

             MOVE              "2",Status
             CALL              DisableRecordButtons
.
. Enable all of the EditText fields and set the EditText fields
. to Non Read-Only
.
             ENABLEITEM        BankEDT
             SETPROP           EBankPhone,Enabled=1
             SETPROP           EBankFax,Enabled=1
             SETPROP           EBankPhone,BGColor=$WINDOW
             SETPROP           EBankFax,BGColor=$WINDOW

             DISABLEITEM       Fill1
             %IFDEF            CBBankActive
             ENABLEITEM        CBBankActive
             %ENDIF

             SETPROP           BankEDT,READONLY=0
             SETPROP           BankEDT,BGCOLOR=$WINDOW
.
. We also need to set any ActiveX controls to the same properties
.
.             SETPROP           EBankDiscPerc,*Enabled=1
.             SETPROP           EBankDiscPerc,*BackColor=$Window
.
. Setup any DEFAULT values
.
.             SETITEM           EBankDISCDAYS,0,"0"
.
. Disable & Enable the proper Buttons along with changing
. the description of the Button's (i.e. Exit --> Change)
.
             DISABLEITEM       BMainCHANGE
             DISABLETOOL       ID_Change

             DISABLEITEM       BMainDELETE
             DISABLETOOL       ID_Delete

             SETITEM           BMainNEW,0,SaveTitle
             ENABLETOOL        ID_Save

             SETITEM           BMainCancel,0,CancelTitle
             ENABLETOOL        ID_Cancel

             DISABLETOOL       ID_New
.
. Disable the Menu Items except for the 'Save' button
.
.1             DISABLEITEM       ChangeMenu,1
.1             DISABLEITEM       ChangeMenu,2
.1             DISABLEITEM       ChangeMenu,3
.1             ENABLEITEM        ChangeMenu,4
.
. Set the Focus to the first field that we're going to be Entering
.
             SETFOCUS          EBankCode
           ENDIF
           RETURN
.=============================================================================
. Change/Save Button has been pressed
.
MAINCHANGE
.
. I'm only getting here if the Change Button has been selected.  Soooooo....Either the
. Change Button has been pressed, or this button now reads 'Save'.  If it reads Save,
. the Status flag will have been set to 1 the first time that this routine has been
. reached.
           IF                (Status = 1)                //'Save' button has been pressed
             CALL              VALIDATE1
             IF                (ValidFlag = 0)           //Great..All fields ARE valid!!!
               CALL              GETMAIN                 //Get all of the fields
               CALL              UPDBank                //Update the record
               CALL              MAINRESET               //Reset the objects & fields
             ENDIF
             RETURN                                      //Voila...Either way, we're RETURNING
           ENDIF
           GETCOUNT          EBankCODE
           IF                (Charcount > 0)
             GETITEM           EBankCODE,0,BankKY      //Read the Primary field ito the Key

             CALL              RDBankLK               //Lock the record so that nobody uses it
.
. Just for arguments sake, let's just make sure that the record hasn't been deleted
. by another user, AND...Let's make sure that it's not being used by another user
. as well!!
.
             IF                (RETURNFL = 1)          //WHAT!!! Somebody deleted this record
               BEEP
               ALERT             STOP,"Record deleted by another User!!",RESULT
               CALL              MAINRESET
               RETURN
             ENDIF
.
. Record is locked...Try again later
.
             IF                (RETURNFL = 2)          //WHAT!!! Somebody's locked the record
               BEEP
               ALERT             NOTE,"Record locked by another User..."::
                                       "Try again later",RESULT,LOCKTITLE
               RETURN
             ENDIF
.
. OK, OK...We've gotten this far...The record is now locked and we
. can safely change the Status to "Modify"
.
             MOVE              "1",Status                  //We've selected the Modify/Change Button
             CALL              DisableRecordButtons
.
. Not only do we have a good record, but we've been able to 'Lock' the record
. and now we're ready to proceed
.
. Enable the Entire Collection of EditText fields as well as setting the
. background colors and making them Non Read-Only
.
             ENABLEITEM        BankCOLL
             DISABLEITEM       Fill1
             %IFDEF            CBBankActive
             ENABLEITEM        CBBankActive
             %ENDIF
             SETPROP           BankCOLL,READONLY=0
             SETPROP           BankCOLL,BGCOLOR=$WINDOW
.
. OK, OK...What do we do with any ActiveX components. We've got to handle
. them as well.  Let's change these to Non Read-Only and change the
. Background colors as well
.
              SETPROP        EBankPhone,Enabled=1
              SETPROP        EBankFax,Enabled=1
              SETPROP        EBankPhone,BGColor=$WINDOW
              SETPROP        EBankFax,BGColor=$WINDOW

.             SETPROP           EBankDiscPerc,*Enabled=1
.             SETPROP           EBankDiscPerc,*BackColor=$Window
.
. Change the Cancel button button to 'Save' and the 'Exit' button to Cancel
.
             SETITEM           BMainCancel,0,CancelTitle
             ENABLETOOL        ID_Cancel

             SETITEM           BMainCHANGE,0,SaveTitle
             ENABLETOOL        ID_Save
.
             SETFOCUS          EBankName               //Set the cursor to the next field
             DISABLEITEM       EBankCODE               //and Disable the Primary Code
           ENDIF
           RETURN
.=============================================================================
. Routine to read the First record and display it
.
MainUndo
. If I've click on the Undo/Reset button, I've already got the 'Key' based on
. the fact that I'm changing a record that already exists and I loaded the
. key the first time.  Soooooo....Simply 'Re-read' the record, Calll the
. SetMain routine and Voila!!!!
.
           BEEP
           ALERT             PLAIN,"Revert back to the 'Original' record",RESULT:
                                   SureTitle
           IF                (RESULT = 1)
             CALL              RDBankLK
             CALL              SetMain
           ENDIF
           RETURN
.=============================================================================
MainFind
           FindSearch        EBankCode
           GETITEM           EBankCode,0,BankKY
           MATCH             BankKY,$SearchKey
           IF                NOT EQUAL
             MOVE              $SearchKey,BankKY
             CALL              RDBank
.
. We've got a record thanks to our Trusy Search/Browse window. Let's
. continue now by setting up the proper Code field and calling the
. MainValid subroutine, that will take care of it for us.
.
             MOVE              "0",Status
             SETITEM           EBankCode,0,BankREC.Code
             CALL              MainValid
           ENDIF
           RETURN
.=============================================================================
. Routine to read the First record and display it
.
MainFirst
           CLEAR             BankKY
           FILL              FirstASCII,BankKY
           CALL              RDBank
           IF                (RETURNFL = 1)  . We didn't find a 'Blank' record
             CALL              KSBank         . Try the 'next' record
             IF                (RETURNFL = 1)  . There are no records in the file
               BEEP
               ALERT             STOP,"No records exist in the system...",RESULT:
                                      FirstTitle
               RETURN
             ENDIF
           ENDIF
.
. We've got a record (either on the READ or the READKS.  Let's now continue
. processing as if we just lost the Focus of the main field.  By calling the
. MainValid subroutine, that will take care of it for us.
           MOVE              "0",Status
           SETITEM           EBankCode,0,BankREC.Code
           CALL              MainValid
           RETURN
.=============================================================================
. Routine to read the Last record and display it
.
MainLast
           CLEAR             BankKY
           FILL              LastASCII,BankKY
           CALL              RDBank
           IF                (RETURNFL = 1)  . We didn't find a 'Blank' record
             CALL              KPBank         . Try the 'Previous' record
             IF                (RETURNFL = 1)  . There are no records in the file
               BEEP
               ALERT             STOP,"No records exist in the system...",RESULT:
                                      LastTitle
               RETURN
             ENDIF
           ENDIF
.
. We've got a record (either on the READ or the READKP.  Let's now continue
. processing as if we just lost the Focus of the main field.  By calling the
. MainValid subroutine, that will take care of it for us.
           MOVE              "0",Status
           SETITEM           EBankCode,0,BankREC.Code
           CALL              MainValid
           RETURN
.=============================================================================
. Routine to read the Next record and display it
.
MainNext
. We can't just do a simple READKS/READKP because of certain conditions including
. 'Attempting' to read past the last record (Next --> EOF) and the reverse
. condition.  Due to this fact, we need to get the current code, and THEN
. do a READKS/READKP
.
           GETCOUNT          EBankCode
           IF                (CharCount <> 0)
             GETITEM           EBankCode,0,BankKY
             CALL              RDBank
           ENDIF
.
           CALL              KSBank         . Try the 'next' record
           IF                (RETURNFL = 1)  . There are no records in the file
             BEEP
             ALERT             STOP,"End of file has been reached.",RESULT:
                                    NextTitle
             RETURN
           ENDIF
.
. We've got a record (either on the READ or the READKS.  Let's now continue
. processing as if we just lost the Focus of the main field.  By calling the
. MainValid subroutine, that will take care of it for us.
           MOVE              "0",Status
           SETITEM           EBankCode,0,BankREC.Code
           CALL              MainValid
           RETURN
.=============================================================================
. Routine to read the Previous record and display it
.
MainPrevious
. We can't just do a simple 'READKS' because of certain conditions including
. 'Attempting' to read past the last record (Next --> EOF) and the reverse
. condition.  Due to this fact, we need to get the current code, and THEN
. do a READKS/READKP
.
           GETCOUNT          EBankCode
           IF                (CharCount <> 0)
             GETITEM           EBankCode,0,BankKY
             CALL              RDBank
           ENDIF
.
           CALL              KPBank         . Try the 'Previous' record
           IF                (RETURNFL = 1)  . There are no records in the file
             BEEP
             ALERT             STOP,"Beginning of file has been reached...",RESULT:
                                    PrevTitle
             RETURN
           ENDIF
.
. We've got a record (either on the READ or the READKS.  Let's now continue
. processing as if we just lost the Focus of the main field.  By calling the
. MainValid subroutine, that will take care of it for us.
           MOVE              "0",Status
           SETITEM           EBankCode,0,BankREC.Code
           CALL              MainValid
           RETURN
.=============================================================================
. Save has been selected from the MENU vs. the Button
.
SAVEMODE
.
. OK...The 'Save' has been selected from the Menu rather than from the Save Button.
. What to do, What to do??  Is this a Save to a 'NEW' record or is it a Save to a
. 'CHANGED' record....
. Let's check the 'Status' flag...1 is a Change record, 2 is a New Record
.
           BRANCH            Status,MainChange,MainNew
           RETURN
.=============================================================================
. Routine to Validate the data from the Form
.
VALIDATE1
           MOVE              "0",ValidFlag

.X           GETITEM           EBankCode,0,TestChars
.X           COUNT             CharCount,TestChars
.X           IF                (CharCount = 0)
.X             MOVE            "1",ValidFlag
.X             BEEP
.X             ALERT             CAUTION,"A Code must be entered into the system",RETURNFL,"Error in Field"
.X             SETFOCUS          EBankCode
.X             RETURN
.X           ENDIF

.
. Everything's OK...Let's just return because the ValidFlag will be set to
. Zero from the top of this routine.
.
           MOVE              "0",ValidFlag
           RETURN
.=============================================================================
. Routine to 'Reset' everything which includes the Button's, Objects,
. fields, etc.
.
MAINRESET
           MOVE              "0",Status     //Reset the status to Not updating
           UNLOCK            BankFL
.
. Reset the fields to 'Blank' and DISABLE all of those fields as well
           SETPROP           EBankPhone,Text=""
           SETPROP           EBankFax,Text=""

           DELETEITEM        BankCOLL,0
           DELETEITEM        STBankGLDesc,0
           DELETEITEM        STCountry,0
           DISABLEITEM       BankCOLL


           SETPROP           BankCOLL,READONLY=1
           SETPROP           BankCOLL,BGCOLOR=$BTNFACE
           SETPROP           EBankPhone,Enabled=0
           SETPROP           EBankFax,Enabled=0
           SETPROP           EBankPhone,BGColor=$BTNFACE
           SETPROP           EBankFax,BGColor=$BTNFACE

           ENABLEITEM        Fill1
.
. Reset the Buttons for the Next record
.
           DISABLEITEM       BMainChange
           DISABLETOOL       ID_Change

           DISABLEITEM       BMainDELETE
           DISABLETOOL       ID_Delete

           SETITEM           BMainCHANGE,0,ChangeTitle

           SETITEM           BMainNEW,0,NewTitle
           ENABLETOOL        ID_New

           ENABLEITEM        BMainNEW
           SETITEM           BMainCancel,0,ExitTitle

           DISABLETOOL       ID_Save
           DISABLETOOL       ID_Undo
           DISABLETOOL       ID_Cancel

           CALL              EnableRecordButtons
.
. Enable & Disable the proper Menu options
.
.X           ENABLEITEM        FileMenu,1
.X           ENABLEITEM        ChangeMenu,1
.X           DISABLEITEM       ChangeMenu,2
.X           DISABLEITEM       ChangeMenu,3
.X           DISABLEITEM       ChangeMenu,4
.
. Setup any ActiveX control fields to what they should be
.
.           SETPROP           EBankDiscPerc,*Text="0"
.           SETPROP           EBankDiscPerc,*Enabled=0
.           SETPROP           EBankDiscPerc,*BackColor=$BTNFACE
.
. Setup the Primary field that is used for Entry purposes
.
           %IFDEF            CBBankActive
           SETITEM           CBBankActive,0,0
           DISABLEITEM       CBBankActive
           %ENDIF

           SETPROP           EBankCODE,READONLY=0
           SETPROP           EBankCode,BGCOLOR=$WINDOW
           ENABLEITEM        EBankCODE
           SETFOCUS          EBankCODE
           RETURN
.=============================================================================
. Cancel Button has been Clicked
.
MAINCLOSE
              DESTROY           WMAIN          . Get rid of the Bank Window
              winshow
              CHAIN             FROMPGM
              STOP
.=============================================================================
. Cancel button has been pressed
.
MainCancel
           IF                (Status = 0)      . They want to exit the program
             DESTROY         WMAIN             . Get rid of the Main Window
             NORETURN
...HR             RETURN
              WINSHOW
              CHAIN             FROMPGM
           ELSE
             IF                (Status = 1 OR Status = 2)  . Change/New Mode
               BEEP
               ALERT              PLAIN,"Do you wish to cancel this operation?",RETURNFL:
                                        "Are you sure?"
               IF                 (RETURNFL = 1)
                 CALL               MAINRESET
                 RETURN
               ELSE
                 RETURN
               ENDIF
             ELSE
               CALL              MAINRESET
             ENDIF
             RETURN
           ENDIF
.=============================================================================
. Delete Button has been Pressed
.
MainDelete
           PARAMTEXT        BankREC.Code,BankTitle,"",""
           BEEP
           ALERT            PLAIN,"Do you wish to Delete the ^1: ^0 ?",#RES1,DelTitle
           IF               (#RES1 = 1)
             CALL             DELBank
             ALERT            NOTE,"A/P Term Code ^0 has been deleted",#RES1,DelOKTitle
             CALL             MAINRESET
           ENDIF
           RETURN
.=============================================================================
.
. Setup all of the fields in the Form based upon the data record
SETMAIN
           SETITEM           EBankCode,0,BankREC.CODE
           SETITEM           EBankName,0,BankREC.NAME
           SETITEM           EBankAddr1,0,BankREC.Address1
           SETITEM           EBankAddr2,0,BankREC.Address2
           SETITEM           EBankAddr3,0,BankREC.Address3
           SETITEM           EBankCity,0,BankREC.City
           SETITEM           EBankSt,0,BankREC.State
           SETITEM           EBankZip,0,BankREC.Zip
           SETITEM           EBankCountry,0,BankREC.Country
           SETITEM           EBankContact,0,BankREC.Contact
           SETITEM           EBankGLAcct,0,BankREC.GLAccount
           SETITEM           EBankRoute,0,BankREC.RoutingNo
           SETITEM           EBankAcct,0,BankREC.AccountNo
           SETITEM           EBankLCheck,0,BankREC.LastCheck
           SETPROP           EBankPhone,Text=BankREC.Telephone
           SETPROP           EBankFax,Text=BankREC.Fax
           CALL              Validate_EBankCountry
           CALL              Validate_EBankGLAcct
           RETURN
.=============================================================================
. Retrieve all of the fields in the Form based upon the data record
.
GETMAIN
          GETITEM          EBankCode,0,BankREC.Code
          GETITEM          EBankName,0,BankREC.Name
          GETITEM          EBankAddr1,0,BankREC.Address1
          GETITEM          EBankAddr2,0,BankREC.Address2
          GETITEM          EBankAddr3,0,BankREC.Address3
          GETITEM          EBankCity,0,BankREC.City
          GETITEM          EBankSt,0,BankREC.State
          GETITEM          EBankZip,0,BankREC.Zip
          GETITEM          EBankCountry,0,BankREC.Country
          GETITEM          EBankContact,0,BankREC.Contact
          GETITEM          EBankGLAcct,0,BankREC.GLAccount
          GETITEM          EBankRoute,0,BankREC.RoutingNo
          GETITEM          EBankAcct,0,BankREC.AccountNo
          GETITEM          EBankLCheck,0,BankREC.LastCheck
          GETPROP          EBankPhone,Text=BankREC.Telephone
          GETPROP          EBankFax,Text=BankREC.Fax
           RETURN
.=============================================================================
.Help Menu selection if required
.
MAINHELP
          RETURN

onClickMainWinChangeMenu
           PERFORM           RESULT OF  MAINNEW,MAINCHANGE,MainDelete,SAVEMODE
           RETURN
.=============================================================================.
onClickMainWinExitButton
.
. check to see if this is masquerading as a CANCEL button
.
           CALL              MainCancel
           RETURN
.=============================================================================.
onClickMainWinFileMenu
.
. process a click on the file menu
.
           PERFORM           result of MainPrint,,MAINCLOSE
           RETURN
.=============================================================================.
onClickMainWinHelpMenu
           PERFORM           result of MAINHELP,MAINHELP,MAINAbout
           RETURN
.=============================================================================.
. Display the Standard "About Box"
.
MAINAbout
.
. display an alert box describing the program
.
           FORMLOAD          About
           RETURN
.=============================================================================
. Print Report option
.
MainPrint
.X           CALL              PRTREPORT
           RETURN
MainToolBar
           RETURN

.=============================================================================
. Disable the 'Record' Buttons because we're in the middle of Updating or
. Creating a New record.
.
DisableRecordButtons
           DISABLETOOL       ID_First
           DISABLETOOL       ID_Next
           DISABLETOOL       ID_Previous
           DISABLETOOL       ID_Last
           DISABLETOOL       ID_Find
           RETURN
.=============================================================================
. Enable the 'Record' Buttons because we're in the middle of Updating or
. Creating a New record.
.
EnableRecordButtons
           ENABLETOOL        ID_First
           ENABLETOOL        ID_Next
           ENABLETOOL        ID_Previous
           ENABLETOOL        ID_Last
           ENABLETOOL        ID_Find
           RETURN
;==========================================================================================================

;=============================================================================
                   include           MenuDefs.inc
